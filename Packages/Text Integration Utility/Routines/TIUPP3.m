TIUPP3 ;SLC/DJP - Patient Posting Cover Sheet API ; 5-JAN-2000 12:38:05
 ;;1.0;TEXT INTEGRATION UTILITIES;**4,54,80**;Jun 20, 1997
ENCOVER(DFN) ; Supports CWAD Display.
 N TIUPP K ^TMP("TIUPPCV",$J)
 I +$G(DFN)'>0 S MSG="-1^DFN required." Q
 D PTPLKP I TIUPP("DATA")="" S MSG="-1^No Patient Postings on file" Q
 S MSG="0^Patient Postings on file"
 Q
 ;
PTPLKP ;Lookup and listing of Patient Posting indicators
 N CTR,GMRARXN,PTP,TIUCN,TIUCW,TIUAR,TIUSTS
 D DOCDEF S TIUPP("DATA")="",CTR=0
 D EN1^GMRAOR1(DFN,"GMRARXN")
 I $G(GMRARXN) D  D BLDG(.PTP,.CTR)
 .S PTP("IEN")="",PTP("ACRN")="A",PTP("CN")="ALLERGIES"
 .S PTP("MOD")="Known allergies",PTP("DATE")=""
 F TIUSTS=7,8 D
 .I $D(^TIU(8925,"ADCPT",+DFN,TIUCN,TIUSTS)) S PTP("ACRN")="C",PTP("CN")="CRISIS NOTE" D BUILD(TIUCN,TIUSTS,.PTP,.CTR)
 .I $D(^TIU(8925,"ADCPT",+DFN,TIUCW,TIUSTS)) S PTP("ACRN")="W",PTP("CN")="CLINICAL WARNING" D BUILD(TIUCW,TIUSTS,.PTP,.CTR)
 .I $D(^TIU(8925,"ADCPT",+DFN,TIUAR,TIUSTS)) S PTP("ACRN")="D",PTP("CN")="ADVANCE DIRECTIVE" D BUILD(TIUAR,TIUSTS,.PTP,.CTR)
 Q
 ;
BUILD(TYPE,STATUS,PTP,CTR) ;Sets PTP Array elements for BLDG
 N TIUDT S TIUDT=0
 F  S TIUDT=$O(^TIU(8925,"ADCPT",+DFN,TYPE,STATUS,TIUDT)) Q:+TIUDT'>0  D
 .N IEN S IEN=0
 .F  S IEN=$O(^TIU(8925,"ADCPT",+DFN,TYPE,STATUS,TIUDT,IEN)) Q:+IEN'>0  D
 ..S PTP("IEN")=IEN
 ..S PTP("TITLE")=$$PNAME^TIULC1(+$G(^TIU(8925,IEN,0)))
 ..S PTP("MOD")=$P($G(^TIU(8925,IEN,17)),U)
 ..S PTP("DATE")=9999999-TIUDT
 ..D BLDG(.PTP,.CTR)
 Q
 ;
BLDG(PTP,CTR) ;Build ^TMP("TIUPPCV",$J,
 ;IEN^Acronym^Category Name^Optional Modifier^Date/Time^Optional Addendum
 N TIUREC
 S TIUPP("DATA")=TIUPP("DATA")_PTP("ACRN")
 S CTR=CTR+1,TIUREC=PTP("IEN")_U_PTP("ACRN")_U_$S($L($G(PTP("TITLE"))):$G(PTP("TITLE")),1:PTP("CN"))_U_PTP("MOD")_U_PTP("DATE")
 I PTP("ACRN")'="A" S TIUREC=TIUREC_U_$$GETADD(PTP("IEN"))
 S ^TMP("TIUPPCV",$J,CTR)=TIUREC
 Q
 ;
GETADD(TIUDA) ;Gets most recent addendum of a posting
 N ADD,TIUY,TIUD0,TIUDT S TIUY=""
 S ADD="",ADD=$O(^TIU(8925,"DAD",TIUDA,ADD),-1) G:+ADD'>0 GETADX
 S TIUD0=$G(^TIU(8925,ADD,0))
 I $S($P(TIUD0,U,5)=7:0,$P(TIUD0,U,5)=8:0,1:1) G GETADX
 S TIUDT=$P($G(^TIU(8925,ADD,12)),U) G:'+TIUDT GETADX
 S TIUY=" (addendum "_$$DATE^TIULS(TIUDT,"MM/DD/YY HR:MIN")_")"
GETADX Q TIUY
 ;
DOCDEF ;Sets IENs for lookup on specific Document Types/Status
 N TIUDC,TIUX,TIUST
 S TIUX="CLINICAL WARNING"
 S TIUDC=0 F  S TIUDC=$O(^TIU(8925.1,"B",TIUX,TIUDC)) Q:+TIUDC'>0!+$G(TIUCW)  D
 . I $P($G(^TIU(8925.1,+TIUDC,0)),U,4)="DC" S TIUCW=+TIUDC
 S TIUX="CRISIS NOTE"
 S TIUDC=0 F  S TIUDC=$O(^TIU(8925.1,"B",TIUX,TIUDC)) Q:+TIUDC'>0!+$G(TIUCN)  D
 . I $P($G(^TIU(8925.1,+TIUDC,0)),U,4)="DC" S TIUCN=+TIUDC
 S TIUX="ADVANCE DIRECTIVE"
 S TIUDC=0 F  S TIUDC=$O(^TIU(8925.1,"B",TIUX,TIUDC)) Q:+TIUDC'>0!+$G(TIUAR)  D
 . I $P($G(^TIU(8925.1,+TIUDC,0)),U,4)="DC" S TIUAR=+TIUDC
 S:'$D(TIUCW) TIUCW=0
 S:'$D(TIUCN) TIUCN=0
 S:'$D(TIUAR) TIUAR=0
 Q
