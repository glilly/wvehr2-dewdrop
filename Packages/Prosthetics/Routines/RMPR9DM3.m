RMPR9DM3 ;HOIFO/HNC - GUI PFFS INSURANCE MINI GRID ;9/18/02  17:01
 ;;3.0;PROSTHETICS;**96**;Feb 09, 1996
A1(IEN) G A2
ENR(DFN) ;entry point for roll and scroll
 G ENC
EN(RESULTS,IEN) ;broker entry point
A2 ;
 S DFN=$P($G(^RMPR(660,IEN,0)),U,2)
 I DFN="" S RESULTS(0)="NOTHING FOUND" Q
 ;new code
ENC ;roll and scroll starts here
 ;call insurance API
 S X=$$INSUR^IBBAPI(DFN,,"RBA",.RMI,"*") I $D(RMI) D
 .S CNT=0
 .S X="" F  S X=$O(RMI("IBBAPI","INSUR",X)) Q:'X  D
 ..S INSUR=$P(RMI("IBBAPI","INSUR",X,1),U,2)
 ..S SUBID=$P(RMI("IBBAPI","INSUR",X,14),U,1)
 ..S HOLDER=$P(RMI("IBBAPI","INSUR",X,12),U,2)
 ..S COB=$E($P(RMI("IBBAPI","INSUR",X,7),U,2),0,1)
 ..S RMPRIND=$P(RMI("IBBAPI","INSUR",X,11),U,1)
 ..I RMPRIND'="" S RMPRIND=$E(RMPRIND,4,5)_"/"_$E(RMPRIND,6,7)_"/"_(($E(RMPRIND,1,3))+1700)
 ..S INSURE=$P(RMI("IBBAPI","INSUR",X,10),U,1)
 ..I INSURE'="" S INSURE=$E(INSURE,4,5)_"/"_$E(INSURE,6,7)_"/"_(($E(INSURE,1,3))+1700)
 ..S INSURG=$P(RMI("IBBAPI","INSUR",X,8),U,1)
 ..S INSURGG=$$GET1^DIQ(355.3,INSURG_",",.04)
 ..S CNT=CNT+1
 ..S RESULTS(CNT)=INSUR_U_SUBID_U_INSURG_U_HOLDER_U_RMPRIND_U_INSURE_U_COB
 ..K INSUR,SUBID,INSURG,HOLDER,RMPRIND,INSURE,COB
 ;
 I '$D(RESULTS) S RESULTS(1)="No Insurance Information"
 ;
 K RMI,IEN,DFN,CNT,INSURGG,X
 ;END
