ONCSPC3 ;HIRMFO/GWB - PCE Study of Soft Tissue Sarcoma Table III;6/19/96
 ;;2.11;ONCOLOGY;**6,11**;Mar 07, 1995
 K TABLE,HTABLE
 S TABLE("PRETREATMENT TUMOR SIZE (mm)")="PTS"
 S TABLE("PATHOLOGIC TUMOR SIZE (mm)")="SOT"
 S TABLE("DEPTH OF TUMOR")="DOT"
 S TABLE("MULTIFOCAL")="M"
 S TABLE("REGIONAL NODES EXAMINED")="RNE"
 S TABLE("REGIONAL NODES POSITIVE")="RNP"
 S TABLE("SITES OF DISTANT METASTASIS")="SODM"
 S TABLE("AJCC CLINICAL STAGE (cTNM)")="ACS"
 S TABLE("AJCC PATHOLOGIC STAGE (pTNM)")="APS"
 S TABLE("CLINICAL STAGED BY")="CSB"
 S TABLE("PATHOLOGIC STAGED BY")="PSB"
 S TABLE("CONSULTATIONS")="C"
 S HTABLE(1)="PRETREATMENT TUMOR SIZE (mm)"
 S HTABLE(2)="PATHOLOGIC TUMOR SIZE (mm)"
 S HTABLE(3)="DEPTH OF TUMOR"
 S HTABLE(4)="MULTIFOCAL"
 S HTABLE(5)="REGIONAL NODES EXAMINED"
 S HTABLE(6)="REGIONAL NODES POSITIVE"
 S HTABLE(7)="SITES OF DISTANT METASTASIS"
 S HTABLE(8)="AJCC CLINICAL STAGE (cTNM)"
 S HTABLE(9)="AJCC PATHOLOGIC STAGE (pTNM)"
 S HTABLE(10)="CLINICAL STAGED BY"
 S HTABLE(11)="PATHOLOGIC STAGED BY"
 S HTABLE(12)="CONSULTATIONS"
 S CHOICES=12
 W @IOF D HEAD^ONCSPC0
 W !?18,"TABLE III- EXTENT OF DISEASE AND AJCC STAGE",!
 S DIE="^ONCO(165.5,",DA=ONCONUM
PTS S DR="29PRETREATMENT TUMOR SIZE (mm)..." D ^DIE G:$D(Y) JUMP
SOT S DR="522PATHOLOGIC TUMOR SIZE (mm)....." D ^DIE G:$D(Y) JUMP
DOT S DR="523DEPTH OF TUMOR................." D ^DIE G:$D(Y) JUMP
M S DR="433MULTIFOCAL....................." D ^DIE G:$D(Y) JUMP
RNE S DR="33REGIONAL NODES EXAMINED........" D ^DIE G:$D(Y) JUMP
RNP S DR="32REGIONAL NODES POSITIVE........" D ^DIE G:$D(Y) JUMP
SODM W !!,"SITES OF DISTANT METASTASIS:",!
 S DR="34  SITE OF DISTANT METASTASIS #1" D ^DIE G:$D(Y) JUMP
 I X=0 D  G ACS
 .S $P(^ONCO(165.5,ONCONUM,2),U,15)=0
 .W !,"  SITE OF DISTANT METASTASIS #2: None"
 .S $P(^ONCO(165.5,ONCONUM,2),U,16)=0
 .W !,"  SITE OF DISTANT METASTASIS #3: None"
 S DR="34.1  SITE OF DISTANT METASTASIS #2" D ^DIE G:$D(Y) JUMP
 I X=0 D  G ACS
 .S $P(^ONCO(165.5,ONCONUM,2),U,16)=0
 .W !,"  SITE OF DISTANT METASTASIS #3: None"
 S DR="34.2  SITE OF DISTANT METASTASIS #3" D ^DIE G:$D(Y) JUMP
ACS W !!,"AJCC CLINICAL STAGE (cTNM):",!
CTCODE S DR="37.1  T-CODE......................." D ^DIE G:$D(Y) JUMP
 D CN1^ONCOTN,CN2^ONCOTN
CNCODE S DR="37.2  N-CODE......................." D ^DIE G:$D(Y) JUMP
CMCODE S DR="37.3  M-CODE......................." D ^DIE G:$D(Y) JUMP
 I '$D(SKAJCC) D CN1^ONCOTN
 S STGIND="C" D ES^ONCOTN
CSTAGE ;W ! S DR="38  AJCC STAGE..................." D ^DIE G:$D(Y) JUMP
CSB S DR="19  CLINICAL STAGED BY..........." D ^DIE G:$D(Y) JUMP
APS W !!,"AJCC PATHOLOGIC STAGE (pTNM):",!
PTCODE S DR="85  T-CODE......................." D ^DIE G:$D(Y) JUMP
 D CN3^ONCOTN,CN4^ONCOTN
PNCODE S DR="86  N-CODE......................." D ^DIE G:$D(Y) JUMP
PMCODE S DR="87  M-CODE......................." D ^DIE G:$D(Y) JUMP
 I '$D(SKAJCC) D CN3^ONCOTN
 S STGIND="P" D ES^ONCOTN
PSTAGE ;W ! S DR="88  AJCC STAGE..................." D ^DIE G:$D(Y) JUMP
PSB S DR="89  PATHOLOGIC STAGED BY........." D ^DIE G:$D(Y) JUMP
C W !!,"CONSULTATIONS:",!
 S DR="524  MEDICAL ONCOLOGIST..........." D ^DIE G:$D(Y) JUMP
 S DR="525  RADIATION ONCOLOGIST........." D ^DIE G:$D(Y) JUMP
 W ! K DIR S DIR(0)="E" D ^DIR
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!,"GO TO: ",X:DTIME I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K HTABLE,TABLE,CHOICES
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
