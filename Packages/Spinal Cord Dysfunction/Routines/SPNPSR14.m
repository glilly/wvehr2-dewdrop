SPNPSR14 ;HIRMFO/DAD,WAA-HUNT: FEE BASIS ;8/7/95  10:36
 ;;2.0;Spinal Cord Dysfunction;**10**;01/02/1997
 ;
EN1(D0,BDATE,EDATE) ; *** Search entry point
 ; Input:
 ;  ACTION,SEQUENCE = Search ACTION,SEQUENCE number
 ;  D0       = SCD (SPINAL CORD) REGISTRY file (#154) IEN
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"BEGINNING DATE") = Date ^ Date_(Ext)
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"ENDING DATE") = Date ^ Date_(Ext)
 ;      BDATE = Beginning date
 ;      EDATE = Ending date
 ; Output:
 ;  $S( D0_Meets_Search_Criteria : 1 , 1 : 0 )
 ;
 N DATE,MEETSRCH
 S MEETSRCH=0
 S DATE("BEG")=BDATE
 S DATE("END")=EDATE
 S DATE("FEE")=+$O(^FBAAC(D0,"AB",9999999.9999-DATE("END")))
 S DATE("FEE")=$S(DATE("FEE"):9999999.9999-DATE("FEE"),1:0)
 I DATE("FEE"),DATE("FEE")'<DATE("BEG") S MEETSRCH=1
 Q MEETSRCH
 ;
EN2(ACTION,SEQUENCE) ; *** Prompt entry point
 ; Input:
 ;  ACTION,SEQUENCE = Search ACTION,SEQUENCE number
 ; Output:
 ;  SPNLEXIT = $S( User_Abort/Timeout : 1 , 1 : 0 )
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"BEGINNING DATE") = Date ^ Date_(Ext)
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"ENDING DATE") = Date ^ Date_(Ext)
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,0) = $$EN1^SPNPSR14(D0,BDATE,EDATE)
 ;
 K ^TMP($J,"SPNPRT",ACTION,SEQUENCE)
 S (BDATE,EDATE)=""
 D EN1^SPNPSR00(ACTION,SEQUENCE,.BDATE,.EDATE)
 I $G(BDATE)'="" I $G(EDATE)'="" S ^TMP($J,"SPNPRT",ACTION,SEQUENCE,0)="$$EN1^SPNPSR14(D0,"_BDATE_","_EDATE_")"
 Q
