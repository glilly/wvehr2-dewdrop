PRCOSS2 ;WISC/DJM-SSO Server Interface to IFCAP ;7/8/94  09:02
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 Q
EN1 ;CALLED FROM PRCPW MAIN MENU OPTION MENU ENTRY ACTION.
 ;NOTIFY PPM THAT NEW ENTRIES SHOULD BE ADDED INTO FILE 445 FOR THE WAREHOUSE
 I $O(^PRCF(423.6,"AD",0))>0 W $C(7),!!!,?3,"There are new GENERIC INVENTORY entries from ISMS SSO transaction to add",!,?3,"to the warehouse inventory." Q
 Q
DISP(PRCPZ) ;CALLED FROM ^PRCPEITO.
 ;DISPLAY ALL ITEMS THAT ARE NOT IN THE WAREHOUSE INVENTORY LIST
 Q:PRCPZ("DPTYPE")'="W"  N HDR,NSN,NSNB,NSNC,SITE,X,X1,X2,Y,Y1,Y2
 S SITE=+^PRCP(445,PRCP("I"),0),HDR="",X=0 F  S X=$O(^PRCF(423.6,"AD",65,X)) Q:X'>0  S X1=^PRCF(423.6,X,1,10000,0) I $P(X1,U,3)=SITE D
 .S Y=0 F  S Y=$O(^PRCF(423.6,X,1,Y)) Q:Y'>0  S Y1=^(Y,0) I $P(Y1,U)="SL" D
 ..S NSN=$P(Y1,U,2),NSN=$E(NSN,1,4)_"-"_$E(NSN,5,6)_"-"_$E(NSN,7,9)_"-"_$E(NSN,10,99),NSNB=$O(^PRC(441,"BB",NSN,0)),NSNC="" S:NSNB>0 NSNC=^PRC(441,NSNB,0) I NSNB>0 S Y2=$G(^PRCP(445,PRCP("I"),1,NSNB,0)) Q:Y2]""
 ..D:HDR=""  W !,"ITEM #: ",NSNB,?17,"ITEM DESCRIPTION: ",$P(NSNC,U,2) Q
 ...S X2="New items from SSO transaction to add to Warehouse" W !,?(IOM-$L(X2))\2,X2,! S HDR=1 Q
 W ! Q
CHECK(PRCPZ) ;CALLED FROM ^PRCPEITO.
 ;SINCE ALL THE ITEMS NEED NOT BE ENTERED INTO THE WAREHOUSE INVENTORY
 ;LIST IN FILE 445, JUST TAKE OFF THE INVENTORY FLAG AND START UP THE
 ;THIRD PART OF PRCOSSO - CREATE THE REPETITIVE ITEM LIST IN FILE 410.3.
 Q:PRCPZ("DPTYPE")'="W"  N %,D,DA,DI,DIC,DIE,DQ,DR,D0,PRCDA,SITE,X,X1,ZTDTH,ZTIO,ZTRTN,ZTSAVE,ZTSK
 S SITE=+^PRCP(445,PRCP("I"),0),X=0 F  S X=$O(^PRCF(423.6,"AD",65,X)) Q:X'>0  S X1=^PRCF(423.6,X,1,10000,0) I $P(X1,U,3)=SITE D
 .S PRCDA=X,ZTSAVE("PRCDA")="",ZTSAVE("ZTREQ")="@",ZTRTN="SSO2^PRCOSS6",ZTDTH=$H,ZTIO="" D ^%ZTLOAD L +^PRCF(423.6,PRCDA,0) S DIE="^PRCF(423.6,",DA=PRCDA,DR="4///@;2///^S X=ZTSK" D ^DIE L -^PRCF(423.6,PRCDA,0)
 .Q
 Q
