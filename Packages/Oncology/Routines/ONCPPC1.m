ONCPPC1 ;Hines OIFO/GWB - PCE Studies of Prostate Cancer ;05/30/00
 ;;2.11;ONCOLOGY;**6,26**;Mar 07, 1995
 ;Table I
 S PRINODE0=^ONCO(165.5,ONCONUM,0)
 K TABLE,HTABLE
 S TABLE("ACCESSION/SEQUENCE NUMBER")="ACC"
 S TABLE("CLASS OF CASE")="COC"
 S TABLE("ZIP CODE")="ZIP"
 S TABLE("BIRTHDATE")="DOB"
 S TABLE("RACE")="RACE"
 S TABLE("PRIMARY PAYER AT DIAGNOSIS")="PPAD"
 S TABLE("DATE OF ADMISSION")="DOA"
 S TABLE("DATE OF DISCHARGE")="DOD"
 S HTABLE(1)="ACCESSION/SEQUENCE NUMBER"
 S HTABLE(2)="CLASS OF CASE"
 S HTABLE(3)="ZIP CODE"
 S HTABLE(4)="BIRTHDATE"
 S HTABLE(5)="RACE"
 S HTABLE(6)="PRIMARY PAYER AT DIAGNOSIS"
 S HTABLE(7)="DATE OF ADMISSION"
 S HTABLE(8)="DATE OF DISCHARGE"
 S CHOICES=8
 K DIQ S DIC="^ONCO(165.5,",DR=".04;.05;.06",DA=ONCONUM,DIQ="ONC"
 D EN^DIQ1
 S D0=ONCOPA D DOB1^ONCOES S Y=X D DATEOT^ONCOPCE S DOB=Y
 S ACCSEQ=ONC(165.5,ONCONUM,.05)_"/"_ONC(165.5,ONCONUM,.06)
 W @IOF D HEAD^ONCPPC0 W !?25,"TABLE I - GENERAL INFORMATION",!
ACC W !,"ACCESSION/SEQUENCE NUMBER.: ",ACCSEQ
COC W !,"CLASS OF CASE.............: ",ONC(165.5,ONCONUM,.04)
ZIP S DIE="^ONCO(165.5,",DA=ONCONUM
 S DR="9ZIP CODE.................." D ^DIE G:$D(Y) JUMP
DOB W !,"BIRTHDATE.................: ",DOB
RACE S DIE="^ONCO(160,",DA=ONCOPA
 S DR="8RACE......................" D ^DIE G:$D(Y) JUMP
PPAD S DIE="^ONCO(165.5,",DA=ONCONUM
 S DR="18PRIMARY PAYER AT DIAGNOSIS" D ^DIE G:$D(Y) JUMP
DOA S DIE="^ONCO(165.5,",DA=ONCONUM
 S DR="1DATE OF ADMISSION........." D ^DIE G:$D(Y) JUMP
DOD S DIE="^ONCO(165.5,",DA=ONCONUM
 S DR="1.1DATE OF DISCHARGE........." D ^DIE G:$D(Y) JUMP
 W ! K DIR S DIR(0)="E" D ^DIR
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!,"GO TO: ",X:DTIME I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K CHOICES,HTABLE,PRINODE0,TABLE
 K ACCSEQ,C,COCEX,COCIN,DOB,CHOICES
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
