%ZVEMOE1 ;DJB,VRROLD**SAVE,UNSAVE,LC,BREAK,JOIN [12/31/94]
 ;;7.0;VPE;;COPYRIGHT David Bolduc @1993
 ;
SAVE ;Save a line
 NEW CNT,VRRLN,X1,X2
 D BOTTOM^%ZVEMOU
SAVEA S VRRLN=$$GETLINE^%ZVEMOU("SAVE WHICH LINE NUMBER(S): ") Q:VRRLN="^"
 I VRRLN="?"!(VRRLN="INVALID") D MSG2^%ZVEMOUM(12) G SAVEA
 I VRRLN="INVALID RNG" D MSG2^%ZVEMOUM(13) G SAVEA
 S CNT=1,(X1,X2)=VRRLN I VRRLN["-" S X1=$P(VRRLN,"-"),X2=$P(VRRLN,"-",2)
 F VRRLN=X1:1:X2 S ^%ZVEMS("E","SAVE",$J,CNT)=^TMP("VEE","VRR",$J,VRRS,"TXT",VRRLN),CNT=CNT+1
 S ^%ZVEMS("E","SAVE",$J,CNT)="" W "   Save Complete"
 Q
UNSAVE ;Unsave code
 I '$D(^%ZVEMS("E","SAVE",$J)) D  D PAUSE^%ZVEMKC(1) Q
 . W $C(7),"   No code saved. Select SA option."
 NEW CD,I,II,VRRLN
 D BOTTOM^%ZVEMOU
UNSAVEA S VRRLN=$$GETLINE^%ZVEMOU("UNSAVE AFTER LINE NUMBER: ") Q:VRRLN="^"
 I VRRLN="?"!(VRRLN["INVALID")!(VRRLN["-") D MSG2^%ZVEMOUM(14) G UNSAVEA
 F I=1:1 Q:'$D(^%ZVEMS("E","SAVE",$J,I))  Q:^(I)=""  S CD=^(I) D  S FLAGSAVE=1
 . S VRRHIGH=VRRHIGH+1
 . S $P(^TMP("VEE","VRR",$J,VRRS,"HOT"),"^",1)=VRRHIGH
 . F II=VRRHIGH:-1:(VRRLN+2) S ^TMP("VEE","VRR",$J,VRRS,"TXT",II)=^TMP("VEE","VRR",$J,VRRS,"TXT",(II-1))
 . S ^TMP("VEE","VRR",$J,VRRS,"TXT",(VRRLN+1))=CD,VRRLN=VRRLN+1
 Q
LC ;Locate and Change
 NEW ADJ,CD,DX,DY,FD,I,NEW,OLD,START,VRRLN,X1,X2
 D BOTTOM^%ZVEMOU
LCA S VRRLN=$$GETLINE^%ZVEMOU("CHANGE WHICH LINE NUMBER(S): ") Q:VRRLN="^"
 I VRRLN="?"!(VRRLN="INVALID") D MSG2^%ZVEMOUM(12) G LCA
 I VRRLN="INVALID RNG" D MSGS^%ZVEMOUM(13) G LCA
 D BOTTOM^%ZVEMOU S DX=0,DY=0 X VEES("XY")
 R "REPLACE: ",OLD:VEE("TIME") Q:OLD=""
 R !,"WITH: ",NEW:VEE("TIME")
 S (X1,X2)=VRRLN I VRRLN["-" S X1=$P(VRRLN,"-"),X2=$P(VRRLN,"-",2)
 F VRRLN=X1:1:X2 D LCRPLC
 Q
LCRPLC ;Replace OLD with NEW
 S CD=$G(^TMP("VEE","VRR",$J,VRRS,"TXT",VRRLN)) I CD'[OLD Q
 S START=0,ADJ=$L(NEW)-$L(OLD) ;ADJ will adjust START if NEW is different length than OLD
 F I=1:1:($L(CD,OLD)-1) S FD=$F(CD,OLD,START),START=FD+ADJ,CD=$E(CD,1,(FD-$L(OLD)-1))_NEW_$E(CD,FD,999)
 I $L(CD,$C(9))'=2!($L(CD)>245) W $C(7) Q  ;No TAB or line too long
 I $L($P($P(CD,$C(9)),"("))>8 W $C(7) Q  ;Line Tag too long
 I CD']"" Q
 S ^TMP("VEE","VRR",$J,VRRS,"TXT",VRRLN)=CD,FLAGSAVE=1
 Q
