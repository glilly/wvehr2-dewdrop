PRCFDPVT ;WISC/LEM/BGJ-PAYMENT ERROR PROCESSING REBUILD/RETRANSMIT ;10/3/95  17:14
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
TYPE(X) N FMSNO,STATUS,STATTXT1,FMSTYPE
 S PRC("SITE")=$P(X,U),FMSTYPE=$P(X,U,2)
 I ("^PV^MO^SO^"'[("^"_FMSTYPE_"^")) D MSG1^PRCFDPVM,OUT Q
 D STAT I $D(DIRUT)!$D(DIROUT)!$D(DUOUT)!$D(DTOUT) G EX
 I 'PRCFA("ERROR"),'$G(PRCFA("TRANS")) G EX
 D NUM^PRCFDPVU W !,"**PONUM=",PONUM,! D GET^PRCFDPVU(421.5,PONUM) I Y<0 D MSG2^PRCFDPVM Q
 L:+Y>0 +^PRCF(421.5,+Y):5 E  W !,"This Payment Voucher is being edited by someone else, please try later!" R !,"Hit <RETURN> to continue.",Y:DTIME G EX
 D TPO
EX D SCREEN K DIRUT,DIROUT,DUOUT,DTOUT,VAR
 QUIT
 ;
STAT S STATUS=GECSDATA(2100.1,GECSDATA,3,"E") D
 .I $E(STATUS,1)="T" D STATT^PRCFDPV2 Q
 .I $E(STATUS,1)="Q" D STATQ^PRCFDPV2 Q
 .I $E(STATUS,1)="M" D STATM^PRCFDPV2 Q
 .I $E(STATUS,1)="E" D STATE^PRCFDPV2 Q
 .I $E(STATUS,1)="A" D STATA^PRCFDPV2 Q
 .I $E(STATUS,1)="R" D STATR^PRCFDPV2 Q
 Q
TPO ; Payment Voucher Error Processing when MOP = Invoice/Rec Rep,CI,Req
 S (D0,PRCF("CIDA"))=+Y D STATR1^PRCFDPV2
 S GO=$P($G(RESP),U) I GO D
 . S DIC=421.5
 . S (FR,TO)=PRCF("CIDA"),L=0,BY="@.01;",FLDS="[CAPTIONED]",IOP="HOME"
 . S PRCF("VIEW")="" D WAIT^PRCFYN,EN1^DIP
 . K PRCF("VIEW"),RECORD,RECORD1,DIC,DK
 . D PAUSE^PRCFDPVU
 . Q
 W ! S RETRAN=$$RETRANS^PRCFDPVU
 K GO S GO=$P($G(RETRAN),U)
 I 'GO D MSG4^PRCFDPVM L -^PRCF(421.5,PRCF("CIDA")) D OUT H 3 Q
 I GO D
 . S PRCFA("RETRAN")=1
 . D EN^PRCFDA3(PRCF("CIDA")),PAUSE^PRCFDPVU
 . Q
 Q
T1358 ; 1358 Error Processing when MOP = MISC OBL(1358)
 D STATR1^PRCFDPV2
 D GENDIQ^PRCFFU7(442,+POIEN,".07","I","")
 S (OB,DA)=$G(PRCTMP(442,+POIEN,".07","I"))
 D NODE^PRCS58OB(DA,.TRNODE)
 I '$D(PRC("CP")) S PRC("CP")=$P(TRNODE(0),"-",4)
 S GO=$P($G(RESP),U) I GO D
 .D PAUSE1^PRCFDPVU
 .S IOP="HOME" D ^%ZIS,^PRCE58P0
 .Q
 W ! S RETRAN=$$RETRANS^PRCFDPVU
 K GO S GO=$P($G(RETRAN),U) I 'GO D MSG4^PRCFDPVM,OUT H 3 Q
 I GO D
 .S PRCFA("RETRAN")=1,DA=OB
 .S PRCF("X")="F" D ^PRCFSITE
 .D SC^PRCESOE
 Q
OUT K GECSDATA,FMSTYPE,FMSNO,STATUS,DIC
 Q
SCREEN ; Control screen display
 I $D(IOF) W @IOF
HDR ; Write Option Header
 I $D(XQY0) W IOINHI,$P(XQY0,U,2),IOINORM
 Q
