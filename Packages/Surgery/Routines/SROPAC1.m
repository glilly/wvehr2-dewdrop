SROPAC1 ;B'HAM ISC/MAM - DAILY ACTIVITY REPORT (CONT)  ; 17 SEPT 1990 8:05 AM
 ;;3.0; Surgery ;;24 Jun 93
 I $Y+8>IOSL D PAGE I SRSOUT Q
 W !,SRNM,?24,SRINTIME,?40,SRDIAG,?92,SRAN1,?115,SRSUR,!,VA("PID"),?16,VADM(4),?24,SROUTIME,?40,SROP(1),?92,SRAN2,?115,SRFST
 W !,SRSLOC,?23,SRTN W:$D(SROP(2)) ?40,SROP(2) W ?115,SRATT
 I $D(SROP(3)) W !,?40,SROP(3) I $D(SROP(4)) W !,?40,SROP(4) I $D(SROP(5)) W !,?40,SROP(5)
 I $D(^SRF(SRTN,"CON")),$P(^("CON"),"^") S SRCON=+^("CON") W !,?24,"Concurrent Case #"_SRCON
 W !
 Q
PAGE S X="" I $E(IOST)'="P" W !!,"Press RETURN to continue, or '^' to quit:  " R X:DTIME I '$T!(X["^") S SRSOUT=1 Q
 I X["?" W !!,"Enter RETURN to continue printing this report, or '^' to exit from this option." G PAGE
 D HDR Q:SRSOUT  W !!,"OPERATING ROOM: "_SROR,!
 Q
HDR ; print heading
 I $D(ZTQUEUED) D ^SROSTOP I SRHALT S SRSOUT=1 Q
 W:$Y @IOF W !,?(132-$L(SRINST)\2),SRINST,!,?58,"SURGICAL SERVICE",!,?47,"DAILY REPORT OF OPERATING ROOM ACTIVITY",!,?58,"FOR: "_SRSDT
 W !!!,"PATIENT",?23,"TIME IN OR",?40,"POSTOPERATIVE DIAGNOSIS",?92,"ANESTHESIOLOGIST",?115,"SURGEON",!,"ID #",?15,"AGE",?23,"TIME OUT OR",?40,"PROCEDURE(S)",?92,"PRIN. ANESTHETIST",?115,"FIRST ASST."
 W !,"WARD",?23,"CASE NUMBER",?115,"ATT SURGEON"
 W ! F LINE=1:1:132 W "="
 Q
