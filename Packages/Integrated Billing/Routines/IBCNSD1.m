IBCNSD1 ;ALB/NLR - BU EDIT TEMPLATES ; 10-JUN-93
 ;;Version 2.0 ; INTEGRATED BILLING ;; 21-MAR-94
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
 ;
ED(IBE) ;
 D SAVEBU
 S DIE="^IBA(355.5,",DA=IBCBU
 S DR=IBE
 L +^IBA(355.5,+IBCBU):5 I '$T D LOCKED^IBTRCD1 G EDQ
 D ^DIE K DIE
 D COMP
 I IBDIF=1 D EDUP
EDQ L -^IBA(355.5,+IBCBU)
 D EXIT
 Q
 ;
SAVEBU ;
 K ^TMP($J,"IBBU")
 S ^TMP($J,"IBBU",355.5,IBCBU,0)=$G(^IBA(355.5,IBCBU,0))
 S ^TMP($J,"IBBU",355.5,IBCBU,1)=$G(^IBA(355.5,IBCBU,1))
 Q
COMP ;
 S IBDIF=0
 I $G(^IBA(355.5,DA,0))'=^TMP($J,"IBBU",355.5,DA,0) S IBDIF=1 Q
 I $G(^IBA(355.5,DA,1))'=^TMP($J,"IBBU",355.5,DA,1) S IBDIF=1 Q
 Q
EDUP ; -- enter date and user if editing has taken place
 S DIE="^IBA(355.5,",DA=IBCBU
 S DR="1.05///NOW;1.06////"_DUZ
 D ^DIE K DIE
 Q
CY ;
 S IBYR1=IBYR K IBYR D INIT^IBCNSD
 I $D(VALMQUIT) S IBYR=IBYR1 K VALMQUIT D EXITRP
 I IBYR=IBYR1 D
 .K IBYR1,VALMQUIT D EXITRP
 E  D EXIT
 Q
 ;
EXIT I $G(IBDIF) D HDR^IBCNSA("Benefits Used"),BLD^IBCNSD
EXITRP S VALMBCK="R"
 Q
