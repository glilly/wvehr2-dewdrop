RMPFQT2 ;DDC/KAW-QUEUE TRANSMISSION BATCH [ 06/16/95   3:06 PM ]
 ;;2.0;REMOTE ORDER/ENTRY SYSTEM;;JUN 16, 1995
 ; input: RMPFX
 ;output: RMPFX,SZ,XX
 S (RMPFNAM,RMPFSSN,RMPFDOB,RMPFADD,RMPFSTAT,RMPFZIP,RMPFCL,RMPEBE)="UNKNOWN" K RMPFE
 S DFN=$P(^RMPF(791810,RMPFX,0),U,4) G END:'DFN D DEM^VADPT
 S RMPFNAM=VADM(1),RMPFSSN=$P(VADM(2),U,1),RMPFDOB=$P(VADM(3),U,1)
 D ELIG^VADPT S RMPFCL=VAEL(7)
 I $D(^RMPF(791810,RMPFX,1)) S S1=^(1),PT=1 D  G ADD:$D(S1)
 .S X=$P(S1,U,7) I X,DT<X K S1 Q
 .S X=$P(S1,U,8) I X,DT>X K S1 Q
 .I $P(S1,U,4)=""!($P(S1,U,5)="") K S1
 .Q
ADD D ADD^VADPT S X=$P(VAPA(5),U,1)
 I X,$D(^DIC(5,X,0)) S:$P(^(0),U,2)'="" RMPFSTAT=$P(^(0),U,2)
 S RMPFADD=VAPA(1)_U_VAPA(2)_U_VAPA(3)_U_VAPA(4)_U_RMPFSTAT_U_VAPA(6)
 S RMPEBE=$P(VAPA(9),U,1)_U_$P(VAPA(10),U,1)
 S RMPFELG=$P($G(^RMPF(791810,RMPFX,2)),U,2)
 S RMPFELGP=$P(VAEL(1),U,2)
ELG1 I RMPFELG,$D(^RMPF(791810.4,RMPFELG,0)) S RMPFELG=$P(^(0),U,1)
 S SZ=RMPFNAM_U_RMPFSSN_U_RMPFDOB_U_RMPFCL_U_RMPFADD_U_RMPFELGP_U_RMPFELG_U_RMPEBE
 S XX="" F I=2:1 Q:'$D(RMPFF(I))  S XX=$S(I>2:XX_U_RMPFF(I),1:RMPFF(I))
END K RMPFNAM,RMPFSSN,RMPFDOB,RMPFELG,RMPFADD,RMPFSTAT,VADM,VAEL,VAPA,VA
 K RMPFELS,RMPFF,RMPFELGD,RMPFELD,RMPFELP,RMPEBE,VAERR,RMPFELGP
 K RMPFL,RMPFZIP,RMPFCL,DFN,RMPFELG,PT,ST,S0,S1,S2,I,X,D,N,P Q
