DGBTR ;ALB/LM - BENEFICIARY TRAVEL RE-PRINT; 1/30/89@8:00 ;4/26/91  14:32
 ;;1.0;Beneficiary Travel;;September 25, 2001
 ;
 Q
DISPLAY S DGBTR=1 D ASK G:'$D(DGBTDT) QUIT G:'$D(^DGBT(392,DGBTDT,0)) QUIT S VADAT("W")=DGBTDT D ^VADATE S DGBTDTE=VADATE("E"),IOP="" D ^%ZIS D SCREEN^DGBTCD D QUIT G DISPLAY
 ;
REPRINT S DGBTR=0 D ASK G:'$D(DGBTDT) QUIT G:'$D(^DGBT(392,DGBTDT,0)) QUIT D START^DGBTCR G QUIT
 ;
ASK S DIC="^DPT(",DIC(0)="AEQMZ" D ^DIC G QUIT:Y<1 S DFN=+Y I '$D(^DGBT(392,"C",DFN)) W !!,"There are no computer entries on file for this patient.",! G ASK
 D 6^VADPT
 K ^UTILITY($J,"DGBT")
 W:'DGBTR !!,"Only claims with ACCOUNT TYPE of ALL OTHER or C&P are listed as choices.",! G LIST
 Q
LIST S X="",(DGBTC,DGBTCH)=0 F I=0:0 S I=$O(^DGBT(392,"AI",DFN,I)) Q:'I  S J=^(I) I $S(DGBTR:1,$D(^DGBT(392,"ACTP",4,J)):1,$D(^DGBT(392,"ACTP",5,J)):1,1:0) S DGBTC=DGBTC+1,^UTILITY($J,"DGBT",DGBTC,I)=9999999.99999-I
 I '$D(^UTILITY($J,"DGBT"))!'$D(^DGBT(392,"C",DFN)) W !,"There are no computer entries on file for this patient with these account types.",! G ASK
 I $D(^UTILITY($J,"DGBT")) W !,"Select Claim DATE/TIME: ",!
 F I=0:0 S I=$O(^UTILITY($J,"DGBT",I)) Q:'I!(DGBTCH)!(X["^")  F J=0:0 S J=$O(^UTILITY($J,"DGBT",I,J)) Q:'J  S K=I,VADAT("W")=^(J) D ^VADATE W !?5,I,".",?10,VADATE("E") I K#5=0 D CHOZ G QUIT:$D(DTOUT) Q:DGBTCH
 D:K#5'=0 CHOZ G QUIT:$D(DTOUT) I DGBTCH S DGBTA=$O(^UTILITY($J,"DGBT",X,0)),DGBTA=^UTILITY($J,"DGBT",X,DGBTA) S (DGBTDT,VADAT("W"))=DGBTA D ^VADATE W "  ",VADATE("E")
 G:'DGBTCH ASK Q
CHOZ ;
 I K'=DGBTC S DIR("A",1)="",DIR("A",2)="Type '^' to Stop, or" S DIR("?")="^D HELP^DGBTR"
 S DIR("A")="Choose 1-"_$S(K=1:"",1:K)_": ",DIR(0)="NOA^1:"_K  D ^DIR K DIR S:$D(DTOUT) DTOUT=1 I Y,$D(^UTILITY($J,"DGBT",Y)) S DGBTCH=1 Q
 Q
QUIT K DIC,DTOUT,X,VA,VADAT,VADATE,DGBTDT,DFN,VAEL,VADM,VAPA,DFN,DGBTA,DGBTC,DGBTCH,DGBTDTE,DGBTR,I,J,K,Y,DGBTFCTY,DGBTMR,DGBTTCTY,VAERR,DGBTCNU,DGBTVAR
 Q
HELP W !!,"ANSWER WITH NUMERIC CHOICE.  BECAUSE ENTRIES ARE STORED BY DATE.TIME.SECONDS,",!,"YOU MUST ENTER A NUMERIC CHOICE." Q
