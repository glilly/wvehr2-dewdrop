ONCP2P6 ;HINES CIOFO/GWB - 1998 Prostate Cancer Study - Table VI ;6/1/98
 ;;2.11;ONCOLOGY;**18,22,24**;Mar 07, 1995
 K TABLE,HTABLE
 S TABLE("66. DATE OF LAST CONTACT OR DEATH")="DLCOD"
 S TABLE("67. VITAL STATUS")="VS"
 S TABLE("68. CANCER STATUS")="CS"
 S TABLE("69. COMPLETED BY")="CB"
 S TABLE("70. CLINICAL REVIEW BY CA COMMITTEE")="CRBCC"
 S HTABLE(1)="66. DATE OF LAST CONTACT OR DEATH"
 S HTABLE(2)="67. VITAL STATUS"
 S HTABLE(3)="68. CANCER STATUS"
 S HTABLE(4)="69. COMPLETED BY"
 S HTABLE(5)="70. CLINICAL REVIEW BY CA COMMITTEE"
 S CHOICES=5
 W @IOF D HEAD^ONCP2P0
 W !," TABLE VI - STATUS AT LAST CONTACT"
 W !," ---------------------------------"
DLCOD S DLC="" I $D(^ONCO(160,ONCOPA,"F","B")) S DLC=$O(^ONCO(160,ONCOPA,"F","B",""),-1)
 I DLC'="" S Y=DLC D DATEOT^ONCOPCE S DLC=Y
 W !," 66. DATE OF LAST CONTACT OR DEATH..: ",DLC
VS S DIE="^ONCO(160,",DA=ONCONUM
 S DR="15 67. VITAL STATUS..................." D ^DIE G:$D(Y) JUMP
 I X=1 S $P(^ONCO(165.5,ONCONUM,7),U,14)=0
CS S CS="" I $D(^ONCO(165.5,ONCONUM,"TS","AA")) D
 .S CSDAT=$O(^ONCO(165.5,ONCONUM,"TS","AA",""))
 .S CSIEN=$O(^ONCO(165.5,ONCONUM,"TS","AA",CSDAT,""))
 .S CSPNT=$P(^ONCO(165.5,ONCONUM,"TS",CSIEN,0),U,2)
 .S:CSPNT'="" CS=$P(^ONCO(164.42,CSPNT,0),U,1)
 W !," 68. CANCER STATUS..................: ",CS
CB S DIE="^ONCO(165.5,",DA=ONCONUM
 S DR="81 69. COMPLETED BY..................." D ^DIE G:$D(Y) JUMP
CRBCC S DIE="^ONCO(165.5,",DA=ONCONUM
 S DR="82 70. CLINICAL REVIEW BY CA COMMITTEE" D ^DIE G:$D(Y) JUMP
 W ! K DIR S DIR(0)="E" D ^DIR S:$D(DIRUT) OUT="Y"
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!," GO TO ITEM NUMBER: ",X:DTIME I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S:X?1.2N X=X_"." S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K CHOICES,HTABLE,TABLE
 K CS,CSDAT,CSIEN,CSPNT,DLC
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
