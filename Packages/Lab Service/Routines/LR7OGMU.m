LR7OGMU ;SLC/STAFF- Interim report rpc memo utility ;6/6/97  16:15
 ;;5.2;LAB SERVICE;**187,312**;Sep 27, 1994
 ;
NEWOLD(Y,DFN) ; from ORWLRR
 N LRDFN
 D DEMO^LR7OGU(DFN,.LRDFN)
 S Y=$$NEWEST(LRDFN)_U_$$OLDEST(LRDFN)
 Q
 ;
NEWEST(LRDFN) ;
 N IDT,FIRSTCH,FIRSTMI
 S (FIRSTCH,FIRSTMI)=""
 S IDT=0 F  S IDT=$O(^LR(LRDFN,"CH",IDT)) Q:IDT<1  I $P($G(^(IDT,0)),U,3) S FIRSTCH=9999999-IDT Q
 S IDT=0 F  S IDT=$O(^LR(LRDFN,"MI",IDT)) Q:IDT<1  S FIRSTMI=9999999-IDT Q
 I FIRSTCH>FIRSTMI Q FIRSTCH
 I FIRSTCH'>FIRSTMI Q FIRSTMI
 Q ""
 ;
OLDEST(LRDFN) ;
 N IDT,FIRSTCH,FIRSTMI
 S (FIRSTCH,FIRSTMI)=""
 S IDT="" F  S IDT=$O(^LR(LRDFN,"CH",IDT),-1) Q:IDT=""  I $P($G(^(IDT,0)),U,3) S FIRSTCH=9999999-IDT Q
 S IDT="" F  S IDT=$O(^LR(LRDFN,"MI",IDT),-1) Q:IDT=""  S FIRSTMI=9999999-IDT Q
 I FIRSTMI="" Q FIRSTCH
 I FIRSTCH="" Q FIRSTMI
 I FIRSTCH<FIRSTMI Q FIRSTCH
 I FIRSTCH'<FIRSTMI Q FIRSTMI
 Q ""
