PXQFS ;ISL/JVS - PARAMETERS SET UP OPTION ;8/29/96  10:31
 ;;1.0;PCE PATIENT CARE ENCOUNTER;**4**;Aug 12, 1996
 ;
 Q
CHK ;--CHECK OUT THE PARAMETERS FILE
 N PXQID
 W $$RE^PXQUTL("~~~~~~~~~~ E R R O R S ~~~~~~~~~~~~~")
 I '$D(^PX(815)) W $$RE^PXQUTL("** PCE PARAMETER FILE # 815 DOESN'T EXSIST YET **") Q
 I '$D(^PX(815,1)) W $$RE^PXQUTL("** PCE PARAMETER FILE #815 CONTAINS NO ENTRIES **") Q
 I '$P($G(^PX(815,1,0)),"^",2) W $$RE^PXQUTL("** NO SD/PCE SWITCH OVER DATE --PCE PARAMETERS FILE **")
 I '$P($G(^PX(815,1,0)),"^",3) W $$RE^PXQUTL("** NO HEALTH SUMMARY START DATE --PCE PARAMETERS FILE **")
 I '$D(^PX(815,1,"DHL",1)) W $$RE^PXQUTL("** NO DISPOSITION CLINIC --PCE PARAMETERS FILE **")
 I '$P($G(^DIC(150.9,1,4)),"^",2) W $$RE^PXQUTL("** MISSING SITE PIECE OF THE VISIT UID --VISIT TRACKING PARAMETERS FILE **")
 S PXQID=$$PKGON^VSIT("PX") D
 .I PXQID=0 W $$RE^PXQUTL("** PCE PACKAGE IS TURNED OFF --VISIT TRACKING PARAMETERS FILE **")
 .I PXQID=-1 W $$RE^PXQUTL("** PCE PACKAGE NOT FOUND IN VISIT TRACKING PARAMETERS FILE **")
 S PXQID=$$PKGON^VSIT("SD") D
 .I PXQID=0 W $$RE^PXQUTL("** SCHEDULING PACKAGE IS TURNED OFF --VISIT TRACKING PARAMETERS FILE **")
 .I PXQID=-1 W $$RE^PXQUTL("** SCHEDULING PACKAGE NOT FOUND IN VISIT TRACKING PARAMETERS FILE **")
 W $$RE^PXQUTL(" ")
 Q
