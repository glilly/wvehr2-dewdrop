NURA6C2 ;HIRMFO/MD,FT-SERVICE POSITION CERTIFICATION REPORT ;8/8/96  13:09
 ;;4.0;NURSING SERVICE;**13**;Apr 25, 1997
 Q:'$D(^DIC(213.9,1,"OFF"))  Q:$P(^DIC(213.9,1,"OFF"),"^",1)=1
 W !
 S (NURQUEUE,NUROUT,NURQUIT)=0
 D EN1^NURSAUTL G QUIT:NUROUT
 I NURMDSW S DIC(0)="AEQZ",NURPLSCR=0 D EN5^NURSAGSP G QUIT:$G(NUROUT)
 I NURMDSW=0,NURPLSW=1 S NURPLSCR=0 D PRD^NURSAGSP K NURPLSCR I $G(NUROUT) G QUIT
 W ! S NRNS=1 D EN2^NURSAGSP G QUIT:NUROUT
 D EN4^NURSAGP0 G QUIT:NUROUT
 W ! S ZTDESC="Service Position Certification Report",ZTRTN="START^NURA6C2" D EN7^NURSUT0 G:POP!($D(ZTSK)) QUIT
START ;
 K ^TMP($J) U IO S (NURPAGE,NCT,NURSW1)=0
 D SORT G:NUROUT QUIT
 D PRINT
QUIT K ^TMP($J) D CLOSE^NURSUT1,^NURAKILL
 Q
 ; DETAIL LINE PRINT ROUTINE
PRINT ;
 S NURFAC="" F  S NURFAC=$O(^TMP($J,"L",NURFAC)) Q:NURFAC=""!(NURQUIT)  D P Q:NURQUIT
 Q
P S NURPROG="" F  S NURPROG=$O(^TMP($J,"L",NURFAC,NURPROG)) Q:NURPROG=""!(NURQUIT)  D:NURSW1 HEADER Q:NURQUIT  D P0 Q:NURQUIT
 Q
P0 S NPRI="" F  S NPRI=$O(^TMP($J,"L",NURFAC,NURPROG,NPRI)) Q:NPRI=""!(NURQUIT)  D P1 Q:NURQUIT
 Q
P1 S NSPN="" F  S NSPN=$O(^TMP($J,"L",NURFAC,NURPROG,NPRI,NSPN)) Q:NSPN=""!(NURQUIT)  D BRK1:NURSW1,P2 Q:NURQUIT
 Q
P2 S NCRT="" F  S NCRT=$O(^TMP($J,"L",NURFAC,NURPROG,NPRI,NSPN,NCRT)) Q:NCRT=""!(NURQUIT)  S NURSORT=$G(^TMP($J,"L",NURFAC,NURPROG,NPRI,NSPN,NCRT)) I NURSORT D P4 Q:NURQUIT
 Q
P4 S NCDT="" F  S NCDT=$O(^TMP($J,"L1",NURSORT,NCDT)) Q:NCDT=""!(NURQUIT)  D P5 W ! Q:NURQUIT
 Q
P5 S N1="" F  S N1=$O(^TMP($J,"L1",NURSORT,NCDT,N1)) Q:N1=""!(NURQUIT)  D:NCRT'="  BLANK" PRINT1 Q:NURQUIT
 Q
PRINT1 I ($Y>(IOSL-10)!'NURSW1) D HEADER,BRK1 Q:NURQUIT
 S NURSW1=1 S NURCTA="" S NSUB=$O(^NURSF(212.2,"C",$E(NCRT,1,30),"")),NURCTA=$S('$D(^NURSF(212.2,NSUB,0)):"",1:$P(^(0),"^",4))
 W:N1'="  BLANK" !,$E(N1,1,20)
 W ?24,$E(NCRT,1,30)
 W:NURCTA'=" BLANK" ?60,NURCTA
 S Y=NCDT D:+Y D^DIQ W:Y'="  BLANK" ?68,Y
 Q
BRK1 Q:NURQUIT  W !!?31,"SERVICE POSITION: ",NSPN W !
 Q
HEADER I 'NURQUEUE,$E(IOST)="C",NURSW1 D ENDPG^NURSUT1 S:NUROUT NURQUIT=+NUROUT Q:NURQUIT
 S NURPAGE=NURPAGE+1 W:$E(IOST)="C"!(NURPAGE>1) @IOF
 I NURMDSW W ?$$CNTR^NURSUT2(NURFAC),$$FACL^NURSUT2(NURFAC)
 W !,"CERTIFICATION PROFILE BY SERVICE POSITION" S X="T" D ^%DT D:+Y D^DIQ W ?58,Y,?72,"PAGE: ",NURPAGE
 W !!?60,"CERT.",?68,"DATE CERT." W !,"NAME",?24,"CERTIFICATION",?60,"AGENCY",?68,"EXPIRES" W !,$$REPEAT^XLFSTR("-",80)
 I $G(NURPLSW),$L(NURPROG)>1 N Z S Z=$$PROD^NURSUT2(NURPROG) W !,?$$CNTR^NURSUT2(Z),$G(Z),!,?$$CNTR^NURSUT2(Z),$$REPEAT^XLFSTR("-",$L(Z)+1)
 Q
SORT W ! S NRPT=2 D EN2^NURAAGS0
 D NODATA^NURA9C1
 Q
