GMRAPEH0 ;HIRMFO/WAA,RM-EDIT A/AR DATA FOR A HISTORICAL ALLERGY ; 1/7/93
 ;;4.0;Adverse Reaction Tracking;;Mar 29, 1996
EN1 ; ENTRY FROM MAIN A/AR EDIT TO EDIT HISTORICAL A/AR
 D EN1^GMRAPER2(GMRAPA,"120.8",.GMRAOUT) G:GMRAOUT EXIT
 I $D(^XUSEC("GMRA-ALLERGY VERIFY",DUZ)) D MECH^GMRAPED0
 G EXIT:GMRAOUT
COM S GMRAVCM="O" D ENDING^GMRAPEM1
 I $D(DTOUT)!$D(Y) S GMRAOUT=1
 I 'GMRAOUT D COMCHECK
 I 'GMRAOUT G:GMRAREQ COM
 S GMRAOUT=0
 K DA,DR,DIE,DUOUT,DTOUT
 Q
COMCHECK ; CHECK TO SEE IF COMMENTS ARE REQUIRED
 S GMRAREQ=($P($G(^GMRD(120.84,+GMRASITE,0)),"^",4)=1) Q:'GMRAREQ
 S X=0,X=$O(^GMR(120.8,GMRAPA,26,X)),GMRAREQ=(X'>0)
 I GMRAREQ W !!,$C(7),"COMMENTS ARE REQUIRED." D HANGT
 I $D(DIRUT) S GMRAOUT=1
 K DIRUT
 Q
HANGT ;Hang for TIMEOUT or press return
 N DIR
 S DIR(0)="EA",DIR("A")="Press RETURN to continue"
 D ^DIR
 Q
EXIT ;
 K DA,DIK,DR,GMRADT,GMRAR10,GMRAPA1,GMRARAD,GMRARDL,GMRAREC,GMRADATE,GMRARODT,GMRAROT,GMRARPR,GMRAX,GMRAY,GMRAZN
 Q
