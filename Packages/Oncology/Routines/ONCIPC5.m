ONCIPC5 ;Hines OIFO/GWB - Primary Intracranial/CNS Tumors PCE Study ;04/26/00
 ;;2.11;ONCOLOGY;**26,27**;Mar 07, 1995
 ;Subsequent Treatment
 K TABLE,HTABLE
 S TABLE("76. DATE OF SUBSEQUENT TREATMENT FOR RECURRENCE/PROGRESSION")="DSTRP"
 S TABLE("77. PROTOCOL PARTICIPATION (SUBSEQUENT TREATMENT)")="PPST"
 S TABLE("78. TYPE OF SUBSEQUENT SURGICAL TREATMENT FOR RECURRENCE/PROGRESSION")="TSSTRP"
 S TABLE("79. TYPE OF SUBSEQUENT RADIATION TREATMENT FOR RECURRENCE/PROGRESSION")="TSRTRP"
 S TABLE("80. TYPE OF SUBSEQUENT CHEMOTHERAPY FOR RECURRENCE/PROGRESSION")="TSCRP"
 S TABLE("81. OTHER TYPE OF SUBSEQUENT TREATMENT FOR RECURRENCE/PROGRESSION")="OTSRP"
 S HTABLE(1)="76. DATE OF SUBSEQUENT TREATMENT FOR RECURRENCE/PROGRESSION"
 S HTABLE(2)="77. PROTOCOL PARTICIPATION (SUBSEQUENT TREATMENT)"
 S HTABLE(3)="78. TYPE OF SUBSEQUENT SURGICAL TREATMENT FOR RECURRENCE/PROGRESSION"
 S HTABLE(4)="79. TYPE OF SUBSEQUENT RADIATION TREATMENT FOR RECURRENCE/PROGRESSION"
 S HTABLE(5)="80. TYPE OF SUBSEQUENT CHEMOTHERAPY FOR RECURRENCE/PROGRESSION"
 S HTABLE(6)="81. OTHER TYPE OF SUBSEQUENT TREATMENT FOR RECURRENCE/PROGRESSION"
 S CHOICES=6
 S IE=ONCONUM
 S DSTRP="00/00/0000"
 S (SUBSUR,SUBRAD,SUBCHE,SUBOTH)=""
 I $D(^ONCO(165.5,IE,4,0)) S SCTIEN=$O(^ONCO(165.5,IE,4,0))
 I $G(SCTIEN) D
 .S DSTRP=$P(^ONCO(165.5,IE,4,SCTIEN,0),U,1)
 .S Y=DSTRP D DATEOT^ONCOPCE S DSTRP=Y
 .S IENS=SCTIEN_","_IE
 .S SUBSUR=$$GET1^DIQ(165.51,IENS,.04)
 .S SUBRAD=$$GET1^DIQ(165.51,IENS,.05)
 .S SUBCHE=$$GET1^DIQ(165.51,IENS,.06)
 .S SUBOTH=$$GET1^DIQ(165.51,IENS,.09)
 W @IOF D HEAD^ONCIPC0
 W !," SUBSEQUENT TREATMENT"
 W !," --------------------"
 S DIE="^ONCO(165.5,",DA=ONCONUM
DSTRP W !," 76. DATE OF SUBSEQUENT TREATMENT                                                     FOR RECURRENCE/PROGRESSION...: ",DSTRP
PPST I DSTRP="00/00/0000" D  G TSSTRP
 .S $P(^ONCO(165.5,IE,"CNS2"),U,88)="00"
 .W !," 77. PROTOCOL PARTICIPATION                                                           (SUBSEQUENT TREATMENT).......: Not on"
 S DR="1373 77. PROTOCOL PARTICIPATION                                                           (SUBSEQUENT TREATMENT)......." D ^DIE G:$D(Y) JUMP
TSSTRP I (DSTRP="00/00/0000")!(SUBSUR["None") D  G TSRTRP
 .S $P(^ONCO(165.5,IE,"CNS2"),U,89)=0
 .W !," 78. TYPE OF SUBSEQUENT SURGICAL TX                                                   FOR RECURRENCE/PROGRESSION...: None, no subsequent surgery"
 S DR="1374 78. TYPE OF SUBSEQUENT SURGICAL TX                                                   FOR RECURRENCE/PROGRESSION..." D ^DIE G:$D(Y) JUMP
TSRTRP I (DSTRP="00/00/0000")!(SUBRAD["None") D  W ! K DIR S DIR(0)="E" D ^DIR G:Y=0 EXIT W @IOF D HEAD^ONCIPC0 G TSCRP
 .S $P(^ONCO(165.5,IE,"CNS2"),U,90)=0
 .W !," 79. TYPE OF SUBSEQUENT RADIATION TX                                                  FOR RECURRENCE/PROGRESSION...: None"
 S DR="1375 79. TYPE OF SUBSEQUENT RADIAITON TX                                                  FOR RECURRENCE/PROGRESSION..." D ^DIE G:$D(Y) JUMP
 W !
TSCRP W !," 80. TYPE OF SUBSEQUENT CHEMOTHERAPY FOR RECURRENCE/PROGRESSION:"
 I (DSTRP="00/00/0000")!(SUBCHE["None") D  W ! G OTSRP
 .F PIECE=1:1:17 S $P(^ONCO(165.5,IE,"CNS3"),U,PIECE)=8
 .W !
 .W !,"      Chemotherapy not administered"
 .W !
 .W !,"      PROCARBAZINE.......: NA        CARBOPLATIN........: NA"
 .W !,"      CCNU...............: NA        TEMOZOLOMIDE.......: NA"
 .W !,"      VINCRISTINE........: NA        CYCLOPHOSPHAMIDE...: NA"
 .W !,"      HYDROXYUREA........: NA        CPT-11.............: NA"
 .W !,"      METHOTREXATE.......: NA        TAMOXIFEN..........: NA"
 .W !,"      CISPLATIN..........: NA        INTERFERON.........: NA"
 .W !,"      BCNU...............: NA        CYTARABINE (ARA-C).: NA"
 .W !,"      BCNU WAFER IMPLANT.: NA        OTHER..............: NA"
 .W !,"      VP-16..............: NA"
 S DR="1376      PROCARBAZINE................." D ^DIE G:$D(Y) JUMP
 S DR="1377      CCNU........................." D ^DIE G:$D(Y) JUMP
 S DR="1378      VINCRISTINE.................." D ^DIE G:$D(Y) JUMP
 S DR="1379      HYDROXYUREA.................." D ^DIE G:$D(Y) JUMP
 S DR="1380      METHOTREXATE................." D ^DIE G:$D(Y) JUMP
 S DR="1381      CISPLATIN...................." D ^DIE G:$D(Y) JUMP
 S DR="1382      BCNU........................." D ^DIE G:$D(Y) JUMP
 S DR="1383      BCNU WAFER IMPLANT..........." D ^DIE G:$D(Y) JUMP
 S DR="1384      VP-16........................" D ^DIE G:$D(Y) JUMP
 S DR="1385      CARBOPLATIN.................." D ^DIE G:$D(Y) JUMP
 S DR="1386      TEMOZOLOMIDE................." D ^DIE G:$D(Y) JUMP
 S DR="1387      CYCLOPHOSPHAMIDE............." D ^DIE G:$D(Y) JUMP
 S DR="1388      CPT-11......................." D ^DIE G:$D(Y) JUMP
 S DR="1389      TAMOXIFEN...................." D ^DIE G:$D(Y) JUMP
 S DR="1390      INTERFERON..................." D ^DIE G:$D(Y) JUMP
 S DR="1391      CYTARABINE (ARA-C)..........." D ^DIE G:$D(Y) JUMP
 S DR="1392      OTHER........................" D ^DIE G:$D(Y) JUMP
 W !
OTSRP I (DSTRP="00/00/0000")!(SUBOTH["None") D  G PRTC
 .S $P(^ONCO(165.5,IE,"CNS3"),U,18)=0
 .W !," 81. OTHER TYPE OF SUBSEQUENT TX                                                      FOR RECURRENCE/PROGRESSION...: None"
 S DR="1393 81. OTHER TYPE OF SUBSEQUENT TX                                                      FOR RECURRENCE/PROGRESSION..." D ^DIE G:$D(Y) JUMP
PRTC W ! K DIR S DIR(0)="E" D ^DIR
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!," GO TO ITEM NUMBER: ",X:DTIME
 I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S:X?1.2N X=X_"." S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K CHOICES,HTABLE,TABLE
 K DSTRP,IENS,PIECE,SUBSUR,SUBRAD,SUBCHE,SUBOTH
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
