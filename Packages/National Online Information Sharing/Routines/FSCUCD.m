FSCUCD ;SLC/STAFF-NOIS Utilities Call Delete ;9/6/98  22:18
 ;;1.1;NOIS;;Sep 06, 1998
 ;
DELETE(CALL) ; from FSCTASKA, FSCUM
 L +^FSCD("CALL",CALL):5 I '$T Q
 D NOTIFY(CALL)
 D SEND(CALL)
 D ALERT(CALL)
 D LIST(CALL)
 D WKLD(CALL)
 D MRE(CALL)
 D MRA(CALL)
 D PRIMARY(CALL)
 D PFIELDS(CALL)
 D CALL(CALL)
 D STATHIST(CALL)
 L -^FSCD("CALL",CALL)
 Q
 ;
NOTIFY(CALL) ;
 N NUM
 S NUM=0 F  S NUM=$O(^FSCD("NOTIFY","B",CALL,NUM)) Q:NUM<1  D DEL("^FSCD(""NOTIFY"",",NUM)
 Q
 ;
SEND(CALL) ;
 N NUM
 S NUM=0 F  S NUM=$O(^FSCD("SEND","AC",CALL,NUM)) Q:NUM<1  D DEL("^FSCD(""SEND"",",NUM)
 Q
 ;
ALERT(CALL) ;
 N NUM
 S NUM=0 F  S NUM=$O(^FSCD("ALERT",NUM)) Q:NUM<1  I $P(^(NUM,0),U,2)=CALL D DEL("^FSCD(""ALERT"",",NUM)
 Q
 ;
LIST(CALL) ;
 N NUM
 S NUM=0 F  S NUM=$O(^FSCD("LISTS","B",CALL,NUM)) Q:NUM<1  D DEL("^FSCD(""LISTS"",",NUM)
 Q
 ;
WKLD(CALL) ;
 N NUM
 S NUM=0 F  S NUM=$O(^FSCD("WKLD","B",CALL,NUM)) Q:NUM<1  D DEL("^FSCD(""WKLD"",",NUM)
 Q
 ;
MRE(CALL) ;
 N NUM
 S NUM=0 F  S NUM=$O(^FSCD("MRE","AC",CALL,NUM)) Q:NUM<1  D DEL("^FSCD(""MRE"",",NUM)
 Q
 ;
MRA(CALL) ;
 N NUM
 S NUM=0 F  S NUM=$O(^FSCD("MRA","AC",CALL,NUM)) Q:NUM<1  D DEL("^FSCD(""MRA"",",NUM)
 Q
 ;
STATHIST(CALL) ;
 N NUM
 S NUM=0 F  S NUM=$O(^FSCD("STATUS HIST","B",CALL,NUM)) Q:NUM<1  D DEL("^FSCD(""STATUS HIST"",",NUM)
 Q
 ;
PRIMARY(CALL) ;
 N SECOND
 S SECOND=0 F  S SECOND=$O(^FSCD("CALL","APRIMARY",CALL,SECOND)) Q:SECOND<1  D
 .D ASSOC^FSCLMPEA(SECOND,SECOND)
 Q
 ;
PFIELDS(CALL) ;
 N NUM
 S NUM=0 F  S NUM=$O(^FSCD("CALL USER","B",CALL,NUM)) Q:NUM<1  D DEL("^FSCD(""CALL USER"",",NUM)
 Q
 ;
CALL(CALL) ;
 D DEL("^FSCD(""CALL"",",CALL)
 Q
 ;
DEL(DIK,DA) ; from FSCAFFIL, FSCLDR, FSCLMPE1, FSCMRK, FSCRPCEB, FSCRPCPF, FSCRPCUF, FSCRPCUR, FSCRPCWS
 D ^DIK
 Q
