RMPFET5 ;DDC/KAW-ENTER/EDIT ORDER INFORMATION [ 06/16/95   3:06 PM ]
 ;;2.0;REMOTE ORDER/ENTRY SYSTEM;;JUN 16, 1995
 ;;input:  DFN,RMPFX,RMPFTYP,RMPFHAT,RMPFTP,RMPFSL (opt)
 ;;output: None
 Q:'$D(RMPFX)  Q:'RMPFX
 I '$D(^RMPF(791810,RMPFX,0)) W $C(7),"*** ORDER DOES NOT EXIST ***" Q
 G END:'RMPFTYP!(RMPFHAT="")!(RMPFTP="")
 D SUB G END:$D(RMPFOUT)
 D ^RMPFET2:RMPFTP="P"
END K DIR,DIE,DR,DA,DR,DI,D0,I,RMPFHAT,RMPFTYP,S0,RMPFSL,RMPFMOD,RM
 K RMPFDUP,X,Y Q
SUB ;;Enter/Edit fields that apply to order type
 ;; input: RMPFTYP,RMPFST (opt.)
 ;;output: None
 S DR="" K RMPFMOD
 I '$D(RMPFSL) S X=0 F I=1:1 S X=$O(^RMPF(791810.1,RMPFTYP,100,X)) Q:'X  S Y=$P(^(X,0),U,2) I Y'="" S DR=DR_$S(DR="":Y,1:";"_Y)
 I $D(RMPFSL) S X=0 F I=1:1 S X=$O(RMPFSL(X)) Q:X=""  I $D(^RMPF(791810.1,RMPFTYP,100,X,0)) S Y=$P(^(0),U,2) I Y'="" S DR=DR_$S(DR="":Y,1:";"_Y)
 Q:DR=""  W ! S DIE="^RMPF(791810,",DA=RMPFX D ^DIE
 I $D(RMPFMOD) D ^RMPFET6 Q:$D(RMPFOUT)
 K RMPFSL Q
