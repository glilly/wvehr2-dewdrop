PPPPRT20 ;ALB/DMB - FFX PRINT ROUTINES ; 3/13/92
 ;;V1.0;PHARMACY PRESCRIPTION PRACTICE;;APR 7,1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
 ;
PRTBYNM ; Print the FFX file by name
 ;
 N PPPARRY,PPPMRT,DROOT,HROOT,PRTFFXST,PRTFFXND,BLINE,X,TMP
 N PATNAME,PATINFO,PATDOB,PATSSN
 N STANAME,STAINFO,STANUM,DOMAIN,LDOV
 N HDRCNT
 ;
 S (VALMCNT,HDRCNT)=0
 ;
 S DROOT="^TMP(""PPPL6"",$J)"
 S HROOT="^TMP(""PPPL6"",$J,""HDR"")"
 S PPPARRY="^TMP(""PPP"",$J,""SRT"")"
 ;
 K @DROOT,@HROOT,@PPPARRY
 ;
 S PPPMRT="PRTBYNM_PPPPRT20"
 S PRTFFXST=1017
 S PRTFFXND=1018
 ;
 S TMP=$$LOGEVNT^PPPMSC1(PRTFFXST,PPPMRT)
 ;
 D HDR1
 S TMP=$$SRTBYNM^PPPPRT4(PPPARRY) ; -- Sorts data
 D ORDER1
 S TMP=$$LOGEVNT^PPPMSC1(PRTFFXND,PPPMRT)
 ; -- Clean up
 K DIR,DIE
 K @PPPARRY
 Q
 ;
HDR1 ; -- Writes heading
 S BLINE=$$SETSTR^VALM1(" ","",1,80)
 ;
 S X=$$CENTER^PPPUTL1(BLINE,"PPP Foreign Facility Xref File")
 D TMPHDR
 S X=$$CENTER^PPPUTL1(BLINE,"by patient as of --> "_$$I2EDT^PPPCNV1(DT))
 D TMPHDR
 S X=" " D TMPHDR
 S X=$$SETSTR^VALM1("PATIENT NAME","",1,40)
 S X=$$SETSTR^VALM1("SSN",X,41,20)
 S X=$$SETSTR^VALM1("DOB",X,61,20)
 D TMPHDR
 S X=$$SETSTR^VALM1("FACILITY NAME","",2,30)
 S X=$$SETSTR^VALM1("NUMBER",X,31,10)
 S X=$$SETSTR^VALM1("DOMAIN",X,41,20)
 S X=$$SETSTR^VALM1("LAST VISIT",X,61,20)
 D TMPHDR
 QUIT
 ;
ORDER1 ; -- Line 1
 ;
 S PATNAME=""
 F  D  Q:PATNAME=""
 .S PATNAME=$O(@PPPARRY@(PATNAME)) Q:PATNAME=""
 .S PATINFO=@PPPARRY@(PATNAME) Q:PATINFO=""
 .S PATDOB=$P(PATINFO,"^")
 .S PATSSN=$P(PATINFO,"^",2)
 .S X=$$SETSTR^VALM1(PATNAME,"",1,40)
 .S X=$$SETSTR^VALM1($E(PATSSN,1,3)_"-"_$E(PATSSN,4,5)_"-"_$E(PATSSN,6,9),X,41,20)
 .S X=$$SETSTR^VALM1(PATDOB,X,61,20)
 .D TMP
ORDER2 .; -- Line 2
 .;
 .S STANAME=""
 .F  D  Q:STANAME=""
 ..S STANAME=$O(@PPPARRY@(PATNAME,STANAME)) Q:STANAME=""
 ..S STAINFO=@PPPARRY@(PATNAME,STANAME) Q:STAINFO=""
 ..S STANUM=$P(STAINFO,"^")
 ..S DOMAIN=$E($P(STAINFO,"^",2),1,18)
 ..S LDOV=$P(STAINFO,"^",3)
 ..S X=$$SETSTR^VALM1($E(STANAME,1,25),"",2,30)
 ..S X=$$SETSTR^VALM1(STANUM,X,31,10)
 ..S X=$$SETSTR^VALM1(DOMAIN,X,41,20)
 ..S X=$$SETSTR^VALM1(LDOV,X,61,20)
 ..D TMP
 .S X=$$SETSTR^VALM1(" ","",1,80) D TMP ; -- null line
 Q
 ;
TMP ; -- Sets up data display array
 S VALMCNT=VALMCNT+1
 S @DROOT@(VALMCNT,0)=$E(X,1,79)
 QUIT
 ;
TMPHDR ; -- Sets up header display array
 S HDRCNT=HDRCNT+1
 S @HROOT@(HDRCNT)=$E(X,1,79)
 QUIT
 ;
