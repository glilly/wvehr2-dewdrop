FSCLMPU ;SLC/STAFF-NOIS List Manager Protocol Utility ;12/8/97  13:40
 ;;1.1;NOIS;;Sep 06, 1998
 ;
SETUP(ACTION,USER) ; from FSCLMP
 ; give full scroll and refresh on return
 D COUNTS(ACTION,USER)
 D FULL^VALM1
 S VALMBCK="R"
 Q
 ;
COUNTS(ACTION,USER) ; from FSCOP, count actions
 L +^XTMP("FSC COUNTS"):5 I '$T Q
 S ^("TOTAL")=$G(^XTMP("FSC COUNTS","TOTAL"))+1
 S ^(ACTION)=$G(^XTMP("FSC COUNTS","ACTION",ACTION))+1
 S ^(ACTION)=$G(^XTMP("FSC COUNTS","USER ACTION",USER,ACTION))+1
 L -^XTMP("FSC COUNTS")
 Q
 ;
COUNT ;
 N ACTION,COUNT,NUM,USER K ^TMP("FSC COUNT",$J)
 S ^XTMP("FSC COUNTS",0)=$$FMADD^XLFDT(DT,7)_U_DT
 W !,"TOTAL: ",$G(^XTMP("FSC COUNTS","TOTAL"))
 W !,"ACTIONS:"
 S ACTION="" F  S ACTION=$O(^XTMP("FSC COUNTS","ACTION",ACTION)) Q:ACTION=""  W !,ACTION,?15," ",+^(ACTION) S ^TMP("FSC COUNT",$J,"CA",+^(ACTION),ACTION)=""
 W !,"ACTIONS BY COUNT:"
 S COUNT="" F  S COUNT=$O(^TMP("FSC COUNT",$J,"CA",COUNT),-1) Q:COUNT=""  D
 .S ACTION="" F  S ACTION=$O(^TMP("FSC COUNT",$J,"CA",COUNT,ACTION)) Q:ACTION=""  W !,COUNT,?6,ACTION
 W !,"USER ACTIONS:"
 S USER="" F  S USER=$O(^XTMP("FSC COUNTS","USER ACTION",USER)) Q:USER=""  D
 .S ACTION="" F  S ACTION=$O(^XTMP("FSC COUNTS","USER ACTION",USER,ACTION)) Q:ACTION=""  S NUM=+^(ACTION) D
 ..W !,$$VALUE^FSCGET(USER,7100,2.1),?25," ",ACTION,?41," ",NUM
 .
 K ^TMP("FSC COUNT",$J)
 Q
 ;
NOACCESS ; from FSCLMP, FSCOP
 ; msg when no access allowed
 W !,"You don't have access to perform this function.",!,$C(7)
 H 2
 S VALMBCK=""
 Q
