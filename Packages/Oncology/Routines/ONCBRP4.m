ONCBRP4 ;HINES CIOFO/GWB - 1998 Breast Cancer Study - Table IV ;6/1/98
 ;;2.11;ONCOLOGY;**18**;Mar 07, 1995
 K TABLE,HTABLE
 S TABLE("40. SIZE OF TUMOR (mm)")="ST"
 S TABLE("41. SIZE OF DCIS TUMOR (mm)")="SDCIST"
 S TABLE("42. REGIONAL NODES EXAMINED")="RNE"
 S TABLE("43. REGIONAL NODES POSITIVE")="RNP"
 S TABLE("44. SENTINEL NODE BIOSPY")="SNB"
 S TABLE("45. NUMBER OF SENTINEL NODES EXAMINED")="NSNE"
 S TABLE("46. NUMBER OF SENTINEL NODES POSITIVE")="NSNP"
 S TABLE("47. SENTINEL NODE DETECTED BY")="NSDB"
 S TABLE("48. AJCC CLINICAL STAGE (cTNM)")="ACS"
 S TABLE("49. AJCC PATHOLOGIC STAGE (pTNM)")="APS"
 S TABLE("50. STAGED BY")="SB"
 S HTABLE(1)="40. SIZE OF TUMOR (mm)"
 S HTABLE(2)="41. SIZE OF DCIS TUMOR (mm)"
 S HTABLE(3)="42. REGIONAL NODES EXAMINED"
 S HTABLE(4)="43. REGIONAL NODES POSITIVE"
 S HTABLE(5)="44. SENTINEL NODE BIOSPY"
 S HTABLE(6)="45. NUMBER OF SENTINEL NODES EXAMINED"
 S HTABLE(7)="46. NUMBER OF SENTINEL NODES POSITIVE"
 S HTABLE(8)="47. SENTINEL NODE DETECTED BY"
 S HTABLE(9)="48. AJCC CLINICAL STAGE (cTNM)"
 S HTABLE(10)="49. AJCC PATHOLOGIC STAGE (pTNM)"
 S HTABLE(11)="50. STAGED BY"
 S CHOICES=11
 W @IOF D HEAD^ONCBRP0
 W !," TABLE IV - EXTENT OF DISEASE AND AJCC STAGE"
 W !," -------------------------------------------"
 S DIE="^ONCO(165.5,",DA=ONCONUM
ST S DR="29 40. SIZE OF TUMOR (mm)......." D ^DIE G:$D(Y) JUMP
SDCIST I IDC=0 D  W ! G RNE
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,43)=888
 .W !," 41. SIZE OF DCIS TUMOR (mm)..: NA, reported tumor not invasive DC"
 S DR="942 41. SIZE OF DCIS TUMOR (mm).." D ^DIE G:$D(Y) JUMP
RNE S DR="33 42. REGIONAL NODES EXAMINED.." D ^DIE G:$D(Y) JUMP
RNP S DR="32 43. REGIONAL NODES POSITIVE.." D ^DIE G:$D(Y) JUMP
SNB W !!," SENTINEL NODES",!
 S DR="943 44. SENTINEL NODE BIOPSY....." D ^DIE G:$D(Y) JUMP
 I $G(X)=0 D  G ACS
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,45)=0
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,46)=7
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,47)=8
 .W !," 45. NUMBER OF SENTINEL NODES"
 .W !,"     EXAMINED.................: None"
 .W !," 46. NUMBER OF SENTINEL NODES"
 .W !,"     POSITIVE.................: None examined"
 .W !," 47. SENTINEL NODE DETECTED BY: NA, not done"
 I $G(X)=9 D  G ACS
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,45)=9
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,46)=9
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,47)=9
 .W !," 45. NUMBER OF SENTINEL NODES"
 .W !,"     EXAMINED.................: Unknown if examined"
 .W !," 46. NUMBER OF SENTINEL NODES"
 .W !,"     POSITIVE.................: Unknown if positive"
 .W !," 47. SENTINEL NODE DETECTED BY: Method unknown"
NSNE S DR="944 45. NUMBER OF SENTINEL NODES                                                        EXAMINED................." D ^DIE G:$D(Y) JUMP
NSNP S DR="945 46. NUMBER OF SENTINEL NODES                                                        POSITIVE................." D ^DIE G:$D(Y) JUMP
 S DR="946 47. SENTINEL NODE DETECTED BY" D ^DIE G:$D(Y) JUMP
ACS W !!," 48. AJCC CLINICAL STAGE (cTNM):",!
CTCODE S DR="37.1     T-CODE..................." D ^DIE G:$D(Y) JUMP
 D CN1^ONCOTN,CN2^ONCOTN
CNCODE S DR="37.2     N-CODE..................." D ^DIE G:$D(Y) JUMP
CMCODE S DR="37.3     M-CODE..................." D ^DIE G:$D(Y) JUMP
 I '$D(SKAJCC) D CN1^ONCOTN
 S STGIND="C" D ES^ONCOTN
APS W !," 49. AJCC PATHOLOGIC STAGE (pTNM):",!
PTCODE S DR="85     T-CODE..................." D ^DIE G:$D(Y) JUMP
 D CN3^ONCOTN,CN4^ONCOTN
PNCODE S DR="86     N-CODE..................." D ^DIE G:$D(Y) JUMP
PMCODE S DR="87     M-CODE..................." D ^DIE G:$D(Y) JUMP
 I '$D(SKAJCC) D CN3^ONCOTN
 S STGIND="P" D ES^ONCOTN
SB W !," 50. STAGED BY:",!
CSB S DR="19     CLINICAL STAGE..........." D ^DIE G:$D(Y) JUMP
PSB S DR="89     PATHOLOGIC STAGE........." D ^DIE G:$D(Y) JUMP
 W ! K DIR S DIR(0)="E" D ^DIR S:$D(DIRUT) OUT="Y"
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!," GO TO ITEM NUMBER: ",X:DTIME I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S:X?1.2N X=X_"." S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K HTABLE,TABLE,CHOICES
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
