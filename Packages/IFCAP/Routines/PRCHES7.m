PRCHES7 ;WISC/RHD/AKS-ESIG MAINT ;8/25/94  14:40
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 ;ROUTINE FOR MAINTAINING SUBFIELD 11 (ELECTRONIC SIGNATURE)
 ; FIELD 50 (AMENDMENT) FILE 442
DECODE(LEVEL0,LEVEL1) ;return esig to readable form
 ;returns "" if unsuccessful
 NEW RECORD,RECORD1,VERSION,PERSON,SIG,CHECKSUM
 S RECORD=$G(^PRC(442,LEVEL0,6,LEVEL1,0)) I RECORD="" Q ""
 S RECORD1=$G(^PRC(442,LEVEL0,6,LEVEL1,1))
 S VERSION=$P(RECORD,"^",11)
 S PERSON=+$P(RECORD1,"^",5)
 I VERSION'="",VERSION'=1 Q ""
 S SIG=$P(RECORD1,"^",6)
D1 ;esig for ver 1
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_LEVEL1_"^"_$$STRING(RECORD,RECORD1))
 Q $$DECODE^PRCUESIG(SIG,PERSON,CHECKSUM)
ENCODE(LEVEL0,LEVEL1,USERNUM,Y) ;Encode esig for version 1 only
 NEW RECORD,RECORD1,SIGBLOCK,CHECKSUM,OLDUSER
 S USERNUM=+USERNUM
 I USERNUM=0 S Y=-3 Q  ;-3 no user number
 S SIGBLOCK=$P($G(^VA(200,USERNUM,20)),"^",2)
 I SIGBLOCK="" S Y=-2 Q  ;-2 no signature block
 S RECORD=$G(^PRC(442,LEVEL0,6,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,6,LEVEL1,1))
 I RECORD="" S Y=-1 Q  ;-1 no record
 I $P(RECORD1,"^",6)'="" S Y=-4 Q  ;-4 cannot re-sign record
 S OLDUSER=+$P(RECORD1,"^",5)
 I OLDUSER=0 S $P(RECORD1,"^",5)=USERNUM
 I OLDUSER>0 S USERNUM=OLDUSER
 S:$P(RECORD1,"^",8)="" $P(RECORD1,"^",8)=$$NOW^PRCUESIG
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_LEVEL1_"^"_$$STRING(RECORD,RECORD1))
 S $P(RECORD1,"^",6)=$$ENCODE^PRCUESIG(SIGBLOCK,USERNUM,CHECKSUM)
 S $P(RECORD,"^",11)="1",$P(RECORD1,"^",7)=$$SUM^PRCUESIG(SIGBLOCK)
 S ^PRC(442,LEVEL0,6,LEVEL1,0)=RECORD
 S ^PRC(442,LEVEL0,6,LEVEL1,1)=RECORD1
 S Y=1 Q
RECODE(LEVEL0,LEVEL1,Y) ;Recode ESIG for version 1 only
 N RECORD,RECORD1,SIGBLOCK,CHECKSUM,USERNUM
 S RECORD=$G(^PRC(442,LEVEL0,6,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,6,LEVEL1,1))
 I RECORD=""!(RECORD1="") S Y=-1 Q  ;-1 no record
 S USERNUM=$P(RECORD1,"^",5)
 I $P(RECORD1,"^",6)=""!(USERNUM=0) S Y=-4 Q  ;-4 cannot recode record
 S SIGBLOCK=$P($G(^VA(200,USERNUM,20)),"^",2)
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_LEVEL1_"^"_$$STRING(RECORD,RECORD1))
 S $P(RECORD1,"^",6)=$$ENCODE^PRCUESIG(SIGBLOCK,USERNUM,CHECKSUM)
 S $P(RECORD,"^",11)="1",$P(RECORD1,"^",7)=$$SUM^PRCUESIG(SIGBLOCK)
 S ^PRC(442,LEVEL0,6,LEVEL1,0)=RECORD
 S ^PRC(442,LEVEL0,6,LEVEL1,1)=RECORD1
 S Y=1 ;1 record recoded properly
 Q
REMOVE(LEVEL0,LEVEL1) ;Entry point to remove esig from record
 NEW RECORD
 S RECORD=$G(^PRC(442,LEVEL0,6,LEVEL1,0)) Q:RECORD=""
 S $P(^PRC(442,LEVEL0,6,LEVEL1,1),"^",5,8)="^^^"
 Q
VERIFY(LEVEL0,LEVEL1)      ;verify version 1 signature.  Returns 1 if valid
 NEW RECORD,RECORD1,VERSION,SIGBLOCK
 S RECORD=$G(^PRC(442,LEVEL0,6,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,6,LEVEL1,1))
 S VERSION=$P(RECORD,"^",11),SIGBLOCK=$P(RECORD1,"^",7)
 I VERSION_SIGBLOCK="" Q 1
 Q ($$SUM^PRCUESIG($$DECODE(LEVEL0,LEVEL1))=SIGBLOCK)
STRING(X,X1)          ;Build String of critical fields
 Q $P(X,"^")_"^"_$P(X,"^",2)_"^"_$P(X,"^",3)_"^"_$P(X1,"^",4)_"^"_$P(X1,"^",8)
