%ZVEMOM ;DJB,VRROLD**Main Menu ; 12/15/00 8:31pm
 ;;7.0;VPE;;COPYRIGHT David Bolduc @1993
 ;
PAGE ;Prepare bottom of screen
 NEW VK
 S FLAGTAG=0
 I FLAGCOL,VRRS>FLAGCOL S FLAGQ=1 G EX ;Collapse
 F I=$Y:1:VEESIZE W ! I $G(FLAGBLNK),I=9,$D(^TMP("VEE","VRR",$J,VRRS,"TXT")) D
 . W ?13,"T=Top   U=UpPg   <AU>=LnUp   G=Goto   <RETURN>=Quit"
 W !,$E(VRRLINE,1,VEE("IOM"))
 S:$G(VRRCRSR)="PGTOP" VRRCRSR=PGTOP
 S:$G(VRRCRSR)="BOTTOM" VRRCRSR=LNUM
 S:$G(VRRCRSR)']"" VRRCRSR=LNUM
 D INIT^%ZVEMOM2 ;Used to locate cursor
 ;
PAGE1 ;Position highlight and display line menu.
 D:'FLAGBLNK NEWHIGH^%ZVEMOM2
 D BOTTOM^%ZVEMOU
 W ?1,"^,"" ""=Quit  N=Navigate  BR=Branch  M=More  ?=Help"
 I $G(FLAGVPE)["EDIT" D
 . W $S(VRRS=1&($G(FLAGHOT)'="YES"):"  ED=Edit",1:"  SA=Save")
 I $G(FLAGDEL)]"" W ?69,"[",FLAGDEL,$S(FLAGDEL?.E1"-":" ",1:""),"]"
 W !?1,"Select: "
PAGE2 ;Get users response
 S VRRACT=$$READ^%ZVEMKR(),VK=$G(VEE("K"))
 I ",^, ,"[(","_VRRACT_",") S VRRACT="^" S FLAGQ=1 G EX
 I ",<TO>,<ESC>,"[(","_VK_",") S FLAGQ=1 G EX
 S VRRACT=$$ALLCAPS^%ZVEMKU(VRRACT)
TAB I VK="<TAB>" S VRRACT=$G(VRRCRSR)
PGKEYS I VK?1"<PG"2U1">" D PGKEYS^%ZVEMOM2 G PAGE2
ARROW I VK?1"<A"1U1">" NEW CHK S CHK=0 D  Q:FLAGQ  G:CHK PAGE2 G PAGE1
 . D ARROW^%ZVEMOM2 D:FLAGQ RESET
 I VK="<RET>" KILL VRRCRSR D  G EX
 . I FLAGL S FLAGQ=1 Q
 . S PGTOP=LNUM+1 D FF^%ZVEMOU
NUMBER I VRRACT?1.N,+VRRACT=VRRACT,VRRACT>0,VRRACT'>VRRHIGH D  D RESET G EX
 . I $G(FLAGVPE)'["EDIT"!(VRRS>1)!($G(FLAGHOT)="YES") D MSG1^%ZVEMOUM(5,1) Q
 . S VRRLN=VRRACT D RUN^%ZVEMOU("CHANGE2^%ZVEMOE")
 I $E(VRRACT,1,3)="COL" S FLAGCOL=$E(VRRACT,4,99) S:FLAGCOL'?1.N!(FLAGCOL<1)!(FLAGCOL>VRRS) FLAGCOL=0 D RESET G EX
 I ",<ESCT>,<ESCD>,"'[(","_VK_","),",?,ASC,B,BK,BR,D,ED,F,FMC,G,H,I,J,L,LC,M,MK,MD,N,PUR,R,RS,SA,SP,T,U,UN,VEDD,VGL,"'[(","_VRRACT_",") W $C(7) G PAGE1
EDIT I ",<ESCT>,<ESCD>,"'[(","_VK_","),",BK,D,ED,I,J,LC,MD,PUR,UN,"[(","_VRRACT_",") I $G(FLAGVPE)'["EDIT"!(VRRS>1)!($G(FLAGHOT)="YES") D MSG1^%ZVEMOUM(5,1) G PAGE1
DELETE I VK="<ESCT>" D CRSRDEL1^%ZVEMOM1 G PAGE1 ;Cursor DELETE
 I VK="<ESCD>" D CRSRDEL2^%ZVEMOM1,RESET G EX
MENUS I ",BR,ED,M,N,"[(","_VRRACT_",") D MENUS^%ZVEMOM1 G PAGE1
 I VRRACT="MK" S VRRMARK=PGTOP D BOTTOM^%ZVEMOU D  G PAGE1
 . W !?1,"Line ",PGTOP," marked" D PAUSE^%ZVEMKC(1)
FIND I VRRACT="F" R !,"TAG: ",TAG:VEE("TIME") S:'$T TAG="^" D  S FLAGQ=2 D FF^%ZVEMOU G EX
 . I "^"[TAG S LNUM=PGTOP-1 Q
 . KILL VRRCRSR S FLAGTAG="FT",LNUM=PGTOP
LOCATE I VRRACT="L" R !,"STRING: ",STNG:VEE("TIME") S:'$T STNG="^" D  S FLAGQ=2 D FF^%ZVEMOU G EX
 . I "^"[STNG S LNUM=PGTOP-1 Q
 . KILL VRRCRSR S FLAGTAG="FS",LNUM=PGTOP
 I VRRACT="MD" D  G PAGE1
 . S ^%ZVEMS("E","EDIT",DUZ)=$S($G(^%ZVEMS("E","EDIT",DUZ))="SCREEN":"LINE",$G(^%ZVEMS("E","EDIT",DUZ))="LINE":"SCREEN",1:"LINE")
 . W "   ",$S(^%ZVEMS("E","EDIT",DUZ)="LINE":"LINE",1:"SCREEN")," EDITOR NOW ACTIVE"
 . D PAUSE^%ZVEMKC(1)
 I VRRACT="PUR" D  G PAGE1
 . KILL ^%ZVEMS("E","SAVE") W "   PURGE Complete" D PAUSE^%ZVEMKC(1)
RTN I VRRACT="R",VRRS>3 D MSG1^%ZVEMOUM(7,1) G PAGE1
 I VRRACT="R" D SYMTAB^%ZVEMKST("C","VRR",VRRS),START^%ZVEMO,SYMTAB^%ZVEMKST("R","VRR",VRRS),RESET G EX
 I VRRACT="SA",$G(FLAGVPE)'["EDIT" D MSG1^%ZVEMOUM(5,1) G PAGE1
 I VRRACT="H",$G(FLAGHOT)="YES" S FLAGQ=1 Q  ;Hot key back to other rtn
 I ",?,ASC,B,BK,D,FMC,H,I,J,LC,RS,SA,SP,T,U,UN,VEDD,VGL,"[(","_VRRACT_",") D ^%ZVEMOM1,RESET G EX
GOTO I VRRACT="G" D GOTO^%ZVEMOM1 G:VRRLN="NO" GOTO Q:VRRLN="^"  S PGTOP=$S(VRRLN>1:VRRLN,1:1) KILL VRRCRSR D RESET G EX
 W $C(7) G PAGE1
EX ;Exit
 KILL VRRLOC
 Q
 ;
RESET ;
 S FLAGQ=2,LNUM=PGTOP-1 D FF^%ZVEMOU
 Q
