VAQDIS27 ;ALB/JFP,JRP - PDX, BUILDS DISPLAY ARRAY FOR MAS DATA ;01MAR93
 ;;1.5;PATIENT DATA EXCHANGE;**13**;NOV 17, 1993
SCR4 ; -- Builds MAS DATA SCREEN 4, APPLICANT/SPOUSE EMPLOYMENT DATA
ROW0 ; -- Builds header
 D BLANK^VAQDIS20
 S VAQLN=$$REPEAT^VAQUTL1(" ",79)
 S VAQCTR=" -- APPLICANT/SPOUSE EMPLOYMENT -- "
 S X=$$CENTER^VAQDIS20(VAQLN,VAQCTR)
 D TMP,BLANK^VAQDIS20
 K VAQLN,VAQCTR
ROW1 ;
 S VAQTMP=$G(@XTRCT@("VALUE",2,.3111,0))
 S VAQINF=$S(VAQTMP="":"UNANSWERED",1:VAQTMP)
 S X=$$SETSTR^VALM1("Employer: "_VAQINF,"",5,44)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.251,0))
 S VAQINF=$S(VAQTMP="":"UNANSWERED",1:VAQTMP)
 S X=$$SETSTR^VALM1("Spouse's: "_VAQINF,X,44,35)
 D TMP K VAQTMP,VAQINF
 ;
EMP ; -- Employer address
 K ADDR
 S VAQTMP=1
 F VAQX=.3113,.3114,.3115 D  Q:(VAQINF="")
 .S VAQINF=$G(@XTRCT@("VALUE",2,VAQX,0))
 .I ((VAQX=.3113)&(VAQINF="")) S VAQINF="STREET ADDRESS UNKNOWN"
 .Q:(VAQINF="")
 .S ADDR(VAQTMP)=VAQINF
 .S VAQTMP=VAQTMP+1
 S CTY=$G(@XTRCT@("VALUE",2,.3116,0))
 S ZIP=$G(@XTRCT@("VALUE",2,.2204,0))
 S STATE=$$STATE^VAQDIS20($G(@XTRCT@("VALUE",2,.3117,0)))
 S VAQINF=$S((CTY="")&(STATE="")&(ZIP=""):"CITY/STATE/ZIP UNKNOWN",1:CTY_$S(STATE="":" ",1:", ")_STATE_" "_ZIP)
 S ADDR(VAQTMP)=VAQINF
EMPSP ;Spouse's employer address
 K ADDRT
 S VAQTMP=1
 F VAQX=.252,.253,.254 D  Q:(VAQINF="")
 .S VAQINF=$G(@XTRCT@("VALUE",2,VAQX,0))
 .I ((VAQX=.252)&(VAQINF="")) S VAQINF="STREET ADDRESS UNKNOWN"
 .Q:(VAQINF="")
 .S ADDRT(VAQTMP)=VAQINF
 .S VAQTMP=VAQTMP+1
 S CTY=$G(@XTRCT@("VALUE",2,.255,0))
 S ZIP=$G(@XTRCT@("VALUE",2,.2206,0))
 S STATE=$$STATE^VAQDIS20($G(@XTRCT@("VALUE",2,.256,0)))
 S VAQINF=$S((CTY="")&(STATE="")&(ZIP=""):"CITY/STATE/ZIP UNKNOWN",1:CTY_$S(STATE="":" ",1:", ")_STATE_" "_ZIP)
 S ADDRT(VAQTMP)=VAQINF
 D ADRLOAD
 K ADDR,ADDRT,VAQTMP,VAQINF,VAQX,CTY,STATE,ZIP
 ;
ROW4 ; -- Phone
 S VAQTMP=$G(@XTRCT@("VALUE",2,.3119,0))
 S VAQINF=$S(VAQTMP="":"UNANSWERED",1:VAQTMP)
 S X=$$SETSTR^VALM1("Phone: "_VAQINF,"",8,46)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.258,0))
 S VAQINF=$S(VAQTMP="":"UNANSWERED",1:VAQTMP)
 S X=$$SETSTR^VALM1("Phone: "_VAQINF,X,47,32)
 D TMP K VAQTMP,VAQINF
 ;
ROW5 ; -- Occupation
 S VAQTMP=$G(@XTRCT@("VALUE",2,.07,0))
 S VAQINF=$S(VAQTMP="":"UNANSWERED",1:VAQTMP)
 S X=$$SETSTR^VALM1("Occupation: "_VAQINF,"",3,41)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.2514,0))
 S VAQINF=$S(VAQTMP="":"UNANSWERED",1:VAQTMP)
 S X=$$SETSTR^VALM1("Occupation: "_VAQINF,X,42,37)
 D TMP K VAQTMP,VAQINF
 ;
ROW6 ;  -- Status
 S VAQTMP=$G(@XTRCT@("VALUE",2,.31115,0))
 S VAQINF=$S(VAQTMP="":"UNANSWERED",1:VAQTMP)
 S X=$$SETSTR^VALM1("Status: "_VAQINF,"",7,46)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.2515,0))
 S VAQINF=$S(VAQTMP="":"UNANSWERED",1:VAQTMP)
 S X=$$SETSTR^VALM1("Status: "_VAQINF,X,46,33)
 D TMP K VAQTMP,VAQINF
 ;
EXIT ; -- Sets the number of lines in display
 QUIT
 ;
ADRLOAD ; -- Loads addresses into display array
 F VAQX=1:1:4 D
 .S VAQINF=""
 .S:($D(ADDR(VAQX))) VAQINF=$$SETSTR^VALM1(ADDR(VAQX),VAQINF,15,53)
 .S:($D(ADDRT(VAQX))) VAQINF=$$SETSTR^VALM1(ADDRT(VAQX),VAQINF,54,25)
 .Q:(VAQINF="")
 .S VALMCNT=VALMCNT+1
 .S @ROOT@(VALMCNT,0)=$E(VAQINF,1,79)
 Q
 ;
TMP ; -- Sets up display array
 S VALMCNT=VALMCNT+1
 S @ROOT@(VALMCNT,0)=$E(X,1,79)
 QUIT
 ;
END ; -- End of code
 QUIT
