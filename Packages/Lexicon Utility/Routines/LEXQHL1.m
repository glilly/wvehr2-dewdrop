LEXQHL1 ;ISL/KER - Query History - ICD Dx Extract ;10/30/2008
 ;;2.0;LEXICON UTILITY;**62**;Sep 23, 1996;Build 16
 ;;
 ;               
 ; Global Variables
 ;    ^ICD9(              ICR   4485
 ;    ^ICM(               ICR   4488
 ;    ^TMP("LEXQHL")      SACC 2.3.2.5.1
 ;               
 ; External References
 ;    $$ICDDX^ICDCODE     ICR   3990
 ;    $$UP^XLFSTR         ICR  10104
 ;               
 Q
EN(X,Y) ; ICD Diagnosis File
 N LEXIEN,LEXDISP,LEXIA,LEXEF,LEXCT,LEXC S LEXIEN=$G(X),LEXDISP=$G(Y),LEXIA="" Q:+LEXIEN'>0  Q:'$D(^ICD9(+LEXIEN,0))  S LEXC=$P($G(^ICD9(+LEXIEN,0)),U,1)
 K ^TMP("LEXQHL",$J) S ^TMP("LEXQHL",$J,"IEN")=LEXIEN,^TMP("LEXQHL",$J,"CODE")=LEXC,^TMP("LEXQHL",$J,"NAME")=$P($$ICDDX^ICDCODE(LEXC),U,4)
 S:'$L(LEXDISP) LEXDISP="SB" D ST,DX,DS,ID^LEXQHL5(LEXC),DG,MC,CC D:$L($G(LEXDISP)) DP K ^TMP("LEXQHL",$J)
 Q
ST ;   1  Status
 N LEXCT,LEXD,LEXE,LEXEF,LEXH,LEXMS,LEXN,LEXS,LEXT
 S LEXCT=0,LEXEF="" F  S LEXEF=$O(^ICD9(+LEXIEN,66,"B",LEXEF)) Q:'$L(LEXEF)  D
 . N LEXH S LEXH=0 F  S LEXH=$O(^ICD9(+LEXIEN,66,"B",LEXEF,LEXH)) Q:+LEXH'>0  D
 . . N LEXN,LEXS,LEXE,LEXT,LEXD,LEXMS S LEXN=$G(^ICD9(+LEXIEN,66,+LEXH,0)),LEXE=$P(LEXN,U,1),LEXS=$P(LEXN,U,2)
 . . Q:+LEXS'>0&(LEXCT'>0)  S LEXCT=LEXCT+1,LEXMS=$$MS^LEXQHLM(LEXE,0),LEXT=$S(+LEXS>0:"Activation",1:"Inactivation")
 . . S:+LEXS>0&(LEXCT=1) LEXT="Initial Activation"_LEXMS,LEXIA=LEXE
 . . S:$O(^ICD9(+LEXIEN,66,"B",LEXEF))=""&(LEXCT>1) LEXT=LEXT_" (final status change)"
 . . S LEXD=$$SD^LEXQHLM(LEXE) S ^TMP("LEXQHL",$J,LEXEF,1,1)=LEXD_U_LEXT
 Q
DX ;   2  Diagnosis
 N LEX,LEXC,LEXCT,LEXD,LEXE,LEXEF,LEXH,LEXI,LEXN,LEXS,LEXT
 S LEXCT=0,LEXEF="" F  S LEXEF=$O(^ICD9(+LEXIEN,67,"B",LEXEF)) Q:'$L(LEXEF)  D
 . N LEXH S LEXH=0 F  S LEXH=$O(^ICD9(+LEXIEN,67,"B",LEXEF,LEXH)) Q:+LEXH'>0  D
 . . N LEXN,LEXS,LEXE,LEXT,LEXD,LEX,LEXI S LEXN=$G(^ICD9(+LEXIEN,67,+LEXH,0)),LEXE=$P(LEXN,U,1),LEXT=$$UP^XLFSTR($P(LEXN,U,2))
 . . S LEXCT=LEXCT+1,LEX(1)=LEXT D PR^LEXQHLM(.LEX,63)
 . . S LEXS=$S(+LEXCT=1:"Initial Diagnosis",+LEXCT>1:"Updated Diagnosis",1:"Diagnosis")
 . . S:$O(^ICD9(+LEXIEN,67,"B",LEXEF))=""&(LEXCT>1) LEXT=LEXT_" (final diagnosis change)"
 . . S LEXD=$$SD^LEXQHLM(LEXE) S ^TMP("LEXQHL",$J,LEXEF,2,1)=LEXD_U_LEXS
 . . S LEXI=0 F  S LEXI=$O(LEX(LEXI)) Q:+LEXI'>0  D
 . . . N LEXC S LEXT=$G(LEX(LEXI)) Q:'$L(LEXT)  S LEXC=$O(^TMP("LEXQHL",$J,LEXEF,2," "),-1)+1
 . . . S ^TMP("LEXQHL",$J,LEXEF,2,LEXC)=U_LEXT
 Q
DS ;   3  Description
 N LEX,LEXC,LEXCT,LEXD,LEXE,LEXEF,LEXH,LEXI,LEXN,LEXS,LEXT
 S LEXCT=0,LEXEF="" F  S LEXEF=$O(^ICD9(+LEXIEN,68,"B",LEXEF)) Q:'$L(LEXEF)  D
 . N LEXH S LEXH=0 F  S LEXH=$O(^ICD9(+LEXIEN,68,"B",LEXEF,LEXH)) Q:+LEXH'>0  D
 . . N LEXN,LEXS,LEXE,LEXT,LEXD,LEX,LEXI S LEXN=$G(^ICD9(+LEXIEN,68,+LEXH,0))
 . . S LEXE=$P(LEXN,U,1),LEXT=$$UP^XLFSTR($G(^ICD9(+LEXIEN,68,+LEXH,1)))
 . . S LEXCT=LEXCT+1,LEX(1)=LEXT D PR^LEXQHLM(.LEX,63)
 . . S LEXS=$S(+LEXCT=1:"Initial Description",+LEXCT>1:"Updated Description",1:"Description")
 . . S:$O(^ICD9(+LEXIEN,68,"B",LEXEF))=""&(LEXCT>1) LEXT=LEXT_" (final description change)"
 . . S LEXD=$$SD^LEXQHLM(LEXE) S ^TMP("LEXQHL",$J,LEXEF,3,1)=LEXD_U_LEXS
 . . S LEXI=0 F  S LEXI=$O(LEX(LEXI)) Q:+LEXI'>0  D
 . . . N LEXC S LEXT=$G(LEX(LEXI)) Q:'$L(LEXT)  S LEXC=$O(^TMP("LEXQHL",$J,LEXEF,3," "),-1)+1
 . . . S ^TMP("LEXQHL",$J,LEXEF,3,LEXC)=U_LEXT
 Q
DG ;   5  DRG Groups
 N LEX,LEXCT,LEXD,LEXDI,LEXDR,LEXDRG,LEXE,LEXEF,LEXH,LEXI,LEXN,LEXS,LEXT,LEXUN,LEXUND
 S LEXUND="",LEXUN=1,LEXN=$G(^ICD9(+LEXIEN,"DRG")) F LEXI=1:1:6 D
 . N LEXD S LEXD=$P(LEXN,U,LEXI) S:$L(LEXD) LEXUND=LEXUND_", "_LEXD
 S LEXUND=$$CS^LEXQHLM(LEXUND),LEXUND=$$AND^LEXQHLM(LEXUND) S:$L(LEXUND) LEXUND="DRG "_LEXUND
 S LEXCT=0,LEXEF="" F  S LEXEF=$O(^ICD9(+LEXIEN,3,"B",LEXEF)) Q:'$L(LEXEF)  D
 . N LEXH S LEXH=0 F  S LEXH=$O(^ICD9(+LEXIEN,3,"B",LEXEF,LEXH)) Q:+LEXH'>0  D
 . . N LEXN,LEXS,LEXE,LEXT,LEXD,LEX,LEXI S LEXN=$G(^ICD9(+LEXIEN,3,+LEXH,0))
 . . S LEXE=$P(LEXN,U,1),LEXT=$$UP^XLFSTR($G(^ICD9(+LEXIEN,3,+LEXH,1)))
 . . S LEXCT=LEXCT+1,LEX(1)=LEXT D PR^LEXQHLM(.LEX,63) S:LEXE=$G(LEXIA) LEXUN=0
 . . S LEXS=$S(+LEXCT=1&(LEXE'=$G(LEXIA)):"Initial Versioned DRG Groups",+LEXCT=1&(LEXE=$G(LEXIA)):"Initial DRG Groups",+LEXCT>1:"Updated DRG Groups",1:"DRG Groups")
 . . S:$O(^ICD9(+LEXIEN,3,"B",LEXEF))=""&(LEXCT>1) LEXT=LEXT_" (final DRG change)"
 . . N LEXDRG,LEXDR,LEXDI
 . . S LEXDRG="",LEXDR="" F  S LEXDR=$O(^ICD9(+LEXIEN,3,+LEXH,1,"B",LEXDR)) Q:'$L(LEXDR)  D
 . . . S LEXDI=0 F  S LEXDI=$O(^ICD9(+LEXIEN,3,+LEXH,1,"B",LEXDR,LEXDI)) Q:+LEXDI'>0  D
 . . . . N LEXN,LEXD S LEXN=+($G(^ICD9(+LEXIEN,3,+LEXH,1,+LEXDI,0)))
 . . . . S LEXDRG=LEXDRG_", "_LEXN S:$E(LEXDRG,1,2)=", " LEXDRG=$E(LEXDRG,3,$L(LEXDRG))
 . . S LEXDRG=$$CS^LEXQHLM(LEXDRG),LEXDRG=$$AND^LEXQHLM(LEXDRG) S:$L(LEXDRG) LEXDRG="DRG "_LEXDRG Q:'$L(LEXDRG)
 . . K LEX S LEX(1)=LEXDRG D PR^LEXQHLM(.LEX,63)
 . . S LEXD=$$SD^LEXQHLM(LEXE) S ^TMP("LEXQHL",$J,LEXEF,5,1)=LEXD_U_LEXS
 . . S LEXI=0 F  S LEXI=$O(LEX(LEXI)) Q:+LEXI'>0  D
 . . . N LEXC S LEXT=$G(LEX(LEXI)) Q:'$L(LEXT)  S LEXC=$O(^TMP("LEXQHL",$J,LEXEF,5," "),-1)+1
 . . . S ^TMP("LEXQHL",$J,LEXEF,5,LEXC)=U_LEXT
 I +($G(LEXUN))>0,$L($G(LEXUND)),+($G(LEXIA))?7N D
 . N LEXI,LEXD,LEXS,LEX S LEXD=$$SD^LEXQHLM(+($G(LEXIA))),LEXS="Initial Unversioned DRG Groups"
 . K LEX S LEX(1)=LEXUND D PR^LEXQHLM(.LEX,63) S ^TMP("LEXQHL",$J,LEXIA,5,1)=LEXD_U_LEXS
 . S LEXI=0 F  S LEXI=$O(LEX(LEXI)) Q:+LEXI'>0  D
 . . N LEXC S LEXT=$G(LEX(LEXI)) Q:'$L(LEXT)  S LEXC=$O(^TMP("LEXQHL",$J,LEXIA,5," "),-1)+1
 . . S ^TMP("LEXQHL",$J,LEXIA,5,LEXC)=U_LEXT
 Q
MC ;   6  Major Diagnostic Category
 N LEX,LEXB,LEXC,LEXCT,LEXD,LEXE,LEXEF,LEXH,LEXI,LEXM,LEXN,LEXS,LEXT,LEXUN,LEXUND
 S LEXUND=$P($G(^ICD9(+LEXIEN,0)),U,5),LEXUND=$$UP^XLFSTR($P($G(^ICM(+LEXUND,0)),U,1)),LEXUN=1
 S LEXCT=0,LEXEF="" F  S LEXEF=$O(^ICD9(+LEXIEN,4,"B",LEXEF)) Q:'$L(LEXEF)  D
 . N LEXH S LEXH=0 F  S LEXH=$O(^ICD9(+LEXIEN,4,"B",LEXEF,LEXH)) Q:+LEXH'>0  D
 . . N LEXB,LEXN,LEXS,LEXE,LEXT,LEXD,LEXM S LEXN=$G(^ICD9(+LEXIEN,4,+LEXH,0)),LEXE=$P(LEXN,U,1),LEXS=$P(LEXN,U,2)
 . . S LEXM=$$UP^XLFSTR($P($G(^ICM(+LEXS,0)),U,1)) Q:'$L(LEXM)  Q:+LEXS'>0&(LEXCT'>0)  S LEXCT=LEXCT+1
 . . S:LEXE=$G(LEXIA) LEXUN=0
 . . S LEXT=$S(+LEXCT=1&(LEXE'=$G(LEXIA)):"Initial Versioned Major Diagnostic Category",+LEXCT=1&(LEXE=$G(LEXIA)):"Initial Major Diagnostic Category",+LEXCT>1:"Updated Major Diagnostic Category",1:"Major Diagnostic Category")
 . . S:$O(^ICD9(+LEXIEN,4,"B",LEXEF))=""&(LEXCT>1) LEXT=LEXT_" (final category change)"
 . . S LEXD=$$SD^LEXQHLM(LEXE) S ^TMP("LEXQHL",$J,LEXEF,6,1)=LEXD_U_LEXT
 . .  S ^TMP("LEXQHL",$J,LEXEF,6,2)=U_LEXM
 I +($G(LEXUN))>0,$L($G(LEXUND)),+($G(LEXIA))?7N D
 . N LEXI,LEXD,LEXS,LEX S LEXD=$$SD^LEXQHLM(+($G(LEXIA))),LEXS="Initial Unversioned Major Diagnostic Category"
 . K LEX S LEX(1)=LEXUND D PR^LEXQHLM(.LEX,63) S ^TMP("LEXQHL",$J,LEXIA,6,1)=LEXD_U_LEXS
 . S LEXI=0 F  S LEXI=$O(LEX(LEXI)) Q:+LEXI'>0  D
 . . N LEXC S LEXT=$G(LEX(LEXI)) Q:'$L(LEXT)  S LEXC=$O(^TMP("LEXQHL",$J,LEXIA,6," "),-1)+1
 . . S ^TMP("LEXQHL",$J,LEXIA,6,LEXC)=U_LEXT
 Q
CC ;   7  Complication/Comorbidity
 N LEX,LEXB,LEXC,LEXCT,LEXD,LEXE,LEXEF,LEXH,LEXI,LEXM,LEXN,LEXS,LEXT,LEXUN,LEXUND
 S LEXUND=$P($G(^ICD9(+LEXIEN,0)),U,7),LEXUND=$S(LEXUND=1:"COMPLICATION/COMORBIDITY",1:""),LEXUN=1
 S LEXCT=0,LEXEF="" F  S LEXEF=$O(^ICD9(+LEXIEN,69,"B",LEXEF)) Q:'$L(LEXEF)  D
 . N LEXH S LEXH=0 F  S LEXH=$O(^ICD9(+LEXIEN,69,"B",LEXEF,LEXH)) Q:+LEXH'>0  D
 . . N LEXB,LEXN,LEXS,LEXE,LEXT,LEXD,LEXM S LEXN=$G(^ICD9(+LEXIEN,69,+LEXH,0)),LEXE=$P(LEXN,U,1),LEXS=$P(LEXN,U,2)
 . . S LEXM=$S(LEXS=1:"COMPLICATION/COMORBIDITY",LEXS=2:"MAJOR COMPLICATION/COMORBIDITY",LEXS="0":"NON-COMPLICATION/COMORBIDITY",1:"") Q:'$L(LEXM)
 . . Q:'$L(LEXS)&(LEXCT'>0)  S LEXCT=LEXCT+1
 . . S:LEXE=$G(LEXIA) LEXUN=0
 . . S LEXT=$S(+LEXCT=1&(LEXE'=$G(LEXIA)):"Initial Versioned Complication/Comorbidity",+LEXCT=1&(LEXE=$G(LEXIA)):"Initial Complication/Comorbidity",+LEXCT>1:"Updated Complication/Comorbidity",1:"Complication/Comorbidity")
 . . S:$O(^ICD9(+LEXIEN,69,"B",LEXEF))=""&(LEXCT>1) LEXT=LEXT_" (final CC change)"
 . . S LEXD=$$SD^LEXQHLM(LEXE) S ^TMP("LEXQHL",$J,LEXEF,7,1)=LEXD_U_LEXT
 . .  S ^TMP("LEXQHL",$J,LEXEF,7,2)=U_LEXM
 I +($G(LEXUN))>0,$L($G(LEXUND)),+($G(LEXIA))?7N D
 . N LEXI,LEXD,LEXS,LEX S LEXD=$$SD^LEXQHLM(+($G(LEXIA))),LEXS="Initial Unversioned Complication/Comorbidity"
 . K LEX S LEX(1)=LEXUND D PR^LEXQHLM(.LEX,63) S ^TMP("LEXQHL",$J,LEXIA,7,1)=LEXD_U_LEXS
 . S LEXI=0 F  S LEXI=$O(LEX(LEXI)) Q:+LEXI'>0  D
 . . N LEXC S LEXT=$G(LEX(LEXI)) Q:'$L(LEXT)  S LEXC=$O(^TMP("LEXQHL",$J,LEXIA,7," "),-1)+1
 . . S ^TMP("LEXQHL",$J,LEXIA,7,LEXC)=U_LEXT
 Q
 ; 
DP ; Display
 S LEXDISP=$G(LEXDISP) Q:$L(LEXDISP)>8  Q:$L(LEXDISP)<2  Q:LEXDISP["^"  N LEXL S LEXL=$T(@LEXDISP+0) Q:'$L(LEXL)  D @LEXDISP
 Q
SB ;   Subjective
 N LEX1,LEX2,LEX3,LEXC,LEXCT,LEXE,LEXHDR,LEXI,LEXID,LEXN,LEXP,LEXS,LEXT
 S LEXC=$G(^TMP("LEXQHL",$J,"CODE")),LEXI=$G(^TMP("LEXQHL",$J,"IEN")),LEXN=$G(^TMP("LEXQHL",$J,"NAME"))
 S LEXT="Code:  "_LEXC,LEXT=LEXT_$J(" ",(16-$L(LEXT)))_LEXN D TL^LEXQHLM(LEXT) S LEXT="",LEXT=LEXT_$J(" ",(16-$L(LEXT)))_"IEN:  "_LEXI D TL^LEXQHLM(LEXT)
 F LEXID=1:1:7 D
 . N LEXHDR,LEXCT S LEXCT=0,LEXHDR=$$HD(LEXID) Q:'$L(LEXHDR)  S LEXP="",LEX1=0 F  S LEX1=$O(^TMP("LEXQHL",$J,LEX1)) Q:+LEX1'>0  D
 . . I LEXID=1 D  Q
 . . . S LEXN=$G(^TMP("LEXQHL",$J,LEX1,LEXID,1)) Q:'$L(LEXN)  S LEXE=$P(LEXN,U,1),LEXS=$P(LEXN,U,2) Q:'$L(LEXE)  Q:'$L(LEXS)  S LEXCT=LEXCT+1
 . . . D:LEXCT=1 BL^LEXQHLM,TL^LEXQHLM((" "_LEXHDR)) S LEXT=LEXE,LEXT=LEXT_$J(" ",(11-$L(LEXT)))_"  "_LEXS,LEXT="   "_LEXT D TL^LEXQHLM(LEXT)
 . . N LEX2 S LEX2=0,LEXE="" F  S LEX2=$O(^TMP("LEXQHL",$J,LEX1,LEXID,LEX2)) Q:+LEX2'>0  D
 . . . S LEXN=$G(^TMP("LEXQHL",$J,LEX1,LEXID,LEX2)) S:LEX2=1 LEXE=$P(LEXN,U,1) Q:LEX2=1  Q:'$L(LEXE)
 . . . I LEX2=2 D  Q
 . . . . S LEXCT=LEXCT+1,LEXT=$G(LEXE),LEXS=$P(LEXN,U,2),LEXT=LEXT_$J(" ",(11-$L(LEXT)))_"  "_LEXS,LEXT="    "_LEXT
 . . . . D:LEXCT=1 BL^LEXQHLM,TL^LEXQHLM((" "_LEXHDR)) D TL^LEXQHLM(LEXT)
 . . . I LEX2>2 D  Q
 . . . . S LEXCT=LEXCT+1,LEXT="",LEXS=$P(LEXN,U,2),LEXT=LEXT_$J(" ",(11-$L(LEXT)))_"  "_LEXS,LEXT="    "_LEXT
 . . . . D:LEXCT=1 BL^LEXQHLM,TL^LEXQHLM((" "_LEXHDR)) D TL^LEXQHLM(LEXT)
 Q
CH ;   Chronological
 N LEX1,LEX2,LEX3,LEXC,LEXD,LEXI,LEXN,LEXP,LEXS,LEXT
 S LEXC=$G(^TMP("LEXQHL",$J,"CODE")),LEXI=$G(^TMP("LEXQHL",$J,"IEN")),LEXN=$G(^TMP("LEXQHL",$J,"NAME"))
 S LEXT="Code:  "_LEXC,LEXT=LEXT_$J(" ",(16-$L(LEXT)))_LEXN D TL^LEXQHLM(LEXT) S LEXT="",LEXT=LEXT_$J(" ",(16-$L(LEXT)))_"IEN:  "_LEXI D TL^LEXQHLM(LEXT)
 S LEXP="",LEX1=0 F  S LEX1=$O(^TMP("LEXQHL",$J,LEX1)) Q:+LEX1'>0  D
 . D BL^LEXQHLM N LEX2 S LEX2=0 F  S LEX2=$O(^TMP("LEXQHL",$J,LEX1,LEX2)) Q:+LEX2'>0  D
 . . N LEX3 S LEX3=0 F  S LEX3=$O(^TMP("LEXQHL",$J,LEX1,LEX2,LEX3)) Q:+LEX3'>0  D
 . . . N LEXN,LEXD,LEXS S LEXN=$G(^TMP("LEXQHL",$J,LEX1,LEX2,LEX3)),LEXD=$P(LEXN,U,1),LEXS=$P(LEXN,U,2)
 . . . S LEXT=$S(LEXD'=LEXP:LEXD,1:""),LEXT=LEXT_$J(" ",(11-$L(LEXT)))_$S($L(LEXD):"- ",1:"  ")_LEXS S LEXT="   "_LEXT D TL^LEXQHLM(LEXT) S:LEXD'="" LEXP=LEXD
 Q
 ; 
 ; Miscellaneous
HD(X) ;   Header
 Q:+($G(X))=1 "Status"  Q:+($G(X))=2 "Diagnosis"  Q:+($G(X))=3 "Description"  Q:+($G(X))=4 "Lexicon Expression"  Q:+($G(X))=5 "DRG Groups"
 Q:+($G(X))=6 "Major Diagnostic Category"  Q:+($G(X))=7 "Complication/Comorbidity"
 Q ""
