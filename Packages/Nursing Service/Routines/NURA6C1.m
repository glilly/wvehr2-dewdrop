NURA6C1 ;HIRMFO/MD,FT-CATEGORY CERTIFICATION REPORT BY SVC. ;7/8/96
 ;;4.0;NURSING SERVICE;**13**;Apr 25, 1997
 Q:'$D(^DIC(213.9,1,"OFF"))  Q:$P(^DIC(213.9,1,"OFF"),"^",1)=1
 S (NURQUEUE,NUROUT,NURQUIT)=0
 D EN1^NURSAUTL G QUIT:NUROUT
 I NURMDSW S DIC(0)="AEQMZ",NURPLSCR=0 D EN5^NURSAGSP G QUIT:$G(NUROUT)
 I NURMDSW=0,NURPLSW=1 S NURPLSCR=0 D PRD^NURSAGSP K NURPLSCR I $G(NUROUT) G QUIT
 S ^TMP("NURSCAT",$J,"R")=""
 D EN4^NURSAGP0 G QUIT:NUROUT
 W ! S ZTDESC="Category Certification Report by Service",ZTRTN="START^NURA6C1",ZTSAVE("^TMP(""NURSCAT"",$J,")="" D EN7^NURSUT0 G:POP!($D(ZTSK)) QUIT
START ;
 K ^TMP($J) U IO S (NURPAGE,NCT,NURSW1)=0
 D SORT G:NUROUT QUIT
 D PRINT
QUIT K ^TMP($J) D CLOSE^NURSUT1,^NURAKILL
 Q
 ; DETAIL LINE PRINT ROUTINE
PRINT ;
 S NURFAC="" F  S NURFAC=$O(^TMP($J,"L",NURFAC)) Q:NURFAC=""!(NURQUIT)  D P0 Q:NURQUIT
 Q
P0 S NURPROG="" F  S NURPROG=$O(^TMP($J,"L",NURFAC,NURPROG)) Q:NURPROG=""  D:NURSW1 HEADER Q:NURQUIT  D P1 Q:NURQUIT
 Q
P1 S NURCAT="" F  S NURCAT=$O(^TMP($J,"L",NURFAC,NURPROG,NURCAT)) Q:NURCAT=""  D BRK1:NURSW1,P2 Q:NURQUIT
 Q
P2 S NCRT="" F  S NCRT=$O(^TMP($J,"L",NURFAC,NURPROG,NURCAT,NCRT)) Q:NCRT=""  D P3 W ! Q:NURQUIT
 Q
P3 S NCDT="" F  S NCDT=$O(^TMP($J,"L",NURFAC,NURPROG,NURCAT,NCRT,NCDT)) Q:NCDT=""  S NURSORT=$G(^TMP($J,"L",NURFAC,NURPROG,NURCAT,NCRT,NCDT)) I NURSORT D P4 Q:NURQUIT
 Q
P4 S N1="" F  S N1=$O(^TMP($J,"L1",NURSORT,N1)) Q:N1=""  D:NCRT'=" BLANK" PRINT1 Q:NURQUIT
 Q
PRINT1 I ($Y>(IOSL-10)!'NURSW1) D HEADER,BRK1 Q:NURQUIT
 S NURSW1=1 S NURCTA="" S NSUB=$O(^NURSF(212.2,"C",$E(NCRT,1,30),"")),NURCTA=$S('$D(^NURSF(212.2,NSUB,0)):"",1:$P(^(0),"^",4))
 W:N1'="  BLANK" !,$E(N1,1,20)
 W ?24,$E(NCRT,1,30)
 W:NURCTA'=" BLANK" ?60,NURCTA
 S Y=NCDT D:+Y D^DIQ W:Y'="  BLANK" ?68,Y
 Q
BRK1 Q:NURQUIT  W !!?31,"SERVICE CATEGORY: ",$$CAT^NURSUT2(NURCAT),!
 Q
HEADER I 'NURQUEUE,$E(IOST)="C",NURSW1 D ENDPG^NURSUT1 S:NUROUT NURQUIT=+NUROUT Q:NURQUIT
 S NURPAGE=NURPAGE+1 W:$E(IOST)="C"!(NURPAGE>1) @IOF
 I NURMDSW W ?$$CNTR^NURSUT2(NURFAC),$$FACL^NURSUT2(NURFAC)
 W !,"CERTIFICATION PROFILE BY SERVICE CATEGORY" S X="T" D ^%DT D:+Y D^DIQ W ?58,Y,?72,"PAGE: ",NURPAGE
 W !!?60,"CERT.",?68,"DATE CERT." W !,"NAME",?24,"CERTIFICATION",?60,"AGENCY",?68,"EXPIRES" W !,$$REPEAT^XLFSTR("-",80)
 I $G(NURPLSW),$L(NURPROG)>1 N Z S Z=$$PROD^NURSUT2(NURPROG) W !,?$$CNTR^NURSUT2(Z),$G(Z),!,?$$CNTR^NURSUT2(Z),$$REPEAT^XLFSTR("-",$L(Z)+1)
 Q
SORT W ! S NRPT=2 D EN1^NURAAGS0
 D NODATA^NURA9C1
 Q
