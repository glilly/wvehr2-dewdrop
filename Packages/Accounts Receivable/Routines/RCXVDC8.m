RCXVDC8 ;DAOU/ALA-AR Data Extraction Data Creation ;02-JUL-03
 ;;4.5;Accounts Receivable;**201,228**;Mar 20, 1995
 ;
 ; AR Batch Payment File (# 344) 
 Q
D344 ;
 N RCSITE
 S RCXVD=$G(^RCY(344,RCXVD0,0)) ;    
 S RCXVP1=$P(RCXVD,U,6) ; PTR TO DEPOSIT TICKET
 I RCXVP1="" Q
 S RCXVD1=$G(^RCY(344.1,RCXVP1,0))
 ;
 S RCXVDA=$P(RCXVD,U,1)
 S RCXVDA=RCXVDA_RCXVU_$P(RCXVD1,U,1) ; Ticket # (P)
 S RCXVDA=RCXVDA_RCXVU_$E($$HLDATE^HLFNC($P(RCXVD,U,8)),1,8) ; Date/Time Proc
 S RCXVDA=RCXVDA_RCXVU_$P(RCXVD1,U,4) ; (344.1) Amt Deposit
 S RCXVDA=RCXVDA_RCXVU_$$RECTOTAL^RCDPURED(RCXVP1) ; Totl Amt of Receipts (Comp)
 S RCXVDA=RCXVDA_RCXVU_$$RECCOUNT^RCDPURED(RCXVP1) ; Totl Receipts (Comp)
 S RCSITE=$P($$SITE^VASITE(),U,3)
 S RCXVDA=RCXVDA_U_RCSITE
 S ^TMP($J,"RCXVDC8",RCXVD0)=RCXVDA
 W "REC:"_$P(RCXVDA,RCXVU,1),!
 W "344:"_$P(RCXVDA,RCXVU,2,7),!
 ;
 S RCXVI=""
 F  S RCXVI=$O(^TMP($J,"7-162A",RCXVI)) Q:RCXVI=""  D
 . W "162:"_$G(^TMP($J,"7-162A",RCXVI))
 . W !
 Q
 ;
