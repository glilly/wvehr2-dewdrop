QAMC11 ;HISC/GJC-CONDITION: PREVIOUS DISCHARGE FOR A TREATING SPECIALTY ;8/4/92  08:22
 ;;1.0;Clinical Monitoring System;;09/13/1993
EN1 ;*** CONDITION CODE
 S QAMDIS=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P1"))#2:+^("P1"),1:0)
 S QAMTSP=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P2"))#2:+^("P2"),1:0)
 S QAMDATE=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P3"))#2:+^("P3"),1:0)
 S X1=QAMTODAY,X2=-QAMDIS D C^%DTC S QAMSTRT=X,QAMSTRT=QAMSTRT-.0000001 K ^UTILITY($J,"QAM TEMP")
 F QAMDISH=QAMSTRT:0 S QAMDISH=$O(^DGPM("AMV3",QAMDISH)) Q:(QAMDISH'>0)!(QAMDISH\1'?7N)!(QAMDISH>(QAMTODAY+.9999999))  F QAMDFN=0:0 S QAMDFN=$O(^DGPM("AMV3",QAMDISH,QAMDFN)) Q:QAMDFN'>0  D STR1
 F QAMDFN=0:0 S QAMDFN=$O(^UTILITY($J,"QAM TEMP",QAMDFN)) Q:QAMDFN'>0  F QAMDC=0:0 S QAMDC=$O(^UTILITY($J,"QAM TEMP",QAMDFN,QAMDC)) Q:QAMDC'>0  D STR2
 K DIR,DIRUT,QAMDATE,QAMDC,QAMDFN,QAMDIS,QAMDISH,QAMGJC,QAMIEN,QAMPARAM,QAMTSP,QAMTSP1,QAMSTRT,X,X1,X2,Y
 K ^UTILITY($J,"QAM TEMP")
 Q
STR1 ;
 F QAMGJC=0:0 S QAMGJC=$O(^DGPM("AMV3",QAMDISH,QAMDFN,QAMGJC)) Q:QAMGJC'>0  D
 . Q:$O(^DGPM("APTT1",QAMDFN,QAMDISH))'>0
 . K ^UTILITY($J,"QAM TEMP",QAMDFN)
 . S ^UTILITY($J,"QAM TEMP",QAMDFN,QAMDISH)=QAMGJC
 . Q
 Q
STR2 ;
 S QAMDC(1)=QAMDC
 S QAMDC=+$O(^DGPM("ATID6",QAMDFN,9999999.9999999-QAMDC))
 S QAMIEN=+$O(^DGPM("ATID6",QAMDFN,QAMDC,0)) Q:QAMIEN'>0
 I QAMTSP S QAMTSP1=+$S($D(^DGPM(QAMIEN,0))#2:$P(^(0),U,9),1:0) Q:$O(^QA(743.5,QAMTSP,"GRP","AB",QAMTSP1,0))'>0
 S ^UTILITY($J,"QAM CONDITION",QAMD1,QAMDFN)="",^UTILITY($J,"QAM CONDITION",QAMD1,QAMDFN,$S(QAMDATE:QAMDC(1),1:QAMTODAY))=^UTILITY($J,"QAM TEMP",QAMDFN,QAMDC(1))
 Q
EN2 ;*** LOOK BACK DATE
 K DIR,DIRUT S DIR(0)="NO^1:365:0",DIR("A")="LOOK BACK DAYS",DIR("B")=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P1"))#2:^("P1"),1:"") K:DIR("B")="" DIR("B")
 S DIR("?",1)="Enter the number of days the condition should 'look back'.",DIR("?")="While trying to find a fall out for this monitor."
 S QAMPARAM="P1" D EN3^QAMUTL1 S:$D(DIRUT) Y=-1 Q:Y<0
 S:Y]"" ^QA(743,QAMD0,"COND",QAMD1,"P1")=+Y
PRT2 K DIC,DIR,DIRUT S DIC="^QA(743.5,",DIC(0)="EMNQZ",DIC("S")="I $P(^(0),U,2)=45.7",DIC("A")="TREATING SPECIALTY GROUP: ",DIC("B")=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P2"))#2:$P(^("P2"),U,2),1:"") K:DIC("B")="" DIC("B")
 S DIR("?",1)="Press 'RETURN' for ALL treating specialties.",DIR("?")="Enter a GROUP name that contains a MAS treating specialty."
 S QAMPARAM="P2" D EN2^QAMUTL1 S:$D(DIRUT) Y=-1 Q:Y<0
 S:Y]"" ^QA(743,QAMD0,"COND",QAMD1,"P2")=+Y_"^"_Y(0,0)
PRT3 K DIR,DIRUT S DIR("A")="ENTER THE TYPE OF DATE",DIR(0)="SOB^0:AUTO ENROLL DATE;1:DISCHARGE DATE",DIR("?",1)="0   AUTO ENROLL DATE",DIR("?",2)="   1     DISCHARGE DATE"
 S DIR("?",3)="Enter the date you wish to be",DIR("?",4)="associated with this condition.",DIR("?")="Enter a 'RETURN' for the AUTO ENROLL DATE."
 S DIR("B")=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P3"))#2:$P(^("P3"),U,2),1:"") K:DIR("B")="" DIR("B")
 S QAMPARAM="P3" D EN3^QAMUTL1 S:$D(DIRUT) Y=-1 Q:Y<0
 S:Y]"" ^QA(743,QAMD0,"COND",QAMD1,"P3")=+Y_"^"_Y(0)
EXIT K Y
 K QAMPARAM
Y Q
