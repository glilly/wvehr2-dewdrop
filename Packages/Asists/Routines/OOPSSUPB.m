OOPSSUPB ;WIOFO/LLH-S/E Supervisor Edit for CA1 ;04/25/2000
 ;;2.0;ASISTS;;Jun 03, 2002
 ;;
 ; Note:  This routine used to be part of OOPSSUP2, but had to be split
 ; due to size limitations.  This routine collects the Supervisor
 ; portion of the CA1.
 ;
CA1 ; CA1 FORM
 N ITEM,DTINJ,DT110,VWIT
 S (AIEN,AGN,ADD,CITY,STATE,ZIP)=""
 S (PNAME,PADD,PCITY,PSTATE,PZIP,PTITLE,STAT,SIEN)=""
 S MAX1=528
 S DR=""
 W !!," Federal Employee's Notice of Traumatic Injury and Claim"
 W !," for Continuation of Pay/Compensation (Form CA-1)"
 W !!,"     Witness"
 W !,"     -------"
 ; Patch 8 - DOL project.  Added collection of multiple witnesses
 ; note: if Witness Statement entry ever allowed, max length = 528
 S DR(2,2260.0125)=".01:5"
 S DR(2,2260.0125,6)="6////SIGNED WITNESS STATEMENT TO FOLLOW."
 S DR(1,2260,1)="125"         ; call to witness multiple
 S DR(1,2260,2)="S VWIT=$$WIT^OOPSUTL3"
 S DR(1,2260,3)="I +VWIT=0!($P(VWIT,U,2)=0) S Y=125"
 S DR(1,2260,4)="W !!,""     Supervisor's Report"""
 S DR(1,2260,5)="W !,""     -------------------"""
 D AGNINFO^OOPSUTL2
 S DR(1,2260,10)="130 17. AGENCY NAME...............//^S X=AGN;I X="""" S Y=""@5"""
 S DR(1,2260,11)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=130"
 S DR(1,2260,15)="131     STREET ADDRESS............//^S X=ADD"
 S DR(1,2260,16)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=131"
 S DR(1,2260,20)="132     CITY......................//^S X=CITY"
 S DR(1,2260,21)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=132"
 S DR(1,2260,25)="133     STATE.....................//^S X=STATE"
 S DR(1,2260,30)="134     ZIP CODE..................//^S X=ZIP"
 S DR(1,2260,35)="S Y=176"
 S DR(1,2260,40)="@5"
 S DR(1,2260,45)="131////@"
 S DR(1,2260,50)="132////@"
 S DR(1,2260,55)="133////@"
 S DR(1,2260,60)="134////@"
 S DR(1,2260,65)="176 18. EMPLOYEE'S DUTY STATION..."
 S DR(1,2260,66)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=176"
 S DR(1,2260,70)="177     STREET ADDRESS............"
 S DR(1,2260,71)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=177"
 S DR(1,2260,75)="178     CITY......................"
 S DR(1,2260,76)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=178"
 S DR(1,2260,80)="179     STATE....................."
 S DR(1,2260,85)="180     ZIP CODE.................."
 S DR(1,2260,90)="60 19. EMPLOYEE'S RETIREMENT.....//^S X=RET;I X<3 S Y=""@6"""
 S DR(1,2260,95)="61     OTHER RETIREMENT..........."
 S DR(1,2260,96)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=61"
 S DR(1,2260,97)="S Y=""@7"""
 S DR(1,2260,98)="@6"
 S DR(1,2260,99)="61////@"
 S DR(1,2260,100)="@7"
 S DR(1,2260,105)="W !,"" 20. REGULAR WORK HOURS:"""
 S DR(1,2260,110)="138     FROM......................"
 S DR(1,2260,115)="139     TO........................"
 S DR(1,2260,120)="S ITEM=21 W !,"" 21. REGULAR WORK SCHEDULE.....: ""_$$GET1^DIQ(2260,D0,140,""E"")"
 S DR(1,2260,124)="K DIR S DIR(""A"")=""                                 "",DIR(""B"")=$$GET1^DIQ(2260,D0,140,""I"") D RWS^OOPSUTL2 S:$D(DIRUT) Y=""@2"""
 ;
 ; Default from 2162, require time entry but allow edit
 S DTINJ=$$GET1^DIQ(2260,IEN,4,"E")
 S DR(1,2260,125)="4 22. DATE/TIME INJURY OCCURRED.......//^S X=DTINJ"
 S DR(1,2260,130)="I $P(X,""."",2)="""" W !,""Time is REQUIRED in this response."" S Y=4"
 S DR(1,2260,135)="S DT110=$$GET1^DIQ(2260,IEN,110,""E"")"
 S DR(1,2260,140)="175 23. DATE OF NOTICE RECEIVED...//^S X=DT110"
 S DR(1,2260,145)="I X'="""",'$$FUT^OOPSUTL4($P(X,""."")) S Y=175"
 S DR(1,2260,150)="142 24. DATE/TIME STOPPED WORK...."
 S DR(1,2260,155)="I X'="""",'$$FUT^OOPSUTL4($P(X,""."")) S Y=142"
 S DR(1,2260,160)="143 25. DATE PAY STOPPED.........."
 S DR(1,2260,165)="I X'="""",'$$FUT^OOPSUTL4($P(X,""."")) S Y=143"
 S DR(1,2260,170)="144 26. DATE 45 DAY PERIOD BEGAN.."
 S DR(1,2260,175)="I X'="""",'$$FUT^OOPSUTL4($P(X,""."")) S Y=144"
 S DR(1,2260,180)="145 27. DATE/TIME RETURNED TO WORK"
 S DR(1,2260,185)="I X'="""",'$$FUT^OOPSUTL4($P(X,""."")) S Y=145"
 S DR(1,2260,190)="146 28. WAS EMPLOYEE INJURED IN PERFORMANCE OF DUTY;I X=""Y"" S Y=148;"
 S DR(1,2260,195)="147     EXPLANATION"
 S DR(1,2260,196)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=147"
 S DR(1,2260,200)="148 29. WAS INJURY CAUSED BY EMPLOYEE'S WILLFUL MISCONDUCT, INTOXICATION, OR            INTENT TO INJURE SELF OR ANOTHER;I X=""N"" S Y=150;"
 S DR(1,2260,205)="149     EXPLANATION"
 S DR(1,2260,206)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=149"
 S DR(1,2260,210)="150 30. WAS INJURY CAUSED BY 3RD PARTY;I X=""N"" S Y=""@1"""
 S DR(1,2260,215)="W !,"" 31. NAME AND ADDRESS OF THIRD PARTY:"""
 S DR(1,2260,220)="151     NAME OF THIRD PARTY.......;I X="""" S Y=""@1"""
 S DR(1,2260,221)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=151"
 S DR(1,2260,225)="152     STREET ADDRESS............"
 S DR(1,2260,226)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=152"
 S DR(1,2260,230)="153     CITY......................"
 S DR(1,2260,231)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=153"
 S DR(1,2260,235)="154     STATE....................."
 S DR(1,2260,236)="155     ZIP CODE.................."
 S DR(1,2260,237)="S Y=""@3"""
 S DR(1,2260,238)="@1"
 S DR(1,2260,239)="151////@"
 S DR(1,2260,240)="152////@"
 S DR(1,2260,241)="153////@"
 S DR(1,2260,242)="154////@"
 S DR(1,2260,243)="155////@"
 S DR(1,2260,244)="@3"
 S DR(1,2260,250)="W !,"" 32. NAME AND ADDRESS OF PHYSICIAN FIRST PROVIDING MEDICAL CARE:"""
 ; Patch 7 - default physician info, if name deleted, del other fields
 D PHINFO^OOPSUTL2
 S DR(1,2260,255)="156     NAME OF PHYSICIAN.........//^S X=PNAME;I X="""" S Y=""@4"""
 S DR(1,2260,256)="I X'="""",'$$NMCHK^OOPSUTL3(X) D NMERR^OOPSUTL3 K X S Y=156"
 S DR(1,2260,260)="157     STREET ADDRESS............//^S X=PADD"
 S DR(1,2260,261)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=157"
 S DR(1,2260,265)="158     CITY......................//^S X=PCITY"
 S DR(1,2260,266)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=158"
 S DR(1,2260,270)="159     STATE.....................//^S X=PSTATE"
 S DR(1,2260,275)="160     ZIP CODE..................//^S X=PZIP"
 S DR(1,2260,280)="182     TITLE.....................//^S X=PTITLE"
 S DR(1,2260,285)="S Y=161"
 S DR(1,2260,290)="@4"
 S DR(1,2260,295)="157////@"
 S DR(1,2260,300)="158////@"
 S DR(1,2260,305)="159////@"
 S DR(1,2260,310)="160////@"
 S DR(1,2260,315)="182////@"
 S DR(1,2260,320)="161 33. 1ST DATE MEDICAL CARE RECEIVED"
 S DR(1,2260,325)="I X'="""",'$$FUT^OOPSUTL4(X) S Y=161"
 S DR(1,2260,330)="162 34. DO MEDICAL REPORTS SHOW EMPLOYEE IS DISABLED FOR WORK"
 S DR(1,2260,335)="163 35. DOES YOUR KNOWLEDGE OF THE FACTS AGREE WITH STATEMENTS OF THE EMPLOYEE;I X=""Y"" S Y=165"
 S DR(1,2260,340)="164     EXPLANATION~"
 S DR(1,2260,345)="W !"
 S DR(1,2260,350)="S MAX=$$WP^OOPSUTL4(164)"
 S DR(1,2260,351)="I '$P(MAX,U,2) D VCHAR^OOPSUTL2 S Y=164"
 S DR(1,2260,355)="I +MAX>MAX1 W !!,""Max length for field is "",MAX1,"" characters, you have entered "",+MAX,"".  Please Edit."",! S Y=164"
 S DR(1,2260,360)="165 36. IF THE EMPLOYING AGENCY CONTROVERTS CONTINUATION OF PAY, STATE THE REASON       IN DETAIL~"
 S DR(1,2260,365)="S MAX=$$WP^OOPSUTL4(165)"
 S DR(1,2260,366)="I '$P(MAX,U,2) D VCHAR^OOPSUTL2 S Y=165"
 S DR(1,2260,370)="I +MAX>MAX1 W !!,""Max length for field is "",MAX1,"" characters, you have entered "",+MAX,"".  Please Edit."",! S Y=165"
 S DR(1,2260,375)="W !!,"" 37. PAY RATE WHEN EMPLOYEE STOPPED WORK:"""
 S DR(1,2260,380)="166     $.........................//^S X=SAL"
 S DR(1,2260,385)="167     PER.......................//^S X=PAY"
 S DR(1,2260,390)="W !!,""     Signature of Supervisor and Filing Instructions"""
 S DR(1,2260,395)="W !,""     -----------------------------------------------"""
 S DR(1,2260,400)="S ITEM=38 D EXCEPT^OOPSUTL2;168     EXCEPTION"
 S DR(1,2260,401)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=168"
 S DR(1,2260,405)="W !,""     NAME OF SUPERVISOR:  ""_SUP"
 S DR(1,2260,410)="172     SUPERVISOR'S TITLE."
 S DR(1,2260,411)="I X'="""",'$$VCHAR^OOPSUTL4(X) D VCHAR^OOPSUTL2 S Y=172"
 S DR(1,2260,415)="173     OFFICE PHONE......."
 ; Patch 8 - add error checking for DOL requirements
 S DR(1,2260,416)="I $TR(X,""/-*#"","""")'?10N W !?3,""Phone number must include area code and 7 digits only.  Example 703-123-8789"" S Y=173"
 S DR(1,2260,420)="174 39. FILING INSTRUCTIONS"
 S DR(1,2260,425)="@2"
 Q
