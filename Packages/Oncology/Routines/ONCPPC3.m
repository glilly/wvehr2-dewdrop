ONCPPC3 ;HIRMFO/GWB - PCE Studies of Prostate Cancer - Table III;7/22/96
 ;;2.11;ONCOLOGY;**6**;Mar 07, 1995
 K TABLE,HTABLE
 S TABLE("AJCC CLINICAL STAGE (cTNM)")="ACS"
 S TABLE("CLINICAL STAGED BY")="CSB"
 S TABLE("AJCC PATHOLOGIC STAGE (pTNM)")="APS"
 S TABLE("PATHOLOGIC STAGED BY")="PSB"
 S HTABLE(1)="AJCC CLINICAL STAGE (cTNM)"
 S HTABLE(2)="CLINICAL STAGED BY"
 S HTABLE(3)="AJCC PATHOLOGIC STAGE (pTNM)"
 S HTABLE(4)="PATHOLOGIC STAGED BY"
 S CHOICES=4
 S DIE="^ONCO(165.5,",DA=ONCONUM
 W @IOF D HEAD^ONCPPC0 W !?26,"TABLE III - STAGE OF DISEASE",!
ACS W !,"AJCC CLINICAL STAGE (cTNM):",!
CTCODE S DR="37.1  T-CODE............" D ^DIE G:$D(Y) JUMP
 D CN1^ONCOTN,CN2^ONCOTN
CNCODE S DR="37.2  N-CODE............" D ^DIE G:$D(Y) JUMP
CMCODE S DR="37.3  M-CODE............" D ^DIE G:$D(Y) JUMP
 I '$D(SKAJCC) D CN1^ONCOTN
 S STGIND="C" D ES^ONCOTN
CSTAGE ;W ! S DR="38  AJCC STAGE....." D ^DIE G:$D(Y) JUMP
CSB S DR="19  CLINICAL STAGED BY.." D ^DIE G:$D(Y) JUMP
APS W !!,"AJCC PATHOLOGIC STAGE (pTNM):",!
PTCODE S DR="85  T-CODE.............." D ^DIE G:$D(Y) JUMP
 D CN3^ONCOTN,CN4^ONCOTN
PNCODE S DR="86  N-CODE.............." D ^DIE G:$D(Y) JUMP
PMCODE S DR="87  M-CODE.............." D ^DIE G:$D(Y) JUMP
 I '$D(SKAJCC) D CN3^ONCOTN
 S STGIND="P" D ES^ONCOTN
PSTAGE ;W ! S DR="88  AJCC STAGE....." D ^DIE G:$D(Y) JUMP
PSB S DR="89  PATHOLOGIC STAGED BY" D ^DIE G:$D(Y) JUMP
 W ! K DIR S DIR(0)="E" D ^DIR
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!,"GO TO: ",X:DTIME I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K CHOICES,HTABLE,TABLE
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
