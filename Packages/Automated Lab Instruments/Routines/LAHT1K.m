LAHT1K ;SLC/DLG - HITACHI 736 WITH JT 1000  ;7/20/90  09:14 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID=ACCESSION OR IDE=MACHINE SEQUENCE OR TRAY/CUP
 ;OPERATE WITH PRINTABLE OPTIONAL CODES AND FRAME FORMAT MODE 2
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 K TV,Y S TOUT=0,A=1 D IN G QUIT:TOUT,LA2:"["'[$E(IN,1),LA2:"NE"'[$E(IN,12) D QC
 I $E(IN,2)'="E" S TOUT=0,BAD=0 F A=2:1 D IN,QC Q:$E(IN,2)="E"  G QUIT:TOUT
 S V=$E(Y(1),3,11) D NUM S ID=+V,V=$E(Y(1),13,16) D NUM S IDE=+V,V=$E(Y(1),17,19) D NUM S TRAY=+V,CUP=+$E(Y(1),20)
 F J=1:1:A S V=$E(Y(J),43,44) D NUM S NTEST=+V,I1=45 F I=1:1:NTEST S V1=$E(Y(J),I1,(I1+9)),I1=I1+10,V=$E(V1,1,2) D NUM S TEST=+V,V=$E(V1,4,10) D NUM I V]"" S:$D(TC(TEST,1))#2 @TC(TEST,1)=+V
LA3 X LAGEN G LA2:'ISQN ;Can be changed by the cross-link code
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S Y(A)=IN Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
OUT S CNT=^LA(TSK,"O")+1,^("O")=CNT,^("O",CNT)=OUT
 LOCK ^LA("Q") S Q=^LA("Q")+1,^("Q")=Q,^("Q",Q)=TSK LOCK
 Q
QUIT G:^LA(TSK,"I")>^("I",0) LA2 I $D(^LA(TSK,"O",0)),^LA(TSK,"O")>^("O",0) H 60 G QUIT
 LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
