MCESCON3 ;WISC/DCB-CONVERT RELEASE CODES TO NEW CODES ;3/9/93
 ;;2.3;Medicine;;09/13/1996
START ; This looks like a good place to start
 S MCARGDA=.9,MCFILE=691.5
 F  D LOOP Q:MCARGDA=0
 K MCARGDA,TEMP,CODE,NEWCOE
 Q
LOOP ; loop at the ES node and check to see if its vailite
 S MCARGDA=+$O(^MCAR(MCFILE,MCARGDA)) Q:MCARGDA=0
 S TEMP=$G(^MCAR(MCFILE,MCARGDA,"ES")) Q:TEMP=""
 S CODE=+$P(TEMP,U,7) ; get the code
 D:CODE'=0 CHANGE(CODE,MCARGDA)
 Q
CHANGE(CODE,DA) ;
 N NEWCODE,DIE,DR
 S NEWCODE=$S(CODE=2:"PD",CODE=3:"RV",CODE=4:"ROV",CODE=5:"RNV",CODE=6:"S",1:"D")
 S DIE="^MCAR("_MCFILE_",",DR="1506////^S X=NEWCODE" D ^DIE
 Q
