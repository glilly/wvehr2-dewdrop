SRORATA ;B'HAM ISC/MAM - CANCEL RATES ; [ 02/12/97  12:16 PM ]
 ;;3.0; Surgery ;**63**;24 Jun 93
 I 'SROTOT D PAGE I SRSOUT Q
 I SROTOT D HDR Q:SRSOUT
 S X=^TMP("SR",$J),Y=$P(X,"^"),TOTAL=Y+$P(X,"^",3),X=$P(X,"^",2),CANRATE=$S(Y>0:X/Y*100,1:0),CANRATE=CANRATE+.5\1*1
 S TOTRATE=$S(TOTAL>0:X/TOTAL*100,1:0),TOTRATE=TOTRATE+.5\1*1,TCAN=$S(TOTAL>0:Y/TOTAL*100,1:0),TCAN=TCAN+.5\1*1
 W !!,"TOTAL SURGICAL CASES SCHEDULED FOR ",SRINST,": ",TOTAL
 W !,"CANCELLATION RATE FOR SCHEDULED CASES: ",TCAN," %"
 W !,"AVOIDABLE CANCELLATION RATE FOR SCHEDULED CASES: ",TOTRATE," %"
 W !,"AVOIDABLE CANCELLATION RATE FOR CANCELLED CASES: ",CANRATE," %"
 D SUB S REASON=0 F  S REASON=$O(^TMP("SR",$J,REASON)) Q:REASON=""!(SRSOUT)  D REASON
 S X=$P(^TMP("SR",$J),"^"),Y=$P(^($J),"^",2)
 W !,?47,"-----",?64,"-----",!,"TOTAL CANCELLATIONS",?47,$J(X,4),?64,$J(Y,4)
 I $D(^TMP("SRT",$J)) D ^SRORATS
 Q
REASON I $Y+4>IOSL D PAGE Q:SRSOUT  D SUB
 S X=$P(^TMP("SR",$J,REASON),"^"),Y=$P(^(REASON),"^",2)
 W !,$S($E(REASON,1,2)="ZZ":$E(REASON,3,45),1:REASON),?47,$J(X,4),?64,$J(Y,4)
 Q
PAGE I 'SRHDR S SRHDR=1 G HDR
 S X="" I $E(IOST)'="P" W !!,"Press RETURN to continue, or '^' to quit:  " R X:DTIME I '$T!(X["^") S SRSOUT=1 Q
 I X["?" W !!,"Enter RETURN to continue printing this report, or '^' to exit from this option." G PAGE
HDR ; print heading
 I $D(ZTQUEUED) D ^SROSTOP I SRHALT S SRSOUT=1 Q
 S X=$E(SRSD,4,5)_"/"_$E(SRSD,6,7)_"/"_$E(SRSD,2,3),Y=$E(SRED,4,5)_"/"_$E(SRED,6,7)_"/"_$E(SRED,2,3),PAGE=PAGE+1 I $Y W @IOF
 I $E(IOST)'="P" Q
 W !,?(80-$L(SRINST)\2),SRINST,?70,"PAGE: "_PAGE,!,?32,"SURGICAL SERVICE",!,?28,"CANCELLATION RATE REPORT",!,?27,"FROM "_X_"  TO "_Y
 S X=$E(DT,4,5)_"/"_$E(DT,6,7)_"/"_$E(DT,2,3) W !,?29,"DATE PRINTED: "_X
 W !,?21,"REVIEWED BY:",?45,"DATE REVIEWED:"
 W ! F LINE=1:1:80 W "="
 Q
SUB ; print column headings
 W !!,"CANCELLATION REASON",?45,"TOTAL CANCELS",?62,"TOTAL AVOIDABLE",! F LINE=1:1:80 W "-"
 Q
