GMRGPOUT ;HIRMFO/JH,RM-TEXT GENERATOR CARE PLAN DATA OUTPUT ;9/1/95
 ;;3.0;Text Generator;;Jan 24, 1996
 ;
EN1 D NOW^%DTC S Y=% D D^DIQ S GMRGDAT=$P(Y,":",1,2),GMRG1SW=1,GMRGSP=0
 S (GMRGA,GMRGPAG)=0 F GMRGD=0:0 S GMRGA=$O(^TMP($J,"GMRGNAR",GMRGA)) Q:GMRGA'>0  D GMRIEN
 I $E(IOST)="C"&'GMRGOUT W !!,"Press return to continue " R X:DTIME I X="^^"!'$T S GMRGOUT=2
 S:GMRGOUT GMRGOUT=$S(GMRGOUT=1:0,1:1) K GMRG1SW,GMRGA,GMRGB,GMRGD,GMRGE,GMRGF,GMRGDAT,GMRGPAG,GMRGSP
 Q
GMRIEN S GMRGF=0 D HEADER F GMRGE=0:0 S GMRGF=$O(^TMP($J,"GMRGNAR",GMRGA,GMRGF)) Q:GMRGF'>0  D PRINT Q:GMRGOUT
 Q
PRINT W ! S GMRGSP=1,GMRGC=0 F GMRGB=1:1 S GMRGC=$O(^TMP($J,"GMRGNAR",GMRGA,GMRGF,GMRGC)) Q:GMRGC'>0  W !,^(GMRGC) D:$Y>(IOSL-3) HEADER Q:GMRGOUT
 Q
HEADER ;
 I 'GMRG1SW,$E(IOST)="C" W !!,"Press return to continue, '^' to stop print " R X:DTIME S:X="^"!(X="^^")!'$T GMRGOUT=$S(X="^":1,1:2) Q:GMRGOUT
 S:GMRG1SW GMRG1SW=0 S GMRGPAG=GMRGPAG+1
 W:GMRGPAG>1 @IOF W !,$P(VADM(1),"^"),"   ",$P(VADM(2),"^",2),?50,$P(VADM(3),"^",2)," (",$P(VADM(4),"^"),")   PAGE: ",GMRGPAG,!,GMRGDAT,?(IOM-$L($P(GMRGRT,"^",2))\2),$P(GMRGRT,"^",2),!,GMRGLIN("-"),! I GMRGSP="1" W !
 Q
