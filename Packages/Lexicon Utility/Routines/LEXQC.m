LEXQC ;ISL/KER - Query - Code Set (CSV) - Extract ;10/30/2008
 ;;2.0;LEXICON UTILITY;**62**;Sep 23, 1996;Build 16
 ;               
 ; Global Variables
 ;    ^DIC(81.3,          ICR   4492
 ;    ^ICD0(              ICR   4485
 ;    ^ICD9(              ICR   4485
 ;    ^ICPT(              ICR   4489
 ;    ^TMP("LEXQC")       SACC 2.3.2.5.1
 ;    ^TMP("LEXQCO")      SACC 2.3.2.5.1
 ;               
 ; External References
 ;    HOME^%ZIS           ICR  10086
 ;    $$FMADD^XLFDT       ICR  10103
 ;               
EN ; Main Entry Point
 N LEXENV S LEXENV=$$EV^LEXQM Q:+LEXENV'>0  K ^TMP("LEXQCO",$J),^TMP("LEXQC",$J) N LEXCDT,LEXEDT,LEXEXIT S LEXCDT=$$CSD^LEXQM Q:'$L(LEXCDT)
 Q:+($G(LEXEXIT))>0  S LEXEDT=$P(LEXCDT,"^",1),LEXCDT=$P(LEXCDT,"^",2) Q:LEXCDT'?7N  D TASK
 Q
TASK ; Task Re-indexing
 N X,Y,ZTSK,ZTDESC,ZTDTH,ZTIO,ZTRTN,ZTSAVE,ZTQUEUED,ZTREQ S LEXCDT=+($G(LEXCDT)) Q:LEXCDT'?7N  S LEXEDT=$G(LEXEDT) Q:'$L(LEXEDT)
 S ZTRTN="SEARCH^LEXQC",ZTDESC="Search for CSV Changes on "_LEXEDT,ZTSAVE("LEXCDT")="",ZTIO="",ZTDTH=$H
 D @ZTRTN D HOME^%ZIS S X=+($G(ZTSK)) W:+X>0 !!,"   A search for CSV changes on ",LEXEDT," has been queued (task ",+X,")",!
 K Y,ZTSK,ZTDESC,ZTDTH,ZTIO,ZTRTN
 Q
SEARCH ; Search for CSV changes
 S:$D(ZTQUEUED) ZTREQ="@"
 S LEXCDT=$G(LEXCDT) Q:LEXCDT'?7N  N LEXBDT,LEXADT,LEXQTOT,LEXQLEN,LEXQSTR S LEXBDT=$$FMADD^XLFDT(LEXCDT,-1),LEXADT=$$FMADD^XLFDT(DT,+1) Q:LEXBDT'?7N  Q:LEXADT'?7N
 K ^TMP("LEXQC",$J) S LEXQTOT=$P($G(^ICPT(0)),"^",4)+$P($G(^ICD9(0)),"^",4)+$P($G(^ICD0(0)),"^",4)+$P($G(^DIC(81.3,0)),"^",4)
 S LEXQLEN=68,LEXQSTR=+(LEXQTOT\LEXQLEN) S:LEXQSTR=0 LEXQSTR=1
 W !!,"   " D ICD,ICP,CPT,MOD
 D EN^LEXQC2 D:$D(^TMP("LEXQCO",$J)) DSP^LEXQO("LEXQCO")
 Q
ICD ; ICD Changes
 K ^TMP("LEXQC",$J,"ICD") N LEX1,LEX2,LEX3,LEX4,LEXAEF,LEXAST,LEXBEF,LEXBST,LEXCEF,LEXCNT,LEXCST,LEXH,LEXIEN,LEXLC,LEXND,LEXPEF,LEXPST,LEXQL,LEXSO,LEXSTID
 S LEXQLEN=+($G(LEXQLEN)),LEXQTOT=+($G(LEXQTOT)),LEXQSTR=+($G(LEXQSTR)),LEXCNT=0,LEXLC=0
 S LEXIEN=0 F  S LEXIEN=$O(^ICD9(LEXIEN)) Q:+LEXIEN'>0  D
 . N LEX1,LEX2,LEX3,LEX4,LEXAEF,LEXAST,LEXBEF,LEXBST,LEXCEF,LEXCST,LEXH,LEXND,LEXPEF,LEXPST,LEXQL,LEXSO,LEXSTID S LEXCNT=LEXCNT+1
 . I LEXCNT'<+($G(LEXQSTR)) S LEXLC=+($G(LEXLC))+1 W:LEXLC'>+($G(LEXQLEN)) "." S LEXCNT=0
 . S LEXSO=$P($G(^ICD9(LEXIEN,0)),"^",1) Q:'$L(LEXSO)
 . S LEXH=$O(^ICD9(+LEXIEN,66,"B",LEXCDT," "),-1),LEXND=$G(^ICD9(+LEXIEN,66,+LEXH,0)),LEXCEF=$P(LEXND,"^",1),LEXCST=$P(LEXND,"^",2)
 . S LEXH=$O(^ICD9(+LEXIEN,66,"B",LEXBDT," "),-1),LEXND=$G(^ICD9(+LEXIEN,66,+LEXH,0)),LEXBEF=$P(LEXND,"^",1),LEXBST=$P(LEXND,"^",2)
 . S LEXH=$O(^ICD9(+LEXIEN,66,"B",LEXADT," "),-1),LEXND=$G(^ICD9(+LEXIEN,66,+LEXH,0)),LEXAEF=$P(LEXND,"^",1),LEXAST=$P(LEXND,"^",2)
 . S LEXH=$O(^ICD9(+LEXIEN,66,"B",LEXCDT),-1),LEXH=$O(^ICD9(+LEXIEN,66,"B",+LEXH," "),-1),LEXND=$G(^ICD9(+LEXIEN,66,+LEXH,0)),LEXPEF=$P(LEXND,"^",1),LEXPST=$P(LEXND,"^",2)
 . S LEX1=$D(^ICD9(+LEXIEN,67,"B",LEXCDT))>0,LEX2=$D(^ICD9(+LEXIEN,68,"B",LEXCDT))>0
 . S LEX3=$O(^ICD9(+LEXIEN,67,"B",LEXCDT),-1),LEX4=$O(^ICD9(+LEXIEN,68,"B",LEXCDT),-1)
 . S LEXQL=0 I $L(LEXCST),$L(LEXCEF) D
 . . Q:$L(LEXBEF)&($L(LEXBST))&(LEXBST'=LEXCST)  Q:$L(LEXAEF)&($L(LEXAST))&(LEXAST'=LEXCST)  Q:(LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))
 . . N LEXSTID Q:'$L($G(LEXSO))  S LEXSTID=$S(+LEXCST>0:"ACT",1:"INA")
 . . I LEXSTID="ACT",$G(LEXPEF)?7N,+($G(LEXPST))'>0,'$L(LEX3),'$L(LEX4) S LEXSTID="REA"
 . . S LEXQL=1,^TMP("LEXQC",$J,"ICD",LEXSTID,1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,"ICD",LEXSTID,0)=+($G(^TMP("LEXQC",$J,"ICD",LEXSTID,0)))+1
 . I 'LEXQL I +LEXPST>0,((LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))) D
 . . Q:'$L($G(LEXSO))  S LEXQL=1 S ^TMP("LEXQC",$J,"ICD","REV",1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,"ICD","REV",0)=+($G(^TMP("LEXQC",$J,"ICD","REV",0)))+1
 . I 'LEXQL S LEXQL=0 I ((LEXCEF?7N&(LEXCST>0))!(LEXPEF?7N&(LEXPST>0))),((LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))) D
 . . S LEXQL=1 S ^TMP("LEXQC",$J,"ICD","REU",1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,"ICD","REU",0)=+($G(^TMP("LEXQC",$J,"ICD","REU",0)))+1
 . I 'LEXQL I $D(^ICD9(+($G(LEXIEN)),69,"B",+($G(LEXCDT)))) D
 . . S LEXQL=1 S ^TMP("LEXQC",$J,"ICC","UPD",1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,"ICC","UPD",0)=+($G(^TMP("LEXQC",$J,"ICC","UPD",0)))+1
 Q
ICP ;
 K ^TMP("LEXQC",$J,"ICP") N LEX1,LEX2,LEX3,LEX4,LEXAEF,LEXAST,LEXBEF,LEXBST,LEXCEF,LEXCNT,LEXCST,LEXH,LEXIEN,LEXLC,LEXND,LEXPEF,LEXPST,LEXQL,LEXSO,LEXSTID
 S LEXQLEN=+($G(LEXQLEN)),LEXQTOT=+($G(LEXQTOT)),LEXQSTR=+($G(LEXQSTR)),LEXCNT=0,LEXLC=0
 S LEXIEN=0 F  S LEXIEN=$O(^ICD0(LEXIEN)) Q:+LEXIEN'>0  D
 . N LEX1,LEX2,LEX3,LEX4,LEXAEF,LEXAST,LEXBEF,LEXBST,LEXCEF,LEXCST,LEXH,LEXND,LEXPEF,LEXPST,LEXQL,LEXSO,LEXSTID S LEXCNT=LEXCNT+1
 . I LEXCNT'<+($G(LEXQSTR)) S LEXLC=+($G(LEXLC))+1 W:LEXLC'>+($G(LEXQLEN)) "." S LEXCNT=0
 . S LEXSO=$P($G(^ICD0(LEXIEN,0)),"^",1) Q:'$L(LEXSO)
 . S LEXH=$O(^ICD0(+LEXIEN,66,"B",LEXCDT," "),-1),LEXND=$G(^ICD0(+LEXIEN,66,+LEXH,0)),LEXCEF=$P(LEXND,"^",1),LEXCST=$P(LEXND,"^",2)
 . S LEXH=$O(^ICD0(+LEXIEN,66,"B",LEXBDT," "),-1),LEXND=$G(^ICD0(+LEXIEN,66,+LEXH,0)),LEXBEF=$P(LEXND,"^",1),LEXBST=$P(LEXND,"^",2)
 . S LEXH=$O(^ICD0(+LEXIEN,66,"B",LEXADT," "),-1),LEXND=$G(^ICD0(+LEXIEN,66,+LEXH,0)),LEXAEF=$P(LEXND,"^",1),LEXAST=$P(LEXND,"^",2)
 . S LEXH=$O(^ICD0(+LEXIEN,66,"B",LEXCDT),-1),LEXH=$O(^ICD0(+LEXIEN,66,"B",+LEXH," "),-1),LEXND=$G(^ICD0(+LEXIEN,66,+LEXH,0)),LEXPEF=$P(LEXND,"^",1),LEXPST=$P(LEXND,"^",2)
 . S LEX1=$D(^ICD0(+LEXIEN,67,"B",LEXCDT))>0,LEX2=$D(^ICD0(+LEXIEN,68,"B",LEXCDT))>0
 . S LEX3=$O(^ICD0(+LEXIEN,67,"B",LEXCDT),-1),LEX4=$O(^ICD0(+LEXIEN,68,"B",LEXCDT),-1)
 . S LEXQL=0 I $L(LEXCST),$L(LEXCEF) D  Q:LEXQL
 . . Q:$L(LEXBEF)&($L(LEXBST))&(LEXBST'=LEXCST)  Q:$L(LEXAEF)&($L(LEXAST))&(LEXAST'=LEXCST)    Q:(LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))
 . . N LEXSTID Q:'$L($G(LEXSO))  S LEXSTID=$S(+LEXCST>0:"ACT",1:"INA")
 . . I LEXSTID="ACT",$G(LEXPEF)?7N,+($G(LEXPST))'>0,'$L(LEX3),'$L(LEX4) S LEXSTID="REA"
 . . S LEXQL=1,^TMP("LEXQC",$J,"ICP",LEXSTID,1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,"ICP",LEXSTID,0)=+($G(^TMP("LEXQC",$J,"ICP",LEXSTID,0)))+1
 . S LEXQL=0 I +LEXPST>0,((LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))) D  Q:LEXQL
 . . Q:'$L($G(LEXSO))
 . . S LEXQL=1 S ^TMP("LEXQC",$J,"ICP","REV",1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,"ICP","REV",0)=+($G(^TMP("LEXQC",$J,"ICP","REV",0)))+1
 . S LEXQL=0 I ((LEXCEF?7N&(LEXCST>0))!(LEXPEF?7N&(LEXPST>0))),((LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))) D
 . . S LEXQL=1 S ^TMP("LEXQC",$J,"ICP","REU",1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,"ICP","REU",0)=+($G(^TMP("LEXQC",$J,"ICP","REU",0)))+1
 Q
CPT ;
 K ^TMP("LEXQC",$J,"CPT"),^TMP("LEXQC",$J,"CPC") N LEX1,LEX2,LEX3,LEX4,LEXAEF,LEXAST,LEXBEF,LEXBST,LEXCEF,LEXCNT,LEXCST,LEXH,LEXIEN,LEXLC,LEXND,LEXPEF,LEXPST,LEXQL,LEXSO,LEXSTID
 S LEXQLEN=+($G(LEXQLEN)),LEXQTOT=+($G(LEXQTOT)),LEXQSTR=+($G(LEXQSTR)),LEXCNT=0,LEXLC=0
 S LEXIEN=0 F  S LEXIEN=$O(^ICPT(LEXIEN)) Q:+LEXIEN'>0  D
 . N LEX1,LEX2,LEX3,LEX4,LEXAEF,LEXAST,LEXBEF,LEXBST,LEXCEF,LEXCST,LEXH,LEXND,LEXPEF,LEXPST,LEXQL,LEXSO,LEXSTID,LEXSID S LEXCNT=LEXCNT+1
 . I LEXCNT'<+($G(LEXQSTR)) S LEXLC=+($G(LEXLC))+1 W:LEXLC'>+($G(LEXQLEN)) "." S LEXCNT=0
 . S LEXSID="CPT",LEXSO=$P($G(^ICPT(LEXIEN,0)),"^",1) Q:'$L(LEXSO)  S:$E(LEXSO,1)?1U LEXSID="CPC"
 . S LEXH=$O(^ICPT(+LEXIEN,60,"B",LEXCDT," "),-1),LEXND=$G(^ICPT(+LEXIEN,60,+LEXH,0)),LEXCEF=$P(LEXND,"^",1),LEXCST=$P(LEXND,"^",2)
 . S LEXH=$O(^ICPT(+LEXIEN,60,"B",LEXBDT," "),-1),LEXND=$G(^ICPT(+LEXIEN,60,+LEXH,0)),LEXBEF=$P(LEXND,"^",1),LEXBST=$P(LEXND,"^",2)
 . S LEXH=$O(^ICPT(+LEXIEN,60,"B",LEXADT," "),-1),LEXND=$G(^ICPT(+LEXIEN,60,+LEXH,0)),LEXAEF=$P(LEXND,"^",1),LEXAST=$P(LEXND,"^",2)
 . S LEXH=$O(^ICPT(+LEXIEN,60,"B",LEXCDT),-1),LEXH=$O(^ICPT(+LEXIEN,60,"B",+LEXH," "),-1),LEXND=$G(^ICPT(+LEXIEN,60,+LEXH,0)),LEXPEF=$P(LEXND,"^",1),LEXPST=$P(LEXND,"^",2)
 . S LEX1=$D(^ICPT(+LEXIEN,61,"B",LEXCDT))>0,LEX2=$D(^ICPT(+LEXIEN,62,"B",LEXCDT))>0
 . S LEX3=$O(^ICPT(+LEXIEN,61,"B",LEXCDT),-1),LEX4=$O(^ICPT(+LEXIEN,62,"B",LEXCDT),-1)
 . S LEXQL=0 I $L(LEXCST),$L(LEXCEF) D  Q:LEXQL
 . . Q:$L(LEXBEF)&($L(LEXBST))&(LEXBST'=LEXCST)  Q:$L(LEXAEF)&($L(LEXAST))&(LEXAST'=LEXCST)    Q:(LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))
 . . N LEXSTID Q:'$L($G(LEXSO))  S LEXSTID=$S(+LEXCST>0:"ACT",1:"INA")
 . . I LEXSTID="ACT",$G(LEXPEF)?7N,+($G(LEXPST))'>0,'$L(LEX3),'$L(LEX4) S LEXSTID="REA"
 . . S LEXQL=1,^TMP("LEXQC",$J,LEXSID,LEXSTID,1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,LEXSID,LEXSTID,0)=+($G(^TMP("LEXQC",$J,LEXSID,LEXSTID,0)))+1
 . S LEXQL=0 I +LEXPST>0,((LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))) D  Q:LEXQL
 . . Q:'$L($G(LEXSO))
 . . S LEXQL=1 S ^TMP("LEXQC",$J,LEXSID,"REV",1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,LEXSID,"REV",0)=+($G(^TMP("LEXQC",$J,LEXSID,"REV",0)))+1
 . S LEXQL=0 I ((LEXCEF?7N&(LEXCST>0))!(LEXPEF?7N&(LEXPST>0))),((LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))) D
 . . S LEXQL=1 S ^TMP("LEXQC",$J,LEXSID,"REU",1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,LEXSID,"REU",0)=+($G(^TMP("LEXQC",$J,LEXSID,"REU",0)))+1
 Q
MOD ;
 K ^TMP("LEXQC",$J,"MOD") N LEX1,LEX2,LEX3,LEX4,LEXAEF,LEXAST,LEXBEF,LEXBST,LEXCEF,LEXCNT,LEXCST,LEXH,LEXIEN,LEXLC,LEXND,LEXPEF,LEXPST,LEXQL,LEXSO,LEXSTID
 S LEXQLEN=+($G(LEXQLEN)),LEXQTOT=+($G(LEXQTOT)),LEXQSTR=+($G(LEXQSTR)),LEXCNT=0,LEXLC=0
 S LEXIEN=0 F  S LEXIEN=$O(^DIC(81.3,LEXIEN)) Q:+LEXIEN'>0  D
 . N LEX1,LEX2,LEX3,LEX4,LEXAEF,LEXAF,LEXAST,LEXACT,LEXBEF,LEXBST,LEXCEF,LEXCST,LEXH,LEXIF,LEXINA,LEXND,LEXPEF,LEXPST,LEXQL,LEXRI,LEXSO,LEXSTID,LEXSID S LEXCNT=LEXCNT+1
 . I LEXCNT'<+($G(LEXQSTR)) S LEXLC=+($G(LEXLC))+1 W:LEXLC'>+($G(LEXQLEN)) "." S LEXCNT=0
 . S LEXSID="MOD" S LEXSO=$P($G(^DIC(81.3,LEXIEN,0)),"^",1) Q:'$L(LEXSO)
 . S LEXH=$O(^DIC(81.3,+LEXIEN,60,"B",LEXCDT," "),-1),LEXND=$G(^DIC(81.3,+LEXIEN,60,+LEXH,0)),LEXCEF=$P(LEXND,"^",1),LEXCST=$P(LEXND,"^",2)
 . S LEXH=$O(^DIC(81.3,+LEXIEN,60,"B",LEXBDT," "),-1),LEXND=$G(^DIC(81.3,+LEXIEN,60,+LEXH,0)),LEXBEF=$P(LEXND,"^",1),LEXBST=$P(LEXND,"^",2)
 . S LEXH=$O(^DIC(81.3,+LEXIEN,60,"B",LEXADT," "),-1),LEXND=$G(^DIC(81.3,+LEXIEN,60,+LEXH,0)),LEXAEF=$P(LEXND,"^",1),LEXAST=$P(LEXND,"^",2)
 . S LEXH=$O(^DIC(81.3,+LEXIEN,60,"B",LEXCDT),-1),LEXH=$O(^DIC(81.3,+LEXIEN,60,"B",+LEXH," "),-1),LEXND=$G(^DIC(81.3,+LEXIEN,60,+LEXH,0)),LEXPEF=$P(LEXND,"^",1),LEXPST=$P(LEXND,"^",2)
 . S LEX1=$D(^DIC(81.3,+LEXIEN,61,"B",LEXCDT))>0,LEX2=$D(^DIC(81.3,+LEXIEN,62,"B",LEXCDT))>0
 . S LEX3=$O(^DIC(81.3,+LEXIEN,61,"B",LEXCDT),-1),LEX4=$O(^DIC(81.3,+LEXIEN,62,"B",LEXCDT),-1)
 . S LEXQL=0 I $L(LEXCST),$L(LEXCEF) D
 . . Q:$L(LEXBEF)&($L(LEXBST))&(LEXBST'=LEXCST)  Q:$L(LEXAEF)&($L(LEXAST))&(LEXAST'=LEXCST)    Q:(LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))
 . . N LEXSTID Q:'$L($G(LEXSO))  S LEXSTID=$S(+LEXCST>0:"ACT",1:"INA")
 . . I LEXSTID="ACT",$G(LEXPEF)?7N,+($G(LEXPST))'>0,'$L(LEX3),'$L(LEX4) S LEXSTID="REA"
 . . S LEXQL=1,^TMP("LEXQC",$J,LEXSID,LEXSTID,1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,LEXSID,LEXSTID,0)=+($G(^TMP("LEXQC",$J,LEXSID,LEXSTID,0)))+1
 . I 'LEXQL I +LEXPST>0,((LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))) D  Q:LEXQL
 . . Q:'$L($G(LEXSO))
 . . S LEXQL=1 S ^TMP("LEXQC",$J,LEXSID,"REV",1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,LEXSID,"REV",0)=+($G(^TMP("LEXQC",$J,LEXSID,"REV",0)))+1
 . I 'LEXQL S LEXQL=0 I ((LEXCEF?7N&(LEXCST>0))!(LEXPEF?7N&(LEXPST>0))),((LEX1>0&(LEX3?7N))!(LEX2>0&(LEX4?7N))) D
 . . S LEXQL=1 S ^TMP("LEXQC",$J,LEXSID,"REU",1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . S ^TMP("LEXQC",$J,LEXSID,"REU",0)=+($G(^TMP("LEXQC",$J,LEXSID,"REU",0)))+1
 . S (LEXAF,LEXIF,LEXRI)=0,LEXSID="RAN" F  S LEXRI=$O(^DIC(81.3,+LEXIEN,10,LEXRI)) Q:+LEXRI'>0  D  Q:(+LEXAF+LEXIF)>1
 . . S LEXND=$G(^DIC(81.3,+LEXIEN,10,LEXRI,0)) S LEXACT=$P(LEXND,"^",3),LEXINA=$P(LEXND,"^",4)
 . . I LEXACT=LEXCDT,'$L(LEXINA) D
 . . . Q:$D(^TMP("LEXQC",$J,LEXSID,"ACT",1,(LEXSO_" ")))
 . . . S ^TMP("LEXQC",$J,LEXSID,"ACT",1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . . S ^TMP("LEXQC",$J,LEXSID,"ACT",0)=+($G(^TMP("LEXQC",$J,LEXSID,"ACT",0)))+1,LEXAF=1
 . . I LEXINA=LEXCDT,$L(LEXACT),LEXINA>LEXACT,(LEXINA-LEXACT)>1 D
 . . . Q:$D(^TMP("LEXQC",$J,LEXSID,"INA",1,(LEXSO_" ")))
 . . . S ^TMP("LEXQC",$J,LEXSID,"INA",1,(LEXSO_" "))=LEXIEN_"^"_LEXSO_"^"_LEXCDT
 . . . S ^TMP("LEXQC",$J,LEXSID,"INA",0)=+($G(^TMP("LEXQC",$J,LEXSID,"INA",0)))+1,LEXIF=1
 Q
