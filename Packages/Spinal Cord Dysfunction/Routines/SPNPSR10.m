SPNPSR10 ;HIRMFO/DAD,WAA-HUNT: WALK/WHEELCHAIR ;8/7/95  15:09
 ;;2.0;Spinal Cord Dysfunction;;01/02/1997
 ;
EN1(D0,AMBULATE,BDATE,EDATE) ; *** Search entry point
 ; Input:
 ;  ACTION,SEQUENCE = Search ACTION,SEQUENCE number
 ;  D0       = SCD (SPINAL CORD) REGISTRY file (#154) IEN
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"AMBULATION") = Internal ^ External
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"BEGINNING DATE") = Date ^ Date_(Ext)
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"ENDING DATE") = Date ^ Date_(Ext)
 ;       AMBILATE = Internal
 ;       BDATE = Beginning Date
 ;       EDATE = Ending Date
 ; Output:
 ;  $S( D0_Meets_Search_Criteria : 1 , 1 : 0 )
 ;
 N MEETSRCH,METHOD
 S MEETSRCH=0
 S SPNFD0=0
 F  S SPNFD0=$O(^SPNL(154.1,"B",D0,SPNFD0)) Q:SPNFD0'>0!MEETSRCH  D
 . S AMBULATE(2.06)=$P($G(^SPNL(154.1,SPNFD0,2)),U,6)
 . S AMBULATE(2.07)=$P($G(^SPNL(154.1,SPNFD0,2)),U,7)
 . S METHOD=$S(AMBULATE(2.07):AMBULATE(2.07)+2,AMBULATE(2.06):AMBULATE(2.06),1:0) Q:METHOD'>0
 . S RECDATE=$P($G(^SPNL(154.1,SPNFD0,0)),U,4) Q:RECDATE=""
 . I RECDATE'<BDATE,RECDATE'>EDATE,AMBULATE=METHOD S MEETSRCH=1
 . Q
 Q MEETSRCH
 ;
EN2(ACTION,SEQUENCE) ; *** Prompt entry point
 ; Input:
 ;  ACTION,SEQUENCE = Search ACTION,SEQUENCE number
 ; Output:
 ;  SPNLEXIT = $S( User_Abort/Timeout : 1 , 1 : 0 )
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"AMBULATION") = Internal ^ External
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"BEGINNINGING DATE") = Date ^ Date_(Ext)
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"ENDING DATE") = Date ^ Date_(Ext)
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,0) = $$EN1^SPNPSR(DO,AMBULATE,BDATE,EDATE)
 ;
 N AMBULATE,DIR,DIRUT,DTOUT,DUOUT
 K ^TMP($J,"SPNPRT",ACTION,SEQUENCE),DIR
 S DIR(0)="SOAM^1:WALK WITHOUT HELP;2:WALK WITH DEVICE;3:MANUAL WHEELCHAIR;4:MOTORIZED WHEELCHAIR;"
 S DIR("A")="Method of ambulation: "
 S DIR("?")="Enter 1 or 2 if the patient can walk, 3 or 4 if the patient uses a wheelchair"
 D ^DIR S AMBULATE=Y_U_$G(Y(0))
 S SPNLEXIT=$S($D(DTOUT):1,$D(DUOUT):1,1:0)
 I 'SPNLEXIT,Y'="" D
 . N BDATE,EDATE
 . S (BDATE,EDATE)=""
 . D EN1^SPNPSR00(ACTION,SEQUENCE+.1,.BDATE,.EDATE) Q:SPNLEXIT
 . S ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"AMBULATION")=AMBULATE
 . S ^TMP($J,"SPNPRT",ACTION,SEQUENCE,0)="$$EN1^SPNPSR10(D0,"_$P(AMBULATE,U)_","_BDATE_","_EDATE_")"
 . Q
EN2EXIT ;
 Q
