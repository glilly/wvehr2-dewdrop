VWLEXF ;PORT CLINTON, OH/SO- FIX DUPLICATE PATCH APPLICATION HISTORY;1:00 PM  27 Jul 2012
 ;;1.0
 Q
 ;
ZAP ;
 D DT^DICRW
 D MES^XPDUTL("BEGIN FIXING PACKAGE MULTIPLE FOR:")
 D MES^XPDUTL("   DRG GROUPER, LEXICON UTILITY, & CPT/HCPCS CODES")
 ;PATCH APPLICATION TO BE FIXED:
 ; DRG GROUPER
 ; LEXICON UTILITY
 ; CPT/HCPCS CODES
 N PKGNAM,DATA,ESC1,ESC2,ESC3
 ;
 S DATA("DRG GROUPER")="18.0^3051119^3001106^1"
 S DATA("LEXICON UTILITY")="2.0^3051119^2961016^1"
 S DATA("CPT/HCPCS CODES")="6.0^3051119^2970528^1"
 S (ESC1,ESC2,ESC3)=0
 ;
TEST I ESC1,ESC2,ESC3 D MES^XPDUTL("FINISHED FIXING PACKAGE MULTIPLE") Q
 I 'ESC1 S PKGNAM="DRG GROUPER" D FIX
 I 'ESC2 S PKGNAM="LEXICON UTILITY" D FIX
 I 'ESC3 S PKGNAM="CPT/HCPCS CODES" D FIX
 G TEST
 ;
FIX ;
 N PKGIEN S PKGIEN=0
 S PKGIEN=+$O(^DIC(9.4,"B",PKGNAM,PKGIEN))
 I 'PKGIEN D MES^XPDUTL("CAN NOT FIND PACKAGE: "_PKGNAM) D ESC Q
 S X=+$O(^DIC(9.4,"B",PKGNAM,PKGIEN))
 I X D MES^XPDUTL("MORE THAN ONE PACKAGE CALLED: "_PKGNAM) D ESC Q
 N CVER S CVER=$G(^DIC(9.4,PKGIEN,"VERSION"))
 I CVER="" D MES^XPDUTL("THERE IS NO CURRENT VERSION FOR PACKAGE: "_PKGNAM) D ESC Q
 N TMP,VERIEN S TMP="",VERIEN=0
L2 F  S VERIEN=$O(^DIC(9.4,PKGIEN,22,"B",CVER,VERIEN)) Q:'VERIEN  D
 . S TMP(VERIEN)=0
 . Q
 S X=0 F I=0:1 S X=$O(TMP(X)) Q:'X
 I I<2 D MES^XPDUTL(PKGNAM_" - PACKAGE FILE OK") D ESC Q
 S VERIEN=0
L3 F  S VERIEN=$O(TMP(VERIEN)) Q:'VERIEN  I ^DIC(9.4,PKGIEN,22,VERIEN,0)'=DATA(PKGNAM) D PLINK Q
 Q
 ;
PLINK ;REMOVE BAD PATCH APPLICATION HISTORY
 D MES^XPDUTL("REMOVING PATCH APPLICATION MULTIPLE FOR:")
 D MES^XPDUTL("  "_PKGNAM_", "_CVER_", MULTIPLE IEN: "_VERIEN)
 N FDA,FILE,IENS
 S FILE=9.49
 S IENS=VERIEN_","_PKGIEN_","
 S FDA(FILE,IENS,.01)="@"
 D FILE^DIE("E","FDA",)
 I $D(DIERR) W SPLAT
 D CLEAN^DILF
 Q
 ;
ESC ;
 I PKGNAM="DRG GROUPER" S ESC1=1 Q
 I PKGNAM="LEXICON UTILITY" S ESC2=1 Q
 I PKGNAM="CPT/HCPCS CODES" S ESC3=1 Q
 Q
