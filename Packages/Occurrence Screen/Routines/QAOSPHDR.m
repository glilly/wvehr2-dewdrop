QAOSPHDR ;HISC/DAD-GENERAL WORKSHEET HEADER ;2/4/93  09:42
 ;;3.0;Occurrence Screen;;09/14/1993
 ; QAOSD0 = IEN OF RECORD IN FILE# 741
 ; QAOSREVR = REVIEW LEVEL # ^ SPECIFIC REVIEWER ^ REVIEW DATE
 ; (NOTE: ALL PIECES SHOULD BE IN EXTERNAL FORMAT)
 S LOC=+$O(^QA(741.2,"C",+QAOSREVR,0)),LOC=$P($G(^QA(741.2,+LOC,0)),"^")
 S:$D(QAOSPAGE)[0 QAOSPAGE=1 S HEADER=LOC_" REVIEW WORKSHEET"
 I +QAOSREVR=1,QAOSHOW'=3,$D(^QA(740,1,"OS"))#2,$P(^("OS"),"^",3) S HEADER=HEADER_" (PART 2)"
 S Y=DT X ^DD("DD")
 W:(QAOSPAGE>1)!($E(IOST)="C") @IOF
 W !!?80-$L(HEADER)/2,HEADER
 I QAOSHOW'=3 W ?68,Y,!?68,"PAGE: ",QAOSPAGE
 E  W ?68,"PAGE: ",QAOSPAGE
 D EN6^QAQAUTL
 S LOCQA=$G(^QA(741,QAOSD0,0)),QAOSDFN=+LOCQA,SCRN=+$G(^("SCRN"))
 S (Y,QAOSWHEN)=$P(LOCQA,"^",3) X ^DD("DD")
 S LOCDPT=$G(^DPT(+LOCQA,0)),NAME=$P(LOCDPT,"^"),SSN=$P(LOCDPT,"^",9)
 S SCRN=$G(^QA(741.1,SCRN,0)),SCREEN=$P(SCRN,"^",2),SCRN=+SCRN
 K UNSC S $P(UNSC,"_",31)=""
 W !,"PATIENT:  ",$S(NAME="":UNSC,1:NAME)
 W ?41,"SSN: ",$S(SSN="":$E(UNSC,1,10),1:SSN)
 W ?57,"DATE: ",$S(Y="":$E(UNSC,1,17),1:Y)
 I SCRN W !,SCRN,?10,SCREEN
 E  W !!,"SCREEN:   ",UNSC,UNSC,$E(UNSC,1,10)
 G:QAOSPAGE>1 UNDL
 S X=+QAOSREVR,REVR=$S(X=1:"CLIN REV",X=2:"PEER REV",X=3:"MGMT REV",X=4:"CMTE REV",X=5:"VAL/CONF",1:"REVIEWER")
 S LOC=$P(QAOSREVR,"^",2) W !!,REVR,": ",$S(LOC="":UNSC,1:LOC)
 W ?41,"REVW DT: ",$S($P(QAOSREVR,"^",3)]"":$P(QAOSREVR,"^",3),1:UNSC)
 S IEN405=+$P(LOCQA,"^",2),LOC405=$G(^DGPM(IEN405,0))
 S LOC=$P(LOCQA,"^",5)
 W !!,"WARD:     ",$S(LOC="":UNSC,$D(^SC(LOC,0))#2:$P(^(0),"^"),1:LOC)
 S LOC=$P(LOCQA,"^",6)
 W ?41,"SERVICE: ",$S(LOC="":UNSC,$D(^DIC(49,LOC,0))#2:$P(^(0),"^"),1:LOC)
 S LOC=$P(LOCQA,"^",7)
 W !!,"TR SPEC:  ",$S(LOC="":UNSC,$D(^DIC(45.7,LOC,0))#2:$P(^(0),"^"),1:LOC)
 S LOC=$P(LOCQA,"^",8)
 W ?41,"MED TM:  ",$S(LOC="":UNSC,$D(^QA(741.93,LOC,0))#2:$P(^(0),"^"),1:LOC)
 S LOC=$P(LOCQA,"^",9)
 W !!,"ATTEND:   ",$S(LOC="":UNSC,$D(^VA(200,LOC,0))#2:$P(^(0),"^"),1:LOC)
 S LOC=$P(LOCQA,"^",10)
 W ?41,"RES/PRV: ",$S(LOC="":UNSC,$D(^VA(200,LOC,0))#2:$P(^(0),"^"),1:LOC)
 S Y=$P(LOC405,"^") X ^DD("DD") W !!,"ADM DATE: ",$S(Y="":UNSC,1:Y)
 S LOC=$P(LOC405,"^",10) W ?41,"ADM DXS: ",$S(LOC="":UNSC,1:LOC)
 S LOC=$P($G(^DIC(42,+$P(LOC405,"^",6),0)),"^")
 W !!,"ADM WARD: ",$S(LOC="":UNSC,1:LOC)
 S LOC=$S($G(^DPT(+LOCQA,.1))]"":^(.1),1:UNSC)
 W ?41,"CUR WRD: ",$S(LOC="":UNSC,1:LOC)
 D 101:SCRN\1=101,106:SCRN\1=106,109:SCRN=109
UNDL K UNDL S $P(UNDL,"=",80)="=",QAOSPAGE=QAOSPAGE+1 W !,UNDL
 Q
101 ;
 S QA=+$P($G(^QA(741,QAOSD0,0)),"^",2),QA=$P($G(^DGPM(QA,0)),"^")
 S QA=+$O(^DGPM("ATID3",QAOSDFN,9999999.9999999-QA))
 S QA=+$O(^DGPM("ATID3",QAOSDFN,QA,0)),(Y,SRV)=""
 I QA D
 . K VAIP S DFN=QAOSDFN,VAIP("E")=QA D IN5^VADPT
 . S Y=$P(VAIP(3),"^",2),SRV=+VAIP(8),SRV=+$P($G(^DIC(45.7,SRV,0)),"^",4)
 . S SRV=$P($G(^DIC(49,SRV,0)),"^") K VAIP
 W !!,"D/C DATE: ",$S(Y="":UNSC,1:Y)
 W ?41,"D/C SRV: ",$S(SRV="":UNSC,1:SRV)
 Q
106 ;
 S SRV=+$P($G(^QA(741,QAOSD0,0)),"^",6),SRV=$P($G(^DIC(49,SRV,0)),"^")
 W !!,"ORG SRV: ",$S(SRV="":UNSC,1:SRV)
 Q
109 ;
 W !!,"AUTOPSY REQUESTED ( Y / N )",?41,"PERFORMED ( Y / N )   CIRCLE 'Y' OR 'N'"
 Q
