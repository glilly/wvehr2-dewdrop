ESPORP2 ;DALISC/CKA - PRINT OFFENSE REPORT CONT. ;10/92
 ;;1.0;POLICE & SECURITY;**17,20**;Mar 31, 1994
EN ;CONTINUED FROM ESPORP1
OFFE ;PRINT OFFENDER(S)
 G:'$D(^ESP(912,ESPID,40)) CONT
 F ESPN=0:0 S ESPN=$O(^ESP(912,ESPID,40,ESPN)) Q:ESPN'>0!(END)  D
 .  S DIC="^ESP(912,"_ESPID_",40,",DA=ESPN,DR=".02:.13;1.01:1.06;2.01:2.06;3.01",DIQ(0)="IE" D EN^DIQ1 Q:'$D(^UTILITY("DIQ1",$J,912.05,DA))
 .  D MIN^ESPORP(10) Q:END
 .  D LINES^ESPORP(2) Q:END  F I=1:1:17 W "* "
 .  W "OFFENDER DATA"
 .  F I=1:1:16 W " *"
 .  D LINES^ESPORP(2) Q:END  W "OFFENDER NAME: ",$G(^UTILITY("DIQ1",$J,912.05,DA,.02,"E"))
 .  S ESPFN=$G(^UTILITY("DIQ1",$J,912.05,DA,.02,"I")),SSN=$P($G(^ESP(910,+ESPFN,0)),U,2),ESPDOB=$P($G(^ESP(910,+ESPFN,0)),U,3),Y=ESPDOB X ^DD("DD") S ESPDOBP=Y
 .  D LINES^ESPORP() Q:END  W "SSN: ",$E(SSN,1,3),"-",$E(SSN,4,5),"-",$E(SSN,6,9)
 .  W ?24,"DOB: ",ESPDOBP,"    " D NOW^%DTC S X1=%,X2=ESPDOB,X="" S:X2]"" X=$E(X1,1,3)-$E(X2,1,3)-($E(X1,4,7)<$E(X2,4,7)) W ?62,"AGE: ",X
 .  D LINES^ESPORP() Q:END  W "SEX: ",$G(^UTILITY("DIQ1",$J,912.05,DA,.03,"E"))
 .  W ?24,"RACE: ",$G(^UTILITY("DIQ1",$J,912.05,DA,.04,"E"))
 .  W ?62,"HEIGHT: ",$G(^UTILITY("DIQ1",$J,912.05,DA,.05,"E"))
 .  D LINES^ESPORP() Q:END  W "WEIGHT: ",$G(^UTILITY("DIQ1",$J,912.05,DA,.06,"E"))
 .  W ?24,"HAIR COLOR: ",$G(^UTILITY("DIQ1",$J,912.05,DA,.07,"E"))
 .  W ?62,"EYE COLOR: ",$G(^UTILITY("DIQ1",$J,912.05,DA,.08,"E"))
 .  D LINES^ESPORP() Q:END  W "SKIN TONE: ",$G(^UTILITY("DIQ1",$J,912.05,DA,.09,"E")),?24,"SCARS/MARKS: ",$G(^UTILITY("DIQ1",$J,912.05,DA,.1,"E"))
 .  D LINES^ESPORP() Q:END  W "STATUS: ",$G(^UTILITY("DIQ1",$J,912.05,DA,.11,"E"))
 .  D LINES^ESPORP() Q:END  W "DRIVER'S LICENSE & STATE: ",$G(^UTILITY("DIQ1",$J,912.05,DA,.12,"E"))," ",$G(^UTILITY("DIQ1",$J,912.05,DA,.13,"E"))
 .  D LINES^ESPORP() Q:END  W "PERSONAL DESCRIPTION: "
 .  D LINES^ESPORP() Q:END  W $G(^UTILITY("DIQ1",$J,912.05,DA,3.01,"E"))
 .  D LINES^ESPORP() Q:END  W "HOME ADDRESS: ",$G(^UTILITY("DIQ1",$J,912.05,DA,1.01,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.05,DA,1.02,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.05,DA,1.02,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.05,DA,1.03,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.05,DA,1.03,"E")),","
 .  W $G(^UTILITY("DIQ1",$J,912.05,DA,1.04,"E"))," ",$G(^UTILITY("DIQ1",$J,912.05,DA,1.05,"E"))
 .  D LINES^ESPORP() Q:END  W "HOME PHONE: ",$G(^UTILITY("DIQ1",$J,912.05,DA,1.06,"E"))
 .  D LINES^ESPORP() Q:END  W "WORK ADDRESS: ",$G(^UTILITY("DIQ1",$J,912.05,DA,2.01,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.05,DA,2.02,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.05,DA,2.02,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.05,DA,2.03,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.05,DA,2.03,"E")),","
 .  W $G(^UTILITY("DIQ1",$J,912.05,DA,2.04,"E"))," ",$G(^UTILITY("DIQ1",$J,912.05,DA,2.05,"E"))
 .  D LINES^ESPORP() Q:END  W "WORK PHONE: ",$G(^UTILITY("DIQ1",$J,912.05,DA,2.06,"E"))
 .  F ESPN1=0:0 S ESPN1=$O(^ESP(912,ESPID,40,ESPN,10,ESPN1)) Q:ESPN1'>0  D
 ..    S DIC="^ESP(912,"_ESPID_",40,"_ESPN_",10,",DA=ESPN1,DR=".01;.02;.03;.04",DIQ(0)="IE" D EN^DIQ1 Q:'$D(^UTILITY("DIQ1",$J,912.11,DA))
 ..    D MIN^ESPORP(5) Q:END
 ..    D LINES^ESPORP() Q:END  W "OFFENSE COMMITTED: ",$G(^UTILITY("DIQ1",$J,912.11,DA,.01,"E"))," ",$P($G(^ESP(915,+$G(^("I")),0)),U,3)," ",$P($G(^(0)),U,5)
 ..    D LINES^ESPORP() Q:END  W "DISPOSITION: " W:$G(^UTILITY("DIQ1",$J,912.11,DA,.02,"E"))'=0 ^("E")
 ..    W "  CHARGING DOCUMENT: ",$G(^UTILITY("DIQ1",$J,912.11,DA,.04,"E"))
 ;
CONT G:END EXIT G ^ESPORP3
EXIT K CL,DA,DIC,DIQ,DIR,DIRUT,DIWF,DIWL,DIWR,ESPDOB,ESPDOBP,DR,DTOUT,DUOUT,END,ESPDTR,ESPID,ESPFN,ESPN,ESPN1,IEN,PAGE,SSN,X,Y
 K ^UTILITY("DIQ1",$J)
 QUIT
