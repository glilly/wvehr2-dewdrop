LBRYRRS0 ;ISC2/DJM-REPRINT ROUTING SLIPS ;[ 08/11/97  12:12 PM ]
 ;;2.5;Library;**2**;Mar 11, 1996
START D NOW^%DTC S LBRYD=%\1
FRM S FROM=$O(^LBRY(682,"A3",FROM)) I FROM=""!(FROM>TO) G EXIT
 S (Q,Q1)=0,Q=$O(^LBRY(682,"A3",FROM,Q)) D WHAT
 G FRM
WHAT S Q1=$O(^LBRY(682,"A3",FROM,Q,Q1)) Q:Q1'>0
 I $P(^LBRY(682,Q,0),U,4)'=LBRYPTR G WHAT
 S LBRYDAT=$G(^LBRY(682,Q,4,Q1,0)) I LBRYDAT'="" D TOC
 G WHAT
TOC S PTR=$P(LBRYDAT,U,3),PTRLST=0 G:$P(LBRYDAT,U)'="ToC" COPY
 D HEADER
TOC1 S PTRLST=$O(^LBRY(681,PTR,2,"AC",PTRLST)) I PTRLST'>0 D TRLR,PAUSE Q
 S PTRL2=$O(^LBRY(681,PTR,2,"AC",PTRLST,0)) D NAME
 G TOC1
COPY D HEADER
CPY S PTRLST=$O(^LBRY(681,PTR,2,"AC",PTRLST)) I PTRLST'>0 D TRLR,PAUSE Q
 S PTRL2=$O(^LBRY(681,PTR,2,"AC",PTRLST,0)) D NAME
 G CPY
HEADER W @IOF,!!!!!!!!!!,?5,"Veterans Administration ROUTING SLIP"
 W !!,?10,"ROUTE TO:",!
 Q
TRLR W !,?5 F K=1:1:40 W $C(42)
 S TITLE=$P(^LBRY(681,PTR,0),U,2),TITN=$P(^LBRY(680,TITLE,0),U)
 S TITLE=$E($P(^LBRY(680.5,TITN,0),U),1,39) W !,?5,TITLE
 S VID=^LBRY(682,Q,1),V=$P(VID,U,2),ISSUE=$P(VID,U,3),Y=$P(VID,U) X ^DD("DD")
 S ISSUE=$S(ISSUE]"":"("_ISSUE_")",1:"") S:V]"" ISSUE=V_ISSUE
 W !,?5,Y,?32,ISSUE S NUMB=$P(LBRYDAT,U)
 W:NUMB="ToC" !,?5,"TABLE of CONTENTS ROUTING SLIP"
 W:NUMB=+NUMB !,?5,"COPY ",NUMB," ROUTING SLIP"
 S LBRRSM=$P($G(^LBRY(680.6,LBRYPTR,0)),U,8) S:LBRRSM="" LBRRSM="142D"
 W !!!!!,?5,"FROM LIBRARY, "_LBRRSM
 S Y=LBRYD X ^DD("DD") W ?35,Y,!
 Q
NAME S NMPTR=$P(^LBRY(681,PTR,2,PTRL2,0),U)
 S NMSVC=$S(NMPTR'="":$G(^LBRY(680.4,NMPTR,0)),1:"")
 S NAME=$P(NMSVC,U),SVC=$P(NMSVC,U,2)
 S SVC=$S(SVC'="":$G(^DIC(49,SVC,0)),1:"")
 S MS=$P(SVC,U,8),SVC=$P(SVC,U)
 W !,?5,MS,?18,NAME W:SVC]"" "/",SVC W !
 Q
PAUSE Q:IO'=IO(0)!($D(ZTSK))  S XZ="Continue//" D PAUSE^LBRYUTL K XZ Q
EXIT D ^%ZISC K:$D(ZTSK) ^%ZTSK(ZTSK),ZTSK
 K %,H,J,K,IOP,FROM,TO,Q,Q1,LBRYDAT,LBRYRET,TITLE,NUMB,LBRYD,PTR,PTRL2
 K PTRLST,VID,V,ISSUE,Y,VI,NMPTR,NMSVC,NAME,SVC,MS,TITN,LBRRSM
 Q
