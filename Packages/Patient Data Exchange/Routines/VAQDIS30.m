VAQDIS30 ;ALB/JFP,JRP - BUILDS DISPLAY ARRAY FOR (MAS DATA);3JUL91 [ 10/02/96  10:02 AM ]
 ;;1.5;PATIENT DATA EXCHANGE;**13,22,40**;NOV 17, 1993
SCR6 ;SCREEN 6 (SECOND HALF)
R9 ;
 S X=$$SETSTR^VALM1("A/O EXP: "_$G(@XTRCT@("VALUE",2,.32102,0)),"",9,16)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.32107,0)) D SCR6A
 S X=$$SETSTR^VALM1("Reg: "_VAQINF,X,26,21)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.32109,0)) D SCR6A
 S X=$$SETSTR^VALM1("Exam: "_VAQINF,X,44,17)
 S X=$$SETSTR^VALM1("A/O #: "_$G(@XTRCT@("VALUE",2,.3211,0)),X,61,17)
 D TMP
R10 ;
 S X=$$SETSTR^VALM1("ION Rad: "_$G(@XTRCT@("VALUE",2,.32103,0)),"",9,17)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.32111,0)) D SCR6A
 S X=$$SETSTR^VALM1("Reg: "_VAQINF,X,26,16)
 S X=$$SETSTR^VALM1("Method: "_$G(@XTRCT@("VALUE",2,.3212,0)),X,42,37)
 D TMP
R11 ;
 S X=$$SETSTR^VALM1("Lebanon: "_$G(@XTRCT@("VALUE",2,.3221,0)),"",9,16)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.3222,0)) D SCR6A
 S X=$$SETSTR^VALM1("From: "_VAQINF,X,25,21)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.3223,0)) D SCR6A
 S X=$$SETSTR^VALM1("To: "_VAQINF,X,46,33)
 D TMP
R12 ;
 S X=$$SETSTR^VALM1("Grenada: "_$G(@XTRCT@("VALUE",2,.3224,0)),"",9,16)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.3225,0)) D SCR6A
 S X=$$SETSTR^VALM1("From: "_VAQINF,X,25,21)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.3226,0)) D SCR6A
 S X=$$SETSTR^VALM1("To: "_VAQINF,X,46,33)
 D TMP
R13 ;
 S X=$$SETSTR^VALM1("Panama: "_$G(@XTRCT@("VALUE",2,.3227,0)),"",10,15)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.3228,0)) D SCR6A
 S X=$$SETSTR^VALM1("From: "_VAQINF,X,25,21)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.3229,0)) D SCR6A
 S X=$$SETSTR^VALM1("To: "_VAQINF,X,46,33)
 D TMP
R14 ;
 S X=$$SETSTR^VALM1("Gulf War: "_$G(@XTRCT@("VALUE",2,.32201,0)),"",8,17)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.322011,0)) D SCR6A
 S X=$$SETSTR^VALM1("From: "_VAQINF,X,25,21)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.322012,0)) D SCR6A
 S X=$$SETSTR^VALM1("To: "_VAQINF,X,46,33)
 D TMP
R15 ;
 S X=$$SETSTR^VALM1("Somalia: "_$G(@XTRCT@("VALUE",2,.322016,0)),"",9,16)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.322017,0)) D SCR6A
 S X=$$SETSTR^VALM1("From: "_VAQINF,X,25,21)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.322018,0)) D SCR6A
 S X=$$SETSTR^VALM1("To: "_VAQINF,X,46,33)
 D TMP
R16 ;
 S X=$$SETSTR^VALM1("Env Contam: "_$G(@XTRCT@("VALUE",2,.322013,0)),"",6,20)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.322014,0)) D SCR6A
 S X=$$SETSTR^VALM1("Reg: "_VAQINF,X,26,18)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.322015,0)) D SCR6A
 S X=$$SETSTR^VALM1("Exam: "_VAQINF,X,44,35)
 D TMP
R17 ;
 S X=$$SETSTR^VALM1("Mil Disab: "_$G(@XTRCT@("VALUE",2,.362,0)),"",7,72)
 D TMP
R18 ;
 ;D BLANK^VAQDIS20
 S X=$$SETSTR^VALM1("Dent Inj: "_$G(@XTRCT@("VALUE",2,.368,0)),"",8,44)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.369,0))
 S VAQINF=$S(VAQTMP'="":VAQTMP,1:"UNANSWERED")
 S X=$$SETSTR^VALM1("Teeth Extracted: "_VAQINF,X,52,27)
 D TMP
R19 ;
 ;DISPLAY DENTAL TREATMENT FROM LEAST RECENT TO MOST RECENT
 S X=""
 F  S X=$O(@XTRCT@("VALUE",2.11,.01,X)) Q:(X="")  D
 .S VAQTMP=@XTRCT@("VALUE",2.11,.01,X)
 .;Check when no dental treatment dates exist
 .Q:(VAQTMP="")
 .D SCR6A
 .S VAQCHK(VAQTMP)=VAQINF_"^"_$G(@XTRCT@("VALUE",2.11,2,X))
 S VAQCHK=""
 F  S VAQCHK=$O(VAQCHK(VAQCHK)) Q:(VAQCHK="")  D
 .S VAQTMP=VAQCHK(VAQCHK)
 .S X="  Trt Date: "_$P(VAQTMP,"^",1)
 .S X=$$SETSTR^VALM1("Cond.:",X,24,6)
 .S VAQINF=$P(VAQTMP,"^",2)
 .S VAQINF(1)=""
 .F VAQTMP=1:1:$L(VAQINF," ") D
 ..S VAQINF(2)=" "_$P(VAQINF," ",VAQTMP)
 ..I ($L(VAQINF(2))>49) D
 ...S $P(VAQINF," ",VAQTMP)=$E(VAQINF(2),50,$L(VAQINF(2)))
 ...S VAQINF(2)=$E(VAQINF(2),1,49)
 ...S VAQTMP=VAQTMP-1
 ..I (($L(VAQINF(1))+$L(VAQINF(2)))>49) D
 ...S X=$$SETSTR^VALM1(VAQINF(1),X,30,49)
 ...D TMP
 ...S (VAQINF(1),X)=""
 ..S VAQINF(1)=VAQINF(1)_VAQINF(2)
 .I (VAQINF(1)'="") D
 ..S X=$$SETSTR^VALM1(VAQINF(1),X,30,49)
 ..D TMP
 K VAQCHK
R20 ;
 S X=$$SETSTR^VALM1("Yugoslavia: "_$G(@XTRCT@("VALUE",2,.322019,0)),"",6,19)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.32202,0)) D SCR6A
 S X=$$SETSTR^VALM1("From: "_VAQINF,X,25,21)
 S VAQTMP=$G(@XTRCT@("VALUE",2,.322021,0)) D SCR6A
 S X=$$SETSTR^VALM1("To: "_VAQINF,X,46,33)
 D TMP
R21 ;
 S VAQTMP=$G(@XTRCT@("VALUE",2,.531,0))
 S X=$$SETSTR^VALM1("Purple Heart: "_VAQTMP,"",4,27)
 I $E(VAQTMP)="Y" D
 . S VAQTMP=$G(@XTRCT@("VALUE",2,.532,0)) Q:VAQTMP']""
 . S X=$$SETSTR^VALM1("PH Status: "_VAQTMP,X,31,48)
 E  I $E(VAQTMP)="N" D
 . S VAQTMP=$G(@XTRCT@("VALUE",2,.533,0)) Q:VAQTMP']""
 . S X=$$SETSTR^VALM1("PH Remarks: "_VAQTMP,X,31,48)
 D TMP
EXIT K VAQTMP,VAQINF
 QUIT
 ;
TMP ; -- Sets up display array
 S VALMCNT=VALMCNT+1
 S @ROOT@(VALMCNT,0)=$E(X,1,79)
 QUIT
 ;
SCR6A ; -- External date to internal date
 I VAQTMP="" S VAQINF="" QUIT
 S VAQTMP=$$DATE^VAQUTL99(VAQTMP)
 S VAQINF=$S(VAQTMP'="":$E(VAQTMP,4,5)_"/"_$E(VAQTMP,6,7)_"/"_$E(VAQTMP,2,3),1:VAQTMP)
 QUIT
 ;
SCR6B I VAQINF["OTHER THAN" S VAQINF="OTHER"
 S VAQINF=$E(VAQINF,1,12)
 QUIT
