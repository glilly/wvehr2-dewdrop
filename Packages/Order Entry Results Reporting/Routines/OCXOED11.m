OCXOED11 ;SLC/RJS,CLA - Rule Editor (Data Field Display) ;10/29/98  12:37
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**32**;Dec 17,1997
 ;;  ;;ORDER CHECK EXPERT version 1.01 released OCT 29,1998
 ;
 ;
S ;
 ;
 Q
EN(OCXD0) ;
 ;
 N OCXACT,OCXRD
 F  K OCXRD,OCXACT S (OCXRD,OCXACT)="" D DISP(OCXD0,.OCXRD,.OCXACT) Q:$$EN^OCXOED12(OCXD0,.OCXRD,.OCXACT)
 ;
 Q
 ;
DISP(OCXD0,OCXRD,OCXACT) ;
 ;
 N OCXTHLN,OCXTNLN,OCXTRLN,OCXTULN,OCXTNLN,OCXD1
 ;
 S OCXTNLN=$C(27,91,48,109),OCXTRLN=$C(27,91,55,109),OCXTULN=$C(27,91,52,109),OCXTHLN=$C(27,91,49,109)
 ;
 S OCXD1=0 D GETDATA(OCXD0,OCXD1,.OCXRD)
 ;
 F  S OCXD1=$O(^OCXS(860.4,OCXD0,"LINK",OCXD1)) Q:'OCXD1  D GETDATA(OCXD0,OCXD1,.OCXRD)
 ;
 W @IOF,OCXTNLN
 W !,$$CENTER($$FIELD("Data Field Edit Screen"),80),!!
 W !," ",$$OPT^OCXOEDT("Edit","EDIT","12",.OCXACT,OCXD0)
 W " ",$$FIELD("Data Field:")," ",$$DATA($G(OCXRD("DF",OCXD0,.01,"E")),50)
 W !,"    ",$$FIELD("Abbrev:")," ",$$DATA($G(OCXRD("DF",OCXD0,1,"E")),50)
 ;
 W !!,$$SEP("Meta Dictionary Links"),!
 S OCXD1=0 F  S OCXD1=$O(OCXRD("DF",OCXD0,"LINK",OCXD1)) Q:'OCXD1  D
 .N OCXLINK,OCXCON,OCXAM
 .S OCXLINK=$G(OCXRD("DF",OCXD0,"LINK",OCXD1,1,"E"))
 .S OCXCON=$G(OCXRD("DF",OCXD0,"LINK",OCXD1,.01,"Z",.02,"E"))
 .S OCXAM=$G(OCXRD("DF",OCXD0,"LINK",OCXD1,.01,"Z",1,"E"))
 .W !!
 .W "  ",$$OPT^OCXOEDT("Link"_OCXD1,"EDLINK","12",.OCXACT,""""_OCXLINK_""","_OCXD1)
 .W "  ",$$FIELD("Link:")," ",$$DATA(OCXLINK_"  ( "_OCXCON_"  "_OCXAM_" )",50)
 ;
 W !!
 W "  ",$$OPT^OCXOEDT("Add Link","LOPT","12",.OCXACT,"""ADD"","_OCXD0,"AL")
 W "  ",$$OPT^OCXOEDT("Delete Link","LOPT","12",.OCXACT,"""DEL"","_OCXD0,"DL")
 ;
 Q
 ;
CENTER(X,M) ;
 N SP S SP="",$P(SP," ",80)=" " Q $E(SP,1,((M\2)-($L(X)\2)))_X
 ;
SEP(OCXHDR) ;
 ;
 N SPACES S SPACES="",$P(SPACES," ",80-$L(OCXHDR))=" " Q OCXTNLN_OCXTHLN_OCXTULN_$G(OCXHDR)_SPACES_OCXTNLN
 ;
FIELD(OCXHDR) ;
 ;
 Q OCXTHLN_$G(OCXHDR)_OCXTNLN
 ;
DATA(OCXVAL,OCXLEN) ;
 ;
 N SPACES S SPACES="",$P(SPACES," ",OCXLEN+5)=" ",OCXVAL=$G(OCXVAL)
 I ($L(OCXVAL)>OCXLEN) Q $E(OCXVAL,1,OCXLEN-3)_"..."
 Q $E((OCXVAL_SPACES),1,OCXLEN)
 ;
GETDATA(OCXD0,OCXD1,OCXD) ;
 ;
 N OCXDIQ,OCXSRC,OCXX S OCXDIQ=""
 ;
 I 'OCXD1 D DIQ("^OCXS(860.4,",OCXD0,"IEN",.OCXDIQ) M OCXD("DF")=OCXDIQ(860.4) Q
 ;
 D DIQ("^OCXS(860.4,"_OCXD0_",""LINK"",",OCXD1,"IEN",.OCXDIQ) M OCXD("DF",OCXD0,"LINK")=OCXDIQ(860.41)
 ;
 K OCXDIQ S OCXDIQ="" D DIQ("^OCXS(860.6,",OCXD1,"IEN",.OCXDIQ)
 M OCXD("DF",OCXD0,"LINK",OCXD1,.01,"Z")=OCXDIQ(860.6,OCXD1) K OCXDIQ S OCXDIQ=""
 ;
 Q
 ;
DIC(OCXDIC,OCXDIC0,OCXDICA,OCXX,OCXDICS,OCXDR) ;
 ;
 N DIC,X,Y
 S DIC=$G(OCXDIC) Q:'$L(DIC) -1
 S DIC(0)=$G(OCXDIC0) S:$L($G(OCXX)) X=OCXX
 S:$L($G(OCXDICS)) DIC("S")=OCXDICS
 S:$L($G(OCXDICA)) DIC("A")=OCXDICA
 S:$L($G(OCXDR)) DIC("DR")=OCXDR
 D ^DIC Q:(Y<1) 0 Q Y
 ;
DIQ(DIC,DA,OCXDIQ0,OCXARY) ;
 N DR,DIQ S DR=".01:99999",DIQ="OCXARY(",DIQ(0)=$G(OCXDIQ0) D EN^DIQ1
 Q
 ;
