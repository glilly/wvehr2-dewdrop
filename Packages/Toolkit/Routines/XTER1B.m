XTER1B ;ISC-SF/JLI - PACKAGE SPECIFIC VARIABLE IDENTIFICATION ;9/9/92  15:41
 ;;8.0;KERNEL;;Jul 10, 1995
EN I '$D(^%ZTER(1,%XTZDAT,1,%XTZNUM,"ZV","B","ZTQUEUED")) W !,"THIS JOB WAS INTERACTIVE -- IT WAS ***NOT*** A QUEUED JOB",!
 F XTER1BI="ZTQUEUED","DUZ","DUZ(0)","XUCI","XQUSER","XQY0","IO(0)","IO","XMZ" D
 .S XTER1BK=XTER1BI I XTER1BI["(" S XTER1BK=$P(XTER1BI,"(")_"c",XTER1BK=XTER1BK_$P($E(XTER1BI,$L(XTER1BK)+1,$L(XTER1BI)),")")_"d"
 .I $D(^%ZTER(1,%XTZDAT,1,%XTZNUM,"ZV","B",XTER1BI)) S XTER1BN=$O(^(XTER1BI,0)) W !,XTER1BI,?10,"(",$P($T(@XTER1BK),";",3),") = ",$S(^%ZTER(1,%XTZDAT,1,%XTZNUM,"ZV",XTER1BN,"D")="":"<null>",1:^("D"))
 .Q
 ;
PKGFIL ;
 S XTER1BN="" F  S XTER1BN=$O(^%ZTER(1,%XTZDAT,1,%XTZNUM,"ZV","B",XTER1BN)) Q:XTER1BN=""  S XTER1BI=$O(^(XTER1BN,0)) I $D(^%ZTER(1,%XTZDAT,1,%XTZNUM,"ZV",XTER1BI,"D")) S XTER1BV=^("D") D
 .F XTER1BP=0:0 S XTER1BP=$O(^DIC(9.4,XTER1BP)) Q:XTER1BP'>0  I $D(^(XTER1BP,0)) S XTER1BPN=$P(^(0),U) I $D(^DIC(9.4,XTER1BP,1933,"AB",XTER1BN)) S XTER1BPV=$O(^(XTER1BN,0)) D  Q
 ..W !!,XTER1BN,?10," = ",XTER1BV,!?5,XTER1BPN," Package"
 ..F XTER1BPI=0:0 S XTER1BPI=$O(^DIC(9.4,XTER1BP,1933,XTER1BPV,1,XTER1BPI)) Q:XTER1BPI'>0  W !,^(XTER1BPI,0)
 Q
END ;
ZTQUEUED ;;INDICATES THAT THIS JOB WAS A QUEUED TASK;
DUZ ;;INTERNAL ENTRY NUMBER FOR USER
DUZc0d ;;FILEMAN ACCESS CODE
XUCI ;;THE UCI THE JOB WAS IN;
IO ;;CURRENTLY SELECTED DEVICE
XMZ ;;INTERNAL NUMBER OF MESSAGE IN FILE 3.9
IOc0d ;;BASIC DEVICE
XQY0 ;;ZERO NODE FROM CURRENT OPTION;
XQUSER ;;THE CURRENT USER'S NAME;
