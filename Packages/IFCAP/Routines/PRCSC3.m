PRCSC3 ;WISC/LEM-ESIG MAINTENANCE ROUTINE ; 08/02/93  8:12 AM
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 ;ROUTINE FOR MAINTAINING FIELD 44.8 (GPF ELECTRONIC SIGNATURE), FILE 410
DECODE(LEVEL0) ;Extrinsic Function to return hashed electronic sig to readable form.
 ;returns "" if unsuccessful
 NEW RECORD7,RECORD71,VERSION,PERSON,SIG,CHECKSUM
 ;get record and check version
 S RECORD7=$G(^PRCS(410,LEVEL0,7)) I RECORD7="" QUIT ""
 S RECORD71=$G(^PRCS(410,LEVEL0,7.1))
 S VERSION=$P(RECORD71,"^",1)
 S PERSON=+$P(RECORD7,"^",8)
 I VERSION'="",VERSION'=1 Q ""
 S SIG=$P(RECORD7,"^",9)
D1 ;decode e signature for version 1
 S RECORD=$G(^PRCS(410,LEVEL0,0))
 S RECORD1=$G(^PRCS(410,LEVEL0,1))
 S RECORD3=$G(^PRCS(410,LEVEL0,3))
 S RECORD4=$G(^PRCS(410,LEVEL0,4))
 S RECORD10=$G(^PRCS(410,LEVEL0,10))
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_$$STRING(RECORD,RECORD1,RECORD3,RECORD4,RECORD7,RECORD10))
 QUIT $$DECODE^PRCUESIG(SIG,PERSON,CHECKSUM)
ENCODE(LEVEL0,USERNUM,Y) ;Encode e signature for version 1 only
 ;Parameter passing entry point
 NEW SIGBLOCK,CHECKSUM,OLDUSER
 NEW RECORD,RECORD1,RECORD3,RECORD4,RECORD7,RECORD71,RECORD10
 ;get record
 S USERNUM=+USERNUM
 I USERNUM=0 S Y=-3 QUIT  ;-3 no user num available
 S SIGBLOCK=$P($G(^VA(200,USERNUM,20)),"^",2)
 I SIGBLOCK="" S Y=-2 QUIT  ;-2 no sigblock in file 200
 S RECORD=$G(^PRCS(410,LEVEL0,0))
 S RECORD1=$G(^PRCS(410,LEVEL0,1))
 S RECORD3=$G(^PRCS(410,LEVEL0,3))
 S RECORD4=$G(^PRCS(410,LEVEL0,4))
 S RECORD7=$G(^PRCS(410,LEVEL0,7))
 S RECORD71=$G(^PRCS(410,LEVEL0,7.1))
 S RECORD10=$G(^PRCS(410,LEVEL0,10))
 I RECORD="" S Y=-1 QUIT  ;-1 no transaction record
 I $P(RECORD7,"^",9)'="" S Y=-4 QUIT  ;-4 cannot re-sign record
 S OLDUSER=+$P(RECORD7,"^",8)
 I OLDUSER=0 S $P(RECORD7,"^",8)=USERNUM
 I OLDUSER>0 S USERNUM=OLDUSER
 S:$P(RECORD7,"^",10)="" $P(RECORD7,"^",10)=$$NOW^PRCUESIG
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_$$STRING(RECORD,RECORD1,RECORD3,RECORD4,RECORD7,RECORD10))
 S $P(RECORD7,"^",9)=$$ENCODE^PRCUESIG(SIGBLOCK,USERNUM,CHECKSUM)
 S $P(RECORD71,"^",1,2)="1^"_$$SUM^PRCUESIG(SIGBLOCK)
 S ^PRCS(410,LEVEL0,7)=RECORD7
 S ^PRCS(410,LEVEL0,7.1)=RECORD71
 S Y=1 QUIT
REMOVE(LEVEL0) ;Entry point to remove e signature from record
 ;NOT an extrinsic function
 NEW RECORD7,RECORD71
 S RECORD7=$G(^PRCS(410,LEVEL0,7))
 S RECORD71=$G(^PRCS(410,LEVEL0,7.1))
 S $P(RECORD7,"^",8,10)="^^"
 S $P(RECORD71,"^",2)=""
 S ^PRCS(410,LEVEL0,7)=RECORD7
 S ^PRCS(410,LEVEL0,7.1)=RECORD71
 QUIT
VERIFY(LEVEL0)      ;extrinsic function to verify version 1 signature.  Returns 1 if valid, 0 if not valid
 NEW RECORD71,VERSION,SIGBLOCK
 ;get record variables
 S RECORD71=$G(^PRCS(410,LEVEL0,7.1))
 S VERSION=$P(RECORD71,"^",1),SIGBLOCK=$P(RECORD71,"^",2)
 I VERSION_SIGBLOCK="" QUIT 1
 QUIT ($$SUM^PRCUESIG($$DECODE(LEVEL0))=SIGBLOCK)
STRING(X,X1,X3,X4,X7,X10)          ;Build String of critical fields
 NEW RECORD,RECORD7,RECORD71,SIGBLOCK,CHECKSUM,OLDUSER
 Q $P(X,"^",1)_"^"_$P(X1,"^",1)_"^"_$P(X3,"^",1)_"^"_$P(X4,"^",1)_"^"_$P(X7,"^",10)_"^"_$P(X10,"^",1)
