FSCTASK ;SLC/STAFF-NOIS Task ;9/6/98  22:14
 ;;1.1;NOIS;;Sep 06, 1998
 ;
UPDATE(FSCCALL) ; from FSCEB, FSCED, FSCEL, FSCELSNS, FSCEU, FSCLMPE1, FSCRPCEC, FSCRPCEF, FSCRPCF, FSCRPCNC
 ;D UPDATE^FSCTASKU Q
 N CALLID,ISC,ZTDESC,ZTDTH,ZTIO,ZTRTN,ZTSAVE K ZTSAVE
 Q:'$D(^FSCD("CALL",FSCCALL,0))  S CALLID=$P(^(0),U),ISC=+$P(^(0),U,16)
 S ZTIO=$$RESOURCE(ISC),ZTRTN="UPDATE^FSCTASKU",ZTDTH=$H,ZTDESC="NOIS Update for call: "_CALLID,ZTSAVE("FSCCALL")=""
 D ^%ZTLOAD
 Q
 ;
AGE ; from option FSC TASK AGE
 N ZTDESC,ZTDTH,ZTIO,ZTRTN,ZTSAVE K ZTSAVE
 S ZTIO="",ZTRTN="AGE^FSCTASKA",ZTDTH=$H,ZTDESC="NOIS Update Computed Fields on Calls"
 D ^%ZTLOAD
 Q
 ;
RESOURCE(ISC) ; $$(isc) -> name of resource
 N RESOURCE
 S RESOURCE=$P($G(^FSC("ISC",+$G(ISC),0)),U,2) I RESOURCE Q $$VALUE^FSCGET(RESOURCE,7105.3,2)
 S RESOURCE=$P($G(^FSC("PARAM",1,0)),U,3) Q $$VALUE^FSCGET(RESOURCE,7109,3)
