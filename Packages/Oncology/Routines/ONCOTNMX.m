ONCOTNMX ;Hines OIFO/GWB - GUIDANCE INPUT TRANSFORM ;7/4/93
 ;;2.11;ONCOLOGY;**6,15,29,35**;Mar 07, 1995
 ;
GUIT ; GUIDANCE INPUT TRANSFORM (#165.5,#142)
 ; New field - stores pointer to "GU5" mult in file #164
 S XCODE=X
 D BPGUCHK^ONCOTNE
 I BPSITE="" K X Q
 S FOUND=0
 F XGU=0:0 S XGU=$O(^ONCO(164,BPSITE,"GU5",XGU)) Q:XGU'>0!(FOUND=1)  D
 .I $P(^ONCO(164,BPSITE,"GU5",XGU,0),"^",2)=X S X=XGU,FOUND=1 Q
 I FOUND=0 K X Q
 W "  ",$P(^ONCO(164,BPSITE,"GU5",X,0),"^",1)
 K FOUND,XGU Q
GUOT ; GUIDANCE OUTPUT TRANSFORM
 Q:Y=""
 D BPGUCHK^ONCOTNE
 I BPSITE="" Q
 S Y=$P($G(^ONCO(164,BPSITE,"GU5",Y,0)),"^",1)
 Q
GUHP ; GUIDANCE HELP
 D BPGUCHK^ONCOTNE
 I BPSITE="" Q
 W !?3,"Select from the following list:",!
 F XGU=0:0 S XGU=$O(^ONCO(164,BPSITE,"GU5",XGU)) Q:XGU'>0  W !?6,$P($G(^ONCO(164,BPSITE,"GU5",XGU,0)),"^",2),?12,$P($G(^ONCO(164,BPSITE,"GU5",XGU,0)),"^",1)
 K XGU Q
