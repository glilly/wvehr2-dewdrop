SDYMPOST ;ALB/CAW - Scheduling Maintenance Update 5/26/94
 ;;5.3;Scheduling;**20**;Aug 13, 1993
 ;
EN ; -- add/inactivate stop codes
 D ADD
 D INACT
 D CHANGE
 D CDRNUM
 ; -- add OPC FY95 FORMAT DATE
 S $P(^DG(43,1,"SCLR"),U,25)=2941001
 Q
 ;
 ;
ADD W !!,">>> Adding new clinic stops to CLINIC STOP FILE (#40.7)..."
 W !,"    [NOTE:  These stop codes CANNOT be used UNTIL 10/1/94]",!
 S DIC(0)="L",DLAYGO=40.7,DIC="^DIC(40.7,"
 F DGX=1:1 K DD,DO,DA S DGXX=$P($T(NEW+DGX),";;",2) Q:DGXX="QUIT"  D
 . S DIC("DR")="1////"_$P(DGXX,"^",2)_$S('+$P(DGXX,U,5):"",1:";4////"_$P(DGXX,"^",5)),X=$P(DGXX,"^",1)
 . I '$D(^DIC(40.7,"C",$P(DGXX,"^",2))) D FILE^DICN,MESS
 K DIC,DGXX
 Q
 ;
INACT W !!,">>> Inactivating clinic stops in CLINIC STOP CODE FILE (#40.7)..."
 W !,"    [NOTE:  These stop codes CANNOT be used AFTER 9/30/94]",!
 D NOW^%DTC S SDATE=X
 F DGX=1:1 K DD,DO,DA S DGXX=$P($T(OLD+DGX),";;",2) Q:DGXX="QUIT"  D
 . S DGDA=+$O(^DIC(40.7,"C",DGXX,0))
 . I $D(^DIC(40.7,DGDA,0)) S DA=DGDA,DR="2////2941001",DIE="^DIC(40.7," D ^DIE,MESI
 K %,%H,%I,DGX,DGDA,DGXX,DR,DA,DIC,DIE,DLAYGO,SDATE,X
 Q
 ;
CHANGE W !!,">>> Changing clinic stop names in CLINIC STOP CODE FILE (#40.7)..."
 F DGX=1:1 K DD,DO,DA S DGXX=$P($T(CHNG+DGX),";;",2) Q:DGXX="QUIT"  S DGDA=+$O(^DIC(40.7,"C",$P(DGXX,U,2),0)) I $D(^DIC(40.7,DGDA,0)) S DA=DGDA,DR=".01///"_$P(DGXX,U,4),DIE="^DIC(40.7," D ^DIE,MESC
 K DGX,DGXX,DGDA,DIE,DR,DA Q
 ;
CDRNUM W !!,">>> Changing CDR numbers in CLINIC STOP CODE FILE (#40.7)..."
 F DGX=1:1 K DD,DO,DA S DGXX=$P($T(CDR+DGX),";;",2) Q:DGXX="QUIT"  S DGDA=+$O(^DIC(40.7,"C",$P(DGXX,U,2),0)) I $D(^DIC(40.7,DGDA,0)) S DA=DGDA,DR="4///"_$P(DGXX,U,4),DIE="^DIC(40.7," D ^DIE,MESN
 K DGX,DGXX,DGDA,DIE,DR,DA Q
 ;
MESS W !?8,"...",X,?40,"(",$P(DGXX,"^",2),") added..."
 Q
 ;
MESI W !?8,"...",$P(^DIC(40.7,DGDA,0),"^"),?40,"(",DGXX,") inactivated as of 10/1/94..."
 Q
 ;
MESC W !?8,"...",$P(DGXX,U)_$P(DGXX,U,3),?44,"changed to ",!?11,$P(DGXX,U,4),"..."
 Q
 ;
MESN W !?8,"...",$P(DGXX,U)_" CDR ACCOUNT NUMBER: "_$P(DGXX,U,3),!,?11,"changed to "_$P(DGXX,U,4),"..."
 Q
 ;
NEW ;stop codes to add - example ;;STOP CODE NAME^NUMBER^^^CDR
 ;;CHAPLAIN SERVICE - INDIVIDUAL^166^^^2610.00
 ;;CHAPLAIN SERVICE - GROUP^167^^^2610.00
 ;;CHAPLAIN SERVICE - COLLATERAL^168^^^2610.00
 ;;TELEPHONE/CHAPLAIN^169^^^2610.00
 ;;HBHC - RN/RNP/PA^171^^^5110.00
 ;;HBHC - NURSE EXTENDER^172^^^5110.00
 ;;HBHC - SOCIAL WORKER^173^^^5110.00
 ;;HBHC - THERAPIST^174^^^5110.00
 ;;HBHC - DIETITIAN^175^^^5110.00
 ;;HBHC - CLINICAL PHARMACIST^176^^^5110.00
 ;;HBHC - OTHER^177^^^5110.00
 ;;HBHC/TELEPHONE^178^^^5110.00
 ;;TELEPHONE/NEUROLOGY^325^^^2110.00
 ;;TELEPHONE/GERIATRICS^326^^^2110.00
 ;;WOMEN SURGERY^426^^^2210.00
 ;;PRIMARY CARE/SURGERY^427^^^2230.00
 ;;TELE/HOMELESS MENTALLY ILL^528^^^2311.00
 ;;HCHV/HMI^529^^^2311.00
 ;;TELEPHONE/HUD-VASH^530^^^2316.00
 ;;PRIMARY CARE/GENERAL PSY^531^^^2331.00
 ;;TELEPHONE/IPCC^546^^^5117.00
 ;;PRIMARY CARE/SPEC. PSY.^563^^^2330.00
 ;;DOM ADM SCREENING SERVICES^728^^^5115.00
 ;;TELEPHONE/DOMICILIARY^729^^^5115.00
 ;;QUIT
 ;
 ;
OLD ;STOP CODES TO BE INACTIVATED - example ;;NUMBER
 ;;501
 ;;543
 ;;544
 ;;545
 ;;QUIT
 ;
CHNG ;NAME CHANGES TO STOP CODES-example ;;STOP CODE NAME^NUMBER^ (NUMBER)^NEW NAME
 ;;HBHC^170^ (170)^HBHC - PHYSICIAN
 ;;REHABILITATION MEDICINE^201^ (201)^PHYSICAL MED & REHAB SVC
 ;;RMS INCENTIVE THERAPY^207^ (207)^PM&RS INCENTIVE THERAPY
 ;;RMS COMPENSATED WORK THERAPY^208^ (208)^PM&RS COMPENSATED WORK THERAPY
 ;;RMS VOCATIONAL ASSISTANCE^213^ (213)^PM&RS VOCATIONAL ASSISTANCE
 ;;GENERAL MEDICINE^301^ (301)^GENERAL INTERNAL MEDICINE
 ;;PRIMARY CARE^323^ (323)^PRIMARY CARE/MEDICINE
 ;;AMBULATORY SURGERY^416^ (416)^AMBULATORY SURGERY OFFICE
 ;;MENTAL HYGIENE-INDIVIDUAL^502^ (502)^MENTAL HEALTH - INDIVIDUAL
 ;;COMMUNITY CLINIC-INDIVIDUAL^504^ (504)^IPCC MEDICAL CENTER VISIT
 ;;MENTAL HYGIENE-GROUP^550^ (550)^MENTAL HEALTH CLINIC-GROUP
 ;;COMMUNITY CLINIC-GROUP^551^ (551)^IPCC COMM CLN/DAY PROGRAM VST
 ;;COMMUNITY DAY PROGRAM^552^ (552)^IPCC COMMUNITY VISIT
 ;;QUIT
 ;
CDR ;CDR ACCOUNT CHANGE-EXAMPLE ;;STOP CODE NAME (NUMBER)^NUMBER^CDR #^CDR#
 ;;PRIMARY CARE/MEDICINE (323)^323^2110.00^2130.00
 ;;IPCC MEDICAL CENTER VISIT (504)^504^5114.00^5117.00
 ;;IPCC COMM CLN/DAY PROGRAM VST (551)^551^5114.00^5117.00
 ;;IPCC COMMUNITY VISIT (552)^552^5114.00^5117.00
 ;;QUIT
