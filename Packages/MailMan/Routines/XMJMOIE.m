XMJMOIE ;ISC-SF/GMB-Edit msg that user has sent to self ;04/19/2002  11:01
 ;;8.0;MailMan;;Jun 28, 2002
EDIT(XMDUZ,XMK,XMZ,XMSUBJ,XMINSTR,XMRESTR) ;
 N XMABORT,XMDIR,XMOPT,XMOX,XMY
 S XMABORT=0
 F  D  Q:XMABORT
 . D SENDSET(.XMINSTR,.XMOPT,.XMOX,.XMDIR)
 . D XMDIR^XMJDIR(.XMDIR,.XMOPT,.XMOX,.XMY,.XMABORT) Q:XMABORT
 . K XMDIR,XMOPT,XMOX
 . D @XMY
 Q
SENDSET(XMINSTR,XMOPT,XMOX,XMDIR) ;
 D OPTEDIT^XMXSEC2(.XMINSTR,.XMOPT,.XMOX,1)
 I $G(XMOPT("NS","?"))=37309.1 K XMOPT("NS","?") ; You have no Network Signature.
 D SET^XMXSEC2("IM",37445,.XMOPT,.XMOX) ; Include responses from another message
 S XMDIR("A")=$$EZBLD^DIALOG(34066) ; Select Edit option:
 S XMDIR("??")="XM-U-MO-EDIT"
 Q
C ; Confidential msg
 N XMMSG
 D CONFID^XMXEDIT(XMZ,.XMINSTR,.XMMSG) I $D(XMERR) D SHOW^XMJERR Q
 W !,XMMSG
 Q
D ; Delivery basket
 N XMMSG
 I $D(XMINSTR("RCPT BSKT")) D  Q
 . D DELIVER^XMXEDIT(XMZ,"@",.XMINSTR,.XMMSG)
 . W !,XMMSG
 D D^XMJMSO
 Q:'$D(XMINSTR("RCPT BSKT"))
 D DELIVER^XMXEDIT(XMZ,XMINSTR("RCPT BSKT"),.XMINSTR,.XMMSG)
 Q
ES ; Edit Subject
 D ES^XMJMSO
 Q
ET ; Edit Text
 I $G(XMPAKMAN) Q:$$NOPAKEDT^XMJMSO
 I $D(XMSECURE) D
 . N XMIA S XMIA=1
 . D DECMSG^XMJMCODE(XMZ)
 D BODY^XMJMS(XMDUZ,XMZ,XMSUBJ,.XMRESTR)
 I $D(XMSECURE) D
 . N XMIA S XMIA=1
 . D ENCMSG^XMJMCODE(XMZ)
 Q
I ; Information only msg
 N XMMSG
 D INFO^XMXEDIT(XMZ,.XMINSTR,.XMMSG)
 W !,XMMSG
 Q
IM ; Include responses from another message
 ;I $G(XMPAKMAN) Q:$$NOPAKEDT^XMJMSO
 I $G(XMPAKMAN) D  Q
 . W !,$$EZBLD^DIALOG(37445.4) ; You may not Include anything into a KIDS or PackMan message.
 I $D(XMSECURE) D
 . N XMIA S XMIA=1
 . D DECMSG^XMJMCODE(XMZ)
 D INCL^XMJMRO(XMDUZ,XMZ,XMZ,XMSUBJ,.XMRESTR,2,.XMABORT)
 I $D(XMSECURE) D
 . N XMIA S XMIA=1
 . D ENCMSG^XMJMCODE(XMZ)
 Q
NS ; Add Network Signature
 D NS^XMJMSO
 Q
P ; Priority msg
 N XMMSG
 D PRIORITY^XMXEDIT(XMZ,.XMINSTR,.XMMSG)
 W !,XMMSG
 Q
R ; Confirm receipt of msg
 N XMMSG
 D CONFIRM^XMXEDIT(XMZ,.XMINSTR,.XMMSG)
 W !,XMMSG
 Q
S ; Scramble msg
 N XMMSG
 I $D(XMSECURE) D  Q
 . D SCRAMBLE^XMXEDIT(XMZ,.XMINSTR,.XMMSG)
 . W !,XMMSG
 D S^XMJMSO
 Q:'$D(XMINSTR("SCR KEY"))
 D SCRAMBLE^XMXEDIT(XMZ,.XMINSTR,.XMMSG)
 Q
V ; Vaporize date
 N XMMSG
 I $G(XMINSTR("VAPOR")) D  Q
 . D VAPOR^XMXEDIT(XMZ,"@",.XMINSTR,.XMMSG)
 . I XMK D KVAPOR^XMXUTIL(XMDUZ,XMK,XMZ,"@")
 . W !,XMMSG
 D V^XMJMSO
 Q:'$D(XMINSTR("VAPOR"))
 I XMK D KVAPOR^XMXUTIL(XMDUZ,XMK,XMZ,XMINSTR("VAPOR"))
 D VAPOR^XMXEDIT(XMZ,XMINSTR("VAPOR"),.XMINSTR,.XMMSG)
 Q
X ; Closed msg
 N XMMSG
 D CLOSED^XMXEDIT(XMZ,.XMINSTR,.XMMSG) I $D(XMERR) D SHOW^XMJERR Q
 W !,XMMSG
 Q
