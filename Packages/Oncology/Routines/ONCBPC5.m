ONCBPC5 ;HIRMFO/GWB - PCE Study of Cancers of the Urinary Bladder - Table V;7/22/96
 ;;2.11;ONCOLOGY;**6**;Mar 07, 1995
 K ONC,TABLE,HTABLE
 S TABLE("TYPE OF FIRST RECURRENCE")="TOFR"
 S TABLE("DATE OF FIRST RECURRENCE")="DOFR"
 S TABLE("DISTANT SITE(S) OF FIRST RECURRENCE")="DSOFR"
 S HTABLE(1)="TYPE OF FIRST RECURRENCE"
 S HTABLE(2)="DATE OF FIRST RECURRENCE"
 S HTABLE(3)="DISTANT SITE(S) OF FIRST RECURRENCE"
 S CHOICES=3
 W @IOF D HEAD^ONCBPC0 W !?27,"TABLE V - FIRST RECURRENCE",!
 S DIC="^ONCO(165.5,",DR="70",DA=ONCONUM,DIQ="ONC",DIQ(0)="IE" D EN^DIQ1
TOFR S DIE="^ONCO(165.5,",DA=ONCONUM
 S DR="387TYPE OF FIRST RECURRENCE/BLADDER.." D ^DIE G:$D(Y) JUMP
 ;W !,"DATE OF FIRST RECURRENCE..........: ",ONC(165.5,ONCONUM,70,"E")
DOFR S DR="70DATE OF FIRST RECURRENCE......... " D ^DIE G:$D(Y) JUMP
DSOFR S DIE="^ONCO(165.5,",DA=ONCONUM
 W !!,"DISTANT SITE(S) OF RECURRENCE:",!
 S DR="71.1  RECURRENCE SITE 1..........." D ^DIE G:$D(Y) JUMP
 I X=0 D  G PRTC
 .S $P(^ONCO(165.5,ONCONUM,5),U,4)=0
 .W !,"  RECURRENCE SITE 2...........: None"
 .S $P(^ONCO(165.5,ONCONUM,5),U,5)=0
 .W !,"  RECURRENCE SITE 3...........: None"
 S DR="71.2  RECURRENCE SITE 2..........." D ^DIE G:$D(Y) JUMP
 I X=0 D  G PRTC
 .S $P(^ONCO(165.5,ONCONUM,5),U,5)=0
 .W !,"  RECURRENCE SITE 3...........: None"
 S DR="71.3  RECURRENCE SITE 3..........." D ^DIE G:$D(Y) JUMP
PRTC W ! K DIR S DIR(0)="E" D ^DIR
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!,"GO TO: ",X:DTIME I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K CHOICES,HTABLE,ONC,TABLE
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
