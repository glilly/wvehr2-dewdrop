ORDV08A ; slc/dan Medicine procedure component ;8/13/01  14:42
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**109**;Dec 17,1997
GETREC(MCL,RMAR,TV,VV,SP) ; Return Single Record
 N LOOP,LINE,LINENO
 S LOOP="",LINENO=1
 S ORDATE=$$RETURN("DATE/TIME",MCL)
 S ORSUM=$$RETURN("SUMMARY",MCL)
 S ORPROC=$$RETURN("PROCEDURE",MCL)
 ;S ^TMP("ORTEMP",$J,LINENO)="",LINENO=LINENO+1
 F  S LOOP=+$O(^TMP("MCAR",$J,MCL,LOOP)) Q:LOOP=0  D REPORT(LOOP,MCL,RMAR,TV,VV,SP)
 Q
REPORT(LOOP,MCL,RMAR,TV,VV,SP) ; Report for Procedure
 N LINE,TEMP,HOLD,TITLE,VALUE,UNITS,MLEN,RANGE
 N TARRAY,VARRY,LARRAY,TMAX,VMAX,MAX,LOOP2
 S LINE=^TMP("MCAR",$J,MCL,LOOP,1)
 S TEMP=$P(LINE,U,1),TITLE=$P(TEMP,";",1)_":"
 S VALUE=$P(LINE,U,3,255),UNITS=$P(LINE,U,2)
 I $P(TEMP,";",2)="W" D WORD(MCL,LOOP,TITLE,RMAR,TV,VV,SP) Q
 S VALUE=VALUE_$S(UNITS="":"",1:" "_UNITS)
 D PRINT(VALUE,VV,TITLE,TV,SP)
 S ^TMP("ORTEMP",$J,LINENO)="",LINENO=LINENO+1
 Q
WARP(VALUE,LENGTH,TEMP,MAX) ; Warp a field
 N DIWL,DIWR,DIWF,X,LOOP3,TEMP1 S LOOP3=""
 K ^UTILITY($J,"W")
 S DIWL=0,DIWR=LENGTH,X=VALUE D ^DIWP
 F MAX=1:1 S LOOP3=+$O(^UTILITY($J,"W",DIWL,LOOP3)) Q:LOOP3=0  D
 . S TEMP1=^UTILITY($J,"W",DIWL,LOOP3,0)
 . S:$E(TEMP1,$L(TEMP1))=" " TEMP1=$E(TEMP1,1,$L(TEMP1)-1)
 . S TEMP(LOOP3)=TEMP1
 S MAX=MAX-1
 Q
WORD(MCL,LOOP,TITLE,RMAR,TV,VV,SP) ; Display Word Processing
 N SLOOP,X,DIWR,DIWL,DIWF,TARRAY,TMAX,LOOP3,SPAC,SPACE
 D WARP(TITLE,TV,.TARRAY,.TMAX) K ^UTILITY($J,"W") S DIWR=VV,DIWL=0
 F SLOOP=0:0 S SLOOP=+$O(^TMP("MCAR",$J,MCL,LOOP,SLOOP)) Q:SLOOP=0  D
 . S X=$P(^TMP("MCAR",$J,MCL,LOOP,SLOOP),U,3) D ^DIWP
 S SLOOP=0
 F LOOP3=1:1 S SLOOP=+$O(^UTILITY($J,"W",DIWL,SLOOP)) Q:'SLOOP  D
 . K SPACE S $P(SPACE," ",SP)=" " S ^TMP("ORTEMP",$J,LINENO)=$J($G(TARRAY(LOOP3)),TV)_SPACE_^UTILITY($J,"W",DIWL,SLOOP,0),LINENO=LINENO+1
 S ^TMP("ORTEMP",$J,LINENO)="",LINENO=LINENO+1
 Q
CONVERT(TITLE) ; Convert to Mixed Case   TEMP = Temp
 N UPPER,LOWER,TEMP,LOOP,HOLD,HOLD2
 S UPPER="ABCDEFGHIJKLMNOPQRSTUVWXYZ",LOWER="abcdefghijklmnopqrstuvwxyz"
 F LOOP=1:1:255 S HOLD=$P(TITLE," ",LOOP) Q:HOLD=""  D
 . S:$D(TEMP) TEMP=TEMP_" "
 . S HOLD2=$E(HOLD,2,$L(HOLD))
 . S TEMP=$G(TEMP)_$E(HOLD,1)_$TR(HOLD2,UPPER,LOWER)
 Q TEMP
PRINT(VALUE,VV,TITLE,TV,SP) ; Print a Field and its Value
 N VMAX,TMAX,TARRAY,VARRAY,SPAC,LOOP2,SPACE
 S TITLE=$$CONVERT(TITLE)
 D WARP(VALUE,VV,.VARRAY,.VMAX)
 D WARP(TITLE,TV,.TARRAY,.TMAX)
 S MAX=$S(VMAX<TMAX:TMAX,VMAX>TMAX:VMAX,1:TMAX)
 S SPAC=TMAX-VMAX S SPAC=$S(SPAC'>0:0,1:SPAC)
 F LOOP2=1:1:MAX D
 . K SPACE S $P(SPACE," ",SP)=" ",^TMP("ORTEMP",$J,LINENO)=$J($G(TARRAY(LOOP2)),TV)_SPACE_$G(VARRAY(LOOP2-SPAC)),LINENO=LINENO+1
 Q:$D(GMTSQIT)
 Q
RETURN(TYPE,LINE) ; Return key Elements
 N MCHOLD,HOLD
 S MCHOLD=+$O(^TMP("MCAR",$J,LINE,"B",TYPE,""))
 S HOLD=$P($G(^TMP("MCAR",$J,LINE,MCHOLD,1)),U,3)
 K ^TMP("MCAR",$J,LINE,"B",TYPE,LINE)
 K ^TMP("MCAR",$J,LINE,MCHOLD,1)
 Q HOLD
EXIT ; Clean up and Quit
 K PR,OT,DA,MCARPPS,MCI,MCJ,R,MCL,S1,S2,S4,S5,S6,LL,LL1,MAX,VA
 K ^TMP("MCAR",$J),K,N,MCARDT,MCARNM,MCARPROC,M,RMAR
 Q
