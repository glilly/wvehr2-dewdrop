IBCEFG41 ;ALB/TMP - OUTPUT FORMATTER MAINT - ACT PROC (CONT) ;22-JAN-96
 ;;2.0;INTEGRATED BILLING;**52,51**;21-MAR-94
 ;
ELEMENT ;Add/Edit Local Data Elements
 D FULL^VALM1
 N DIC,DIE,DR,DA,Y,Z,DINUM,LDINUM
 S LDINUM=$O(^IBA(364.5,"A"),-1) S:LDINUM<100000 LDINUM=99999
 F LDINUM=LDINUM+1:1 L +^IBA(364.5,LDINUM):1 I $T S DINUM=LDINUM Q
 S DIC("A")="Select LOCAL DATA ELEMENT Name: ",DIC("S")="I $P($G(^(0)),U,2)=""L""",DIC="^IBA(364.5,",DIC(0)="AELMQ",DIC("DR")=".02////L",DLAYGO=364.5 D ^DIC K DIC,DLAYGO G:Y<0 ELEQ
 I $P(Y,U,3),$E(^IBA(364.5,+Y,0),1,2)="N-" W !,"ONLY NATIONAL FIELDS CAN BEGIN WITH 'N-'" S DA=+Y,DIK="^IBA(364.5," D ^DIK G ELEMENT
 S $P(^IBA(364.5,0),U,3)=$O(^IBA(364.5,9999),-1) L -^IBA(364.5,LDINUM)
 K DINUM
 S DIE="^IBA(364.5,",DR="[IBCE DEFINE LOCAL ELEMENT]",DA=+Y D ^DIE
ELEQ S VALMBCK="R"
 Q
 ;
DELETE ; Delete form
 N DIR,Y,IBDA,IBDA1,IBF
 D FULL^VALM1
 I $G(IBCEXDA) D
 .S IBF=$P($G(^IBE(353,IBCEXDA,0)),U)
 .K DIR S DIR(0)="YA",DIR("A")="Are you sure you want to DELETE LOCAL FORM - "_IBF_": ",DIR("B")="NO",DIR("A",1)="If you choose to delete this form, the form's field content definitions will also be deleted" D ^DIR K DIR
 .Q:$D(DIRUT)!('Y)
 .S IBDA=0 F  S IBDA=$O(^IBA(364.6,"B",IBCEXDA,IBDA)) Q:'IBDA  D
 ..S IBDA1=0 F  S IBDA1=$O(^IBA(364.7,"B",IBDA,IBDA1)) D  Q:'IBDA1
 ...I 'IBDA1 S DIK="^IBA(364.6,",DA=IBDA D ^DIK Q
 ...S DIK="^IBA(364.7,",DA=IBDA1 D ^DIK
 .S DIK="^IBE(353,",DA=IBCEXDA D ^DIK
 .W !!,"Form ",IBF," Deleted" D PAUSE^VALM1
 S VALMBCK=$S($D(^IBE(353,+$G(IBCEXDA))):"R",1:"Q")
 Q
 ;
