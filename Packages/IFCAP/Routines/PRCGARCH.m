PRCGARCH ;WIRMFO@ALTOONA/CTB  CLEAR FILE 443.9 ;12/10/97  9:51 AM
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 N PERCENT,MIN,SEC,TIME,DX,DY,HOURS,TREC,LREC,LINE,DA,LASTENT,XPOS,A,BTIME,XCOUNT,COUNT
 S TREC=$P(^PRC(443.9,0),"^",4)
 QUIT:TREC=0
 S MESSAGE="CLEARING FILE 443.9"
 D BEGIN^PRCGU
 S DA=0,LASTENT=0,BTIME=$P($H,",",2),XCOUNT=0
 D PERCENT^PRCGU
 F  D  S XCOUNT=XCOUNT+COUNT D:'$D(ZTQUEUED) PERCENT^PRCGU Q:('DA)!($D(UPOUT))
 . F COUNT=1:1:LREC S DA=$O(^PRC(443.9,DA)) Q:'DA  D  R X:0 I X["^" S UPOUT=1 W !!!,"Option Terminated.",*7 QUIT
 . . I '$D(^PRC(442,DA,0)) D REMOVE QUIT
 . . I $P(^PRC(442,DA,0),"-")=PRC("SITE") D REMOVE
 . . S LASTENT=DA
 . . QUIT
 . QUIT
 S $P(^PRC(443.9,0),"^",3)=LASTENT
 D END^PRCGU
 QUIT
REMOVE ;Remove one entry from 443.9
 N X,DIK
 S DIK="^PRC(443.9," D ^DIK
 QUIT
