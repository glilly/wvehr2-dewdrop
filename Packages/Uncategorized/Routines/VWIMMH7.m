VWIMMH7 ;WV//MSF/
 ;;1.0;IMM;June 16, 2011
EN(XTMPDT,XTMPDFN) ;
 I '$D(^XTMP("VWIMMPT",XTMPDT,XTMPDFN)) D EN^DDIOL("No such entry") Q
 K ^XTMP("VWIMMH7") S ^XTMP("VWIMMH7")=0
 N HL,HLECH1 D INIT^HLFNC2(4299,.HL) S HLECH1=$E(HL("ECH"))
 D MSH
 D ORD(XTMPDT,XTMPDFN)
 D ^%ZIS
 U IO S X=$NA(^XTMP("VWIMMH7")) F  S X=$Q(@X) Q:X'["VWIMMH7"  W:@X]"" @X,!
 D ^%ZISC
 Q
MSH ;
 N MSHIN,MSHOUT
 S MSHIN(0)="MSH",MSHIN(1)=HL("ECH"),MSHIN(2)=HL("SAN"),MSHIN(3)=HL("SAF")
 S MSHIN(4)="CDC IMMUNIZATION REG",MSHIN(5)="CDC",MSHIN(9)=123,MSHIN(16)="USA"
 S MSHIN(6)=$$FMTHL7^XLFDT($$NOW^XLFDT)
 S MSHIN(8)=HL("MTN")_HLECH1_HL("ETN")_HLECH1_HL("MTN_ETN")
 S MSHIN(10)="P",MSHIN(11)=HL("VER"),MSHIN(14)=HL("ACAT"),MSHIN(15)=HL("APAT")
 D BUILDSEG^LA7VHLU(.MSHIN,.MSHOUT,HL("FS"))
 D FILESEG(.MSHOUT)
 Q
ORD(XTMPDT,XTMPDFN) ;
 N PTOUT,DFN,ORC,ORCIN
 N RXIN,RXOUT,RXA,IMMIN,IMMSUB
 M RXIN=^XTMP("VWIMMPT",XTMPDT,XTMPDFN)
 S DFN=+XTMPDFN
 D BLDPID^VAFCQRY(DFN,1,"3,5,7,8,10,11,13,22",.PTOUT,.HL)
 D KLUGE(.PTOUT,DFN)
 D FILESEG(.PTOUT)
 S IMMSUB=0 F  S IMMSUB=$O(RXIN(IMMSUB)) Q:'IMMSUB  D
 .M IMMIN=RXIN(IMMSUB)
 .S ORCIN(0)="ORC",ORCIN(1)="RE",ORCIN(3)=$G(IMMIN("ORDER"))
 .S ORCIN(12)=$$ORC12^LA7VORC($G(IMMIN("ORDEREDBY")),"",HL("FS"),HL("ECH"))
 .;S ORCIN(17)=$$ORC17^LA7VORC("",HL("FS"),HL("ECH")),ORCIN(21)=ORCIN(17)
 .D BUILDSEG^LA7VHLU(.ORCIN,.ORC,HL("FS"))
 .D FILESEG(.ORC)
 .D RXA(.IMMIN,.RXOUT)
 .D BUILDSEG^LA7VHLU(.RXOUT,.RXA,HL("FS"))
 .D FILESEG(.RXA)
 ;ZWR RXA
 Q
KLUGE(IN,DFN) ;
 N I,J,X,Y
 S X="" F I=1:1 Q:'$D(IN(I))  S X=X_IN(I)
 S $P(X,"|",4)=DFN_"^^^DFN^MR" ;$$REMIDS($P(X,"|",4))
 S Y=$G(^DPT(DFN,.02,+$O(^DPT(DFN,.02,0)),0))
 S J=$G(^DIC(10,+Y,0)) S:J]"" $P(X,"|",11)=$P(J,U,3)_U_$P(J,U)_"^HL70005"
 S Y=$G(^DPT(DFN,.06,+$O(^DPT(DFN,.06,0)),0))
 S J=$G(^DIC(10.2,+Y,0)) S:J]"" $P(X,"|",23)=$P(J,U,2)_U_$P(J,U)_"^HL70189"
 S Y=$P(X,"|",14),J=$TR($P(Y,U),"-() ",""),Y=U_$P(Y,U,2)_"^^^^"_$E(J,1,3)_U_$E(J,4,10),$P(X,"|",14)=Y
 K IN S J=1 F I=1:1 S Y=$E(X,J,J+244) Q:Y=""  S IN(I)=Y,J=J+245
 Q
REMIDS(X) ;
 N I,Y
 F I=4,9,14 S Y=$P(X,U,I),$P(Y,"&",3)="",$P(X,U,I)=Y
 F I=6,11,16 S Y=$P(X,U,I),$P(Y,"&",3)=$TR($P(Y,"&",3),"L",""),$P(X,U,I)=Y
 Q X
FILESEG(FILIN) ;
 D FILESEG^LA7VHLU("^XTMP(""VWIMMH7"")",.FILIN)
 Q
RXA(RXAIN,RXA) ;
 S RXA(0)="RXA",RXA(1)=0,RXA(2)=1,RXA(3)=$$FMTHL7^XLFDT($G(RXAIN("TIME")))
 S RXA(4)=RXA(3)
 S:$G(RXAIN("CVX"))]"" RXA(5)=RXAIN("CVX")_HLECH1_$$TEXT(15827,RXAIN("CVX"))_HLECH1_"CVX"
 S RXA(6)=$S($G(RXAIN("QUANTITY")):RXAIN("QUANTITY"),1:999)
 I $G(RXAIN("QUANTITY")),$G(RXAIN("UNITS"))]"" S RXA(7)=$G(RXAIN("UNITS"))_HLECH1_$G(RXAIN("UNITS"))_HLECH1_"HL70396"
 S RXA(15)=$G(RXAIN("LOT"))
 S:$G(RXAIN("MFC"))]"" RXA(17)=RXAIN("MFC")_HLECH1_$$TEXT(15826,RXAIN("MFC"))_HLECH1_"MVX"
 S RXA(21)="A"
 Q
TEXT(TABLE,CODE) ;Get text from table entry in ^ORD(101.41
 Q $P($P(";"_$P($G(^ORD(101.41,TABLE,1)),"^",2),";"_CODE_":",2),";")
