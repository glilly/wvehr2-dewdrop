FBCHDI ;AISC/DMK,TET-DISPLAY INVOICE ;10/22/92  12:35
 ;;3.5;FEE BASIS;;JAN 30, 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
ASKIN K FBAANQ W !! S DIC="^FBAAI(",DIC(0)="AEQM" D ^DIC G END:X="^"!(X=""),ASKIN:Y<0 S FBI=+Y
 S VAR="FBI",VAL=FBI,PGM="START^FBCHDI" D ZIS^FBAAUTL G:FBPOP END S:IO=IO(0) FBAANQ=1
START S Q="",$P(Q,"=",80)="=",FBAAOUT=0 U IO D VET
EN K ^TMP($J,"FB") D:'$D(FBPG) INIT
 D SET^FBPAY67 D:$G(FBLISTC)&($E(IOST,1,2)["C-") CR^FBPAY21 G:FBOUT END D EN1^FBPAY671 G:FBAAOUT!(FBOUT) END
 F J=5,16,17 S FBIN(J)=$P(FBIN,"^",J)
 S Y=FBIN(16) D PDF^FBAAUTL S FBIN(16)=Y S:$P(FBIN(5),";",2)="FB583(" FBIN(5)="" I FBIN(5)]"" S FBIN(5)=$S($D(^FB7078(+FBIN(5),0)):$P(^(0),"^",1),1:"")
 I FBIN(17)]"" S FBIN(17)=$S($D(^FBAA(161.7,FBIN(17),0)):$P(^(0),"^",1),1:"")
 I FBIN(5)]"" W !,?4,"Associated 7078: ",FBIN(5)
 W !,?4,"Batch #: ",FBIN(17),?40,"Date Finalized: ",FBIN(16),!
 I $D(^FBAAI(FBI,"FBREJ")),$P(^("FBREJ"),"^",1)]"" W ?4,"Rejects Pending!",?25,"Reject reason: ",$P(^("FBREJ"),"^",2),!,?4,"Old Batch #: ",$S($P(^("FBREJ"),"^",3):$S($D(^FBAA(161.7,$P(^("FBREJ"),"^",3),0)):$P(^(0),"^"),1:""),1:"")
 Q:$D(FBLISTC)
 G:$D(FBAANQ) ASKIN
END K VA,FBINODE,FBNODE,FBPRGNAM,FBPROG,FBDX,FBDX1,FBPAT,FBPROC,FBVEN,FBPG,FBAAOUT,FBPROC1,Q D KILL^FBPAY Q
INIT ;initialize variables needed in fbpay67&671
 S (FBPROG,FBSORT)=1,FBPG=$S($G(FBLISTC):1,1:0),FBCRT=$S($E(IOST,1,2)="C-":1,1:0),$P(FBDASH,"=",80)="",$P(FBDASH1,"-",80)="",FBOUT=0
 Q
VET ;set vet variables
 S FBINODE=$G(^FBAAI(FBI,0)),DFN=+$P(FBINODE,U,4),FBPNAME=$P($G(^DPT(DFN,0)),U),FBPID=$$SSN^FBAAUTL(DFN),FBPROG=$P(FBINODE,U,12),FBPRGNAM=$P($G(^FBAA(161.8,+FBPROG,0)),U),FBPROG(+FBPROG)=FBPRGNAM
 S FBIEN=DFN,FBPI=FBPROG,FBM=+$P(FBINODE,U,6) S:FBPNAME']"" FBPNAME="UNKNOWN" S:FBPRGNAM']"" FBPRGNAM="UNKNOWN"
 Q
