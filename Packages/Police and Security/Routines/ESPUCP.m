ESPUCP ;DALISC/CKA -PRINT UNIFORM CRIME REPORT- 3/93
 ;;1.0;POLICE & SECURITY;**11**;Mar 31, 1994
EN ;
 I '$D(DUZ(2)) W !,"Site # is not defined!" G EX
DATE ;ASK BEGINNING DATE
 D DT^DICRW K BEGDATE
ASK ;ASK FOR CRIME DATA DATE TO PRINT
 S DIC="^ESP(912.4,",DIC(0)="AEMQ",DIC("A")="  Beginning DATE of Report to Print : " D ^DIC
 I Y<0 G EX
 S ESPIEN=+Y
PRT ;PRINT REPORT
Q S %ZIS="Q" D ^%ZIS G:POP EX I '$D(IO("Q")) U IO D START G EX
 S ZTRTN="START^ESPUCP",ZTSAVE("ESP*")="",ZTDESC="UNIFORM CRIME REPORT" D ^%ZTLOAD,HOME^%ZIS G EX
START ; BEGINS THE PRINT OF THIS CRIME REPORT
 K ^UTILITY("DIQ1",$J) S (END,PAGE)=0 D HDR G:END EX
 S DIC="^ESP(912.4,",DA=ESPIEN,DR=".01;.02",DIQ(0)="E" D EN^DIQ1 I '$D(^UTILITY("DIQ1",$J,912.4,DA)) W !,"RECORD DOESN'T EXIST!" G EX
BDT W ?45,"BEGINNING DATE: ",$G(^UTILITY("DIQ1",$J,912.4,DA,.01,"E"))
EDT W !,"Date/Time Printed",?45,"ENDING DATE: ",$G(^UTILITY("DIQ1",$J,912.4,DA,.02,"E"))
 D NOW^%DTC S Y=% X ^DD("DD") W !,$P(Y,":",1,2)
 F ESPN=0:0 S ESPN=$O(^ESP(912.4,ESPIEN,1,ESPN)) Q:ESPN'>0  D PRT^ESPUCP1
 W !!,"Done."
 QUIT
EX D ^%ZISC
 K %ZIS,BEGDATE,DA,DIC,DIQ,DR,END,ESPIEN,ESPN,PAGE,TOT,X,Y,^UTILITY("DIQ1",$J)
 QUIT
HDR ;PRINT HEADING
 I $E(IOST,1,2)="C-" W !,"Press RETURN to continue or '^' to exit: " R X:DTIME S END='$T!(X="^") Q:END
 S PAGE=PAGE+1 W @IOF,!?25,"DEPARTMENT OF VETERANS AFFAIRS",?70,"PAGE ",$J(PAGE,3)
 W !?35,"VA POLICE",!?33,"UNIFORM CRIME REPORT"
 W !!,"VA Facility ",$P(^ESP(912.4,ESPIEN,1,1,0),U)
 QUIT
