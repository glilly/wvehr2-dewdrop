LAL1312 ;SLC/RWF- IL 1312 BLOOD GAS INSTRUMENT ;7/20/90  09:20 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID OR IDE
LA1 S:$D(ZTQUEUED) ZTREQ="@"
 S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 S TOUT=0 D IN G QUIT:TOUT,LA2:$E($P(IN,":",2),5,6)'=12
LA2A K TV,TEST S ID=+$E($P(IN,":",2),1,4) S TOUT=0 D IN G QUIT:TOUT
 S TOUT=0 D IN G QUIT:TOUT,LA2:IN["Q.C." S V=$E($P(IN,"Pat.id.",2),1,11) D NUM S IDE=+V I IN["B.P." S V=$E($P(IN,"B.P.",2),1,5) D NUM S TEST("B.P.")=V
 I IN["%FIO2" S V=$E($P(IN,"%FIO2",2),1,5) D NUM S TEST("%FIO2")=V
 I IN["Hb" S V=$E($P(IN,"Hb",2),1,5) D NUM S TEST("Hb")=V
LA2B K Y S TOUT=0 D IN G:$L(IN)<4!($E($P(IN,":",2),5,6)=12)!TOUT LA2C F I=4:7:90 S V=$E(IN,I,I+7) D NUM Q:V=""  S:$D(TEST(V)) V="'"_V S Y(I)=V
 S TOUT=0 D IN G QUIT:TOUT F I=4:7:90 Q:'$D(Y(I))  S V=$E(IN,I,I+7) D NUM S TEST(Y(I))=V
 G LA2B
LA2C F I=0:0 S I=$O(TC(I)) Q:I<1  S II=0 F III=0:0 S II=$O(TEST(II)) Q:II=""  I II=TC(I,4) S @TC(I,1)=TEST(II)
LA3 X LAGEN G LA2:'ISQN ;Can be changed by the cross-link code
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2A:$E($P(IN,":",2),5,6)=12,LA2
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
