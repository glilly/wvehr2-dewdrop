LRACSUM3 ;SLC/DCM - PRINT INDIVIDUAL PATIENT SUMMARY ; 3/3/88  13:30 ;
 ;;5.2;LAB SERVICE;**201,225**;Sep 27, 1994
 S LRAG=0,LRYESCOM=0,LRIL=0,LRFULL=0
 D LRMH S LRMH="MISC" G PRE^LRACSUM6
LRMH S LRMH=0 F  S LRMH=$O(^TMP($J,LRDFN,LRMH)) Q:LRMH<1  S X=^(LRMH) D MH1
 Q
MH1 S LRMHN=$P(X,U,1),LRSH=0
 S LRPG=1
 D TOP^LRACSUM6
 S LROFMT="",LRFDE=0 D LRSH D:'LRFDE LRBOT^LRACSUM6 K LRTM,^TMP($J,"TM") S LRFULL=0,LRTM=0,LROFMT="",LRFDE=0
 Q
LRSH ;from LRACSUM4, LRACSUM5
 S LRSH=$O(^TMP($J,LRDFN,LRMH,LRSH)) Q:LRSH<1  G:$D(^(LRSH))'=11 LRSH S X=^(LRSH),LRSHN=$P(X,U,1),LRTOPP=$P(X,U,2),LRSHD=$P(X,U,3),LRFMT=$P(X,U,4),LRFMT(1)=$E(LRFMT,1),LROFMT(1)=$E(LROFMT,1)
 I (LROFMT["V"&(LRFMT["V"))!(LROFMT'=""&(LRFMT(1)'=LROFMT(1))) S LROFMT="" D HEAD^LRACSUM6
 S LROFMT=LRFMT,LRTOPP=$E($P(^LAB(61,LRTOPP,0),U,1),1,13),LRTOT=0,LRPL=1,LRACT=0,LRJS=0,LRTS=0,LRFDE=0
 S LRNP=0,LRFDT=0,LRLFDT=0,LRFFDT=0 D LRNP
LOOP ;from LRACSUM5
 I LRACT<LRPL S LRFDT=LRFFDT G:LRFMT["H" TS^LRACSUM5 I LRFMT["V" S LRMULT=1,LRMU=0 D MUL G BS^LRACSUM4
 D TXT1^LRACSUM5 I LRCTR'<LRLNS!(IOSL-18<$Y) S LRFULL=1 S:'LRFDT LRFED=1 D:LRFDE LRBOT^LRACSUM6 D:'LRFDT HEAD^LRACSUM6 S LRFULL=0 G LRSH
 G LRSH
LRNP ;from LRACSUM4
 S I=0 F  S I=$O(^LAB(64.5,1,1,LRMH,1,LRSH,1,I)) Q:I<1  S LRTOT=LRTOT+$P(^(I,0),U,2) I LRTOT>(IOM-20) S LRPL=LRPL+1,LRTOT=$P(^(0),U,2)
LRLNS ;from LRACSUM5
 K LRTM,^TMP($J,"TM") S LRTM=0
 S LRLNS=((IOSL-18)-($Y+(6*LRPL)))\LRPL
 S LRCL=(IOM/2)-(5+($L(LRSHN)/2)) W !!?LRCL,"---- ",LRSHN," ----"
 S LRACT=0,LRJS=0,LRNP=1
 Q
UDT ;from LRACSUM4, LRACSUM5
 S LRBDT=LRFDT,LRFDT=$S($P(^LAB(64.5,1,1,LRMH,1,LRSH,0),U,3)["I":$P(^TMP($J,LRDFN,LRMH,LRSH,LRFDT,0),U,2),1:LRFDT),LRTIM=$E(LRFDT,9,12) F I=0:0 Q:$L(LRTIM)=4  S LRTIM=LRTIM_0
 S LRTIM=$S(LRTIM?4"0":"     ",1:$E(LRTIM,1,2)_":"_$E(LRTIM,3,4))
 S LRUDT=$E($$Y2K^LRX($P(LRFDT,".")),1,5)_" "_$J(LRTIM,4)_" " S LRFDT=LRBDT D:LRTM LRTM
 Q
LRTM S LRNXSW=0 S:'$D(LRTM(0)) LRTM(0)=96
 I $D(^TMP($J,"TM",LRFDT)) S LRNXSW=1
 E  I $D(^TMP($J,LRDFN,LRMH,LRSH,LRFDT,"TX")) S LRTM(0)=LRTM(0)+1,LRNX=$C(LRTM(0)),^TMP($J,"TM",LRFDT)=LRNX,LRNXSW=1 S I=0 F  S I=$O(^TMP($J,LRDFN,LRMH,LRSH,LRFDT,"TX",I)) Q:'I  S ^TMP($J,"TM",LRFDT,I)=^(I,0)
 ;S:LRNXSW LRUDT=$P(^TMP($J,"TM",LRFDT),U,1)_" "_LRUDT
 D Y2KALT^LRAC3
 Q
MUL F I=0:0 Q:LRMULT*(LRSHD+15)>(IOSL-9)  S LRMULT=LRMULT+1
 Q
