LAMONARK ;SLC/RWF - IL MONARK ;7/20/90  09:45 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID OR IDE
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 S (IDE,NIDE)=0 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
 F I=0:0 S I=$O(TC(I)) Q:I<1  S LA(TC(I,4))=I
LA2 S TOUT=0,A=1 D IN G QUIT:TOUT I $P(IN,",",1)=6 D LA3 G LA2
 G LA2:$P(IN,",",1)'=5 S V=$P(IN,",",7) D NUM S NIDE=+V I IDE'=NIDE D LA3
 S IDE=NIDE,CUP=$P(IN,",",5),TRAY=1,ID=CUP I $P(IN,",",9)>11 G LA2
 S X=+$P(IN,",",4) G LA2:'$D(LA(X)) S I=LA(X),V=$P(IN,",",10) D NUM X:TC(I,2)]"" TC(I,2) S @TC(I,1)=V
 G LA2
LA3 X LAGEN Q:'ISQN  ;Can be changed by the cross-link code
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 ;I $D(RMK),$L(RMK) D RMK^LASET
 K TV Q
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S Y(A)=IN Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
OUT S CNT=^LA(TSK,"O")+1,^("O")=CNT,^("O",CNT)=TSK_OUT
 LOCK ^LA("Q") S Q=^LA("Q")+1,^("Q")=Q,^("Q",Q)=TSK LOCK
 Q
QUIT D LA3:$D(TV)&IDE LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
