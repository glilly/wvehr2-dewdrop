EEOEFIN ;HISC/JWR - WRITES DATE NOTICE OF FINAL INTERVIEW LETTER ;APR 20, 1995
 ;;2.0;EEO Complaint Tracking;**1,2**;Apr 27, 1995
REPORT ;Sets up security variables, does lookup
 S XQY0="" D ^EEOEOSE K EEOCOLM,EEOI1 S $P(EEOOFF,"_",18)="",DIC("A")="Select Complainant:  "
 D HOME^%ZIS S EEOII=IOS,^XTMP("EEOSYS",0)=DT+7_"^"_DT
 S DIC="^EEO(785,",DIC(0)="AEMQZ",DIC("S")="I $$SCREEN^EEOEOSE(Y) I +$G(^EEO(785,+Y,""SEC""))=DUZ" D ^DIC I Y>0 S DA=+Y S EEONAME=$P(^EEO(785,DA,0),U) G GATHER
DIC I X=""!(X="^") K DIC D EXIT Q
 G:Y<1 REPORT
GATHER ;Gathers specific complaint information from file 785
 S EEOCNAME=$P($G(^EEO(785,DA,1)),U)
 I EEOCNAME'="" S:$D(^VA(200,EEOCNAME)) EEOCNAME=$P($G(^(EEOCNAME,0)),U)
COP S DIR(0)="NAO^1:4",DIR("A")="Number of Copies: ",DIR("B")=1,DIR("?")="Enter the number of copies of this report that are needed." D ^DIR
 I X'>0 W !,"You cannot exit or delete at this prompt!",! G COP
 S EEOCOP=X,EEOSEL=2
 S Y=DT D DD^%DT
 S %DT="AEPX",%DT("B")=Y,%DT("A")="Date of Notice of Final Interview: " D ^%DT I Y<0 D EXIT G REPORT
 D DD^%DT S EEODT=Y
 K DIC("S") S DIC="^VA(200,",DIC("A")="COUNSELOR:  ",DIC("B")=EEOCNAME D ^DIC G:Y<1&(X'="") REPORT  S:$P(Y,U,2)'="" EEOCNAME=$P(Y,U,2) S EEOFLNAM=$P(EEOCNAME,",",2)_" "_$P(EEOCNAME,",")
 S EEOTITL="" F EEOADD=1:1:3 S EEOLIN(EEOADD)=""
OFF K DIC S DIC="^VA(200,",DIC(0)="AEMQZ",DIC("A")="EEO OFFICER: "
 I $G(^EEO(789.5,1,1))>0 I $D(^VA(200,+$G(^EEO(789.5,1,1)),0)) S DIC("B")=$P($G(^(0)),U,1)
 D ^DIC K DIC
 I $D(DUOUT)!($D(DTOUT)) D EXIT G REPORT
 I Y'>0 G OFF
 K EEOADD S EEOOFF=X,EEOOFF=$P(EEOOFF,",",2)_" "_$P(EEOOFF,",")
 K DR,DIE I X="^"!($D(DTOUT))!(X="") D EXIT G REPORT
 D
 .S EEOTITL=$P($G(^VA(200,X,20)),U,3)
 .S EEOTITL=$S($L(EEOTITL)>2:EEOTITL,1:"""EEO OFFICER"""),EEOTITL=$TR(EEOTITL,$C(34))
 .K DIR S DIR(0)="FAO^3:35" F EEOADD=1:1:3 S EEOV=EEOADD+1 D
 ..S DIR("A")="EEO OFFICER ADDRESS LINE #"_EEOADD_": "
 ..S DIR("B")=$S($P($G(^XTMP("EEOSYS",1)),U,EEOV)'="":$P($G(^(1)),U,EEOV),1:"")
 ..D ^DIR S:Y'="^" EEOLIN(EEOADD)=Y,$P(^XTMP("EEOSYS",1),U,EEOV)=Y
 .F EEOADD=1:1:3 S EEOLIN(EEOADD)=$P($G(^XTMP("EEOSYS",1)),U,EEOADD+1)
 K EEOV,EEOADD,DIR
 S %ZIS="Q" K IOP,ZTIO,ZTSAVE D ^%ZIS G:POP=1 EXIT
 I $D(IO("Q")) S EEOQ=1,ZTRTN="START^EEOEFIN1",ZTSAVE("EEO*")="",ZTDESC=" " D ^%ZTLOAD G EXIT
 D START^EEOEFIN1 G REPORT
EXIT D ^%ZISC D KILL^EEOEOSE K EEOOFF,EEOCNAME,EEOCOP,EEOFLNAM,EEOFORM,EEOLTH,EEOTX,EEOTX1,EEOLIN,EEOADD,EEOSEL Q
IOF W:IO(0)'=IO @IOF Q
