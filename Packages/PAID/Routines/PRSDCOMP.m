PRSDCOMP ;HISC/FPT,GWB-COMPUTED FIELDS FOR 450 & 459 ;12/09/2002
 ;;4.0;PAID;**78**;Sep 21, 1995
EN1 ;calculate computed fields
 S ZERO=$G(^PRST(459,PPIEN,"P",IEN,0))
 S ONE=$G(^PRST(459,PPIEN,"P",IEN,1))
 S TWO=$G(^PRST(459,PPIEN,"P",IEN,2))
 S THREE=$G(^PRST(459,PPIEN,"P",IEN,3))
 S FOUR=$G(^PRST(459,PPIEN,"P",IEN,4))
 S FIVE=$G(^PRST(459,PPIEN,"P",IEN,5))
 S SIX=$G(^PRST(459,PPIEN,"P",IEN,6))
 S SUBACCT=$P(ZERO,U,9),PAYPLAN=$P(ZERO,U,3)
 S MEDICARE=$P(ONE,U,6),VAFICA=$P(ONE,U,10),VALICOST=$P(ONE,U,15),VAHBCOST=$P(ONE,U,18),VARETIRE=$P(ONE,U,21)
 S TSP=$P(TWO,U,13)+$P(TWO,U,14)+$P(TWO,U,15)+$P(TWO,U,16)+$P(TWO,U,17)+$P(TWO,U,18)
 S SEVPAY=$P(THREE,U,2)
 S ALUSED=$P(FOUR,U,1),SLUSED=$P(FOUR,U,2)
 S GROSSPAY=$P(FIVE,U,1),ANNUITY=$P(FIVE,U,4),BASEPAY=$P(FIVE,U,5),HOLPAY=$P(FIVE,U,8),ONCALL=$P(FIVE,U,13),OTPAY=$P(FIVE,U,14),SATPAY=$P(FIVE,U,17),STANDPAY=$P(FIVE,U,18),SUNPAY=$P(FIVE,U,19)
 S LIVING=$P(FIVE,U,22),AWARDS=$P(FIVE,U,24),SESAWARD=$P(FIVE,U,25),LUMPSUM=$P(FIVE,U,26),MOVING=$P(FIVE,U,28),UNIFORM=$P(FIVE,U,29)
 S MDDDPAY=$P(SIX,U,3)
 ;gross pay plus benefits
 S COMPUTED=GROSSPAY+UNIFORM+MEDICARE+VALICOST+VAHBCOST+VAFICA+VARETIRE+TSP+ANNUITY D DECIMAL
 S $P(^PRST(459,PPIEN,"P",IEN,8),U,1)=COMPUTED,$P(^PRSPC(IEN,"PAY"),U,10)=COMPUTED
 ;annual leave used
 S COMPUTED=$S(SUBACCT=81!(SUBACCT=71)!(PAYPLAN="Y"):ALUSED*8,1:ALUSED) D DECIMAL
 S $P(^PRST(459,PPIEN,"P",IEN,4),U,7)=COMPUTED,$P(^PRSPC(IEN,"ANNUAL"),U,14)=COMPUTED
 ;sick leave used
 S COMPUTED=$S(SUBACCT=81!(SUBACCT=71)!(PAYPLAN="Y"):SLUSED*8,1:SLUSED) D DECIMAL
 S $P(^PRST(459,PPIEN,"P",IEN,4),U,8)=COMPUTED,$P(^PRSPC(IEN,"SICK"),U,8)=COMPUTED
 I $D(^PRSPC(IEN,"BOND1")),$P(^PRSPC(IEN,"BOND1"),U,11)="Y" K ^PRSPC(IEN,"BOND1")
 I $D(^PRSPC(IEN,"BOND2")),$P(^PRSPC(IEN,"BOND2"),U,10)="Y" K ^PRSPC(IEN,"BOND2")
 I $D(^PRSPC(IEN,"BOND3")),$P(^PRSPC(IEN,"BOND3"),U,10)="Y" K ^PRSPC(IEN,"BOND3")
 I $D(^PRSPC(IEN,"BOND4")),$P(^PRSPC(IEN,"BOND4"),U,10)="Y" K ^PRSPC(IEN,"BOND4")
KILL1 K ALUSED,ANNUITY,AWARDS,BASEPAY,COMPUTED,FOUR,FIVE,GROSSPAY,HOLPAY,LIVING,LUMPSUM,MDDDPAY,MEDICARE,MOVING,ONCALL,ONE,OTPAY
 K PAYPLAN,SATPAY,SESAWARD,SEVPAY,SIX,SLUSED,STANDPAY,SUBACCT,SUNPAY,THREE,TSP,TWO,UNIFORM,VAFICA,VAHBCOST,VALICOST,VARETIRE,ZERO
 Q
DECIMAL ;check decimal digits
 S:COMPUTED'["." COMPUTED=COMPUTED_"."
 S:$L($P(COMPUTED,".",1))=0 COMPUTED="0"_COMPUTED
 S:$L($P(COMPUTED,".",2))=0 COMPUTED=COMPUTED_"00"
 S:$L($P(COMPUTED,".",2))=1 COMPUTED=COMPUTED_"0"
 Q
FTE ;FTE computation
 S FTEE=DATA,DB=$P(^PRSPC(IEN,0),U,10),NH8B=$P(^PRSPC(IEN,0),U,16) D
 .I DB=1,FTEE=0 S $P(^PRSPC(IEN,"MISC4"),U,11)=1 Q
 .I DB=1,FTEE>0 S $P(^PRSPC(IEN,"MISC4"),U,11)="."_FTEE Q
 .I DB=2,NH8B=1 S $P(^PRSPC(IEN,"MISC4"),U,11)=0 Q
 .I DB=2 S FTEE=$FN(NH8B/80,"",2),$P(^PRSPC(IEN,"MISC4"),U,11)=+FTEE Q
 .I DB=3,FTEE=0 S $P(^PRSPC(IEN,"MISC4"),U,11)=0 Q
 .I DB=3,FTEE>0 S $P(^PRSPC(IEN,"MISC4"),U,11)="."_FTEE
 S NODE=""
 K DB,FTEE,NH8B Q
BONDS ;Savings Bond Balance computation
 I $D(^PRSPC(IEN,"BOND1")) S $P(^PRSPC(IEN,"BOND1"),U,9)="" I $P(RCD,":",8)="" D
 .S SBBAL=$P(^PRSPC(IEN,"BOND1"),U,5)
 .S SBEPP=$P(^PRSPC(IEN,"BOND1"),U,10)
 .S SBDEM=$P(^PRSPC(IEN,"BOND1"),U,11)
 .S SBDEM=$S(SBDEM=4:50,SBDEM=8:100,SBDEM="B":250,SBDEM="D":500,SBDEM="F":2500,1:0)
 .I SBEPP+SBBAL=SBDEM S $P(^PRSPC(IEN,"BOND1"),U,5)=""
 I $D(^PRSPC(IEN,"BOND2")) S $P(^PRSPC(IEN,"BOND2"),U,8)="" I $P(RCD,":",9)="" D
 .S SBBAL=$P(^PRSPC(IEN,"BOND2"),U,4)
 .S SBEPP=$P(^PRSPC(IEN,"BOND2"),U,9)
 .S SBDEM=$P(^PRSPC(IEN,"BOND2"),U,10)
 .S SBDEM=$S(SBDEM=4:50,SBDEM=8:100,SBDEM="B":250,SBDEM="D":500,SBDEM="F":2500,1:0)
 .I SBEPP+SBBAL=SBDEM S $P(^PRSPC(IEN,"BOND2"),U,4)=""
 I $D(^PRSPC(IEN,"BOND3")) S $P(^PRSPC(IEN,"BOND3"),U,8)="" I $P(RCD,":",9)="" D
 .S SBBAL=$P(^PRSPC(IEN,"BOND3"),U,4)
 .S SBEPP=$P(^PRSPC(IEN,"BOND3"),U,9)
 .S SBDEM=$P(^PRSPC(IEN,"BOND3"),U,10)
 .S SBDEM=$S(SBDEM=4:50,SBDEM=8:100,SBDEM="B":250,SBDEM="D":500,SBDEM="F":2500,1:0)
 .I SBEPP+SBBAL=SBDEM S $P(^PRSPC(IEN,"BOND3"),U,4)=""
 I $D(^PRSPC(IEN,"BOND4")) S $P(^PRSPC(IEN,"BOND4"),U,8)="" I $P(RCD,":",9)="" D
 .S SBBAL=$P(^PRSPC(IEN,"BOND4"),U,4)
 .S SBEPP=$P(^PRSPC(IEN,"BOND4"),U,9)
 .S SBDEM=$P(^PRSPC(IEN,"BOND4"),U,10)
 .S SBDEM=$S(SBDEM=4:50,SBDEM=8:100,SBDEM="B":250,SBDEM="D":500,SBDEM="F":2500,1:0)
 .I SBEPP+SBBAL=SBDEM S $P(^PRSPC(IEN,"BOND4"),U,4)=""
 K SBBAL,SBEPP,SBDEM Q
