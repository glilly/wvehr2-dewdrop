FSCRPTS ;SLC/STAFF-NOIS Report ;1/13/98  16:31
 ;;1.1;NOIS;;Sep 06, 1998
 ;
REPORTS ; from FSCOPT
 N CHOICE,CNT,DFORM,DIC,DIR,DISPLAY,FIELD,FIELDS,FORMAT,FSCLIMIT,FSCSTYLE,FSCINDX,FSCSORT,LIST,OK,REPORT,X,Y K DIC,DIR,DFORM,FIELDS,FORMAT,FSCSTYLE
 S DIR(0)="SAMO^REPORT:REPORT;CUSTOM:CUSTOM",DIR("A")="Select (R)eport or (C)ustom: "
 S DIR("?",1)="Enter REPORT to select reports (a report is a list with a sort and format)."
 S DIR("?",2)="Enter CUSTOM to select a list, sort, and format."
 S DIR("?")="^D HELP^FSCU(.DIR)"
 S DIR("??")="FSC U1 NOIS"
 D ^DIR K DIR
 I $D(DIRUT) Q
 S CHOICE=Y
 I CHOICE="REPORT" D
 .D LOOKUP^FSCULOOK("RPT",.REPORT,"AEMOQ",.OK)
 .I 'OK Q
 .D PARTS(+REPORT,.LIST,.FSCSORT,.FORMAT,.DFORM)
 .Q:'$L(LIST)  S FSCLNUM=+LIST,FSCL0=$G(^FSC("LIST",FSCLNUM,0)),FSCLNAME=$P(FSCL0,U)
 .I $P(FSCL0,U,5) W !,"For the list ",FSCLNAME D INDEX^FSCLM(.FSCLNAME,.FSCINDX,FSCL0) I '$G(FSCINDX) K LIST
 .S FSCSTYLE=$P(DFORM(0),U,2),FSCSTYLE(0)=DFORM(0),OK=1 D
 ..I FSCSTYLE="BRIEF" S FSCSTYLE="B" Q
 ..I FSCSTYLE="DETAIL" S FSCSTYLE="D" Q
 ..I 'FORMAT S OK=0 Q
 ..I FSCSTYLE="FM" D  Q
 ...I '$L($P(FORMAT(0),U,3)) S OK=0 Q
 ...S FSCSTYLE("T")="["_$P(FORMAT(0),U,3)_"]"
 ..I FSCSTYLE="EXECUTE" S FSCSTYLE("F")=FORMAT_U_$P(FORMAT(0),U)
 ..I FSCSTYLE="EXTRACT" S FSCSTYLE("E")=""
 ..S CNT=0 F  S CNT=$O(^FSC("FORMAT",FORMAT,2,CNT)) Q:CNT<1  S FIELD=+^(CNT,0),FSCSTYLE(CNT)=^FSC("FLD",FIELD,0)
 ..S FSCSTYLE=$$CHOICE^FSCUX(FSCSTYLE)
 .I 'OK D OTHER^FSCUX(.FSCSTYLE,.OK)
 .S FSCSTYLE=$$STYLE^FSCU(FSCSTYLE)
 I CHOICE="CUSTOM" D
 .D LIST^FSCULOOK(.LIST,.FSCLIMIT,.OK)
 .I 'OK Q
 .S FSCLNAME=$P(LIST,U,2),FSCLNUM=+LIST,FSCL0=$G(^FSC("LIST",FSCLNUM,0))
 .I $P(FSCL0,U,5) D INDEX^FSCLM(.FSCLNAME,.FSCINDX,FSCL0) Q:'$G(FSCINDX)
 .S DIC=7107.6,DIC(0)="AEMOQ",DIC("A")="Select Sort (optional): ",DIC("S")="I $P(^(0),U,2)=""S"""
 .D ^DIC K DIC
 .Q:$D(DUOUT)  Q:$D(DTOUT)
 .S FSCSORT=+Y
 .D EXPAND^FSCUX(.FSCSTYLE,.OK)
 .I 'OK Q
 .S FSCSTYLE=$$STYLE^FSCU(FSCSTYLE)
 Q:'$D(LIST)  Q:'$D(FSCSTYLE)
 S DISPLAY="" I '($D(FSCSTYLE("E"))!$D(FSCSTYLE("T"))) D DISPLAY^FSCRU(.DISPLAY,.OK) I 'OK Q
 I DISPLAY="DEVICE" D DEV^FSCRU("","^TMP(""FSC LIST"",$J,","DQ^FSCRUDQ","NOIS Report",.OK) Q
 D SETUP I $D(VALMQUIT) Q
 D VIEW^FSCLM(VALMAR,"VVALUES",.FSCSTYLE)
 Q
 ;
SETUP ; from FSCOPT, FSCRUDQ
 N CALLNUM,CNT
 K VALMQUIT D ENTRY^FSCLML I $D(VALMQUIT) Q
 S VALMAR="^TMP(""FSC LIST CALLS"",$J)"
 ;I '^TMP("FSC LIST CALLS",$J) W !,"No calls to View." H 2 Q
 I $G(FSCSORT) D SETSORT(FSCSORT,.FIELDS),SORT^FSCUS(.FIELDS)
 I FSCSTYLE="FM" D  Q
 .I '$L($G(FSCSTYLE("T"))) Q
 .S CNT=0 F  S CNT=$O(^TMP("FSC LIST CALLS",$J,"ICX",CNT)) Q:CNT<1  D
 ..S CALLNUM=0 F  S CALLNUM=$O(^TMP("FSC LIST CALLS",$J,"ICX",CNT,CALLNUM)) Q:CALLNUM<1  D TEMP^FSCFORMP
 K ^TMP("FSC SELECT",$J,"VVALUES") S ^("VVALUES")=1_"-"_VALMCNT
 F CNT=1:1:VALMCNT S ^TMP("FSC SELECT",$J,"VVALUES",CNT)=""
 Q
 ;
SETSORT(SORT,FIELDS) ;
 N CNT,FIELD K FIELDS
 K FIELDS S CNT=0 F  S CNT=$O(^FSC("FORMAT",SORT,2,CNT)) Q:CNT<1  S FIELD=^(CNT,0),FIELDS(CNT)=$G(^FSC("FLD",+FIELD,0)) I $P(FIELD,U,8) S FIELDS(CNT,"D")=""
 Q
 ;
PARTS(REPORT,LIST,SORT,FORMAT,DFORM) ;
 N ZERO
 S ZERO=$G(^FSC("RPT",+REPORT,0)),LIST=$P(ZERO,U,3),SORT=$P(ZERO,U,5),FORMAT=$P(ZERO,U,4),DFORM=$P(ZERO,U,6)
 S FORMAT(0)=$G(^FSC("FORMAT",+FORMAT,0)),DFORM(0)=$G(^FSC("FTYPE",+DFORM,0))
 Q
