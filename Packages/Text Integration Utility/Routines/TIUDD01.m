TIUDD01 ; SLC/JER,AJB - KILL LOGIC for Cross-references on 8925
 ;;1.0;TEXT INTEGRATION UTILITIES;**65,153**;Jun 20, 1997
KACLPT(FLD,X) ; KILL Logic for ACLPT
 N TIUD0,TIUD13
 S TIUD0=$G(^TIU(8925,+DA,0)),TIUD13=$G(^(13))
 I $S(FLD=.05:1,FLD=1501:1,FLD=1507:1,1:0) D  ;P65 add ACLPT to fld .05
 . I +$P(TIUD0,U),+$P(TIUD13,U),+$P(TIUD0,U,2) K ^TIU(8925,"ACLPT",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=.01 D
 . I +$P(TIUD13,U),+$P(TIUD0,U,2) K ^TIU(8925,"ACLPT",+$$CLINDOC^TIULC1(+X,+DA),+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=.02 D
 . I +$P(TIUD0,U),+$P(TIUD13,U) K ^TIU(8925,"ACLPT",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+X,$$INVDATE($P(TIUD13,U)),DA)
 I FLD=1301 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2) K ^TIU(8925,"ACLPT",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD0,U,2),$$INVDATE(+X),DA)
 Q
 ;
KACLAU(FLD,X) ; KILL Logic for ACLAU
 N TIUD0,TIUD13,TIUD12
 S TIUD0=$G(^TIU(8925,+DA,0)),TIUD13=$G(^(13)),TIUD12=$G(^(12))
 I FLD=.05 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD13,U),+$P(TIUD12,U,2) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD12,U,2),+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=1501 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD13,U),+$P(TIUD12,U,2) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD12,U,2),+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=.01 D
 . I +$P(TIUD0,U,2),+$P(TIUD13,U),+$P(TIUD12,U,2) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+X,+DA),+$P(TIUD12,U,2),+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=1202 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD13,U) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+X,+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=.02 D
 . I +$P(TIUD0,U),+$P(TIUD13,U),+$P(TIUD12,U,2) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD12,U,2),+X,$$INVDATE($P(TIUD13,U)),DA)
 I FLD=1301 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD12,U,2) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD12,U,2),+$P(TIUD0,U,2),$$INVDATE(+X),DA)
 Q
 ;
KACLAU1(FLD,X) ; KILL Logic for ACLAU - TRANSCRIPTIONIST (ENTERED BY)
 N TIUD0,TIUD13,TIUD12
 S TIUD0=$G(^TIU(8925,+DA,0)),TIUD13=$G(^(13))
 I FLD=.05 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD13,U),+$P(TIUD13,U,2) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD13,U,2),+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=1501 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD13,U),+$P(TIUD13,U,2) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD13,U,2),+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=.01 D
 . I +$P(TIUD0,U,2),+$P(TIUD13,U),+$P(TIUD13,U,2) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+X,+DA),+$P(TIUD13,U,2),+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=1302 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD13,U) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+X,+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=.02 D
 . I +$P(TIUD0,U),+$P(TIUD13,U),+$P(TIUD13,U,2) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD13,U,2),+X,$$INVDATE($P(TIUD13,U)),DA)
 I FLD=1301 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD13,U,2) K ^TIU(8925,"ACLAU",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD13,U,2),+$P(TIUD0,U,2),$$INVDATE(+X),DA)
 Q
 ;
KACLEC(FLD,X) ; KILL Logic For ACLEC
 N TIUD0,TIUD13,TIUD12
 S TIUD0=$G(^TIU(8925,+DA,0)),TIUD13=$G(^(13)),TIUD12=$G(^(12))
 I $S(FLD=.05:1,FLD=1501:1,FLD=1507:1,1:0) D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD13,U),+$P(TIUD12,U,8) K ^TIU(8925,"ACLEC",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD12,U,8),+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=.01 D
 . I +$P(TIUD0,U,2),+$P(TIUD13,U),+$P(TIUD12,U,8) K ^TIU(8925,"ACLEC",+$$CLINDOC^TIULC1(+X,+DA),+$P(TIUD12,U,8),+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=1208 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD13,U) K ^TIU(8925,"ACLEC",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+X,+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=.02 D
 . I +$P(TIUD0,U),+$P(TIUD13,U),+$P(TIUD12,U,8) K ^TIU(8925,"ACLEC",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD12,U,8),+X,$$INVDATE($P(TIUD13,U)),DA)
 I FLD=1301 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD12,U,8) K ^TIU(8925,"ACLEC",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD12,U,8),+$P(TIUD0,U,2),$$INVDATE(+X),DA)
 Q
 ;
KACLSB(FLD,X) ; KILL Logic for ACLSB
 N TIUD0,TIUD13,TIUD15
 S TIUD0=$G(^TIU(8925,+DA,0)),TIUD13=$G(^(13)),TIUD15=$G(^(15))
 I FLD=.01 D
 . I +$P(TIUD0,U,2),+$P(TIUD13,U),+$P(TIUD15,U,2) K ^TIU(8925,"ACLSB",+$$CLINDOC^TIULC1(+X,+DA),+$P(TIUD15,U,2),+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=1502 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD13,U) K ^TIU(8925,"ACLSB",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+X,+$P(TIUD0,U,2),$$INVDATE($P(TIUD13,U)),DA)
 I FLD=.02 D
 . I +$P(TIUD0,U),+$P(TIUD13,U),+$P(TIUD15,U,2) K ^TIU(8925,"ACLSB",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD15,U,2),+X,$$INVDATE($P(TIUD13,U)),DA)
 I FLD=1301 D
 . I +$P(TIUD0,U),+$P(TIUD0,U,2),+$P(TIUD15,U,2) K ^TIU(8925,"ACLSB",+$$CLINDOC^TIULC1(+$P(TIUD0,U),+DA),+$P(TIUD15,U,2),+$P(TIUD0,U,2),$$INVDATE(+X),DA)
 Q
 ;
KAPTLD(FLD,X) ; KILL Logic for "APTLD"
 ; APTLD on fields .02,.01,"1211;.07;.13",.03
 N TIUD0,TIUD12
 S TIUD0=$G(^TIU(8925,+DA,0)),TIUD12=$G(^(12))
 I FLD=.02 D
 . I +TIUD0,+$P(TIUD0,U,7),$L($P(TIUD0,U,13)),+$P(TIUD12,U,11) D
 . . N TIUVS
 . . ; TIUVS="Hosp Loc;Visit/Adm Date/time;Visit Type"
 . . S TIUVS=$P(TIUD12,U,11)_";"_$P(TIUD0,U,7)_";"_$P(TIUD0,U,13)
 . . K ^TIU(8925,"APTLD",+X,+TIUD0,TIUVS,DA)
 . . I +$P(TIUD0,U,3) K ^TIU(8925,"AVSTRV",+X,TIUVS,+$P(TIUD0,U,3),DA)
 I FLD=.01 D
 . I +$P(TIUD0,U,2),+$P(TIUD0,U,7),$L($P(TIUD0,U,13)),+$P(TIUD12,U,11) D
 . . N TIUVS
 . . ; TIUVS="Hosp Loc;Visit/Adm Date/time;Visit Type"
 . . S TIUVS=$P(TIUD12,U,11)_";"_$P(TIUD0,U,7)_";"_$P(TIUD0,U,13)
 . . K ^TIU(8925,"APTLD",+$P(TIUD0,U,2),+X,TIUVS,DA)
 I FLD=1211 D
 . I +TIUD0,+$P(TIUD0,U,2),+$P(TIUD0,U,7),$L($P(TIUD0,U,13)) D
 . . N TIUVS
 . . ; TIUVS="Hosp Loc;Visit/Adm Date/time;Visit Type"
 . . S TIUVS=+X_";"_$P(TIUD0,U,7)_";"_$P(TIUD0,U,13)
 . . K ^TIU(8925,"APTLD",+$P(TIUD0,U,2),+TIUD0,TIUVS,DA)
 . . I +$P(TIUD0,U,3) K ^TIU(8925,"AVSTRV",+$P(TIUD0,U,2),TIUVS,+$P(TIUD0,U,3),DA)
 I FLD=.07 D
 . I +TIUD0,+$P(TIUD0,U,2),$L($P(TIUD0,U,13)),+$P(TIUD12,U,11) D
 . . N TIUVS
 . . ; TIUVS="Hosp Loc;Visit/Adm Date/time;Visit Type"
 . . S TIUVS=$P(TIUD12,U,11)_";"_+X_";"_$P(TIUD0,U,13)
 . . K ^TIU(8925,"APTLD",+$P(TIUD0,U,2),+TIUD0,TIUVS,DA)
 . . I +$P(TIUD0,U,3) K ^TIU(8925,"AVSTRV",+$P(TIUD0,U,2),TIUVS,+$P(TIUD0,U,3),DA)
 I FLD=.13 D
 . I +TIUD0,+$P(TIUD0,U,2),+$P(TIUD0,U,7),+$P(TIUD12,U,11) D
 . . N TIUVS
 . . ; TIUVS="Hosp Loc;Visit/Adm Date/time;Visit Type"
 . . S TIUVS=$P(TIUD12,U,11)_";"_$P(TIUD0,U,7)_";"_X
 . . K ^TIU(8925,"APTLD",+$P(TIUD0,U,2),+TIUD0,TIUVS,DA)
 . . I +$P(TIUD0,U,3) K ^TIU(8925,"AVSTRV",+$P(TIUD0,U,2),TIUVS,+$P(TIUD0,U,3),DA)
 ; SET V-String/Visit Map if Visit record exists
 I FLD=.03 D
 . I +$P(TIUD0,U,2),+$P(TIUD0,U,7),$L($P(TIUD0,U,13)),+$P(TIUD12,U,11) D
 . . N TIUVS
 . . ; TIUVS="Hosp Loc;Visit/Adm Date/time;Visit Type"
 . . S TIUVS=$P(TIUD12,U,11)_";"_$P(TIUD0,U,7)_";"_$P(TIUD0,U,13)
 . . K ^TIU(8925,"AVSTRV",+$P(TIUD0,U,2),TIUVS,+X,DA)
 Q
 ;
INVDATE(DATE) ; Inverts date
 Q 9999999-DATE
