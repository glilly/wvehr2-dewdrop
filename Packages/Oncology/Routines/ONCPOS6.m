ONCPOS6 ;HIRMFO/GWB-POST-INSTALL ROUTINE FOR PATCH ONC*2.11*6  08/26/96
 ;;2.11;ONCOLOGY;**6**;Mar 07, 1995
 ;
 ;Convert Bladder PCE data from 166.1 -> 165.5
 S IEN=0 F  S IEN=$O(^ONCO(166.1,IEN)) Q:IEN'>0  D  S DIK="^ONCO(166.1,",DA=IEN D ^DIK
 .Q:'$D(^ONCO(165.5,IEN))
 .F PIECE=2:1:19 I $P(^ONCO(166.1,IEN,0),U,PIECE)'="" S $P(^ONCO(165.5,IEN,"BLA1"),U,PIECE+16)=$P(^ONCO(166.1,IEN,0),U,PIECE)
 .I $D(^ONCO(166.1,IEN,1)) D
 ..F PIECE=1:1:11 I $P(^ONCO(166.1,IEN,1),U,PIECE)'="" S $P(^ONCO(165.5,IEN,"BLA1"),U,PIECE+35)=$P(^ONCO(166.1,IEN,1),U,PIECE)
 .I $D(^ONCO(166.1,IEN,2)) D
 ..F PIECE=1:1:41 I $P(^ONCO(166.1,IEN,2),U,PIECE)'="" S $P(^ONCO(165.5,IEN,"BLA2"),U,PIECE)=$P(^ONCO(166.1,IEN,2),U,PIECE)
 .I $D(^ONCO(166.1,IEN,3)) D
 ..I $P(^ONCO(166.1,IEN,3),U,1)'="" S $P(^ONCO(165.5,IEN,"BLA2"),U,42)=$P(^ONCO(166.1,IEN,3),U,1)
 ..I $P(^ONCO(166.1,IEN,3),U,2)'="" S $P(^ONCO(165.5,IEN,7),U,12)=$P(^ONCO(166.1,IEN,3),U,2)
 ..I $P(^ONCO(166.1,IEN,3),U,3)'="" S $P(^ONCO(165.5,IEN,7),U,13)=$P(^ONCO(166.1,IEN,3),U,3)
 .I $D(^ONCO(166.1,IEN,4)) D
 ..F PIECE=1:1:17 I $P(^ONCO(166.1,IEN,4),U,PIECE)'="" S $P(^ONCO(165.5,IEN,"BLA1"),U,PIECE)=$P(^ONCO(166.1,IEN,4),U,PIECE)
 K IEN,PIECE
BRM ;Shorten IMMUNOTHERAPY code 9 definition
 S DIE="^ONCO(160.5,",DA=9,DR="1///Unknown if BRM therapy administered"
 D ^DIE
STAGE ;Split staging data between CLINICAL, PATHOLOGIC and OTHER
 S IEN=0,CNT=0 F  S IEN=$O(^ONCO(165.5,IEN)) Q:IEN'>0  I $G(^ONCO(165.5,IEN,27))'="Y" D  S CNT=CNT+1 W:CNT#100=0 "."
 .S SB=$P($G(^ONCO(165.5,IEN,2)),U,18)
 .I (SB="P")!(SB="A") D
 ..S $P(^ONCO(165.5,IEN,2.1),U,1)=$P($G(^ONCO(165.5,IEN,2)),U,25)
 ..S $P(^ONCO(165.5,IEN,2.1),U,2)=$P($G(^ONCO(165.5,IEN,2)),U,26)
 ..S $P(^ONCO(165.5,IEN,2.1),U,3)=$P($G(^ONCO(165.5,IEN,2)),U,27)
 ..S $P(^ONCO(165.5,IEN,2.1),U,4)=$P($G(^ONCO(165.5,IEN,2)),U,20)
 ..S $P(^ONCO(165.5,IEN,2.1),U,5)=$P($G(^ONCO(165.5,IEN,3)),U,32)
 ..S $P(^ONCO(165.5,IEN,2),U,25)=""
 ..S $P(^ONCO(165.5,IEN,2),U,26)=""
 ..S $P(^ONCO(165.5,IEN,2),U,27)=""
 ..S $P(^ONCO(165.5,IEN,2),U,20)=""
 ..S $P(^ONCO(165.5,IEN,3),U,32)=""
 .I SB="R" D
 ..S $P(^ONCO(165.5,IEN,2.1),U,6)=$P($G(^ONCO(165.5,IEN,2)),U,25)
 ..S $P(^ONCO(165.5,IEN,2.1),U,7)=$P($G(^ONCO(165.5,IEN,2)),U,26)
 ..S $P(^ONCO(165.5,IEN,2.1),U,8)=$P($G(^ONCO(165.5,IEN,2)),U,27)
 ..S $P(^ONCO(165.5,IEN,2.1),U,9)=$P($G(^ONCO(165.5,IEN,2)),U,20)
 ..S $P(^ONCO(165.5,IEN,2.1),U,10)=$P($G(^ONCO(165.5,IEN,3)),U,32)
 ..S $P(^ONCO(165.5,IEN,2),U,25)=""
 ..S $P(^ONCO(165.5,IEN,2),U,26)=""
 ..S $P(^ONCO(165.5,IEN,2),U,27)=""
 ..S $P(^ONCO(165.5,IEN,2),U,20)=""
 ..S $P(^ONCO(165.5,IEN,3),U,32)=""
 .S $P(^ONCO(165.5,IEN,27),U,1)="Y"
 Q
