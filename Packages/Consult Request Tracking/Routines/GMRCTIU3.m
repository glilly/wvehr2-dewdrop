GMRCTIU3 ;SLC/DCM,JFR - Extract medicine results for consults tracking ;7/1/00 09:30
 ;;3.0;CONSULT/REQUEST TRACKING;**4,15**;DEC 27, 1997
 ;
 ; This routine invokes IA #3171
 ;
EN(GMRCO,ORIFN,GLOBAL,COUNT) ;;This entry point is used to collect consult data from the Medicine Package.
 ;Consult/Request Tracking
 ;GMRCSEL: The IEN of the consult from file 123.
 ;COUNT:   The current position in ^TMP where data is to be placed.
 D ENDT,EXIT Q
 ;
ENDT ;Get Medicine Results for display
 S @GLOBAL@(COUNT,0)="",COUNT=COUNT+1
 S @GLOBAL@(COUNT,0)=$$CJ^XLFSTR("Medicine Report",80),COUNT=COUNT+1
 D MCRPT(GMRCSR,GLOBAL,.COUNT)
 S @GLOBAL@(COUNT,0)="",COUNT=COUNT+1
 Q
MCRPT(RESULT,GBL,CNT) ; call MCAPI with OR WORKSTATION and format global  
 N GMRCDFN
 S GMRCDFN=$G(DFN)
 N DFN
 D START^ORWRP(80,"EN^MCAPI(RESULT,1)")
 I '$D(^TMP("ORDATA",$J)) D  Q
 . S @GBL@(CNT,0)="No Medicine Result"
 . S CNT=CNT+1
 N I S I=0
 F  S I=$O(^TMP("ORDATA",$J,1,I)) Q:'I  D
 . S @GBL@(CNT,0)=^TMP("ORDATA",$J,1,I)
 . S CNT=CNT+1
 K ^TMP("ORDATA",$J)
 Q
EXIT K GMRCH,GMRCX,GMRCSR,GMRCFT,GMRCFLD,GMRCSUM,GMRCFLDN,GMRCSUMP,GMRCRTIT,GMRCPRNM,GMRCFLDP,MCC,MCK,MCMFLD,MCMUP
 K ^TMP("MC",$J)
 Q
SETFLD S GMRCFLD=0,GMRCFLDP="" F  S GMRCFLD=$O(^TMP("MC",$J,GMRCFT,GMRCFLD)) Q:GMRCFLD=""  S GMRCFLD(0)=^(GMRCFLD) D
 .S GMRCFLDN=$P(GMRCFLD(0),"^",2) Q:GMRCFLDN[";W"
 .I GMRCFLDP=GMRCFLDN S COUNT=COUNT-1,@GLOBAL@(COUNT,0)=@GLOBAL@(COUNT,0)_", "
 .E  S GMRCFLDP=GMRCFLDN,@GLOBAL@(COUNT,0)=GMRCFLDP_":"_$E(TAB,1,18-$L(GMRCFLDP))
 .I $L($P(GMRCFLD(0),"^",1))>45 S @GLOBAL@(COUNT,0)=@GLOBAL@(COUNT,0)_$P(GMRCFLD(0),"^",1),COUNT=COUNT+1 Q
 .I $L($P(GMRCFLD(0),"^",1))'>77 S @GLOBAL@(COUNT,0)=@GLOBAL@(COUNT,0)_$P(GMRCFLD(0),"^",1)
 .S COUNT=COUNT+1 Q
 Q
SUM ;
 S GMRCSUM=$P(^TMP("MC",$J,"S"),"^",1),GMRCSUMP=$P(^TMP("MC",$J,"S"),"^",2)
 I $L(GMRCSUM)!($L(GMRCSUMP)) S @GLOBAL@(COUNT,0)="SUMMARY:"
 I $L(GMRCSUM) S @GLOBAL@(COUNT,0)=@GLOBAL@(COUNT,0)_$E(TAB,1,11)_GMRCSUM,COUNT=COUNT+1
 I $L(GMRCSUMP) S @GLOBAL@(COUNT,0)="SUMMARY PROCEDURE: "_GMRCSUMP,COUNT=COUNT+1
 Q
END S GMRCCT=COUNT-1
 K COUNT,GMRCH,GMRCFT,GMRCFLD,GMRCSUM,GMRCFLDN,GMRCSUMP,GMRCRTIT,GMRCPRNM,GMRCFLDP,MCC,MCK,MCMFLD,MCMUP
 Q
