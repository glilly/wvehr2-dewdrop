ESPSCR0 ;DALISC/CKA - OFFENSE REPORT SCREENS- CONTINUED;7/93
 ;;1.0;POLICE & SECURITY;;Mar 31, 1994
EN ;
VEH ;VEHICLE SCREEN INPUT
 S ESPNOT=0 K DIRUT
 F ESPN=1:1 D VEIN Q:ESPNOT!($D(DUOUT))!($D(DTOUT))
 QUIT
VEIN ;VEHICLE INFO INPUT
 I $D(^TMP($J,"UOR","VE",ESPN)) D WARN I 'ESPNOT K ^TMP($J,"UOR","VE")
 QUIT:ESPNOT  W !
 S DIR(0)="Y",DIR("A")="Do you want to enter Vehicle #"_ESPN_" Information",DIR("B")="NO" D ^DIR K DIR
 I 'Y S ESPNOT=1 Q
 W !!,"VEHICLE #",ESPN
TAG W ! S DIR(0)="912.09,.02" D RD Q:$D(DUOUT)!($D(DTOUT))  S ESPD(.02)=Y
VST S DIR(0)="912.09,.03" D RD G:$D(DUOUT) SV G:$D(DTOUT) NOUPD S ESPD(.03)=$P(Y,U)
MAKE S DIR(0)="912.09,.04" D RD G:$D(DUOUT) SV G:$D(DTOUT) NOUPD S ESPD(.04)=$P(Y,U)
MOD S DIR(0)="912.09,.05" D RD G:$D(DUOUT) SV G:$D(DTOUT) NOUPD S ESPD(.05)=Y
STY S DIR(0)="912.09,.06" D RD G:$D(DUOUT) SV G:$D(DTOUT) NOUPD S ESPD(.06)=Y
COL S DIR(0)="912.09,.07" D RD G:$D(DUOUT) SV G:$D(DTOUT) NOUPD S ESPD(.07)=$P(Y,U)
DEC S DIR(0)="912.09,.08" D RD G:$D(DUOUT) SV G:$D(DTOUT) NOUPD S ESPD(.08)=$P(Y,U)
DCOL S DIR(0)="912.09,.09" D RD G:$D(DUOUT) SV G:$D(DTOUT) NOUPD S ESPD(.09)=$P(Y,U)
YR S DIR(0)="912.09,.1" D RD G:$D(DUOUT) SV G:$D(DTOUT) NOUPD S ESPD(.1)=Y
OWN S DIR(0)="912.09,.11" D RD G:$D(DUOUT) SV S ESPD(.11)=$P(Y,U)
SV S ^TMP($J,"UOR","VE",ESPN)=ESPN_"^"_$G(ESPD(.02))_"^"_$G(ESPD(.03))_"^"_$G(ESPD(.04))_"^"_$G(ESPD(.05))_"^"_$G(ESPD(.06))_"^"_$G(ESPD(.07))_"^"_$G(ESPD(.08))_"^"_$G(ESPD(.09))_"^"_$G(ESPD(.1))_"^"_$G(ESPD(.11)) K ESPD
 QUIT
PROP ;PROPERTY (LOST) SCREEN INPUT
 S ESPNOT=0 K DIRUT
 F ESPN=1:1 D LPIN Q:ESPNOT!($D(DTOUT))!($D(DUOUT))
 QUIT
LPIN ;LOST PROPERTY INPUT
 I $D(^TMP($J,"UOR","P",ESPN)) D WARN I 'ESPNOT K ^TMP($J,"UOR","P")
 QUIT:ESPNOT
 W !!,"ITEM #",ESPN
NAM S DIR(0)="912.1,.01" D ^DIR K DIR Q:$D(DTOUT)!($D(DUOUT))  S ESPD(.01)=Y
 I ESPD(.01)']"" S ESPNOT=1 Q
 I $D(^TMP($J,"UOR","P",ESPD(.01))) W !,$C(7),"There is already an item named ",ESPD(.01),".  Please enter another name." G NAM
LDESC S DIR(0)="912.1,.02" D RD G:$D(DUOUT) SPR G:$D(DTOUT) NOUPD S ESPD(.02)=Y
VAL S DIR(0)="912.1,.03" D RD G:$D(DUOUT) SPR G:$D(DTOUT) NOUPD S ESPD(.03)=Y
REC S DIR(0)="912.1,.04" D RD G:$D(DUOUT) SPR G:$D(DTOUT) NOUPD S ESPD(.04)=Y
SPR S ^TMP($J,"UOR","P",ESPD(.01))=ESPD(.01)_"^"_$G(ESPD(.02))_"^"_$G(ESPD(.03))_"^"_$G(ESPD(.04)) K ESPD
 QUIT
EXIT K ESPNO,ESPTEST,X,Y
 QUIT
NO W $C(7),!!?5,"NO '^'S ALLOWED!",!!
 QUIT
NOUPD W !!,$C(7),?20,"NO UPDATING HAS OCCURRED!!!",!! K ESPCL,ESPD,ESPDTR,ESPX,ESPY,^TMP($J) S NOUPD=1 QUIT
RD D ^DIR I $S($L(X)>1&($E(X)=U):1,($L(X)>1&(X[U)):1,1:0) D NO K X,Y G RD
 K DIR
 QUIT
WARN W !!,$C(7),"Warning:  The names that you have previously entered will be replaced if you enter a name."
 S DIR(0)="Y",DIR("A")="Are you sure you want to do this",DIR("B")="NO" D ^DIR K DIR
 I 'Y S ESPNOT=1
 QUIT
