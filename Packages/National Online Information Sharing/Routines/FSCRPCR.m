FSCRPCR ;SLC/STAFF-NOIS RPC Remove ;1/29/98  22:49
 ;;1.1;NOIS;;Sep 06, 1998
 ;
LISTS(IN,OUT) ; from FSCRPX (RPCRemoveLists)
 N CALL,CALLX,CNT,COUNT,DATEO,INDX,INPUT,LIMITDFM,LIMITDTO,LIMITNUM,LIST,LISTNUM,LNAME,LNUM,NUM,OK,RLIST,ROK,TIME
 K ^TMP("FSC MERGE",$J) S COUNT=0
 S LNUM=0 F  S LNUM=$O(^TMP("FSC CURRENT LIST",$J,LNUM)) Q:LNUM<1  S CALL=+^(LNUM) D
 .S ^TMP("FSC MERGE",$J,LNUM,CALL)=""
 K ^TMP("FSC CURRENT LIST",$J)
 S LNUM=0 F  S LNUM=$O(^TMP("FSC MERGE",$J,LNUM)) Q:LNUM<1  S CALL=$O(^(LNUM,0)) D
 .S OK=1,LISTNUM=0 F  S LISTNUM=$O(^TMP("FSCRPC",$J,"INPUT",LISTNUM)) Q:LISTNUM<1  S INPUT=^(LISTNUM) D  I 'OK Q
 ..S LIST=+INPUT,INDX=+$P(INPUT,U,2),LIMITNUM=$P(INPUT,U,3),LIMITDTO=$P(INPUT,U,4),LIMITDFM=$P(INPUT,U,5)
 ..I 'LIST Q
 ..D LIST^FSCRPCA(LIST,INDX,.RLIST,.ROK) I 'ROK Q
 ..S LNAME=$P(^FSC("LIST",LIST,0),U)
 ..I LNAME="MRE:" D
 ...S TIME="" F  S TIME=$O(^FSCD("MRE","AUTC",INDX,TIME)) Q:TIME=""  D  I 'OK Q
 ....S CALLX=0 F  S CALLX=$O(^FSCD("MRE","AUTC",INDX,TIME,CALLX)) Q:CALLX<1  I CALLX=CALL S OK=0 Q
 ..E  I LNAME="MRA:" D
 ...S TIME="" F  S TIME=$O(^FSCD("MRA","AUTC",INDX,TIME)) Q:TIME=""  D  I 'OK Q
 ....S CALLX=0 F  S CALLX=$O(^FSCD("MRA","AUTC",INDX,TIME,CALLX)) Q:CALLX<1  I CALLX=CALL S OK=0 Q
 ..E  D
 ...I $D(@RLIST@(CALL)) D
 ....I 'LIMITNUM,'LIMITDTO,'LIMITDFM S OK=0 Q
 ....I LIMITNUM D  Q
 .....S CNT=0,NUM="A" F  S NUM=$O(@RLIST@(NUM),-1) Q:NUM<1  S CNT=CNT+1 I NUM=CALL S:CNT'>LIMITNUM OK=0 Q
 ....I 'LIMITDTO,'LIMITDFM S OK=0 Q
 ....S DATEO=$P(^FSCD("CALL",CALL,0),U,3)
 ....I DATEO'<LIMITDTO,DATEO'>LIMITDFM S OK=0
 .I OK D SETUP^FSCRPCA(CALL,.COUNT)
 D OUTPUT^FSCRPCA
 Q
 ;
CALLS(IN,OUT) ; from FSCRPX (RPCRemoveCalls)
 N CALL,LNUM,NUM
 S NUM=0 F  S NUM=$O(^TMP("FSCRPC",$J,"INPUT",NUM)) Q:NUM<1  S CALL=+$G(^(NUM)) D
 .I $D(^TMP("FSC CURRENT LIST",$J,"C",CALL)) S LNUM=+^(CALL) D
 ..K ^TMP("FSC CURRENT LIST",$J,LNUM)
 ..K ^TMP("FSC CURRENT LIST",$J,"C",CALL)
 D OUTPUT^FSCRPCA
 Q
