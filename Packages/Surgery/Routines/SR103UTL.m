SR103UTL ;BIR/ADM-Utility routine for patch SR*3*103; [06/14/01  10:33 AM ]
 ;;3.0; Surgery ;**103**;24 Jun 93
 Q
POST ; post-install action for SR*3*103
 D NOW^%DTC S (SRNOW,ZTDTH)=+$E(%,1,12),ZTRTN="EN1^SR103UTL",ZTDESC="SR*3*103 Post-Install",ZTIO="" D ^%ZTLOAD H 2
 D OTH,MES^XPDUTL("  Post-install process queued...") K SRNOW
 Q
OTH N SRD S SRD=^XMB("NETNAME") I SRD["TST."!(SRD["TEST")!(SRD["UTL.")!(SRD["TRAIN")!(SRD[".IHS.GOV")!(SRD["CPRS") Q
 S ZTDTH=SRNOW,ZTRTN="QR^SR103UTL",ZTDESC="Surgery Quarterly Report",ZTIO="" D ^%ZTLOAD
 Q
EN1 ; remove trailing zeros in field 1.098
 N SREQDT,SRTN S SRTN=0 F  S SRTN=$O(^SRF(SRTN)) Q:'SRTN  S SREQDT=$P($G(^SRF(SRTN,"1.0")),"^",11) I SREQDT'="" S $P(^SRF(SRTN,"1.0"),"^",11)=+SREQDT
 S ZTREQ="@"
 Q
QR ; transmit quarterly reports for FY2000
 S (SRFLG,SRT)=1 D NOW^%DTC S SRNOW=+$E(%,1,12)
 S SRSTART=3001001,SREND=3001231 D TSK H 2
 S SRSTART=3010101,SREND=3010331 D TSK H 2
 I DT>3010813 S SRSTART=3010401,SREND=3010630 D TSK H 2
 I DT>3011113 S SRSTART=3010701,SREND=3010930 D TSK
 S ZTREQ="@"
 Q
TSK S ZTDTH=SRNOW,ZTIO="",ZTDESC="Surgery Quarterly Report",(ZTSAVE("SRSTART"),ZTSAVE("SREND"),ZTSAVE("SRFLG"),ZTSAVE("SRT"))="",ZTRTN="EN^SROQT" D ^%ZTLOAD
 Q
