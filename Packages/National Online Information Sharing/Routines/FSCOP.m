FSCOP ;SLC/STAFF-NOIS Option ;12/8/97  13:54
 ;;1.1;NOIS;;Sep 06, 1998
 ;
OPT(TAG,ACCESS) ; from NOIS option actions
 I '$$ACCESS^FSCU(DUZ,$G(ACCESS)) D NOACCESS^FSCLMPU Q
 D COUNTS^FSCLMPU("OPTION "_TAG,DUZ)
 D
 .I TAG="NEW" D NEW^FSCOPT Q
 .I TAG="EDIT" D EDIT^FSCOPT Q
 .I TAG="CLOSE" D CLOSE^FSCOPT Q
 .I TAG="VIEW" D VIEW^FSCOPT Q
 .I TAG="LIST" D LIST^FSCOPT Q
 .I TAG="QUERY" D QUERY^FSCOPT Q
 .I TAG="REPORTS" D REPORTS^FSCOPT Q
 .I TAG="FILES" D FILES^FSCOPT Q
 .I TAG="EVENT" D EVENT^FSCOPT Q
 Q
 ;
ENTRY ; from entry action on option FSC MENU NOIS
 D TMP^FSCOPTU
 K ^TMP("FSC SESSION",$J)
 N CNT,LINE,NUM
 I $D(^FSC("SPEC",DUZ,0))#2 D
 .S $P(^FSC("SPEC",DUZ,0),U,5)=$P($$NOW^XLFDT,".")
 .S DTIME=$S($P(^(0),U,12):$P(^(0),U,12),1:DTIME)
 .S ^TMP("FSC SESSION",$J)=DUZ_U_$P(^VA(200,DUZ,0),U)
 W !
 S (CNT,NUM)=0 F  S NUM=$O(^FSC("PARAM",1,1,NUM)) Q:NUM<1  S LINE=^(NUM,0) D  I CNT>9 Q
 .S CNT=CNT+1
 .W !,LINE
 Q
 ;
EXIT ; from exit action on option FSC MENU NOIS
 S DTIME=$$DTIME^XUP(DUZ)
 K ^TMP("FSC SESSION",$J)
 Q
