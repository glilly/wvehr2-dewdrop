RMPRPIYE ;PHX/RFM,RVD-EDIT ISSUE FROM STOCK ;3/8/05  08:04
 ;;3.0;PROSTHETICS;**61**;Feb 09, 1996
 ; RVD #61 - phase III of PIP enhancement.
 ;DBIA #227 - Read access to .01 field of file #445.
 ;DBIA #800 - FILEMAN look up of file #440.
 ;
EN ;EDIT STOCK ISSUES
 K RMPR6,RMPR11,RMPR11I,RMPR5,RMPR7I,RMPR7,RMPR9,RMPR1
 D HOME^%ZIS,DIV4^RMPRSIT G:$D(X) EXIT^RMPRPIYF
 S DIC("S")="S RM661=$G(^RMPR(660,+Y,1)) I ($P(^RMPR(660,+Y,0),U,13)=11),($P(^(0),U,10)=RMPR(""STA""))"
 ;I RMPRSITE=1 S DIC("S")=DIC("S")_"!($P(^(0),U,10)="""")"
 S DIC="^RMPR(660,",DIC(0)="AEMQ",DIC("A")="Select PATIENT: ",DIC("W")="D ^RMPRD1" D ^DIC G:Y<0 EXIT^RMPRPIYF
 S RMPRIEN=+Y
 L +^RMPR(660,+Y):1 I $T=0 W !,?5,$C(7),"Someone else is Editing this entry!" G EXIT^RMPRPIYF
 S (RMDFN,DFN)=$P(^RMPR(660,+Y,0),U,2)
 S RMPRF=$P(^RMPR(660,+Y,0),U,13)
INVSE S %X=DIC_+Y_",",%Y="R1(" D %XY^%RCR
 S %X=DIC_+Y_",",%Y="R1BCK(" D %XY^%RCR
 S:'$D(R1(2)) R1(2)="" S:'$D(R1BCK(2)) R1BCK(2)=""
 D DEM^VADPT
 S RMPRNAM=$P(VADM(1),U),RMPRSSN=$P(VADM(2),U)
 S (RMHCPC,RMHCNEW,RMHCOLD)=$P($G(R1(1)),U,4),(RSTCK,REDIT)=1,RMPRCOST=0
 S RMCPT=$P(R1(1),U,6)
 S (RMQNEW,RMQOLD)=$P($G(R1(0)),U,7)
 S (RMLOCNEW,RMLOCOLD,RMITNEW,RMITOLD)=""
 S RMSTOCK=$P($G(R1(1)),U,5)
 I $G(RMSTOCK) S R6612=$G(^RMPR(661.6,RMSTOCK,0)),(RMLOC,RMLOCNEW,RMLOCOLD)=$P(R6612,U,14),(RMIT,RMITNEW,RMITOLD)=$P(R1(2),U,1)
 S R12(0)=R1(0),RMPRREL=$P(R1(0),U,16)
 I $D(^RMPR(660,RMPRIEN,1)),+$P(^RMPR(660,RMPRIEN,1),U,3) S (RMPRIP,RIPOLD)=$P(^PRCP(445,$P(^RMPR(660,RMPRIEN,1),U,3),0),U)
 K DIC S R3("D")="",R4("D")=""
 G SET
 ;
CO ;DISPLAYS STOCK ISSUE
 D CHK^RMPRED5
 D ^RMPRPIYK
 ;
EDX ;POST
 S DIR(0)="SBO^P:POST;E:EDIT;D:DELETE"
 S DIR("A")="Would you like to POST/EDIT/DELETE this entry",DIR("B")="P",DIR("?")="Answer `P` to post the transaction, `E` to edit the transaction,'D' to delete the transaction"
 D ^DIR K DIR G:Y="P" POST^RMPRPIYF G:Y="D" DEL1^RMPRPIYF
 G:Y="E" EDT
 I $D(DIRUT)!$D(DUOUT)!$D(DTOUT) G EXIT^RMPRPIYF
DEL ;
 S DIR(0)="SBO^E:EDIT;D:DELETE",DIR("B")="E"
 S DIR("A")="Would you like to EDIT/DELETE this Transaction",DIR("?")="Answer 'E' to EDIT the transaction or 'D' to DELETE the transaction." D ^DIR
 I $D(DIRUT)!$D(DTOUT)!$D(DUOUT) G EXIT^RMPRPIYF
 I Y="E" G EDT
 I Y="D" G DEL1^RMPRPIYF
 ;
EDT ;edit patient 2319
 W @IOF,!?30,RMPRNAM,!
 ;
EDU S RMTY=$P(R1(0),U,4)
 K DIR W ! S DIR(0)="660,2",DIR("B")=$S(RMTY="I":"INITIAL ISSUE",RMTY="X":"REPAIR",RMTY="R":"REPLACE",RMTY="S":"SPARE",1:"")
 D ^DIR G:$D(DIRUT) CO S $P(R1(0),U,4)=Y,$P(R3("D"),U,4)=$S(Y="I":"INITIAL ISSUE",Y="X":"REPAIR",Y="R":"REPLACE",Y="S":"SPARE",1:"")
 S DIR(0)="660,62",DIR("B")=$P(R4("D"),U,3) D ^DIR G:$D(DIRUT) CO S $P(R1("AM"),U,3)=Y,$P(R4("D"),U,3)=$S(Y=1:"SC/OP",Y=2:"SC/IP",Y=3:"NSC/IP",Y=4:"NSC/OP",1:"")
 I Y<4 S $P(R1("AM"),U,4)="",$P(R4("D"),U,4)="" G 2
 K DIR I Y=4 S DIR(0)="660,63" S:$P(R1("AM"),U,4)?1N.N DIR("B")=$P(R4("D"),U,4) D ^DIR G:$D(DUOUT)!$D(DTOUT) CO G:$D(DIRUT)!(X="") 2
 I $P(R1("AM"),U,3)=4 S $P(R1("AM"),U,4)=Y,$P(R4("D"),U,4)=$S(Y=1:"SPECIAL LEGISLATION",Y=2:"A&A",Y=3:"PHC",Y=4:"ELIGIBILITY REFORM",1:"")
 ;
2 ;S DIC(0)="AEQM",DIC=661 S:$P(R1(0),U,6) DIC("B")=$P(^RMPR(661,$P(R1(0),U,6),0),U) S DIC("A")="ITEM: "
 ;
HCPCS ;scanning an item is mandatory.
 W ! D SCAN^RMPRPIYS
 I $P(R3("D"),U,6)&((RMPREXC="^")!(RMPREXC="P")) G CO^RMPRPIYE
 I RMPREXC="^" G CO^RMPRPIYE
 I RMPREXC="P" G CO^RMPRPIYE
 I RMPREXC="T" G CO^RMPRPIYE
 I RMPRBARC="",$G(REDIT) G VEN0
 I RMPRBARC="" G HCPCS
 D HCPCS3^RMPRPIY1
 ;set ALL variables based on the scanned label
 S $P(R1(0),U,6)=$G(RMPR11I("ITEM MASTER IEN"))
 S RMPRCOST=0
 I RMPR7("VALUE")>0,RMPR7("QUANTITY")>0 S RMPRCOST=RMPR7("VALUE")/RMPR7("QUANTITY")
 S $P(R1(0),U,16)=RMPRCOST
 S $P(R1(1),U,4)=RMDAHC
 S $P(R1(0),U,14)=RMPR11I("SOURCE")
 G VEN0
 ;
CPT ;ask for CPT Modifier
 K DIC,Y,RQUIT
 S RDA=RMDAHC_"^"_$P(R1(0),U,4)_"^"_$P(R1(0),U,14)_"^"_660
 D:$D(RMCPT) CHK^RMPRED5
 W:$G(REDIT) !,"OLD CPT MODIFIER: ",$P(R1(1),U,6)
 I RMHCOLD'=RMDAHC D CPT^RMPRCPTU(RDA) G:$D(DUOUT)!$D(DTOUT) CO S $P(R1(1),U,6)=$G(RMCPT) W:$G(REDIT) !,"NEW CPT MODIFIER: ",$G(RMCPT)
 I RMHCOLD'="",(RMHCOLD=RMDAHC),$G(REDIT) D
 .S DIR(0)="Y",DIR("A")="Would you like to Edit CPT MODIFIER Entry  ",DIR("B")="N" D ^DIR Q:$D(DTOUT)!$D(DUOUT)
 .I $G(Y) D
 ..S RMCPOLD=RMCPT
 ..D CPT^RMPRCPTU(RDA) Q:$D(DUOUT)!$D(DUOUT)  S $P(R1(1),U,6)=$G(RMCPT)
 ..W:RMCPOLD=RMCPT !!,"*** Based on the information given above, CPT Modifier string has not changed!!!",!
 ..W:RMCPOLD'=RMCPT !,"NEW CPT MODIFIER: ",$G(RMCPT)
 K DIR
 ;
VEN0 ;process vendor
 K DIC,DIR
 S:$D(RMPR6("VENDOR")) DIC("B")=RMPR6("VENDOR")
 S:'$D(RMPR6("VENDOR")) DIC("B")=$P(R1(0),U,9)
 S DIC(0)="AEQM"
 ;S DIC("S")="I $D(RMPRVEN(+Y))"
 S DIC("A")="VENDOR:"
 S DIC="^PRC(440,",DIC(0)="AEQM" D ^DIC I $D(DUOUT)!$D(DTOUT) G CO
 G:+Y<0 VEN0
 S $P(R1(0),U,9)=+Y,$P(R3("D"),U,9)=$P(Y,U,2) K DIR,DIC
 ;
SOURCE ;
 K DIR S DIR(0)="660,12",DIR("B")=$P(R1(0),U,14),DIR("A")="SOURCE"
 D ^DIR G:$D(DIRUT)!$D(DUOUT) CO^RMPRPIYE G:$D(DTOUT) EXIT^RMPRPIYF
 S $P(R1(0),U,14)=Y,$P(R3("D"),U,14)=$S(Y="C":"Commercial",1:"VA")
 ;
QTY K DIR S DIR("A")="QUANTITY"
 S DIR(0)="660,5",DIR("B")=$P(R1(0),U,7)
 D ^DIR G:$D(DIRUT)!$D(DUOUT) CO^RMPRPIYE G:$D(DTOUT) EXIT^RMPRPIYF
 I $D(RMUBA),((RMUBA+$P(R1(0),U,7))-Y<0) D LOWBA^RMPRPIYI G HCPCS^RMPRPIYE
 S $P(R1(0),U,7)=Y K DIR
 ;
CP G ^RMPRPIYF
 ;
SET ;set the original variables.
 S $P(R3("D"),U,14)=$S($P(R1(0),U,14)="V":"VA",$P(R1(0),U,14)="C":"COMMERCIAL",1:"")
 S $P(R3("D"),U,4)=$S($P(R1(0),U,4)="I":"INITIAL ISSUE",$P(R1(0),U,4)="X":"REPAIR",$P(R1(0),U,4)="R":"REPLACE",$P(R1(0),U,4)="S":"SPARE",1:"")
 S $P(R4("D"),U,3)=$S($P(R1("AM"),U,3)=1:"SC/OP",$P(R1("AM"),U,3)=2:"SC/IP",$P(R1("AM"),U,3)=3:"NSC/IP",$P(R1("AM"),U,3)=4:"NSC/OP")
 S:$P(R1("AM"),U,3)=4&($P(R1("AM"),U,4)) $P(R4("D"),U,4)=$S($P(R1("AM"),U,4)=1:"SPECIAL LEGISLATION",$P(R1("AM"),U,4)=2:"A&A",$P(R1("AM"),U,4)=3:"PHC",$P(R1("AM"),U,4)=4:"ELIGIBILITY REFORM",1:"")
 S RMHCOLD=$P($G(R1(1)),U,4),RMPRPF=$P(R1(0),U,13),RMQOLD=$P(R1(0),U,7)
 S RMSO=$P(R1(0),U,14)
 I $G(RMQOLD),$P($G(R1(0)),U,16) S RMPRCOST=$P(R1(0),U,16)/RMQOLD
 S $P(R3("D"),U,6)=$P(^RMPR(661,$P(R1(0),U,6),0),U,1),RITOLD=$P(R1(0),U,6),RMQOLD=$P(R1(0),U,7),Y=$P(R1(0),U,12) G:Y="" CO D DD^%DT S $P(R3("D"),U,12)=Y
 S Y=$P(R1(1),U,8) G:Y="" CO D DD^%DT S $P(R1("D"),U,8)=Y
 D ^RMPRPIYK G DEL
 Q
 ;
SET60 ;
 ;RMPR60 -array of data fields for 660 file record.
 S RMPR60("ISSUE TYPE")=$P(R1(0),U,4)
 S RMPR60("IFCAP ITEM")=$P(R1(0),U,6)
 S RMPR60("QUANTITY")=$P(R1(0),U,7)
 S RMPR60("UNIT")=$P(R1(0),U,8)
 S RMPR60("VENDOR IEN")=$P(R1(0),U,9)
 S RMPR60("SERIAL NUM")=$P(R1(0),U,11)
 S RMPR60("DELIV DATE")=$P(R1(0),U,12)
 S RMPR60("DATE OF SERVICE")=$P(R1(1),U,8)
 S RMPR60("SOURCE")=$P(R1(0),U,14)
 S RMPR60("COST")=$P(R1(0),U,16)
 S RMPR60("REMARKS")=$P(R1(0),U,18)
 S RMPR60("LOT NUM")=$P(R1(0),U,24)
 S RMPR60("CPT IEN")=$P(R1(0),U,22)
 S RMPR60("USER")=$P(R1(0),U,27)
 S RMPR60("CPT MOD")=$P(R1(1),U,6)
 S RMPR60("HCPCS")=$P(R1(1),U,4)
 S RMPR60("PAT CAT")=$P(R1("AM"),U,3)
 S RMPR60("SPEC CAT")=$P(R1("AM"),U,4)
 S RMPR60("VENDOR")=$P(R1(0),U,9)
 S:$G(RMDAHC) RMPR60("HCPCS")=RMDAHC
 ;S:$D(RMPR11I("HCPCS")) RMPR60("HCPCS")=RMPR11I("HCPCS")
 S:$D(RMPR11I("ITEM")) RMPR60("ITEM")=RMPR11I("ITEM")
 S:$D(R1("DATE&TIME")) RMPR60("DATE&TIME")=R1("DATE&TIME")
 S RMPR60("VALUE")=RMPR60("COST")
 S:'$D(RMPR11I("STATION")) RMPR11I("STATION")=$G(RMPR("STA"))
 S:$P(R1("AM"),U,3)'=4 RMPR60("SPEC CAT")="@"
 Q
