FSCFORMC ;SLC/STAFF-NOIS Format Custom ;5/18/98  12:38
 ;;1.1;NOIS;;Sep 06, 1998
 ;
CUSTOM ; from FSCFORM
 N ABBREV,CNT,IEN,LINE,SECTION,TEXT
 S CNT=0 F  S CNT=$O(STYLE(CNT)) Q:CNT<1  S ABBREV=$P(STYLE(CNT),U,7) I $L(ABBREV) S FIELD(ABBREV)=""
 D GET^FSCGET("CUSTOM",CALLNUM,.FIELD)
 N FORMAT K FORMAT
 S SECTION=1,(CNT,LINE)=0 F  S CNT=$O(STYLE(CNT)) Q:CNT<1  S ABBREV=$P(STYLE(CNT),U,7) I $L(ABBREV),ABBREV'="REF",ABBREV'="SUBJECT" D
 .S LINE=LINE+1 S TEXT=$J($P(STYLE(CNT),U,2)_": ",14)_$P(FIELD(ABBREV),U,2)
 .D SETTEXT^FSCFORM(SECTION,LINE,2,TEXT)
 .I ABBREV="DESC" D
 ..S SECTION=SECTION+1
 ..D SETMULT^FSCFORM(SECTION,"^FSCD(""CALL"","_CALLNUM_",30)")
 ..S SECTION=SECTION+1,LINE=0
 .I ABBREV="ACTIONS" D
 ..S SECTION=SECTION+1
 ..D SETMULT^FSCFORM(SECTION,"^FSCD(""CALL"","_CALLNUM_",50)")
 ..S SECTION=SECTION+1,LINE=0
 .I ABBREV="SUM" D
 ..S SECTION=SECTION+1
 ..D SETMULT^FSCFORM(SECTION,"^FSCD(""CALL"","_CALLNUM_",80)")
 ..S SECTION=SECTION+1,LINE=0
 .I ABBREV="STATHIST" D
 ..S SECTION=SECTION+1
 ..D SETMULT^FSCFORM(SECTION,"^FSCD(""CALL"","_CALLNUM_",110)")
 ..S SECTION=SECTION+1,LINE=0
 .I ABBREV="PNOTE" D
 ..S IEN=+$G(^FSCD("CALL USER","AUC",DUZ,CALLNUM))
 ..I 'IEN Q
 ..S SECTION=SECTION+1
 ..D SETMULT^FSCFORM(SECTION,"^FSCD(""CALL USER"","_IEN_",2)")
 ..S SECTION=SECTION+1,LINE=0
 I TYPE["VIEW" D VIEW^FSCFORMU(LISTNUM,CALLNUM,.FORMAT,STYLE,.LASTLINE,TYPE) Q
 D SETUP^FSCFORMU(LISTNUM,CALLNUM,.FORMAT,STYLE,.LASTLINE,TYPE)
 Q
