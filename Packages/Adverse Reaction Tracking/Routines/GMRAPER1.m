GMRAPER1 ;HIRMFO/RM-REACTION EDIT MODULE DELETE REACTIONS ;12/1/95  13:52
 ;;4.0;Adverse Reaction Tracking;;Mar 29, 1996
DELREAC ; DELETE SIGNS/SYMPTOMS FROM PATIENT DATA
 K DIR S DIR(0)="LOA^1:"_GMRACTR
 S DIR(0)=DIR(0)_"^I X[""."" W !,""DO NOT USE DECIMAL VALUES."",$C(7) K X Q"
 S DIR("A")="Delete which signs/symptoms: "
 S DIR("?",1)="PLEASE ENTER THE NUMBERS OF THE SIGNS/SYMPTOMS YOU WOULD LIKE TO DELETE."
 S DIR("?",2)="RANGES CAN BE SEPARATED BY A HYPHEN (-) AND GROUPS OF NUMBERS,"
 S DIR("?")="SEPARATED BY A COMMA (,)."
 D ^DIR K DIR
 I $D(DTOUT)!($D(DUOUT)) S GMRAOUT=1 Q
 S GMRADO=Y
 F Y=1:1:$L(GMRADO,",") S GMRAY=$P(GMRADO,",",Y) I +GMRAY D DEL
 Q
VALD ; VALIDATE CHOICE NUMBER
 I +GMRAPC(1)'=GMRAPC(1)!(GMRAPC(1)>GMRACTR)!(GMRAPC(1)<1) S GMRAOK=0
 Q
DEL ;
 S GMRARECN=$P(GMRACHC(GMRAY),"^"),GMRAREAC=$P(GMRACHC(GMRAY),"^",2)
 I GMRARECN'=GMRAOTH S GMRARDL(GMRARECN)="" K GMRARPR(GMRAREAC,GMRARECN),GMRARAD(GMRARECN)
 E  K GMRAROT(GMRAREAC),GMRARPR(GMRAREAC,GMRARECN) S GMRAROTD(GMRAREAC)=""
 Q
