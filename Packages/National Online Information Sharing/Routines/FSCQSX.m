FSCQSX ;SLC/STAFF-NOIS Query Search Exists ;4/22/94  12:01
 ;;1.1;NOIS;;Sep 06, 1998
 ;
EX ; from FSCQS
 I LIST=1 D  Q
 .S NODE=+$P(CODE,";"),PIECE=$P(CODE,";",2)
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..I $L($P($G(^FSCD("CALL",CALL,NODE)),U,PIECE)) X ACTION
 I LIST=0 D  Q
 .S CVALUE="" F  S CVALUE=$O(^FSCD("CALL",INDEX,CVALUE)) Q:CVALUE=""  D
 ..S CALL=0 F  S CALL=$O(^FSCD("CALL",INDEX,CVALUE,CALL)) Q:CALL<1  D
 ...I $D(^TMP("FSC USELIST",$J,CALL)) X ACTION
 I LIST="" D  Q
 .S CVALUE="" F  S CVALUE=$O(^FSCD("CALL",INDEX,CVALUE)) Q:CVALUE=""  D
 ..S CALL=0 F  S CALL=$O(^FSCD("CALL",INDEX,CVALUE,CALL)) Q:CALL<1  X ACTION
 Q
NEX ; from FSCQS
 I LIST=1!(LIST=0) D  Q
 .S NODE=+$P(CODE,";"),PIECE=$P(CODE,";",2)
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..I '$L($P($G(^FSCD("CALL",CALL,NODE)),U,PIECE)) X ACTION
 I LIST="" D  Q
 .S NODE=+$P(CODE,";"),PIECE=$P(CODE,";",2)
 .S CALL=0 F  S CALL=$O(^FSCD("CALL",CALL)) Q:CALL<1  D
 ..I '$L($P($G(^FSCD("CALL",CALL,NODE)),U,PIECE)) X ACTION
 Q
