IMRPLAB ;HCIOFO/FAI-LOOKUP LAB TEST VALUES ;11/13/01  09:39;
 ;;2.1;IMMUNOLOGY CASE REGISTRY;**5,16**;Feb 09, 1998
TYPE ; Entry with IMRFN defined and pointers for local lab test name & NLT
 ; FIND TYPE OF TEST EX:VIRAL LOAD
 D HEAD,DATA,SORT
 D KILL
 Q
DATA K IMRCD
 I $G(IMRTSTLR)="" W !!,?13,"** SORRY NO LABORATORY REFERENCE IN PLACE **" Q
 S (IMRTSTI,IMRTSTII)="",ILR=IMRTSTLR
 D ^IMRTDSP
CHEMS S LDT="" F  S LDT=$O(^LR(ILR,"CH",LDT)) Q:LDT=""  D LINK
 Q
LINK S DNAM="" F  S DNAM=$O(IMRVALS(DNAM)),LDR="" Q:DNAM=""  D
 . F  S LDR=$O(IMRVALS(DNAM,LDR)) Q:LDR=""  S GRP=$P(IMRVALS(DNAM,LDR),U,1),TYP=$P(IMRVALS(DNAM,LDR),U,2),LNM=$P(IMRVALS(DNAM,LDR),U,3) D LVAL
 Q
LVAL S LRES=$P($G(^LR(ILR,"CH",LDT,DNAM)),U,1),DTRC=$P($G(^LR(ILR,"CH",LDT,0)),U,1),Y=DTRC D DD^%DT S DTAA=Y D PLBS
 Q
PLBS Q:LRES=""
 Q:(LRES["CANC")!(LRES["canc")
 Q:(DTRC["CANC")!(DTRC["canc")
 Q:(DTRC<IMRHNBEG)!(DTRC>IMRHNEND)
 S DTAA=$E(DTAA,1,18),LDO=$E(LDT,1,9)
 S ^TMP("V",$J,TYP,LDT,LRES,DNAM)=LNM_U_GRP_U_LDR_U_DTRC_U_DTAA
 Q
HEAD W !,?3,">>>>> Please wait. Searching for Viral Load & CD4 Values.....",!!,"Type of Test",?23,"Date/Time",?44,"Name",?70,"Result"
 W !,"------------",?23,"---------",?44,"-----",?70,"------"
 Q
SORT I '$D(^TMP("V",$J)) W !,"**NO DATA FOUND**" Q
 S TY=""
 S I="" F  S I=$O(^TMP("V",$J,I)) Q:I=""  S T="" F J=0:0 S T=$O(^TMP("V",$J,I,T)) Q:T'>0  S G="" F K=0:0 S G=$O(^TMP("V",$J,I,T,G)) Q:G=""  S H="" F M=0:0 S H=$O(^TMP("V",$J,I,T,G,H)) Q:H=""  D SCR
 Q
SCR S RC=^TMP("V",$J,I,T,G,H),LN=$P(RC,U,1),IMDATE=$P(RC,U,5)
 W:TY'=I !
 W !,$E(I,1,14),?17,IMDATE,?41,$E(LN,1,23),?61,$E($J(G,15),1,20)
 S TY=I
 Q
KILL ; kill variables
 K ^TMP("V",$J),CDAR,CDP,HVR,IMDATE,IMRCD4,IMRCD4D,IMRCD4E,IMRCD4X,IMRCDX,IMRCDXD,IMREDIT,IMRESULT,IMRLOOP,IMRPN,X,Y
 K HIVNM,IMRLNODE,IMRLTEST,IMRP103,IMRSTN,IMRTSTI,IMRTSTII,IMRVLIEN,IMRXCAT,LCDD,LLOC,MDT,PLOW,RC
 Q
