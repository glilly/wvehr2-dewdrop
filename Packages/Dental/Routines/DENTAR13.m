DENTAR13 ;ISC2/SAW,HAG-REVIEW/RELEASE TREATMENT DATA SUMMARY REPORT FOR CLINIC ; 10/27/88  10:33 AM ;
 ;VERSION 1.2
 S %ZIS="MQ" K IO("Q") D ^%ZIS G EXIT:IO=""
 I $D(IO("Q")) S ZTRTN="QUE^DENTAR13",ZTSAVE("DENT*")="",ZTSAVE("H*")="",ZTSAVE("U")="",ZTSAVE("Z")="",ZTSAVE("Z1")="",ZTSAVE("Z2")="",ZTSAVE("Z3")="",ZTSAVE("Z4")="",ZTSAVE("Z5")="" D ^%ZTLOAD K ZTSK,ZTRTN,ZTSAV G EXIT
QUE U IO S (DENTC(1),DENTC)=0,DENTSD=DENTSD-.0001,Q=1,DT1=$E(DT,1,5)_"08" D RPT G NONE:'DENTC D A^DENTAR15 G EXIT
RPT F I=0:0 S DENTSD=$O(^DENT(221,"A",Z3,DENTSD)) Q:DENTSD>DENTED!(DENTSD="")!(DT<DT1&($E(DENTSD,1,5)=$E(DT,1,5)))  S DENT="" F K=0:0 S DENT=$O(^DENT(221,"A",Z3,DENTSD,DENT)) Q:DENT=""  D:$D(^DENT(221,DENT,0)) P1
 Q
P1 I $D(^DENT(221,DENT,.1)),$P(^(.1),U,1) Q
 S DENTC=DENTC+1,X=^DENT(221,DENT,0) I '$D(DENTF1) S ^UTILITY($J,"DENTP",DENT)=DENTSD I DENTC=181 S:IO=IO(0) ^UTILITY($J,"DENTV",Q,DENTC-1,0)=^UTILITY($J,"DENTV",Q,DENTC-1,0)_"$" S Q=Q+1,DENTC(1)=DENTC(1)+180,DENTC=1
 I $P(X,U,27)'=""!($P(X,U,44)'="") D SPOT Q
P1A D CHK^DENTAR15 Q:DENTF  S DENTCAT=$P(X,U,19)
 I IO=IO(0),'$D(DENTF1),'$D(DENTV) D DENTV
 I $P(X,U,41) S X(2)=0_$P(X,U,41),^UTILITY($J,"DENTR",+X(2),DENTCAT)=$S($D(^UTILITY($J,"DENTR",+X(2),DENTCAT)):^(DENTCAT)+1,1:1) I IO=IO(0),'$D(DENTF1) S DENTV=DENTV_X(2)_"01"
 I $P(X,U,8) S ^UTILITY($J,"DENTR",39,DENTCAT)=$S($D(^UTILITY($J,"DENTR",39,DENTCAT)):^(DENTCAT)+1,1:1) I IO=IO(0),'$D(DENTF1) S DENTV=DENTV_3501
 I $P(X,U,7)'="" S X(2)=$S($P(X,U,7)="S":"04",1:"05"),^UTILITY($J,"DENTR",+X(2),DENTCAT)=$S($D(^UTILITY($J,"DENTR",+X(2),DENTCAT)):^(DENTCAT)+1,1:1)
 I $P(X,U,7)'="",IO=IO(0),'$D(DENTF1) S DENTV=DENTV_X(2)_"01"
 F M=9,11:1:18,20,22:1:26,28:1:38,42:1:43 I $P(X,U,M) D P11
 I IO=IO(0),'$D(DENTF1) S DENTV=DENTV_"                                                   ",^UTILITY($J,"DENTV",Q,DENTC,0)=$E(DENTV,1,80) K DENTV
 Q
P11 S X(2)=$P($T(S),";",M),X(3)=$P(X,U,M),X(3)=0_X(3),X(3)=$E(X(3),($L(X(3))-1),$L(X(3))),^UTILITY($J,"DENTR",+X(2),DENTCAT)=$S($D(^UTILITY($J,"DENTR",+X(2),DENTCAT)):^(DENTCAT)+X(3),1:+X(3))
 I IO=IO(0),'$D(DENTF1) S DENTV=DENTV_X(2)_X(3)
 Q
SPOT S X(1)=$S($P(X,U,44)'="":$P(X,U,44),1:$P(X,U,27)),X(2)=$S(X(1)=1:35,X(1)=2:36,1:37)
 D CHK^DENTAR15 Q:DENTF  S DENTCAT=$P(X,U,19)
 S ^UTILITY($J,"DENTR",X(2),DENTCAT)=$S($D(^UTILITY($J,"DENTR",X(2),DENTCAT)):^(DENTCAT)+1,1:1)
 I $P(X,U,45) S ^UTILITY($J,"DENTR",38,DENTCAT)=$S($D(^UTILITY($J,"DENTR",38,DENTCAT)):^(DENTCAT)+$P(X,U,45),1:$P(X,U,45))
 S DENTCAT="00"_DENTCAT,DENTCAT=$E(DENTCAT,($L(DENTCAT)-2),$L(DENTCAT)),DENTDAT=$P(X,U,1),DENTDAT=$E(DENTDAT,4,5)_$E(DENTDAT,6,7)_$E(DENTDAT,2,3)
 I IO=IO(0),'$D(DENTF1) S ^UTILITY($J,"DENTV",Q,DENTC,0)=$S($P(X,U,5)="D":"C",1:2)_DENTSTA_$P(X,U,10)_$E($P(X,U,2),1,9)_"0000"_DENTDAT_DENTCAT_X(1)_$S(X(1)=2:" ",$P(X,U,45)="":0,1:$P(X,U,45))_"                                              "
 Q
DENTV S X(1)=$P(X,U,6),X(1)=$S(X(1)="":"  ",1:0_X(1)),X(1)=$E(X(1),$L(X(1))-1,$L(X(1))),DENTDAT=$P(X,U,1),DENTDAT=$E(DENTDAT,4,5)_$E(DENTDAT,6,7)_$E(DENTDAT,2,3)
 S DENTCAT1="00"_DENTCAT,DENTCAT1=$E(DENTCAT1,$L(DENTCAT1)-1,$L(DENTCAT1))
 S DENTV=$S($P(X,U,5)="C":6,$P(X,U,5)="D":"B",1:1)_DENTSTA_$P(X,U,10)_$E($P(X,U,2),1,9)_DENTCAT1_X(1)_DENTDAT Q
NONE S DENTF1=1 W !,"There is no treatment data for review/release for the time frame you specified",*7 G EXIT
S ;;;;;;;;08;;09;15;16;33;10;20;21;22;;23;;11;12;13;14;17;;24;25;26;27;28;29;30;31;18;19;32;;;;34;06
EXIT X ^%ZIS("C") K DENT,DENTCAT,DENTCAT1,DENTDAT,DENTED,DENTSD,I,K,M,X D:$D(ZTSK) EXIT1^DENTAR1 Q
