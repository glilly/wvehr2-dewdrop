LRARCMR1 ;DALISC/CKA - ARCHIVED WKLD STATS REPORT - STD/QC/RPT/MAN PRINT ; 5/22/95
 ;;5.2;LAB SERVICE;**59**;Aug 31, 1995
 ;same as LRCAPMR1 except archived wkld file
 ;
INITMAN ;Called by: LRARCMA1,LRARCML1,LRRP8B
 K ^TMP("LRAR",$J,"GCOM")
 K ^TMP("LRAR",$J,"CCOM")
 K ^TMP("LRAR",$J,"DCOM")
 K ^TMP("LRAR",$J,"CCN")
 S (LRGSTND,LRGQC,LRGRPT,LRGMANL,LRGCN,LRCCN,LRDCN)=0
 Q
CLNMAN ;Called by: LRARCMA,LRARCML,LRRP8
 K ^TMP("LRAR",$J,"GCOM")
 K ^TMP("LRAR",$J,"CCOM")
 K ^TMP("LRAR",$J,"DCOM")
 K ^TMP("LRAR",$J,"CCN")
 K LRGSTND,LRGQC,LRGRPT,LRGMANL,LRGCN,LRCCN,LRDCN
 Q
PRNTMAN ;Called from LRARCMA2,LRARCML2,LRRP8C
 N LRSKIP,LRSTND,LRQC,LRRPT,LRMANL,LRCAPNUM,LRHDR,LRHDR3,LRCLHDR
 S LRHDR="ARCHIVED WORKLOAD INPUT MANUALLY"
 S LRHDR3="[Includes all manual archived workload data for date range]"
 S LRCLHDR="Workload Procedure              Code      STANDARD       QC   REPEAT   MANUAL  "
 D HDR^LRARCU
 I '((LRGSTND)!(LRGQC)!(LRGRPT)!(LRGMANL)) D
 . W !!,"  *** NO SQRM DATA FOR THIS REPORT ***",!!
 . D:$E(IOST,1,2)="C-" PAUSE^LRARCU Q:LREND  W @IOF
 . S LRSKIP=1
 Q:$G(LRSKIP)!(LREND)
 S LRCAPNAM=""
 F  S LRCAPNAM=$O(^TMP("LRAR",$J,"CCN",LRCAPNAM)) Q:(LRCAPNAM="")!(LREND)  D
 . S LRSQRM=$G(^TMP("LRAR",$J,"CCN",LRCAPNAM,"SQRM",0))
 . S LRSTND=+$P(LRSQRM,U),LRQC=+$P(LRSQRM,U,2),LRRPT=+$P(LRSQRM,U,3)
 . S LRMANL=+$P(LRSQRM,U,4),LRCAPNUM=$P(LRSQRM,U,5)
 . Q:'(LRSTND+LRQC+LRRPT+LRMANL)
 . I $Y+6'<IOSL D NPG^LRARCU Q:LREND
 . W $E(LRCAPNAM,1,30),?32,LRCAPNUM,?43,$J(LRSTND,7)
 . W ?52,$J(LRQC,7),?61,$J(LRRPT,7),?70,$J(LRMANL,7),!
 Q:LREND
 W !!,"Grand SQRM Totals: ",?43,$J(LRGSTND,7),?52,$J(LRGQC,7)
 W ?61,$J(LRGRPT,7),?70,$J(LRGMANL,7),!
 D:$E(IOST,1,2)="C-" PAUSE^LRARCU Q:LREND  W @IOF
 Q
BMPMANL ;Count WKLD entered manually
 ;Called by: LRARCMA1,LRARCML1,LRRP8B
 S $P(^TMP("LRAR",$J,"CCN",LRCAPNAM,"SQRM",0),U,5)=LRCAPNUM
 S LRMNODE=$G(^LAR(64.19999,LRIN,1,LRCDTN,1,LRCCN,"S"))
 ;Grand totals for manual stuff
 S LRGSTND=LRGSTND+$P(LRMNODE,U)
 S LRGQC=LRGQC+$P(LRMNODE,U,2)
 S LRGRPT=LRGRPT+$P(LRMNODE,U,3)
 S LRGMANL=LRGMANL+$P(LRMNODE,U,4)
 ;WKLD code totals for manual stuff
 S LRSQRM=$G(^TMP("LRAR",$J,"CCN",LRCAPNAM,"SQRM",0))
 S $P(LRSQRM,U)=$P(LRSQRM,U)+$P(LRMNODE,U)
 S $P(LRSQRM,U,2)=$P(LRSQRM,U,2)+$P(LRMNODE,U,2)
 S $P(LRSQRM,U,3)=$P(LRSQRM,U,3)+$P(LRMNODE,U,3)
 S $P(LRSQRM,U,4)=$P(LRSQRM,U,4)+$P(LRMNODE,U,4)
 S ^TMP("LRAR",$J,"CCN",LRCAPNAM,"SQRM",0)=LRSQRM
 Q
GENCOM ;Called by: LRARCMA1,LRARCML1,LRRP8B
 S LRCOM=0
 F  S LRCOM=$O(^LAR(64.19999,LRIN,2,LRCOM)) Q:'LRCOM  D
 . S LRGCN=LRGCN+1
 . S ^TMP("LRAR",$J,"GCOM",LRGCN)=$G(^LAR(64.19999,LRIN,2,LRCOM,0))
 Q
CAPCOM ;Called by: LRARCMA1,LRARCML1,LRRP8B
 S LRCC=0
 F  S LRCC=$O(^LAR(64.19999,LRIN,3,LRCC)) Q:'LRCC  D
 . I $G(LRCAPS) Q:'$D(LRCAPS(LRCC))
 . S LRCAPNAM=$$WKLDNAME^LRARCU(LRCC)
 . S ^TMP("LRAR",$J,"CCOM",LRCAPNAM,0)=LRCAPNUM
 . S LRCOM=0
 . F  S LRCOM=$O(^LAR(64.19999,LRIN,3,LRCC,1,LRCOM)) Q:'LRCOM  D
 . . S LRCCN=LRCCN+1
 . . S ^TMP("LRAR",$J,"CCOM",LRCAPNAM,LRCCN)=$G(^LAR(64.19999,LRIN,3,LRCC,1,LRCOM,0))
 Q
DATCOM ;Called by: LRARCMA1,LRARCML1,LRRP8B
 S LRCOM=0
 F  S LRCOM=$O(^LAR(64.19999,LRIN,1,LRCDTN,2,LRCOM)) Q:'LRCOM  D
 . S LRDCN=LRDCN+1
 . S ^TMP("LRAR",$J,"DCOM",LRCDT,LRDCN)=$G(^LAR(64.19999,LRIN,1,LRCDTN,2,LRCOM,0))
 Q
GETA ;Get pointer value for file 68
 K DIC S DIC=68,DIC(0)="XMZ"
 D ^DIC Q:Y=-1
 Q
