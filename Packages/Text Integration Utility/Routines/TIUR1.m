TIUR1 ; SLC/JER - Integrated Document Review ;11/01/03
 ;;1.0;TEXT INTEGRATION UTILITIES;**79,100,113,112**;Jun 20, 1997
 ; 11/30/00 moved PUTLIST & ADDELMNT here from TIUR. 
 ; 12/12/00 moved PUTLIST, ADDELMNT, PARENT, EXPANDKD to TIUR2
GATHER(TIUI,TIUPREF,CLASS,STATIFNS,EARLY,LATE,XREF,SCREEN) ; Find/sort
 ; Uses title info from ^TMP("TIUTYP",$J) array, set in SELTYP^TIULA
 N TIUT,TIUTP,TIUS,TIUSTAT,TIUSFLD,TIUD0,TIUD12,TIUD13,TIUD15
 N TIUIFN,TIULD
 S TIUSFLD=$P(TIUPREF,U,3)
 S TIUSFLD=$S(TIUSFLD="P":".02",TIUSFLD="D":".01",TIUSFLD="S":".05",TIUSFLD="C":"1507",TIUSFLD="A":"1202",TIUSFLD="E":"1208",1:"1301")
 S TIUT=0 F  S TIUT=$O(^TMP("TIUTYP",$J,TIUT)) Q:+TIUT'>0  D
 . S TIUTP=+$P($G(^TMP("TIUTYP",$J,TIUT)),U,2) Q:TIUTP'>0
 . S TIUS=1 F  S TIUSTAT=$P(STATIFNS,";",TIUS) Q:'TIUSTAT  D
 . . S TIUS=TIUS+1
 . . S (TIUJ,TIULD)=LATE
 . . F  S TIUJ=$O(^TIU(8925,XREF,TIUI,TIUTP,TIUSTAT,TIUJ)) Q:+TIUJ'>0!(+TIUJ>EARLY)  D
 . . . S TIUIFN=0
 . . . F  S TIUIFN=$O(^TIU(8925,XREF,TIUI,TIUTP,TIUSTAT,TIUJ,TIUIFN)) Q:+TIUIFN'>0  D
 . . . . I ($P(TIUPRM0,U,6)="S"),(+$$CANDO^TIULP(TIUIFN,"VIEW")'>0) Q
 . . . . I +$G(ORVP),(+$P($G(^TIU(8925,+TIUIFN,0)),U,2)'=+$G(ORVP)) Q
 . . . . I TIUTP=81,(^TMP("TIUTYP",$J)>1),($P(^TMP("TIUTYP",$J,TIUT),U,4)="NOT PICKED"),(+$P($G(^TIU(8925,+TIUIFN,0)),U,5)>6) Q
 . . . . I TIUTP=81,(^TMP("TIUTYP",$J)>1),($P(^TMP("TIUTYP",$J,TIUT),U,4)="NOT PICKED"),'+$$DADINTYP(TIUIFN) Q
 . . . . S TIUQ=$$RESOLVE(TIUIFN,TIUSFLD)
 . . . . ; S ^TMP("TIUI",$J,TIUQ,TIUJ,TIUIFN)="",TIULD=TIUJ ; P113
 . . . . ; TIUK=1: 1 search category chosen (SCREEN="")
 . . . . ; TIUK>1: 2+ search categories chosen (SCREEN=1)
 . . . . ; ALL my unsigned (SCREEN="ALL")
 . . . . I SCREEN=1!(SCREEN="ALL") S ^TMP("TIUI",$J,TIUQ,TIUJ,TIUIFN)=1
 . . . . E  D
 . . . . . I $D(^TMP("TIUI",$J,TIUQ,TIUJ,TIUIFN)),$G(^TMP("TIUI",$J,TIUQ,TIUJ,TIUIFN))=(SCREEN-1) S ^TMP("TIUI",$J,TIUQ,TIUJ,TIUIFN)=SCREEN
 . . . . S TIULD=TIUJ
 I +$G(TIUQUIK)=1,+$G(TIULD) D ADDSIGN(DUZ,CLASS,TIULD,TIUSFLD)
 Q
DADINTYP(TIUDA) ; addendum's parent belong? 12/1/00 Removed param TYPES
 N TIUI,TIUDTYP,TIUY S (TIUI,TIUY)=0
 S TIUDTYP=+$G(^TIU(8925,+$P($G(^TIU(8925,+TIUDA,0)),U,6),0))
 F  S TIUI=$O(^TMP("TIUTYP",$J,TIUI)) Q:+TIUI'>0!+TIUY  D
 . I +$P(^TMP("TIUTYP",$J,TIUI),U,2)=TIUDTYP S TIUY=1
 Q TIUY
RESOLVE(DA,DR) ; resolve sort field values
 N TIUD0,TIUD12,TIUD13,TIUD15,TIUY
 S TIUD0=$G(^TIU(8925,+DA,0)),TIUD12=$G(^TIU(8925,+DA,12))
 S TIUD13=$G(^TIU(8925,+DA,13)),TIUD15=$G(^TIU(8925,+DA,15))
 I DR=.01 S TIUY=$$PNAME^TIULC1(+TIUD0) G RESX
 I DR=.02 S TIUY=$$PTNAME^TIULC1(+$P(TIUD0,U,2)) G RESX
 I DR=.05 S TIUY=$P(TIUD0,U,5) G RESX
 I DR=1202 S TIUY=$$PERSNAME^TIULC1(+$P(TIUD12,U,2)) S:TIUY="UNKNOWN" TIUY="" G RESX
 I DR=1208 S TIUY=$$PERSNAME^TIULC1(+$P(TIUD12,U,8)) S:TIUY="UNKNOWN" TIUY="" G RESX
 I DR=1301 S TIUY=$P(TIUD13,U) G RESX
 ;I DR=1507,($P(TIUD0,U,5)=7),(+$P(TIUD15,U,7)'>0) S DR=1501
 I DR=1507,(($P(TIUD0,U,5)=7)!($P(TIUD0,U,5)=8)),(+$P(TIUD15,U,7)'>0) S DR=1501 ;TIU*1*100 amended notes were sorting at top w sortval=ZZZZEMPTY for sortfld=complete, even tho they had sign date and displayed it.
 I DR=1501 S TIUY=$P(TIUD15,U) G RESX
 I DR=1507 S TIUY=$P(TIUD15,U,7)
RESX I $G(TIUY)']"" S TIUY="ZZZZEMPTY"
 Q TIUY
 ;
ADDSIGN(USER,CLASS,DATE,SORTBY) ; Get documents for which the user is the additional signer
 N TIUI,TIUY S TIUI=0
 D NEEDSIG^TIULX(.TIUY,USER,CLASS)
 F  S TIUI=$O(@TIUY@(TIUI)) Q:+TIUI'>0  D
 . N TIUDA,TIUD13,TIUQ,TIUJ
 . S TIUDA=+$G(@TIUY@(TIUI)),TIUD13=$G(^TIU(8925,TIUDA,13))
 . S TIUQ=$$RESOLVE(TIUDA,SORTBY),TIUJ=9999999-+TIUD13
 . ; S ^TMP("TIUI",$J,TIUQ,TIUJ,TIUDA)="" ; P113
 . S ^TMP("TIUI",$J,TIUQ,TIUJ,TIUDA)=1
 K @TIUY
 Q
