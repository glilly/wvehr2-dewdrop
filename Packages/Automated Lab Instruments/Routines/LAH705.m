LAH705 ;SLC/RWF - HITACHI 704/705 ;7/20/90  08:37 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID OR CUP
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 S TOUT=0 D IN G QUIT:TOUT,LA2:"EN"'[$E(IN,1)
 S Y(1)=IN
 F IX=16:10 S V=$E(Y(1),IX,IX+1),BAD=0 D NUM,QC Q:'$L(V)  I 'BAD S I=+V,V=$E(Y(1),IX+4,IX+8) D NUM S:$D(TC(I)) @TC(I,1)=+V
 S V=$E(Y(1),2,4) D NUM S ID=+V,V=$E(Y(1),9,14) D NUM S IDE=+V,TRAY=+$E(Y(1),6),CUP=+$E(Y(1),7,8) S:TRAY=0 TRAY=10 ; TRAY 0 IS CHANGED TO TRAY 10
LA3 X LAGEN
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 K TV,Y G LA2
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S:$E(Y(1),IX+2)'=" " BAD=1 Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
