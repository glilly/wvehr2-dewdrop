MCPRE05 ;HIRMFO/DAD-CLEAN UP DATA IN INDICATION FILE (#694.1) ;4/26/96  14:49
 ;;2.3;Medicine;;09/13/1996
 ;
 N MCDATA,MCORSPEL,MCLOOP,MCTXT,MCD0,DIE,DA,DR
 S MCDATA(1)=""
 S MCDATA(2)="Correcting misspellings in the Indication file (#694.1)."
 D MES^XPDUTL(.MCDATA)
 ;
 S MCORSPEL=$P($T(CORRECT+1),";",3)
 F MCLOOP=1:1 S MCTXT=$P($T(MISP+MCLOOP),";",3)  Q:MCTXT=""  D
 . S MCD0=0
 . F  S MCD0=$O(^MCAR(694.1,"B",MCTXT,MCD0)) Q:MCD0'>0  D
 .. I $P($G(^MCAR(694.1,MCD0,0)),U)'=MCTXT Q
 .. S DIE="^MCAR(694.1,",DA=MCD0,DR=".01///"_MCORSPEL
 .. D ^DIE
 .. Q
 . Q
 Q
CORRECT ;Correct spelling
 ;;ASSESS PROGRESSION OR STATUS OF KNOWN HEMATOLOGIC DISEASE OR DISORDER
MISP ;Known misspelling
 ;;ASSES PROGRESSION OF STATUS OF KNOW HEMATOLOGIC DISEASE OR
 ;;ASSES PROGRESSION OF STATUS OF KNOW HEMATOLOGIC DISEASE OR DISORDER
 ;;ASSES PROGRESSION OR STATUS OF KNOW HEMATOLOGIC DISEASE OR DISORDER
 ;;ASSESS PROGRESSION OR STATUS OF KNOW HEMATOLOGIC DISEASE OR DISORDER
 ;;ASSESS PROGRESSION OR STATUS OF HEMATOLOGIC DISEASE OR DISORDER
