TIUPS115 ; SLC/JER - Post-install for TIU*1*115 ;4/20/2001 16:19
 ;;1.0;Text Integration Utilities;**115**;Jun 20, 1997
MAIN ; Control Subroutine
 N TIUDA,TIUCNT,XPDIDTOT S (TIUCNT,TIUDA)=0
 ; Remove the existing ACL x-ref
 K ^TIU(8925.1,"ACL")
 D BMES^XPDUTL("BUILDING NEW ""ACL"" CROSS-REFERENCE ON FILE 8925.1")
 S XPDIDTOT=$P(^TIU(8925.1,0),U,4),XPDIDVT=+$G(XPDIDVT)
 D UPDATE^XPDID(0)
 F  S TIUDA=$O(^TIU(8925.1,TIUDA)) Q:+TIUDA'>0  D
 . D XREF(TIUDA) S TIUCNT=+$G(TIUCNT)+1
 . I '(TIUCNT#10) D UPDATE^XPDID(TIUCNT)
 Q
XREF(DA) ; Call EN1^DIK to build "ACL" cross-reference on ^TIU(8925.1,
 N DIK
 S DIK="^TIU(8925.1,",DIK(1)=".01^ACL"
 D EN1^DIK
 S DIK="^TIU(8925.1,",DIK(1)=".02^ACL02"
 D EN1^DIK
 S DIK="^TIU(8925.1,",DIK(1)=".03^ACL03"
 D EN1^DIK
 Q
