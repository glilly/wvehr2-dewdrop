LACLNTE ;SLC/RWF - AMES CLINI-TEK FORM PRINTER AUTOMATED DATA  ;8/16/90  14:53 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;1.0 ;FORM PRINTER
 ;CROSS LINK BY ID OR CUP
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 S TOUT=0,BAD=0,A=1 D IN G QUIT:TOUT,LA2:IN'["-"
 S Y(1)=IN
 F A=2:1:10 S Y(A)="" D IN G LA2:TOUT S Y(A)=IN
 F I=0:0 S I=$O(TC(I)) Q:I<1  X TC(I,2) S @TC(I,1)=V
 S ID=+$P(Y(1),"-",2),TRAY=+$P($E(Y(1),2,5),"-",1),CUP=ID
LA3 X LAGEN
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
NUM S Y=V,V="" F J=4:1:$L(Y) Q:$E(Y,J)?1A  S:$E(Y,J)'=" " V=V_$E(Y,J)
 Q
PLUS S Y=V,V=0 F J=4:1:$L(Y) S:$E(Y,J)="+" V=V+1
 S:V V=V_"+" S:'V V="Neg." Q
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S Y(A)=IN Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J) L
 Q
TRAP D ^LABERR S T=SK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
