%ZVEMOE ;DJB,VRROLD**INSERT,DELETE,CHANGE [12/31/94]
 ;;7.0;VPE;;COPYRIGHT David Bolduc @1993
 ;
DELETE ;Delete a line
 NEW VRRLN,I,II,X1,X2 D BOTTOM^%ZVEMOU
DELETEA S VRRLN=$$GETLINE^%ZVEMOU("DELETE WHICH LINE NUMBER(S): ") Q:VRRLN="^"
DELETEB I VRRLN=1 W $C(7) Q
 I VRRLN="?"!(VRRLN="INVALID") D MSG2^%ZVEMOUM(12) G DELETEA
 I VRRLN="INVALID RNG" D MSG2^%ZVEMOUM(13) G DELETEA
 S (X1,X2)=VRRLN I VRRLN["-" S X1=$P(VRRLN,"-"),X2=$P(VRRLN,"-",2),VRRLN=X1
 F I=X1:1:X2 D
 . F II=VRRLN:1:(VRRHIGH-1) S ^TMP("VEE","VRR",$J,VRRS,"TXT",II)=^TMP("VEE","VRR",$J,VRRS,"TXT",(II+1))
 . KILL ^TMP("VEE","VRR",$J,VRRS,"TXT",VRRHIGH) S VRRHIGH=VRRHIGH-1,FLAGSAVE=1
 . S $P(^TMP("VEE","VRR",$J,VRRS,"HOT"),"^",1)=VRRHIGH
 Q
 ;====================================================================
INSERT ;Insert line of code
 NEW VRRLN,CD
 D BOTTOM^%ZVEMOU
INSERTA S VRRLN=$$GETLINE^%ZVEMOU("INSERT AFTER LINE NUMBER: ") Q:VRRLN="^"
 I VRRLN'?1.N D MSG2^%ZVEMOUM(14) G INSERTA
 D BOTTOM^%ZVEMOU W ?1,"ENTER CODE:"
INSERT1 D BOTTOM1^%ZVEMOU
INSERT2 R ?1,"*",CD:VEE("TIME") S:'$T CD="^" I "^"[CD Q
 I "??"[CD D MSGS^%ZVEMOUM(15,1) G INSERT1
 I $$TAGCHK^%ZVEMOEU(CD) Q
 I $$LINECHK^%ZVEMOEU(CD) Q
 S VRRHIGH=VRRHIGH+1
 S $P(^TMP("VEE","VRR",$J,VRRS,"HOT"),"^",1)=VRRHIGH
 F I=VRRHIGH:-1:(VRRLN+2) S ^TMP("VEE","VRR",$J,VRRS,"TXT",I)=^TMP("VEE","VRR",$J,VRRS,"TXT",(I-1))
 S VRRLN=VRRLN+1 D SAVE^%ZVEMOEU,KILLCHK^%ZVEMKU(CD)
 W ! G INSERT2
 ;====================================================================
CHANGE ;Change a line
 NEW VRRLN D BOTTOM^%ZVEMOU
CHANGE1 S VRRLN=$$GETLINE^%ZVEMOU("CHANGE LINE NUMBER: ") G:VRRLN="^" EX
 I VRRLN'?1.N D MSGS^%ZVEMOUM(3) G CHANGE1
CHANGE2 ;Entry point for ^%ZVEMOM
 NEW CD,CDHLD,CHK,NEW,OLD,TAB,TEMP,TEMP1,VALID
 S (CD,CDHLD)=$G(^TMP("VEE","VRR",$J,VRRS,"TXT",VRRLN)) G:CD']"" EX
 I $G(^%ZVEMS("E","EDIT",DUZ))'="LINE" D ^%ZVEMOEA G EX ;Screen mode editor.
CHANGE3 D LINE^%ZVEMKE("") G:CD=CDHLD EX
 S TEMP=$$TAGCHK^%ZVEMOEU(CD) I TEMP S CD=CDHLD G CHANGE3
 S TEMP=$$LINECHK^%ZVEMOEU(CD) I TEMP D:TEMP=2 DELLINE^%ZVEMOEA G:TEMP=2 EX S CD=CDHLD G CHANGE3
 D SAVE^%ZVEMOEU
EX ;Exit
 D KILLCHK^%ZVEMKU(CD)
 Q
