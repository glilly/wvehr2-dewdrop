ABSV88B ;VAMC ALTOONA/CTB - MANAGE TT88 TRANSMISSION LISTS ;7/29/93  3:07 PM
V ;;4.0;VOLUNTARY TIMEKEEPING;;JULY 6, 1994
ADD ;ADD VOLUNTEER TO LIST OF TT88'S TO BE TRANSMITTED TO AUSTIN
 NEW %,%W,%Y,C,D0,DA,DI,DIC,DIE,DQ,DR,DUOUT,DTOUT,DIRUT,DIROUT,I,N,X
 D ^ABSVSITE Q:'%
A1 D GETVOL G OUT:'$D(ABSVX("VOLDA"))
 S DA(1)=ABSVX("VOLDA")
 I $P($G(^ABS(503330,DA(1),4,ABSV("INST"),0)),"^",13)=1 S X="Volunteer IS currently marked for transmission for station "_ABSV("SITE")_".  NO ACTION TAKEN.*" D MSG^ABSVQ,OUT G A1
 S DIE="^ABS(503330,"_DA(1)_",4,",DA=ABSV("INST"),DR="13;11////1" D ^DIE
 I $D(Y) S DR="13///@;11///@" D ^DIE S X="   <No Action Taken>" D MSG^ABSVQ,OUT G A1
 I $P($G(^ABS(503330,DA(1),4,ABSV("INST"),0)),"^",13)'=1 S X="ERROR HAS OCCURRED, Record has not been marked.  Please try again."
 E  S X="<Record marked for transmission>*"
 D MSG^ABSVQ,OUT G A1
 QUIT
 ;
DEL ;DELETE VOLUNTEER FROM OF TT88'S TO BE TRANSMITTED TO AUSTIN
 NEW %,%W,%Y,C,D0,DA,DI,DIC,DIE,DQ,DR,DUOUT,DTOUT,DIRUT,DIROUT,I,N,X,ZZ
 D ^ABSVSITE Q:'%
D1 D GETVOL I '$D(ABSVX("VOLDA")) D OUT QUIT
 S DA(1)=ABSVX("VOLDA")
 I $P($G(^ABS(503330,DA(1),4,ABSV("INST"),0)),"^",13)'=1,$P($G(^(0)),"^",14)'=1 S X="Volunteer IS NOT currently marked for transmission for station "_ABSV("SITE")_".  NO ACTION TAKEN.*" D MSG^ABSVQ,OUT G D1
 S ABSVXA="OK to REMOVE record from Austin Transmission List",%=1
 D ^ABSVYN I %'=1 S ZZ=%,X="<No Action Taken>*" D MSG^ABSVQ S %=ZZ K ZZ G OUT:%<0,D1:%=2
 S DIE="^ABS(503330,"_DA(1)_",4,",DA=ABSV("INST"),DR="11////@;12////@;13////0" D ^DIE
 I $P($G(^ABS(503330,DA(1),4,ABSV("INST"),0)),"^",13)=1!($P($G(^(0)),"^",14)=1) S X="ERROR HAS OCCURRED, Record has not been UNMARKED.  Please try again."
 E  S X="<Record Removed from List>*"
 D MSG^ABSVQ,OUT G D1
OUT QUIT
GETVOL ;GET SINGLE VOLUNTEER RECORD
 NEW DA,DIC,DLAYGO,Y
 K ABSVX("VOLDA") S DIC=503330,DIC(0)="AEMQ",DIC("A")="Select VOLUNTEER: ",DIC("S")="I $D(^ABS(503330,+Y,4,ABSV(""INST""),0))"
 D MDIV^ABSVSITE,^DIC K DIC Q:+Y<0  S ABSVX("VOLDA")=+Y,DA=+Y
 QUIT
VIEW ;VIEW/PRINT LIST OF PENDING TT88'S
 D ^ABSVSITE G:'% OUT
 S L=0,DIC=503330,BY="[ABSV TT88 VIEW SORT]"
 I ABSV("SITE")=+ABSV("SITE") S FR=ABSV("SITE")-.0001,TO=ABSV("SITE")+.0001
 E  S (FR,TO)=ABSV("SITE")
 S DIS(0)="I (+$P($G(^ABS(503330,D0,4,ABSV(""INST""),0)),U,13)!(+$P($G(^(0)),U,14)))"
 S FLDS="[ABSV TT88 LIST]",DHD="PENDING TT88'S FOR STATION "_ABSV("SITE") D EN1^DIP
 K %X,B,DIJ,DIPASS,DP,P QUIT
