PRCHRPL ;SF/TKW,WISC/CLH-LOCAL PROCUREMENT PUBLIC LAW 100-322 REPORT ; 6/17/97 9:23 AM
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 ;
EN1 S PRCF("X")="SP" D ^PRCFSITE Q:'$D(PRC("SITE"))
 ;
EN10 S PRCHD="DATE",M="DATE RECEIVED" D RNG^PRCHRPT1 G Q:FR["^"!(TO["^") I FR["?"!(TO["?") W $C(7),!!,"Enter a beginning and ending RECEIPT DATE range for this report." G EN10
 S PRCHNULL=0 I (FR["@")!(TO["@") S PRCHNULL=1 S:FR["@" FR="" S:TO["@" TO="z"
 ;
EN11 K PRCHQ S PRCHQ="EN2^PRCHRPL" D ^PRCHQUE
 G Q
 ;
EN2 D NOW^%DTC S Y=% D DD^%DT S PRCHPDAT=Y K ^TMP($J) S PRCHSITE="** INVALID STATION **",X=$O(^PRC(411,"B",PRC("SITE"),0)) I $D(^PRC(411,+X,0)),$D(^DIC(4,+$P(^(0),U,10),0)) S PRCHSITE=$P(^(0),U,1)
 S PRCHFT="ALL DATES" I FR S Y=FR D DD^%DT S PRCHFT=Y_" through "
 S:'TO PRCHFT=PRCHFT_" LAST date" I TO S Y=TO D DD^%DT S PRCHFT=PRCHFT_Y
 ;
 ;**** NOTE: FSC CODES SELECTED ARE SET HERE--CAN BE CHANGED BY ADDING
 ;           OR DELETING FROM LIST IN ^TMP
 ;
 F I=65,66,73 S ^TMP($J,"FSCG",I)=$P($G(^PRC(441.3,+I,0)),U,2)
 S (PRCHPO,PRCHCNT)=0 D RD
 ;
P S PRCHPAGE=0,PRCHDY=0 D EN^PRCHRPL1
 W !,$C(13) D:$D(ZTSK) KILL^%ZTLOAD K ZTSK,ZTSKT
 G Q
 ;
RD S PRCHPO=$O(^PRC(442,PRCHPO)) Q:'PRCHPO  G:'$D(^(PRCHPO,0)) RD G:'$D(^(1)) RD S PRCH0=^(0),X=^(1) G:+PRCH0'=PRC("SITE") RD G:'$O(^PRC(442,PRCHPO,11,0)) RD G:"13478"'[($P(PRCH0,U,2)) RD
 G:$P(X,U,18)="N" RD S PRCHDT=$P(X,U,15) G:PRCHDT]TO RD
 S PRCHV=+X,PRCHRC=$P(X,U,19),PRCHEMG=$P(X,U,17),PRCHSRC=$P($G(^PRCD(420.8,+$P(X,U,7),0)),U,1)
 G:PRCHRC="" RD S PRCHI=0 D RD1
 G RD
 ;
RD1 S PRCHI=$O(^PRC(442,PRCHPO,2,PRCHI)) Q:'PRCHI  G:'$O(^(PRCHI,3,0)) RD1 S PRCHI0=^PRC(442,PRCHPO,2,PRCHI,0)
 I PRCHSRC["B",$D(^PRC(442,PRCHPO,2,PRCHI,2)),$P(^(2),U,2)]"" G RD1
 I $D(^PRC(442,PRCHPO,2,PRCHI,2)) S X=+$P(^(2),U,3) I $D(^TMP($J,"FSCG",$E(X,1,2))) S PRCHFSC=X S:'$D(^TMP($J,"FSC",X)) ^TMP($J,"FSC",X)=$P($G(^PRC(441.2,X,0)),U,2) S PRCHR=0 D RD2
 G RD1
 ;
RD2 S PRCHR=$O(^PRC(442,PRCHPO,2,PRCHI,3,PRCHR)) Q:'PRCHR  G:'$D(^(PRCHR,0)) RD2 S PRCHD0=^(0),PRCHRDT=$P(^(0),U,1) G:FR]PRCHRDT!(PRCHRDT]TO) RD2 D BLD
 G RD2
 ;
BLD I '$D(^TMP($J,"V",PRCHV)) S:$D(^PRC(440,+PRCHV,0)) ^TMP($J,"V",PRCHV)=$P(^(0),U,1) S:'$D(^TMP($J,"V",PRCHV)) ^(PRCHV)="**INVALID VENDOR**"
 S PRCHDESC="",PRCHDESC=$P($G(^PRC(441,+$P(PRCHI0,U,5),0)),U,2)
 I PRCHDESC="" S X=$O(^PRC(442,PRCHPO,2,PRCHI,1,0)) I X,$D(^(X,0)) S PRCHDESC=^(0) ;$S($D(
 S:PRCHDESC="" PRCHDESC="** MISSING ITEM DESCRIPTION **"
 S PRCHCNT=PRCHCNT+1,PRCHTOT=$P(PRCHD0,U,3),(X,PRCHNIIN)=$P(PRCHI0,U,13) I X]"" S PRCHNIIN=$P(X,"-",2)_"-"_$P(X,"-",3)_"-"_$P(X,"-",4)
 S ^TMP($J,"R",PRCHRC,PRCHFSC,$E(PRCHDESC,1,30),^TMP($J,"V",PRCHV),PRCHCNT)=$P($P(PRCH0,U,1),"-",2)_U_PRCHNIIN_U_$P(PRCHI0,U,15)_U_$P(PRCHD0,U,2)_U_$P(PRCHI0,U,9)_U_PRCHTOT_U_$P($P(PRCH0,U,3)," ",1)_U_PRCHEMG_U_PRCHSRC
 S X=^TMP($J,"FSC",PRCHFSC),$P(X,U,2)=$P(X,U,2)+PRCHTOT
 S ^TMP($J,"FSC",PRCHFSC)=X
 I '$D(^TMP($J,"RC",PRCHRC)) S ^(PRCHRC)=0
 S ^TMP($J,"RC",PRCHRC)=^(PRCHRC)+$P(PRCHD0,U,3)
 I '$D(^TMP($J,"RC","FSC",PRCHFSC)) S ^(PRCHFSC)=0
 S ^TMP($J,"RC","FSC",PRCHFSC)=^(PRCHFSC)+$P(PRCHD0,U,3)
 I '$D(^TMP($J,"RC","FSC",PRCHRC,PRCHFSC)) S ^(PRCHFSC)=0
 S ^TMP($J,"RC","FSC",PRCHRC,PRCHFSC)=^(PRCHFSC)+$P(PRCHD0,U,3)
 Q
 ;
Q K %,%ZIS,IO("Q"),IOP,I,J,K,L,M,PRC,PRCF,PRCH0,PRCHCNT,PRCHD,PRCHD0,PRCHDESC,PRCHDET,PRCHDT,PRCHDY,PRCHEMG,PRCHFSC,PRCHFSCG,PRCHFT,PRCHGT,PRCHI,PRCHI0,PRCHNIIN,PRCHNULL
 K PRCHPAGE,PRCHPDAT,PRCHPO,PRCHQ,PRCHR,PRCHRDT,PRCHSITE,PRCHSRC,PRCHT,PRCHTOT,PRCHV,ZTRTN,PRCHRC,^TMP($J),ZZI,ZZJ
 Q
