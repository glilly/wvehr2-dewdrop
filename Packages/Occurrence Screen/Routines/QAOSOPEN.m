QAOSOPEN ;HISC/DAD-REOPEN CLOSED OR DELETED OCCURRENCES ;6/11/93  15:54
 ;;3.0;Occurrence Screen;;09/14/1993
 W !!?32,"*** WARNING ***"
 W !!?11,"Reopening a record will delete all final disposition data",*7
 S QALIMIT="I $P(^(0),""^"",11)>0",QAOSPROG="DFN^QAOSOPEN" D EN2^QAOEDT
EXIT ;
 K %,DA,DIC,DIE,DR,QAUDIT,QAOSWHAT,Y,QAWHO,QAOSD0,%W,%Y,%Y1,C,D0,DI,DIG
 K DIH,DIU,DIV,DIW,DQ,I,QA,QACLOSE,QAHOLD,QAI,QALINE,QAOSWRD,QASCREEN
 K QAWO,SAVEX,SAVEY,X,Y,Z,QALIMIT,QAOSONE,QAOSPROG,QAOSQUIT
 Q
DFN ;
 S QAOSWHAT="REOPENED" D ENDISP^QAOUTL0
ASK W !!?5,"Reopen this record (Y/N) "
 S %=2 D YN^DICN S QAOSQUIT=$S(%=-1:1,1:0) Q:%=2!QAOSQUIT
 I '% W !!?10,"Please answer Y(es) or N(o)" G ASK
 S DIE="^QA(741,",DA=QAOSD0
 S DR="11///^S X=0;14///^S X=""@"";15///^S X=""@"";16///^S X=""@"""
 D ^DIE
 S QAUDIT("FILE")="741^27",QAUDIT("DA")=DA,QAUDIT("ACTION")="o"
 S QAUDIT("COMMENT")="REOPEN A CLOSED/DELETED RECORD" D ^QAQAUDIT
 W !!?5,"*** RECORD HAS BEEN REOPENED ***",*7,!
 Q
