ONCNPC3 ;HIRMFO/GWB - PCE Study of Non-Hodgkin's Lymphoma - Table III;4/15/97
 ;;2.11;ONCOLOGY;**11**;Mar 07, 1995
 K TABLE,HTABLE
 S TABLE("AJCC CLINICAL STAGE GROUP")="ACS"
 S TABLE("CLINICALLY STAGED BY")="CSB"
 S TABLE("AJCC PATHOLOGIC STAGE GROUP")="APS"
 S TABLE("PATHOLOGICALLY STAGED BY")="PSB"
 S TABLE("TYPE OF STAGING SYSTEM (PEDIATRIC)")="TOSS"
 S TABLE("PEDIATRIC STAGE")="PS"
 S TABLE("STAGED BY (PEDIATRIC STAGE)")="SB"
 S TABLE("EXTRANODAL SITES")="ES"
 S HTABLE(1)="AJCC CLINICAL STAGE GROUP"
 S HTABLE(2)="CLINICALLY STAGED BY"
 S HTABLE(3)="AJCC PATHOLOGIC STAGE GROUP"
 S HTABLE(4)="PATHOLOGICALLY STAGED BY"
 S HTABLE(5)="TYPE OF STAGING SYSTEM (PEDIATRIC)"
 S HTABLE(6)="PEDIATRIC STAGE"
 S HTABLE(7)="STAGED BY (PEDIATRIC STAGE)"
 S HTABLE(8)="EXTRANODAL SITES"
 S CHOICES=8
 K DIQ S DIC="^ONCO(165.5,",DR="38;88",DA=ONCONUM,DIQ="ONC" D EN^DIQ1
 W @IOF D HEAD^ONCNPC0
 W !?18,"TABLE III- EXTENT OF DISEASE AND AJCC STAGE"
 W !?18,"-------------------------------------------"
 S DIE="^ONCO(165.5,",DA=ONCONUM
ACS W !!,"AJCC CLINICAL STAGE GROUP......: ",ONC(165.5,ONCONUM,38)
CSB S DR="19CLINICALLY STAGED BY..........." D ^DIE G:$D(Y) JUMP
APS W !!,"AJCC PATHOLOGIC STAGE GROUP ...: ",ONC(165.5,ONCONUM,88)
PSB S DR="89PATHOLOGICALLY STAGED BY......." D ^DIE G:$D(Y) JUMP
 W !
TOSS S DR="849TYPE OF STAGING SYS (PEDIATRIC)" D ^DIE G:$D(Y) JUMP
PS S DR="850PEDIATRIC STAGE................" D ^DIE G:$D(Y) JUMP
SB S DR="851STAGED BY (PEDIATRIC STAGE)...." D ^DIE G:$D(Y) JUMP
ES W !!,"EXTRANODAL SITES:",!
 S DR="852  EXTRANODAL SITE 1............" D ^DIE G:$D(Y) JUMP
 S DR="853  EXTRANODAL SITE 2............" D ^DIE G:$D(Y) JUMP
 S DR="854  EXTRANODAL SITE 3............" D ^DIE G:$D(Y) JUMP
 W ! K DIR S DIR(0)="E" D ^DIR
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!,"GO TO: ",X:DTIME I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K HTABLE,TABLE,CHOICES
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
