ONCHPC3 ;Hines OIFO/GWB - 2000 Hepatocellular Cancers PCE Study ;01/10/00
 ;;2.11;ONCOLOGY;**26**;Mar 07, 1995
 ;Stage of Disease at Diagnosis 
 K TABLE,HTABLE
 S TABLE("23. SIZE OF TUMOR")="ST"
 S TABLE("24. REGIONAL NODES EXAMINED")="RNE"
 S TABLE("25. REGIONAL NODES POSITIVE")="RNP"
 S TABLE("26. AJCC CLINICAL STAGE (cTNM)")="ACS"
 S TABLE("27. AJCC PATHOLOGIC STAGE (pTNM)")="APS"
 S TABLE("28. STAGED BY")="SB"
 S HTABLE(1)="23. SIZE OF TUMOR"
 S HTABLE(2)="24. REGIONAL NODES EXAMINED"
 S HTABLE(3)="25. REGIONAL NODES POSITIVE"
 S HTABLE(4)="26. AJCC CLINICAL STAGE (cTNM)"
 S HTABLE(5)="27. AJCC PATHOLOGIC STAGE (pTNM)"
 S HTABLE(6)="28. STAGED BY"
 S CHOICES=6
 W @IOF D HEAD^ONCHPC0
 W !," STAGE OF DISEASE AT DIAGNOSIS"
 W !," -----------------------------"
 S DIE="^ONCO(165.5,",DA=ONCONUM
ST S DR="29 23. SIZE OF TUMOR ................" D ^DIE G:$D(Y) JUMP
RNE S DR="33 24. REGIONAL NODES EXAMINED......." D ^DIE G:$D(Y) JUMP
RNP S DR="32 25. REGIONAL NODES POSITIVE......." D ^DIE G:$D(Y) JUMP
ACS W !!," 26. AJCC CLINICAL STAGE (cTNM):"
 S DR="37.1      AJCC CLINICAL T.............." D ^DIE G:$D(Y) JUMP
 D CN1^ONCOTN,CN2^ONCOTN
 S DR="37.2      AJCC CLINICAL N.............." D ^DIE G:$D(Y) JUMP
 S DR="37.3      AJCC CLINICAL M.............." D ^DIE G:$D(Y) JUMP
 I '$D(SKAJCC) D CN1^ONCOTN
 S STGIND="C" D ES^ONCOTN
APS W !," 27. AJCC PATHOLOGIC STAGE (pTNM):",!
 S DR="85      AJCC PATHOLOGIC T............" D ^DIE G:$D(Y) JUMP
 D CN3^ONCOTN,CN4^ONCOTN
 S DR="86      AJCC PATHOLOGIC N............" D ^DIE G:$D(Y) JUMP
 S DR="87      AJCC PATHOLOGIC M............" D ^DIE G:$D(Y) JUMP
 I '$D(SKAJCC) D CN3^ONCOTN
 S STGIND="P" D ES^ONCOTN
SB W !," 28. STAGED BY:"
 S DR="19      CLINICALLY STAGED BY........." D ^DIE G:$D(Y) JUMP
 S DR="89      PATHOLOGICALLY STAGED BY....." D ^DIE G:$D(Y) JUMP
 W ! K DIR S DIR(0)="E" D ^DIR S:$D(DIRUT) OUT="Y"
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!," GO TO ITEM NUMBER: ",X:DTIME
 I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S:X?1.2N X=X_"." S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K HTABLE,TABLE,CHOICES
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
