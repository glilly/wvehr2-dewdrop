TIURM1 ; SLC/JER - MIS Document Review ;9/24/03
 ;;1.0;TEXT INTEGRATION UTILITIES;**100,113**;Jun 20, 1997
GATHER(TIUPREF,CLASS,STATIFNS,EARLY,LATE,DIVIFNS) ; Find/sort
 ;records for the list
 N TIUDA,TIUSFLD,TIUI,TIUQ
 S TIUSFLD=$P(TIUPREF,U,3)
 S TIUSFLD=$S(TIUSFLD="P":".02",TIUSFLD="D":".01",TIUSFLD="S":".05",TIUSFLD="C":"1507",TIUSFLD="A":"1202",TIUSFLD="E":"1208",1:"1301")
 S TIUI=EARLY F  S TIUI=$O(^TIU(8925,"F",TIUI)) Q:+TIUI'>0!(+TIUI>LATE)  D
 . S TIUDA=0
 . F  S TIUDA=$O(^TIU(8925,"F",TIUI,TIUDA)) Q:+TIUDA'>0  D
 . . ; Consider adding view check here
 . . I '$$INTYPES(TIUDA) Q
 . . I '$$INSTATUS(TIUDA,STATIFNS) Q
 . . I +$G(DIVIFNS("ENTRIES")),'$$ININST(TIUDA,.DIVIFNS) Q
 . . S TIUQ=$$RESOLVE^TIUR1(TIUDA,TIUSFLD)
 . . S ^TMP("TIUI",$J,TIUQ,(9999999-TIUI),TIUDA)=""
 Q
 ;
INTYPES(TIUDA) ; Evaluates whether a record is among the selected types
 N TIUI,TIUTYP,TIUY S (TIUI,TIUY)=0
 S TIUTYP=+$G(^TIU(8925,+TIUDA,0))
 I TIUTYP=81 S TIUY=+$$DADINTYP(TIUDA) G INTYPX
 F  S TIUI=$O(^TMP("TIUTYP",$J,TIUI)) Q:+TIUI'>0!+TIUY  D
 . I +$P(^TMP("TIUTYP",$J,TIUI),U,2)=TIUTYP S TIUY=1
INTYPX Q TIUY
 ;
INSTATUS(TIUDA,STATIFNS) ; Evaluates whether a record
 ;is among the selected statuses
 N TIUS,TIUSTAT,TIUSTATE,TIUY S TIUY=0
 S TIUSTATE=+$P($G(^TIU(8925,+TIUDA,0)),U,5)
 S TIUS=1 F  S TIUSTAT=$P(STATIFNS,";",TIUS) Q:'TIUSTAT!TIUY  D
 . S TIUS=TIUS+1
 . I TIUSTAT=TIUSTATE S TIUY=1
 Q TIUY
ININST(TIUDA,TIUDI) ; Evaluates whether a TIU DOCUMENT record
 ; is among the selected divisions
 ; Input -- TIUDA    TIU DOCUMENT file (#8925) IEN
 ;       -- TIUDI(   i.e. TIUDI(file #40.8 IEN)=Institution file
 ;                                           pointer for file #40.8 entry
 ; Output - TIUY     0= record not in selected division
 ;                   1= record in selected division
 N TIUIFP,TIUI,TIUINST,TIUY S TIUY=0
 S TIUINST=+$P($G(^TIU(8925,+TIUDA,12)),U,12)
 S TIUI=0 F  S TIUI=$O(TIUDI(TIUI)) Q:'TIUI!TIUY  D
 . S TIUIFP=$G(TIUDI(TIUI))
 . I TIUIFP=TIUINST S TIUY=1
 Q TIUY
DADINTYP(TIUDA) ; Evaluates whether addendum's parent is among
 ;                 the selected types
 N TIUI,TIUDTYP,TIUY S (TIUI,TIUY)=0
 S TIUDTYP=+$G(^TIU(8925,+$P($G(^TIU(8925,+TIUDA,0)),U,6),0))
 F  S TIUI=$O(^TMP("TIUTYP",$J,TIUI)) Q:+TIUI'>0!+TIUY  D
 . I +$P(^TMP("TIUTYP",$J,TIUI),U,2)=TIUDTYP S TIUY=1
 Q TIUY
 ;
PUTLIST(TIUPREF,TIUCLASS,STATUS,DIVIFNS) ; Expands list elements for LM Template
 N TIUJ,TIUQ,TIUDA,TIUPICK,TIUORDER,TIUEXPKD,TIUSFLD
 S VALMCNT=0
 S TIUSFLD=$P(TIUPREF,U,3)
 S TIUSFLD=$S(TIUSFLD="P":".02",TIUSFLD="D":".01",TIUSFLD="S":".05",TIUSFLD="C":"1507",TIUSFLD="A":"1202",TIUSFLD="E":"1208",1:"1301")
 S TIUORDER=$S($P(TIUPREF,U,4)="A":1,1:-1)
 S TIUPICK=+$O(^ORD(101,"B","TIU ACTION SELECT LIST ELEMENT",0))
 S TIUQ="" F  S TIUQ=$O(^TMP("TIUI",$J,TIUQ)) Q:TIUQ']""  D
 . S TIUJ=0 F  S TIUJ=$O(^TMP("TIUI",$J,TIUQ,TIUJ)) Q:+TIUJ'>0  D
 . . S TIUDA=0
 . . F  S TIUDA=$O(^TMP("TIUI",$J,TIUQ,TIUJ,TIUDA)) Q:'TIUDA  D
 . . . D REPLACE^TIUR2(TIUDA,TIUQ,TIUSFLD,TIUJ,.TIUEXPKD)
 D SETLIST(TIUORDER,.VALMCNT)
 S ^TMP("TIUR",$J,0)=+$G(VALMCNT)_U_STATUS("WORDS")
 S ^TMP("TIUR",$J,"CLASS")=TIUCLASS
 S ^TMP("TIUR",$J,"#")=TIUPICK_"^1:"_+$G(VALMCNT)
 M ^TMP("TIUR",$J,"DIV")=DIVIFNS
 I $D(VALMHDR)>9 D HDR^TIURMH
 I +$G(VALMCNT)'>0 D
 . S ^TMP("TIUR",$J,1,0)="",VALMCNT=2
 . S ^TMP("TIUR",$J,2,0)="     No records found to satisfy search criteria."
 . S ^TMP("TIUR",$J,"IDX",1,0)="" ; User can't select lines 1 or 2
 . S ^TMP("TIUR",$J,"IDX",2,0)=""
 ; -- Expand to show kids that fit:
 I '$G(TIURBLD),$D(TIUEXPKD) D EXPANDKD^TIUR2(.STATUS,.TIUEXPKD)
 Q
SETLIST(TIUORDER,VALMCNT) ; Set items from ^TMP("TIUI",$J) into
 ;List Template list
 N SORTVAL,TIUDTM,TIUDA
 S SORTVAL=""
 F  S SORTVAL=$O(^TMP("TIUI",$J,SORTVAL),TIUORDER) Q:SORTVAL=""  D
 . S TIUDTM=0
 . F  S TIUDTM=$O(^TMP("TIUI",$J,SORTVAL,TIUDTM)) Q:'TIUDTM  D
 . . S TIUDA=0
 . . F  S TIUDA=$O(^TMP("TIUI",$J,SORTVAL,TIUDTM,TIUDA)) Q:'TIUDA  D
 . . . D ADDELMNT(TIUDA,.VALMCNT)
 Q
 ;
ADDELMNT(DA,TIUCNT,APPEND) ; Add each element to the list
 N PT,ADT,DDT,AUT,AMD,EDT,LCT,SDT,TIULST4,INSTA,TIUSTN
 N TIUREC,TIUD0,TIUD12,TIUD13,TIUD15,STATX,TIULI,DOC ;P74 newed DOC
 N PREFIX,TIUGDATA
 I '$D(^TIU(8925,TIUDA,0)) Q
 I $G(^TMP("TIUR",$J,2,0))="     No records found to satisfy search criteria." D
 . K ^TMP("TIUR",$J,2),^TMP("TIUR",$J,"IDX",2),^TMP("TIUR",$J,"IDX",1)
 . S TIUCNT=0
 S TIUD0=$G(^TIU(8925,+DA,0)),TIUD12=$G(^TIU(8925,+DA,12))
 S TIUD13=$G(^TIU(8925,+DA,13)),TIUD15=$G(^TIU(8925,+DA,15))
 S DOC=$$PNAME^TIULC1(+TIUD0)
 I DOC="Addendum" S DOC=DOC_" to "_$$PNAME^TIULC1(+$G(^TIU(8925,+$P(TIUD0,U,6),0)))
 S PREFIX=$$PREFIX^TIULA2(TIUDA,0)
 S PT=$$NAME^TIULS($$PTNAME^TIULC1($P(TIUD0,U,2)),"LAST,FI MI")
 S TIULI=$E(PT)
 S PT=PREFIX_PT
 S TIULST4=$E($P($G(^DPT(+$P(TIUD0,U,2),0)),U,9),6,9)
 S TIULST4="("_TIULI_TIULST4_")"
 S ADT=$$DATE^TIULS($P(TIUD0,U,7),"MM/DD/YY")
 S DDT=$$DATE^TIULS($P(TIUD0,U,8),"MM/DD/YY")
 S AMD=$$PERSNAME^TIULC1($P(TIUD12,U,8)) S:AMD="UNKNOWN" AMD=""
 S AUT=$$PERSNAME^TIULC1($P(TIUD12,U,2)) S:AUT="UNKNOWN" AUT=""
 S AMD=$$NAME^TIULS(AMD,"LAST, FI MI")
 S AUT=$$NAME^TIULS(AUT,"LAST, FI MI")
 S EDT=$$DATE^TIULS($P(TIUD13,U,7),"MM/DD/YY")
 S SDT=$S(+$P(TIUD15,U,7):+$P(TIUD15,U,7),+$P(TIUD0,U,5)'<7:+$P(TIUD15,U),1:"")
 S SDT=$$DATE^TIULS(SDT,"MM/DD/YY")
 S STATX=$P($G(^TIU(8925.6,+$P(TIUD0,U,5),0)),U)
 S LCT=$P(TIUD0,U,10)
 S INSTA=""
 I +$P(TIUD12,U,12)>0 D
 . S TIUSTN=$$NS^XUAF4($P(TIUD12,U,12))
 . I $P(TIUSTN,U,2)]"" S INSTA=$P(TIUSTN,U,2)
 S INSTA=$E(INSTA,1,8)
 S TIUCNT=+$G(TIUCNT)+1
 S TIUREC=$$SETFLD^VALM1(TIUCNT,"","NUMBER")
 S TIUREC=$$SETFLD^VALM1(PT,TIUREC,"PATIENT NAME")
 S TIUREC=$$SETFLD^VALM1(TIULST4,TIUREC,"LAST I/LAST 4")
 S TIUREC=$$SETFLD^VALM1(DOC,TIUREC,"DOCUMENT TYPE")
 S TIUREC=$$SETFLD^VALM1(ADT,TIUREC,"ADMISSION DATE")
 S TIUREC=$$SETFLD^VALM1(DDT,TIUREC,"DISCH DATE")
 S TIUREC=$$SETFLD^VALM1(EDT,TIUREC,"DICT DATE")
 S TIUREC=$$SETFLD^VALM1(LCT,TIUREC,"LINE COUNT")
 S TIUREC=$$SETFLD^VALM1($$LOWER^TIULS(STATX),TIUREC,"STATUS")
 S TIUREC=$$SETFLD^VALM1(AUT,TIUREC,"AUTHOR")
 S TIUREC=$$SETFLD^VALM1(AMD,TIUREC,"ATTENDING")
 S TIUREC=$$SETFLD^VALM1(INSTA,TIUREC,"DIVISION")
 S ^TMP("TIUR",$J,TIUCNT,0)=TIUREC
 S ^TMP("TIUR",$J,"IDX",TIUCNT,TIUCNT)="" W "."
 S ^TMP("TIURIDX",$J,TIUCNT)=TIUCNT_U_DA_U_PREFIX
 S ^TMP("TIUR",$J,"IEN",DA,TIUCNT)=""
 S TIUGDATA=$$IDDATA^TIURECL1(DA,TIUD0)
 I TIUGDATA S ^TMP("TIUR",$J,"IDDATA",DA)=TIUGDATA
 I +$G(APPEND) D
 . D RESTORE^VALM10(TIUCNT)
 . D CNTRL^VALM10(TIUCNT,1,$G(VALM("RM")),IOINHI,IOINORM),HDR^TIURMH
 . S VALMSG="** Item #"_TIUCNT_" Added **"
 . S $P(^TMP("TIUR",$J,0),U)=TIUCNT
 . S $P(^TMP("TIUR",$J,"#"),":",2)=TIUCNT
 . S VALMCNT=TIUCNT
 . I $D(VALMHDR)>9 D HDR^TIURMH
 Q
