LRWD ;SLC/RWF-DISPLAY NAMES OF PATIENTS WITH RECENTLY VERIFIED DATA ;8/11/97
 ;;5.2;LAB SERVICE;**121,221**;Sep 27, 1994
 D D K DIC,LRDC,LRDFN,DFN,LRDPF
 Q
D S DIC=44,DIC(0)="AEMOQZ" D ^DIC I Y<1 Q
 S LROLLOC=+Y,LRTREA=$P(Y(0),U,20)
 S LRLLOC=$P(Y(0),U,2)
 I $$VER^LR7OU1<3 S ORL=+Y_";SC(" ;OE/RR 2.5
 Q:LRLLOC=""
 Q:'$D(^LRO(69,"AN",LRLLOC))  S LRDC=0
 S LRDFN=0 F  S LRDFN=$O(^LRO(69,"AN",LRLLOC,LRDFN)) Q:LRDFN<1  I $D(^(LRDFN))[0 S ^(LRDFN)="" D WRT
 Q:LRDC  W !,"There is patient data. Want to see the FULL list" S %=2 D YN^DICN Q:%'=1
 S LRDFN=0 F  S LRDFN=$O(^LRO(69,"AN",LRLLOC,LRDFN)) Q:LRDFN<1  D WRT
 Q
WRT I 'LRDC W !!,$C(7),"PATIENTS with NEW lab data",!,$C(7) S LRDC=1
 S X=$S($D(^LR(LRDFN,0)):^(0),1:""),DFN=$P(X,U,3),LRDPF=$P(X,U,2) I LRDPF=2 D DEM^LRX W !,SSN,?16,VADM(1)
 Q
