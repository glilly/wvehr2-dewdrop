SDYBSTOP ;ALB/MLI - Add new Clinic Stop Codes ; 12/10/93
 ;;5.3;Scheduling;**13**;Aug 13, 1993
 ;
EN ; -- add/inactivate stop codes
 D ADD
 D INACT
 Q
 ;
 ;
ADD W !!,">>> Adding new clinic stops to CLINIC STOP FILE (#40.7)..."
 W !,"    [NOTE:  These stop codes CANNOT be used UNTIL 2/1/94]",!
 S DIC(0)="L",DLAYGO=40.7,DIC="^DIC(40.7,"
 F DGX=1:1 K DD,DO,DA S DGXX=$P($T(NEW+DGX),";;",2) Q:DGXX="QUIT"  D
 . S DIC("DR")="1////"_$P(DGXX,"^",2)_$S('+$P(DGXX,U,5):"",1:";4////"_$P(DGXX,"^",5)),X=$P(DGXX,"^",1)
 . I '$D(^DIC(40.7,"C",$P(DGXX,"^",2))) D FILE^DICN,MESS
 K DIC,DGXX
 Q
 ;
 ;
INACT W !!,">>> Inactivating clinic stops in CLINIC STOP CODE FILE (#40.7)..."
 W !,"    [NOTE:  These stop codes CANNOT be used AFTER 1/31/94]",!
 D NOW^%DTC S SDATE=X
 F DGX=1:1 K DD,DO,DA S DGXX=$P($T(OLD+DGX),";;",2) Q:DGXX="QUIT"  D
 . S DGDA=+$O(^DIC(40.7,"C",DGXX,0))
 . I $D(^DIC(40.7,DGDA,0)) S DA=DGDA,DR="2////2940131",DIE="^DIC(40.7," D ^DIE,MESI
 K %,%H,%I,DGX,DGDA,DGXX,DR,DA,DIC,DIE,DLAYGO,SDATE,X
 Q
 ;
 ;
MESS W !?8,"...",X,?40,"(",$P(DGXX,"^",2),") added..."
 Q
 ;
 ;
MESI W !?8,"...",$P(^DIC(40.7,DGDA,0),"^"),?40,"(",DGXX,") inactivated as of 1/31/94..."
 Q
 ;
 ;
NEW ;stop codes to add
 ;;TELEPHONE TRIAGE^103^^^2111.00
 ;;PRIMARY CARE^323^^^2110.00
 ;;TELEPHONE/MEDICINE^324^^^2110.00
 ;;TELEPHONE/SURGERY^424^^^2210.00
 ;;TELEPHONE/SPECIAL PSYCHIATRY^526^^^2310.00
 ;;TELEPHONE/GENERAL PSYCHIATRY^527^^^2311.00
 ;;TELEPHONE/PTSD^542^^^2313.00
 ;;TELEPHONE/ALCOHOL DEPENDENCE^543^^^2316.00
 ;;TELEPHONE/DRUG DEPENDENCE^544^^^2316.00
 ;;TELEPHONE/SUBSTANCE ABUSE^545^^^2316.00
 ;;TELEPHONE/ANCILLARY^147^^^2610.00
 ;;TELEPHONE/REHAB AND SUPPORT^216^^^2611.00
 ;;TELEPHONE/DIAGNOSTIC^148^^^2612.00
 ;;TELEPHONE/PROSTHETICS/ORTHOTIC^425^^^2614.00
 ;;TELEPHONE/DENTAL^181^^^2710.00
 ;;TELEPHONE/DIALYSIS^611^^^2410.00
 ;;WOMEN'S STRESS DISORDER TEAMS^525^^^2311.00
 ;;QUIT
 ;
 ;
OLD ;STOP CODES TO BE INACTIVATED
 ;;571
 ;;572
 ;;QUIT
