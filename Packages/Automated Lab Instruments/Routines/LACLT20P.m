LACLT20P ;SLC/RWF/RAF/PAC - AMES CLINITEK 200 PLUS ; 7/14/93  8:20 AM ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;**16**;Sep 27, 1994
 ;CROSS LINK BY ID OR CUP
LA1 S:$D(ZTQUEUED) ZTREQ="@"
 S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 S TOUT=0,A=1 D IN G QUIT:TOUT,LA2:IN'["ID" D QC
 S TOUT=0,BAD=0 F A=2:1:13 D IN,QC G QUIT:TOUT
 S I=0 F  S I=$O(TC(I)) Q:I<1  S V="" X TC(I,2) S @TC(I,1)=V
 S V=+$P(Y(1),"=",2) S (ID,IDE,CUP)=+$$NUM(V),TRAY=1
LA3 X LAGEN
 S I=0 F  S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S Y(A)=IN Q
NUM(XX) N X,JJ S X="" F JJ=1:1:$L(XX) S:$A(XX,JJ)>32 X=X_$E(XX,JJ)
 Q X
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK)
 K A,BAD,CNT,CUP,I,ID,IDE,IN,ISQN,JJ,LAGEN,LANM,LWL,T,TC,TOUT,TRAY
 K TSK,TV,V,X,Y
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM)
FIX S V=$S(V="NEGATIVE":"NEG",V="POSITIVE":"POS",1:V) Q
