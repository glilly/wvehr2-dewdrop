DGNTDD ;ALB/RPM - Cross reference for field #.09 ; 9/13/01 14:02
 ;;5.3;Registration;**397,423**;Aug 13, 1993
 Q
 ;
SET(DGDA) ;
 N DGDFN,DGIEN
 S DGDFN=$P($G(^DGNT(28.11,DGDA,0)),"^")
 Q:'DGDFN
 I $D(^DGNT(28.11,"APR",DGDFN,1)) D
 . S DGIEN=$O(^DGNT(28.11,"APR",DGDFN,1,0))
 . Q:'DGIEN
 . S $P(^DGNT(28.11,DGIEN,0),"^",9)=""
 . S $P(^DGNT(28.11,DGIEN,0),"^",10)=$$NOW^XLFDT
 . K ^DGNT(28.11,"APR",DGDFN,1,DGIEN)
 S ^DGNT(28.11,"APR",DGDFN,1,DGDA)=""
 Q
 ;
KILL(DGDA) ;
 N DGDFN
 S DGDFN=$P($G(^DGNT(28.11,DGDA,0)),"^")
 Q:'DGDFN
 K ^DGNT(28.11,"APR",DGDFN,1,DGDA)
 Q
 ;
SETAST(DGDA) ;
 N DGDFN,DGSTAT,DGIEN
 S DGDFN=$G(^DGNT(28.11,DGDA,0))
 Q:'+DGDFN
 S DGIEN=$O(^DGNT(28.11,"B",+DGDFN,DGDA),-1) D
 . Q:DGIEN=""
 . I $P($G(^DGNT(28.11,DGIEN,0)),U,9)'=1 D
 . . S DGSTAT=$P($G(^DGNT(28.11,DGIEN,0)),U,3)
 . . Q:'+DGSTAT
 . . K ^DGNT(28.11,"AST",DGSTAT,1,DGIEN)
 S ^DGNT(28.11,"AST",$P(DGDFN,U,3),1,DGDA)=""
 Q
 ;
KILAST(DGDA) ;
 N DGSTAT
 S DGSTAT=$P($G(^DGNT(28.11,DGDA,0)),U,3)
 Q:'DGSTAT
 K ^DGNT(28.11,"AST",DGSTAT,1,DGDA)
 Q
