FSCELL ;SLC/STAFF-NOIS Edit Log List ;4/22/94  10:39
 ;;1.1;NOIS;;Sep 06, 1998
 ;
LIST(CALL) ; from FSCED, FSCEL
 N LASTCNT,LISTCNT
 I '$D(^TMP("FSC LIST CALLS",$J)) Q
 I '$D(VALMAR) Q
 I '$D(FSCLNAME) S FSCLNAME=""
 S LISTCNT=+$P(^TMP("FSC LIST CALLS",$J),U,2)
 I '$D(^TMP("FSC LIST CALLS",$J,"CX",CALL)) D
 .D SETUP^FSCLMPQU(.LISTCNT,CALL)
 .D COUNT^FSCLMPQU(LISTCNT)
 S FSCCNT=+^TMP("FSC LIST CALLS",$J)
 I VALMAR="^TMP(""FSC LIST CALLS"",$J)" D  Q
 .S VALMCNT=+$P(^TMP("FSC LIST CALLS",$J),U,2)
 .S ^TMP("FSC LIST CALLS",$J,VALMCNT,0)=$$SHORT^FSCGETS(CALL,FSCCNT)
 .I FSCCNT=1 K ^TMP("FSC LIST CALLS",$J,2)
 I $D(^TMP("FSC SELECT",$J,"VVALUES")) D
 .I '$D(^TMP("FSC SELECT",$J,"VVALUES",FSCCNT)) S ^("VVALUES")=^TMP("FSC SELECT",$J,"VVALUES")_","_FSCCNT,^("VVALUES",FSCCNT)=""
 .I '$D(^TMP("FSC SELECT",$J,"EVALUES")) S LASTCNT=+$P(@VALMAR,U,2)
 I $G(FSCCALLS)="EVALUES" D
 .I '$D(^TMP("FSC SELECT",$J,"EVALUES",FSCCNT)) S ^("EVALUES")=^TMP("FSC SELECT",$J,"EVALUES")_","_FSCCNT,^("EVALUES",FSCCNT)=""
 .S VALMCAP=$$CAP^FSCU("E",.FSCSTYLE,FSCCNT)
 .S ^TMP("FSC MULT",$J,FSCCNT)=CALL
 .D BUILD^FSCEU(CALL)
 I $D(^TMP("FSC SELECT",$J,"VVALUES")),'$D(^TMP("FSC SELECT",$J,"EVALUES")) D
 .S VALMBG=LASTCNT+1
 .D ENTRY^FSCLMV,HEADER^FSCLMV
 Q
