NURCES01 ;HIRMFO/YH-END OF SHIFT REPORT PART 1/2 ;12/6/96
 ;;4.0;NURSING SERVICE;;Apr 25, 1997
PRINT1 ;
 D ^NURSAPCH
 S GMROUT=0 I GPACK D SETARRY^NURCES2
 I GFH D DIET^NURCES4
 D EN6^NURSCUTL S NURSCLAS("CL")=1 D EN2^NURSCUTL
 I NURSCLAS S NDATA(1)=$S($D(^NURSA(214.6,NURSCLAS,0)):^(0),1:"") S NURFACT=$S($P(NDATA(1),"^",4)'="":$P(NDATA(1),"^",4),1:""),NURCAT=$S(+$P(NDATA(1),"^",3):$P(NDATA(1),"^",3),1:""),NCOM=$S($P(NDATA(1),"^",7)'="":$P(NDATA(1),"^",7),1:"")
 I $D(^NURSF(214,DFN,0)) S NDATA(2)=^(0),NSEC=$S('$D(NDATA(2)):"",'$P(NDATA(2),"^",4):"",'$D(^NURSF(213.3,$P(NDATA(2),"^",4),0)):"",1:$P(^NURSF(213.3,$P(NDATA(2),"^",4),1),"^"))
 D PTDATA^NURCES2,PTPROB^NURCES1 Q:NURQUIT
 I GPACK,$D(NURIV),NURIV>0 S N=0 F  S N=$O(NURIV(N)) Q:N'>0  D
 . D FITLINE^NURCES5(NURIV(N),62,.NPR)
 I GPACK,$D(NURTUBE),NURTUBE>0 S NPR=NPR+1,NPR(NPR)="Last tube changed:",GSITE="" F  S GSITE=$O(NURTUBE(GSITE)) Q:GSITE=""  S Y=NURTUBE(GSITE) X ^DD("DD") S NPR=NPR+1,NPR(NPR)="  "_GSITE_" on "_$P(Y,":",1,2)
 K NURIV,NURTUBE,GLEFT,GSITE,GSAVEH S NN(1)=2 S:NPR>2&(NOPT=1) NN(1)=NPR S:NADM>NN(1) NN(1)=NADM S:NPT>NN(1) NN(1)=NPT S:NVM>NN(1) NN(1)=NVM F NN=1:1:NN(1) D:$Y>(IOSL-8) HEADER^NURCES2 Q:NURQUIT  D PRINT2^NURCES2 Q:NURQUIT
 I NOPT=2 D PART2
 Q:NURQUIT  W $S(NOPT=1:NURX,NOPT=2:$E(NURX,1,79),1:NURX),!
 Q
PART2 ;
 W !,?10,"Patient Problems:",!
 I NPR>0 F NN=1:1:NPR W ?12,NPR(NN),! D:$Y>(IOSL-8) HEADER^NURCES2 Q:NURQUIT
 I NPR=0 W ?49,"|",?64,"|",! D:$Y>(IOSL-8) HEADER^NURCES2
 Q
