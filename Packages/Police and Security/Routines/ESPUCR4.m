ESPUCR4 ;DALISC/CKA - UNIFORM CRIME REPORT COUNT AND GET TOTALS- 1/92 [8/1/96 7:30am]
 ;;1.0;POLICE & SECURITY;**12,17,22**;Mar 31, 1994
EN Q  ;CALLED FROM ESPUCR1
STOP ;STOP & ARRESTS TOTALS
OFFA ; COUNT PHYSICAL ARREST OFFENDERS
 S ^(110)=^ESP(912.4,ESPIEN,1,ESPINS,110)+1
 S ESPOF=0
 F ESPX=1:1 S ESPOF=$O(^ESP(912,ESPOFN,40,ESPOF)) Q:ESPOF'>0  D
 .  S ESPOFF=$P(^ESP(912,ESPOFN,40,ESPOF,0),U,11) D
 ..  I ESPTYPE=42,$P(^ESP(912,ESPOFN,40,ESPOF,0),U,2) S ^(111)=^ESP(912.4,ESPIEN,1,ESPINS,111)+1 D  QUIT
 ...  I ESPOFF="E"!(ESPOFF="PO") S ^(112)=^ESP(912.4,ESPIEN,1,ESPINS,112)+1 QUIT
 ...  I ESPOFF="O" S ^(113)=^ESP(912.4,ESPIEN,1,ESPINS,113)+1 QUIT
 ...  I ESPOFF="P" S ^(114)=^ESP(912.4,ESPIEN,1,ESPINS,114)+1 QUIT
 ...  I ESPOFF="V" S ^(115)=^ESP(912.4,ESPIEN,1,ESPINS,115)+1 QUIT
 I ESPTYPE=43 S ^(116)=^ESP(912.4,ESPIEN,1,ESPINS,116)+1 D  QUIT
 .  I ESPSUB=17 S ^(118)=^ESP(912.4,ESPIEN,1,ESPINS,118)+1 QUIT
 .  I ESPSUB=18 S ^(117)=^ESP(912.4,ESPIEN,1,ESPINS,117)+1 QUIT
 QUIT
THEFT ;THEFT TOTALS
 S ^(119)=^ESP(912.4,ESPIEN,1,ESPINS,119)+1
 ;COIN-OPERATED MACHINES
 I ESPTYPE=44 S ^(120)=^ESP(912.4,ESPIEN,1,ESPINS,120)+1 D  QUIT
 .  S ESPDOL=0
 .  F ESPX=1:1 S ESPDOL=$O(^ESP(912,ESPOFN,90,ESPDOL)) Q:ESPDOL'>0  D
 ..  S ^ESP(912.4,ESPIEN,1,ESPINS,121)=^ESP(912.4,ESPIEN,1,ESPINS,121)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,3)
 ..  S ^ESP(912.4,ESPIEN,1,ESPINS,122)=^ESP(912.4,ESPIEN,1,ESPINS,122)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,4)
 ;DRUG THEFTS
 I ESPTYPE=45 S ^(129)=^ESP(912.4,ESPIEN,1,ESPINS,129)+1 D  D DDOL QUIT
 .  I ESPSUB=19 S ^(125)=^ESP(912.4,ESPIEN,1,ESPINS,125)+1,^(123)=^ESP(912.4,ESPIEN,1,ESPINS,123)+1 QUIT
 .  I ESPSUB=21 S ^(126)=^ESP(912.4,ESPIEN,1,ESPINS,126)+1,^(123)=^ESP(912.4,ESPIEN,1,ESPINS,123)+1 QUIT
 .  I ESPSUB=20 S ^(127)=^ESP(912.4,ESPIEN,1,ESPINS,127)+1,^(124)=^ESP(912.4,ESPIEN,1,ESPINS,124)+1 QUIT
 .  I ESPSUB=22 S ^(128)=^ESP(912.4,ESPIEN,1,ESPINS,128)+1,^(124)=^ESP(912.4,ESPIEN,1,ESPINS,124)+1 QUIT
 ;GOVERNMENT PROPERTY
 I ESPTYPE=46 S ^(132)=^ESP(912.4,ESPIEN,1,ESPINS,132)+1 D  D GDOL QUIT
 .  I ESPSUB=23 S ^(133)=^ESP(912.4,ESPIEN,1,ESPINS,133)+1 QUIT
 .  I ESPSUB=24 S ^(134)=^ESP(912.4,ESPIEN,1,ESPINS,134)+1 QUIT
 .  I ESPSUB=39 S ^(133.1)=^ESP(912.4,ESPIEN,1,ESPINS,133.1)+1 QUIT
 .  I ESPSUB=40 S ^(134.1)=^ESP(912.4,ESPIEN,1,ESPINS,134.1)+1 QUIT
 ;PERSONAL PROPERTY
 I ESPTYPE=47 S ^(137)=^ESP(912.4,ESPIEN,1,ESPINS,137)+1 D  D PDOL QUIT
 .  I ESPSUB=25 S ^(138)=^ESP(912.4,ESPIEN,1,ESPINS,138)+1 QUIT
 .  I ESPSUB=26 S ^(139)=^ESP(912.4,ESPIEN,1,ESPINS,139)+1 QUIT
 .  I ESPSUB=41 S ^(138.1)=^ESP(912.4,ESPIEN,1,ESPINS,138.1)+1 QUIT
 .  I ESPSUB=42 S ^(139.1)=^ESP(912.4,ESPIEN,1,ESPINS,139.1)+1 QUIT
 ;MOTOR VEHICLES
 I ESPTYPE=48 S ^(142)=^ESP(912.4,ESPIEN,1,ESPINS,142)+1
 ;GOVT VEHICLE
 I ESPSUB=27 S ^(143)=^ESP(912.4,ESPIEN,1,ESPINS,143)+1 D   QUIT
 .  ;GOVT VEHICLE DOLLARS
 .  S ESPDOL=0
 .  F ESPX=1:1 S ESPDOL=$O(^ESP(912,ESPOFN,90,ESPDOL)) Q:ESPDOL'>0  D
 ..  S ^ESP(912.4,ESPIEN,1,ESPINS,144)=^ESP(912.4,ESPIEN,1,ESPINS,144)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,3)
 ..  S ^ESP(912.4,ESPIEN,1,ESPINS,145)=^ESP(912.4,ESPIEN,1,ESPINS,145)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,4)
 ..  I $P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,4)'="" S ^(146)=^ESP(912.4,ESPIEN,1,ESPINS,146)+1
 ;PRIVATE VEHICLE
 I ESPSUB=28 S ^(147)=^ESP(912.4,ESPIEN,1,ESPINS,147)+1 D  QUIT
 .  ;PERSONAL VEHICLE DOLLARS
 .  S ESPDOL=0
 .  F ESPX=1:1 S ESPDOL=$O(^ESP(912,ESPOFN,90,ESPDOL)) Q:ESPDOL'>0  D
 ..  S ^ESP(912.4,ESPIEN,1,ESPINS,148)=^ESP(912.4,ESPIEN,1,ESPINS,148)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,3)
 ..  S ^ESP(912.4,ESPIEN,1,ESPINS,149)=^ESP(912.4,ESPIEN,1,ESPINS,149)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,4)
 ..  I $P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,4)'="" S ^(150)=^ESP(912.4,ESPIEN,1,ESPINS,150)+1
 QUIT
EX Q
DDOL ;DRUG DOLLARS
 S ESPDOL=0
 F ESPX=1:1 S ESPDOL=$O(^ESP(912,ESPOFN,90,ESPDOL)) Q:ESPDOL'>0  D
 .  S ^ESP(912.4,ESPIEN,1,ESPINS,130)=^ESP(912.4,ESPIEN,1,ESPINS,130)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,3)
 .  S ^ESP(912.4,ESPIEN,1,ESPINS,131)=^ESP(912.4,ESPIEN,1,ESPINS,131)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,4)
 QUIT
GDOL ;GOV'T PROPERTY DOLLARS
 S ESPDOL=0
 F ESPX=1:1 S ESPDOL=$O(^ESP(912,ESPOFN,90,ESPDOL)) Q:ESPDOL'>0  D
 .  S ^ESP(912.4,ESPIEN,1,ESPINS,135)=^ESP(912.4,ESPIEN,1,ESPINS,135)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,3)
 .  S ^ESP(912.4,ESPIEN,1,ESPINS,136)=^ESP(912.4,ESPIEN,1,ESPINS,136)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,4)
 QUIT
PDOL ;PERSONAL PROPERTY DOLLARS
 S ESPDOL=0
 F ESPX=1:1 S ESPDOL=$O(^ESP(912,ESPOFN,90,ESPDOL)) Q:ESPDOL'>0  D
 .  S ^ESP(912.4,ESPIEN,1,ESPINS,140)=^ESP(912.4,ESPIEN,1,ESPINS,140)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,3)
 .  S ^ESP(912.4,ESPIEN,1,ESPINS,141)=^ESP(912.4,ESPIEN,1,ESPINS,141)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,4)
 QUIT
ADOL ;ARSON PROPERTY DOLLARS - CALLED BY ^ESPUCR2 TO TOTAL FOR UCR
 S ESPDOL=0
 F ESPX=1:1 S ESPDOL=$O(^ESP(912,ESPOFN,90,ESPDOL)) Q:ESPDOL'>0  D
 .  S ^ESP(912.4,ESPIEN,1,ESPINS,83)=^ESP(912.4,ESPIEN,1,ESPINS,83)+$P($G(^ESP(912,ESPOFN,90,ESPDOL,0)),U,3)
 QUIT
