FSCRPXUS ;SLC/STAFF-NOIS Get Call Info - Short Line of Call ;1/13/98  16:41
 ;;1.1;NOIS;;Sep 06, 1998
 ;
SHORT(CALL,USER) ; $$(call number,user) -> delimited call title line for user
 N CALLID,DEF,DEV,LINE,MOD,PRI,SPEC,SPECD,SUBJECT,SUP
 Q:'$D(^FSCD("CALL",+CALL,0)) "" S SUBJECT=$G(^(1)),CALLID=$P(^(0),U)
 S DEF=$P($G(^FSC("SPEC",+USER,0)),U,11)
 I '$L(DEF) S DEF="SMU" ;*** S LINE=CALLID_U_SUBJECT Q LINE
 S DEF="SMU" ;********
 S SUP=$P(^FSCD("CALL",+CALL,0),U,2),DEV=$P(^(0),U,17),SPEC=$P(^(0),U,9),SPECD=$P(^(0),U,21),MOD=$P(^(0),U,8),PRI=$P(^(0),U,10)
 S LINE=CALLID
 I DEF["S" S LINE=LINE_U_$P($$STAT^FSCESU(SUP,DEV),U,2)
 I DEF["M" S LINE=LINE_U_$$MODINI^FSCUF(MOD)
 I DEF["U" S LINE=LINE_U_$$USERINI^FSCUF(SPEC)
 I DEF["R" S LINE=LINE_U_$$USERINI^FSCUF(SPECD)
 I DEF["P" S LINE=LINE_U_$E($$VALUE^FSCGET(PRI,7100,6))
 S LINE=LINE_U_SUBJECT
 Q LINE
