PXRMDRCL ; SLC/PKR - Handle VA DRUG CLASS findings. ;08/04/2006
 ;;2.0;CLINICAL REMINDERS;**4**;Feb 04, 2005;Build 21
 ;
 ;====================================================
EVALFI(DFN,DEFARR,ENODE,FIEVAL) ;Evaluate drug class (file #50.605) findings.
 D EVALFI^PXRMDRGR(DFN,.DEFARR,ENODE,"VAC",.FIEVAL)
 Q
 ;
 ;====================================================
EVALTERM(DFN,FINDPA,ENODE,TERMARR,TFIEVAL) ;Evaluate drug class
 ;(file #50.605) terms.
 D EVALTERM^PXRMDRGR(DFN,.FINDPA,ENODE,"VAC",.TERMARR,.TFIEVAL)
 Q
 ;
 ;====================================================
EVALPL(FINDPA,ENODE,TERMARR,PLIST) ;Evaluate VA Drug Class terms
 ;for building patient lists.
 D EVALPL^PXRMDRGR(.FINDPA,ENODE,"VAC",.TERMARR,.PLIST)
 Q
 ;
 ;====================================================
MHVOUT(INDENT,IFIEVAL,NLINES,TEXT) ;Produce the MHV output.
 N CLASSIEN,DRUGCLAS,FTYPE,IND,PFIEVAL,PNAME
 S CLASSIEN=$P(IFIEVAL("FINDING"),";",1)
 ;DBIA #2531
 S DRUGCLAS=$$CLASS2^PSNAPIS(CLASSIEN)
 S PNAME=$P(DRUGCLAS,U,1)_"-"_$P(DRUGCLAS,U,2)
 S NLINES=NLINES+1,TEXT(NLINES)="Drug Class: "_PNAME
 S IND=0
 F  S IND=+$O(IFIEVAL(IND)) Q:IND=0  D
 . S TEMP=IFIEVAL(IND,"FINDING")
 . S FTYPE=$P(TEMP,";",2)
 . K PFIEVAL M PFIEVAL=IFIEVAL(IND)
 . I FTYPE="PS(55," D MHVOUT^PXRMDIN(INDENT+1,.PFIEVAL,.NLINES,.TEXT) Q
 . I FTYPE="PS(55NVA," D MHVOUT^PXRMDNVA(INDENT+1,.PFIEVAL,.NLINES,.TEXT) Q
 . I FTYPE="PSRX(" D MHVOUT^PXRMDOUT(INDENT+1,.PFIEVAL,.NLINES,.TEXT) Q
 S NLINES=NLINES+1,TEXT(NLINES)=""
 Q
 ;
 ;====================================================
OUTPUT(INDENT,IFIEVAL,NLINES,TEXT) ;Produce the clinical
 ;maintenance output.
 N CLASSIEN,DRUGCLAS,FTYPE,IND,PFIEVAL,TEMP
 S CLASSIEN=$P(IFIEVAL("FINDING"),";",1)
 ;DBIA #2531
 S DRUGCLAS=$$CLASS2^PSNAPIS(CLASSIEN)
 S TEMP=$P(DRUGCLAS,U,1)_"-"_$P(DRUGCLAS,U,2)
 S NLINES=NLINES+1
 S TEXT(NLINES)=$$INSCHR^PXRMEXLC(INDENT," ")_"Drug Class: "_TEMP
 S IND=0
 F  S IND=+$O(IFIEVAL(IND)) Q:IND=0  D
 . S TEMP=IFIEVAL(IND,"FINDING")
 . S FTYPE=$P(TEMP,";",2)
 . K PFIEVAL M PFIEVAL=IFIEVAL(IND)
 . I FTYPE="PS(55," D OUTPUT^PXRMDIN(INDENT+1,.PFIEVAL,.NLINES,.TEXT) Q
 . I FTYPE="PS(55NVA," D OUTPUT^PXRMDNVA(INDENT+1,.PFIEVAL,.NLINES,.TEXT) Q
 . I FTYPE="PSRX(" D OUTPUT^PXRMDOUT(INDENT+1,.PFIEVAL,.NLINES,.TEXT) Q
 Q
 ;
