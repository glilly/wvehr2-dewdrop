LRTOT ;SLC/CJS - TALLY OF TESTS ;2/19/91  13:09 ;
 ;;5.2;LAB SERVICE;;Sep 27, 1994
 S U="^",%DT="AE" D ^%DT Q:Y<1  S LRAD=Y K ^TMP($J)
 R !!,"TESTS BY:",!!,?10,"P ==> PRACTITIONER",!,?10,"V ==> VERIFIER",!!,?10,X:DTIME S LRTT=$S($E(X)="P":1,$E(X)="V":2,1:"") G END:LRTT="" W $S(LRTT=1:"RACTITIONER",LRTT=2:"ERIFIER"),!
 G:LRTT=2 TO10
 S LRSN=0
TO1 S LRSN=$O(^LRO(69,LRAD,1,LRSN)) G:LRSN<1 TO5
 S LRDOC=$P(^LRO(69,LRAD,1,LRSN,0),U,6) G:LRDOC="" TO1
 S I=0 F  S I=$O(^LRO(69,LRAD,1,LRSN,2,I)) Q:I<1  S LRTSTS=+^LRO(69,LRAD,1,LRSN,2,I,0) S:'$D(^TMP($J,LRTSTS,LRDOC)) ^TMP($J,LRTSTS,LRDOC)=0 S ^(LRDOC)=^(LRDOC)+1
 G TO1
TO5 W !,"TEST:" S I=0 F  S I=$O(^TMP($J,I)) Q:I<1  W !,$P(^LAB(60,I,0),U) D TO6
 G END
TO6 W !,?5,"BY:" S J=0 F  S J=$O(^TMP($J,I,J)) Q:J<1  S LRDOCT=$S($D(^VA(200,J,0)):$P(^(0),U),1:J) W !,?5,LRDOCT,?30,^TMP($J,I,J)
 Q
TO10 S DIC="^LRO(68,",DIC(0)="AEOQZ" D ^DIC G END:Y<1 S LRAA=+Y
 S LRAN=0 F  S LRAN=$O(^LRO(68,LRAA,1,LRAD,1,LRAN)) Q:LRAN<1  Q:+LRAN'=LRAN  D TO11
 W !,"TEST:" S LRTSTS=0 F  S LRTSTS=$O(^TMP($J,LRTSTS)) Q:LRTSTS<1  W !,$S($D(^LAB(60,LRTSTS,0)):$P(^(0),U),1:LRTSTS) D TO12
 K ^TMP($J) G TO10
TO11 S LRTN=0 F  S LRTN=$O(^LRO(68,LRAA,1,LRAD,1,LRAN,4,LRTN)) Q:LRTN<1  S LRINI=$P(^(LRTN,0),U,4),LRTSTS=$P(^(0),U) Q:LRINI=""!(LRTSTS="")  S:'$D(^TMP($J,LRTSTS,LRINI)) ^TMP($J,LRTSTS,LRINI)=0 S ^(LRINI)=^(LRINI)+1
 Q
TO12 W !,?5,"BY:" S LRINI=0 F  S LRINI=$O(^TMP($J,LRTSTS,LRINI)) Q:LRINI<1  W !,?5,LRINI,?30,^TMP($J,LRTSTS,LRINI)
 Q
END K LRDOCT
 Q
