XPDKRN ;SFISC/RSD - Kernel Install program ;07/02/2003  12:59
V ;;8.0;KERNEL;**304**;Jul 10, 1995
EN N X,Y
 W !!,"KIDS "_$P($T(V),";",3),!
 D DT^DICRW:'$D(DT),INIT:($O(^DOPT("XPD","B"),-1)'=5)
 I '$G(DUZ) W !,"DUZ must be defined as an active user!!" Q
 D  Q:Y<0  G @X
 .N DIC
 .S DIC="^DOPT(""XPD"",",DIC(0)="AEQZ"
 .D ^DIC Q:Y<0
 .S X=$P(Y(0),U,2,99)
 Q
 ;
INIT N DIK,I,X
 K ^DOPT("XPD")
 S ^DOPT("XPD",0)="KIDS OPTION^1N^",DIK="^DOPT(""XPD"","
 F I=1:1 S X=$E($T(OPT+I),4,99) Q:X=""  S ^DOPT("XPD",I,0)=X
 D IXALL^DIK
 Q
ADXREF ;New-style 'AD' xref for FILE (#9.64) subfile of the BUILD (#9.6) file.
 ;If DATA COMES WITH FILE is changed from YES to NO, then
 ;make sure these other fields are empty.
 N XPDFDA,XPDIENS
 S XPDIENS=DA_","_DA(1)_","
 S XPDFDA(9.64,XPDIENS,222.5)="@" ; RESOLVE POINTERS
 S XPDFDA(9.64,XPDIENS,222.6)="@" ; DATA LIST
 S XPDFDA(9.64,XPDIENS,222.8)="@" ; SITE'S DATA
 S XPDFDA(9.64,XPDIENS,222.9)="@" ; MAY USER OVERRIDE DATA UPDATE
 S XPDFDA(9.64,XPDIENS,224)="@"   ; SCREEN TO SELECT DATA
 D FILE^DIE("","XPDFDA")
 Q
OPT ;
 ;;LOAD A DISTRIBUTION^EN1^XPDIL
 ;;VERIFY CHECKSUMS IN TRANSPORT GLOBAL^EN1^XPDDCS
 ;;PRINT TRANSPORT GLOBAL^EN2^XPDDP
 ;;COMPARE TRANSPORT GLOBAL TO CURRENT SYSTEM^EN1^XPDCOM
 ;;BACKUP A TRANSPORT GLOBAL^EN^XPDIB
 ;;INSTALL PACKAGE(S)^EN^XPDI
 ;;RESTART INSTALL OF PACKAGE(S)^EN^XPDIR
 ;;UNLOAD A DISTRIBUTION^EN1^XPDIU
