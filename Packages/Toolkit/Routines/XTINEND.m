XTINEND ;SF-ISC/STAFF - POSTINIT FOR TOOLKIT ;02/10/95  12:04
 ;;7.3;TOOLKIT;;Apr 25, 1995
A I ^%ZOSF("OS")["DSM" D
 .W !!,"Will now install the VAX/Alpha Performance Monitor (VPM)",!!
 .N DIFROM,DIFQN,%X,%Y S %X="^UTILITY(""DIF"",$J,",%Y="^UTILITY(""TMP"",$J," D %XY^%RCR
 .S X="XUCINIT" X ^%ZOSF("TEST") I  D ^XUCINIT
 .I $$LKOPT^XPDMENU("XUCM MAIN") S %=$$ADD^XPDMENU("XTCM MAIN","XUCM MAIN")
 .S %X="^UTILITY(""TMP"",$J,",%Y="^UTILITY(""DIF"",$J," D %XY^%RCR
B I ^%ZOSF("OS")["MSM" D
 .W !!,"Will now install the MSM 486 Performance Monitor (MPM)",!!
 .N DIFROM,DIFQN,%X,%Y S %X="^UTILITY(""DIF"",$J,",%Y="^UTILITY(""TMP"",$J," D %XY^%RCR
 .S X="XUCSINIT" X ^%ZOSF("TEST") I  D ^XUCSINIT
 .I $$LKOPT^XPDMENU("XUCS MANAGER MENU") S %=$$ADD^XPDMENU("XTCM MAIN","XUCS MANAGER MENU")
 .S %X="^UTILITY(""TMP"",$J,",%Y="^UTILITY(""DIF"",$J," D %XY^%RCR
A1 S U="^",%=$$ADD^XPDMENU("EVE","XUPROG"),%=$$ADD^XPDMENU("EVE","XTMENU"),%=$$ADD^XPDMENU("EVE","XTCM MAIN")
 I % S %=$$DELETE^XPDMENU("XUSITEMGR","XURTLM"),%=$$DELETE^XPDMENU("XUSITEMGR","XUCPMENU"),%=$$DELETE^XPDMENU("EVE","XUCM MAIN")
 ;
A2 ;Cleanup unnecessary "IX" nodes for X-refs which don't exist.  This
 ;part of the code can be removed after Toolkit 7.3 has been released
 K ^DD(15,0,"IX","AMFI",15,999999901),^DD(15,0,"IX","APOT",15,.04)
 K ^DD(15,0,"IX","AZ1",15,.05),^DD(15.01101,0,"IX","ARDY",15.01101,.02)
 ;
A3 ;MTLU POST INIT
 D ^XTLKPST
A99 S X="XTINITY" X ^%ZOSF("TEST") I $T D @("^"_X)
EXIT K %
 S XMB="XU-INSTALL-DONE",XMB(1)="TOOLKIT",XMB(2)=$P($T(+2),";",3),XMB(3)=$$HTE^XLFDT($H,"1P"),XMB(4)=$G(^DD("SITE")),XMB(5)=$S($D(^VA(200,DUZ,0))#2:$P(^(0),"^"),1:"Unk"),XMY("G.KERNEL_INSTALL@ISC-SF.VA.GOV")=""
 F I=1:1:4 I $D(XUINTIME(I)) S XMB(I+5)=$$HTE^XLFDT(XUINTIME(I),"1P")
 S:$D(XUINTIME(1)) XMB(10)=$$HDIFF^XLFDT($H,XUINTIME(1),3) D ^XMB
MES W !!,$C(7),"TO PROTECT THE SECURITY OF DHCP SYSTEMS, DISTRIBUTION OF THIS"
 W !,"SOFTWARE FOR USE ON ANY OTHER COMPUTER SYSTEM IS PROHIBITED."
 W !,"ALL REQUESTS FOR COPIES OF THE KERNEL FOR NON-DHCP USE SHOULD"
 W !,"BE REFERRED TO YOUR LOCAL ISC."
 K XMB,XMY,XUINTIME
 Q
TIME() Q $$HTE^XLFDT($H,1)
 ;
