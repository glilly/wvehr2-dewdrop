LBRYSTA ;ISC2/DJM-SERIALS STATISTICAL REPORT ;[ 05/23/97  12:13 PM ]
 ;;2.5;Library;**2**;Mar 11, 1996
 ;
START W @IOF,!,?5,"VA Library Serials Statistical Report for "_LBRYNAM
 S Y=DT X ^DD("DD") S YDT=Y W ?60,YDT,!
S1 W !,"Please enter the Month and Year for the Statistical Counts Report."
 S %DT="AE",%DT(0)="-NOW" D ^%DT G:Y<0 EXIT
 S MO=$E(Y,4,5) I +MO=0 W !!,"You must enter a month." G S1
 S (LBTIME,LBDT)=$E(Y,1,5)_"00"-.0001,(LBCR,LBCRR,LBTR,LBTRR,LBTOT)=0
 F  S LBTIME=$O(^LBRY(682,"A3",LBTIME)) Q:LBTIME'>0  Q:$E(LBTIME,4,5)'=MO  S TITLE=0 D S2
 K ^TMP($J,DUZ)
 F  S LBDT=$O(^LBRY(682,"A4",LBDT)) Q:LBDT'>0  Q:$E(LBDT,4,5)'=MO  D S1A
 G S1B
S1A S (X4,X5)=0
 F  S X4=$O(^LBRY(682,"A4",LBDT,X4)) Q:X4'>0  D
 . F  S X5=$O(^LBRY(682,"A4",LBDT,X4,X5)) Q:X5'>0  D
 .. I $G(^LBRY(682,X5,0))="" K ^LBRY(682,"A4",LBDT,X4,X5) Q
 .. I $P(^LBRY(682,X5,0),U,4)'=LBRYPTR Q
 .. I '$D(^TMP($J,DUZ,X4,X5)) S ^(X5)="",LBTOT=LBTOT+1
 Q
S1B W !!,"ISSUES ROUTED: ",LBCR,?30,"NUMBER OF RECIPIENTS ROUTED TO: ",LBCRR
 W !!,"ToC ISSUES ROUTED: ",LBTR,?30,"NUMBER OF ToC RECIPIENTS ROUTED TO: ",LBTRR
 W !!,"TITLES WITH CHECK-IN ACTIVITY: ",LBTOT
 K ^TMP($J,DUZ)
 W ! S XZ="Exit// " D PAUSE^LBRYCK0 K XZ G START
S2 F  S TITLE=$O(^LBRY(682,"A3",LBTIME,TITLE)) Q:TITLE'>0  D
 . I $G(^LBRY(682,TITLE,0))="" K ^LBRY(682,"A3",LBTIME,TITLE) Q
 . I $P(^LBRY(682,TITLE,0),U,4)'=LBRYPTR Q
 . S TITLE1=0 F  S TITLE1=$O(^LBRY(682,"A3",LBTIME,TITLE,TITLE1)) Q:TITLE1'>0  D
 .. S XX=$G(^LBRY(682,TITLE,4,TITLE1,0)) I XX'="" D MORE
 .. I XX="" K ^LBRY(682,"A3",LBTIME,TITLE,TITLE1)
 Q
MORE S X1=$P(XX,U,3),(X2,X3)=0
 F  S X2=$O(^LBRY(681,X1,2,X2)) Q:X2'>0  S X3=X3+1
 I $P(XX,U,1)'="ToC" S LBCR=LBCR+1,LBCRR=LBCRR+X3 Q
 S LBTR=LBTR+1,LBTRR=LBTRR+X3
 Q
EXIT K YDT,%DT,MO,I,J,K,LBTIME,LBDT,LBCR,LBCRR,LBTR,LBTRR,LBTOT,TITLE
 K TITLE1,XX,X1,X2,X3,X4,X5
 Q
