LRAPQAM ;AVAMC/REG/WTY - PRINT PATH MICRO MODIFICATIONS ;9/25/00
 ;;5.2;LAB SERVICE;**58,72,248**;Sep 27, 1994
 ;
 S LRDICS="SPCYEM" D ^LRAP G:'$D(Y) END D B^LRU G:Y<0 END
 S LRSDT=LRSDT-.01,LRLDT=LRLDT+.99
 S ZTRTN="QUE^LRAPQAM" D BEG^LRUTL G:POP!($D(ZTSK)) END
QUE U IO S LRA=1,LR("DIWF")="W"
 D XR^LRU,L^LRU,S^LRU,L1^LRU,EN2^LRUA,SET^LRUA
 F LRX=0:0 S LRSDT=$O(^LR(LRXR,LRSDT)) Q:'LRSDT!(LRSDT>LRLDT)  D
 .F LRDFN=0:0 S LRDFN=$O(^LR(LRXR,LRSDT,LRDFN)) Q:'LRDFN!(LR("Q"))  D L
 W:'$D(LRSAV) !,"No modifications to print" D END^LRUTL,END Q
L F LRI=0:0 S LRI=$O(^LR(LRXR,LRSDT,LRDFN,LRI)) Q:'LRI!(LR("Q"))  D
 .I $D(^LR(LRDFN,LRSS,LRI,4))!($D(^(5)))!($D(^(6)))!($D(^(7))) D W Q
 .;Check for supplementary report changes
 .I $D(^LR(LRDFN,LRSS,LRI,1.2)) D
 ..S LRFLG=0
 ..F I=0:0 S I=$O(^LR(LRDFN,LRSS,LRI,1.2,I)) Q:'I!(LR("Q"))!(LRFLG)  D
 ...I $D(^LR(LRDFN,LRSS,LRI,1.2,I,2)) S LRFLG=1
 ..I LRFLG D W K LRFLG
 Q
W S (LRQ(9),LRS(99))=1,LRAP=LRDFN_"^"_LRI,LRSAV=1 D EN^LRSPRPT
 Q
 ;
END D V^LRU Q
