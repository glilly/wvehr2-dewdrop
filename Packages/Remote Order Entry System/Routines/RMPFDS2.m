RMPFDS2 ;DDC/KAW-DISPLAY ORDERS IN ERROR STATUS [ 06/16/95   3:06 PM ]
 ;;2.0;REMOTE ORDER/ENTRY SYSTEM;**17**;06/06/01
RMPFSET I '$D(RMPFMENU) D MENU^RMPFUTL I '$D(RMPFMENU) W !!,$C(7),"*** A MENU SELECTION MUST BE MADE ***" Q  ;;RMPFMENU must be defined
 I '$D(RMPFSTAN)!'$D(RMPFDAT)!'$D(RMPFSYS) D ^RMPFUTL Q:'$D(RMPFSTAN)!'$D(RMPFDAT)!'$D(RMPFSYS)
 I RMPFMENU=10,$D(^RMPF(791810,"AF")) D  G END:$D(RMPFOUT)
 .W $C(7),@IOF,!?15,"***                    ROES                    ***"
 .W !?15,"*** ELIGIBILITY DETERMINATIONS AWAITING ACTION ***",!
 .D ^RMPFDE
 S (X,FL)=0,MU=$O(^RMPF(791810.5,"C",RMPFMENU,0))
 G END:'$P(RMPFSYS,U,9),END:'$D(^RMPF(791810,"AC",6))
 F  S X=$O(^RMPF(791810,"AC",6,X)) Q:'X  I $D(^RMPF(791810,X,0)),$P(^(0),U,15)=MU S FL=1 Q
 G END:'FL
 S RMPFORD="S",RMPFTP="B",RMPFP(6)="" D ^RMPFDS1 G END:$D(RMPFOUT)
 D CONT^RMPFDS1
END K X,FL,MU,S0,RMPFORD,RMPFTP,RMPFP,RMPFCX,RMPFDOD,RMPFOUT,RMPFQUT,RMPFS
 K SS,%XX,%YY,DFN,I,Y,RMPFOUT,RMPFQUT Q
CONT F  Q:$Y>21  W !
 W !,"Type <RETURN> to continue." D READ Q
READ K RMPFOUT,RMPFQUT
 R Y:DTIME I '$T W $C(7) R Y:5 G READ:Y="." S:'$T Y=U
 I Y?1"^".E S (RMPFOUT,Y)="" Q
 S:Y?1"?".E (RMPFQUT,Y)=""
 Q
QUE W ! S %ZIS="NPQ" D ^%ZIS G END:POP
 I IO=IO(0),'$D(IO("S")) D ^RMPFDS1,CONT^RMPFDS1 S RMPFOUT="" G QUEE
 I $D(IO("S")) S %ZIS="",IOP=ION D ^%ZIS G ^RMPFDS1
 S ZTRTN="^RMPFDS1",ZTSAVE("RMPF*")="",ZTSAVE("DFN")=""
 S ZTSAVE("RMPFP(")="",ZTIO=ION D ^%ZTLOAD
 D HOME^%ZIS S RMPFOUT=""
 W:$D(ZTSK) !!,"*** Request Queued ***" H 2
QUEE K %T,%ZIS,POP,ZTRTN,ZTSAVE,ZTIO,ZTSK Q
