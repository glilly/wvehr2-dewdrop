LEXQIP2 ;ISL/KER - Query - ICD Procedure - Extract ;10/30/2008
 ;;2.0;LEXICON UTILITY;**62**;Sep 23, 1996;Build 16
 ;               
 ; Global Variables
 ;    ^ICD(               ICR   4487
 ;    ^ICD0(              ICR   4485
 ;    ^ICM(               ICR   4488
 ;               
 ; External References
 ;    DRGD^ICDGTDRG       ICR   4052
 ;    $$DT^XLFDT          ICR  10103
 ;    $$UP^XLFSTR         ICR  10104
 ;               
 ; Local Variables NEWed or KILLed Elsewhere
 ;    LEXINT              Internal value for O.R. Procedures
 ;               
 Q
MDCDRG(X,LEXCDT,LEX,LEXLEN) ; Major Diagnostic Category/DRG
 N LEXC,LEXDI,LEXDR,LEXDRG,LEXDRG1,LEXDRG2,LEXDRGI,LEXDRGC,LEXDRGD,LEXDRI,LEXEF,LEXHR,LEXI,LEXIEN,LEXL,LEXLC,LEXN,LEXMDCC,LEXMDCE
 N LEXMDCI,LEXMI,LEXMR,LEXT,LEXUD,LEXUM,LEXVDT S LEXVDT=+($G(LEXCDT)) S:LEXVDT'?7N LEXVDT=$$DT^XLFDT K LEXUM,LEXUD,LEX
 S LEXLC=0,LEXIEN=+($G(X)),LEXCDT=$G(LEXCDT),LEXLEN=+($G(LEXLEN)) S:+LEXLEN'>0 LEXLEN=62 Q:LEXCDT'?7N
 S LEXEF=$O(^ICD0(+LEXIEN,2,"B",(LEXCDT+.9999)),-1),LEXHR=$O(^ICD0(+LEXIEN,2,"B",+LEXEF," "),-1) Q:+LEXHR'>0
 S LEXMDCC=0,LEXMI="" F  S LEXMI=$O(^ICD0(+LEXIEN,2,+LEXHR,1,"B",LEXMI)) Q:'$L(LEXMI)  D
 . S LEXMR=$O(^ICD0(+LEXIEN,2,+LEXHR,1,"B",LEXMI," "),-1) Q:+LEXMR'>0
 . Q:'$D(^ICD0(+LEXIEN,2,+LEXHR,1,+LEXMR,0))  S LEXMDCI=$G(^ICD0(+LEXIEN,2,+LEXHR,1,+LEXMR,0)) Q:+LEXMDCI'>0
 . S LEXMDCE=$P($G(^ICM(+LEXMDCI,0)),"^",1) Q:'$L(LEXMDCE)  Q:$D(LEXUM(LEXMDCE))
 . S LEXMDCC=+($G(LEXMDCC))+1 S LEXLC=LEXLC+1,LEX(LEXLC)=LEXMDCE
 . S LEXDRGC=0  S LEXDI="" F  S LEXDI=$O(^ICD0(+LEXIEN,2,+LEXHR,1,+LEXMR,1,"B",LEXDI)) Q:'$L(LEXDI)  D
 . . S LEXDR=$O(^ICD0(+LEXIEN,2,+LEXHR,1,+LEXMR,1,"B",LEXDI," "),-1) Q:+LEXDR'>0
 . . Q:'$D(^ICD0(+LEXIEN,2,+LEXHR,1,+LEXMR,1,+LEXDR,0))  S LEXDRGI=$G(^ICD0(+LEXIEN,2,+LEXHR,1,+LEXMR,1,+LEXDR,0)) Q:+LEXDRGI'>0
 . . N LEXC,LEXDRG,LEXDRG1,LEXDRG2,LEXDRGC,LEXI,LEXDRGD,LEXL,LEXN,LEXT S LEXDRG=$P($G(^ICD(+LEXDRGI,0)),"^",1) K LEXDRGD
 . . D DRGD^ICDGTDRG(LEXDRG,"LEXDRGD",,+LEXVDT) S LEXDRG=$TR(LEXDRG,"DRG",""),LEXDRG=+LEXDRG Q:+LEXDRG'>0  S LEXI=0
 . . F  S LEXI=$O(LEXDRGD(LEXI)) Q:+LEXI'>0  S LEXT=$$TM^LEXQM($G(LEXDRGD(LEXI))) I '$L(LEXT)!(LEXT["CODE TEXT MAY BE INACCURATE") K LEXDRGD(LEXI)
 . . S LEXDRGD=+($O(LEXDRGD(" "),-1)) D PR^LEXQM(.LEXDRGD,(LEXLEN-14)) S LEXDRG1="DRG "_LEXDRG,LEXDRG1=LEXDRG1_$J(" ",(10-$L(LEXDRG1))),LEXDRG2=$J(" ",10)
 . . S (LEXC,LEXI)=0 F  S LEXI=$O(LEXDRGD(LEXI)) Q:+LEXI'>0  D
 . . . N LEXT,LEXL,LEXN S LEXT=$$TM^LEXQM($G(LEXDRGD(LEXI))) Q:'$L(LEXT)  S LEXC=LEXC+1 S:LEXC=1 LEXL=LEXDRG1_LEXT,LEXDRGC=+($G(LEXDRGC))+1
 . . . S:LEXC>1 LEXL=LEXDRG2_LEXT S LEXDRGD(LEXI)=LEXL
 . . S:+LEXDRGD>0 LEXDRGC=+($G(LEXDRGC))+1
 . . S (LEXC,LEXI)=0 F  S LEXI=$O(LEXDRGD(LEXI)) Q:+LEXI'>0  D
 . . . N LEXT S LEXT=$G(LEXDRGD(LEXI)) Q:'$L(LEXT)  S LEXC=LEXC+1
 . . . S LEXLC=LEXLC+1,LEX(LEXLC)="    "_LEXT
 S:LEXEF?7N&($L(LEX(1))) LEX(0)=$$SD^LEXQM(LEXEF) S LEX=+($O(LEX(" "),-1))
 Q
MAJ(X,LEX) ; Major O.R. Procedures
 N LEXC,LEXCHR,LEXHDR,LEXI,LEXI1,LEXI2,LEXIDI,LEXIEN,LEXPC,LEXSTR,LEXT S LEXIEN=+($G(X)) Q:+LEXIEN'>0  Q:'$D(^ICD0(+LEXIEN,0))
 S LEXSTR=$P($G(^ICD0(+LEXIEN,"M")),"^",1) Q:'$L(LEXSTR)  D OR(LEXSTR,.LEX)
 Q
OR(X,LEX) ; O.R. Procedures
 K LEX N LEXC,LEXCHR,LEXHDR,LEXI,LEXI1,LEXI2,LEXIDI,LEXPC,LEXSTR,LEXT S LEXSTR=$G(X) Q:'$L(LEXSTR)
 S LEXHDR="Major O.R. ID",LEXPC=0,LEXCHR="" F LEXC=1:1  Q:'$L($E(LEXSTR,LEXC))  D
 . S LEXCHR=$E(LEXSTR,LEXC) Q:LEXCHR=""  F LEXI=1:1 S LEXIDI=$T(MID+LEXI),LEXIDI=$P(LEXIDI,";;",2) Q:LEXIDI="EXIT"  D
 . . S LEXI1=$$TM^LEXQM($P(LEXIDI,"=")),LEXI2=$$TM^LEXQM($P(LEXIDI,"=",2)) Q:$L(LEXI1)'=1  Q:LEXI1'=LEXCHR  Q:'$L(LEXI2)
 . . S LEXT=LEXI2 S:$D(LEXINT) LEXT=LEXT_$J(" ",(22-$L(LEXT)))_"("_LEXI1_")" S LEXPC=LEXPC+1,LEX(1,LEXPC)=$$UP^XLFSTR(LEXT)
 S:+($O(LEX(1," "),-1))>0 LEX(0)=$$UP^XLFSTR(LEXHDR),LEX(1)=$$UP^XLFSTR(LEXSTR),LEX=+($O(LEX(1," "),-1))
 Q
MID ; Major O.R. Procedures Text
 ;;1=Bowel
 ;;2=Chest
 ;;3=Lymphoma/Leukemia
 ;;4=Joint
 ;;5=Pancreas/Liver
 ;;6=Pelvic
 ;;7=Shoulder/Elbow
 ;;8=Thumb/Joint
 ;;9=Head/Neck
 ;;A=Cardio
 ;;M=Musculoskeletal
 ;;EXIT
 Q
CLR ; Clear
 N LEXINT
 Q
