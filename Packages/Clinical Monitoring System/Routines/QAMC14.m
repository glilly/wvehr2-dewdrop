QAMC14 ;HISC/DAD-CONDITION: SPECIALTY TRANSFER ;7/27/92  13:05
 ;;1.0;Clinical Monitoring System;;09/13/1993
EN1 ; *** CONDITION CODE
 S TXSPGRP=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P1"))#2:+^("P1"),1:0)
 F TXSPDT=(QAMTODAY-.0000001):0 S TXSPDT=$O(^DGPM("AMV6",TXSPDT)) Q:(TXSPDT'>0)!(TXSPDT\1'?7N)!(TXSPDT>(QAMTODAY+.9999999))  F QAMDFN=0:0 S QAMDFN=$O(^DGPM("AMV6",TXSPDT,QAMDFN)) Q:QAMDFN'>0  D LOOP0
 K TXSPGRP,TXSPDT,TXSPD0,TXSP
 Q
LOOP0 F TXSPD0=0:0 S TXSPD0=$O(^DGPM("AMV6",TXSPDT,QAMDFN,TXSPD0)) Q:TXSPD0'>0  D LOOP1
 Q
LOOP1 I TXSPGRP S TXSP=+$S($D(^DGPM(TXSPD0,0))#2:$P(^(0),"^",9),1:0) Q:$O(^QA(743.5,TXSPGRP,"GRP","AB",+TXSP,0))'>0
 S ^UTILITY($J,"QAM CONDITION",QAMD1,QAMDFN)="",^(QAMDFN,TXSPDT)=TXSPD0
 Q
EN2 ; *** PARAMETER CODE
 K DIC,DIR,DIRUT S DIC=743.5,DIC(0)="EMNQZ",DIC("S")="I $P(^QA(743.5,+Y,0),""^"",2)=45.7"
 S DIC("A")="TREATING SPECIALTY GROUP: ",DIC("B")=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P1"))#2:$P(^("P1"),"^",2),1:"") K:DIC("B")="" DIC("B")
 S DIR("?",1)="Enter a GROUP name that contains MAS treating specialties,",DIR("?")="or press 'RETURN' for all treating specialties."
 S QAMPARAM="P1" D EN2^QAMUTL1 I $D(DIRUT) S Y=-1 G Y
 S:Y]"" ^QA(743,QAMD0,"COND",QAMD1,"P1")=+Y_"^"_Y(0,0)
EXIT K Y
 K QAMPARAM,QAMY
Y Q
