GMTSLTR ; SLC/JER,KER - Print Big Letters ; 08/27/2002
 ;;2.7;Health Summary;**28,56**;Oct 20, 1995
 ;                   
 ; External Reference
 ;   DBIA 10060  ^VA(200
 ;   DBIA  2056  $$GET1^DIQ (file #200)
 ;                   
 Q:'$L($G(GMTSLTR))  I '$D(^UTILITY("GMTSLTR",$J,"A")) D ^GMTSLTR2
B1 ; Setup for banner print
 N GMTSDIV,GMTSLEN,GMTSXXX,GMTSBLNK,X
 S GMTSLEN=$L(GMTSLTR),GMTSDIV=60\GMTSLEN,$P(GMTSXXX,"X",(GMTSDIV\5+1))=""
 S $P(GMTSBLNK," ",(GMTSDIV\5+1))=""
 S GMTSLTY=$E(GMTSLTR,1,$S(GMTSLEN'>2:GMTSLEN,1:(IOM\GMTSDIV-1))),GMTSLTX=""
 F GMTSLT1=1:1:$L(GMTSLTY) D
 . I $A(GMTSLTY,GMTSLT1)>32,$D(^UTILITY("GMTSLTR",$J,$E(GMTSLTY,GMTSLT1))) D
 . . S GMTSLTX=GMTSLTX_$E(GMTSLTY,GMTSLT1)
 W:$L($P($G(GMTSSC),U,2)) "HEALTH SUMMARY REPORT FOR PATIENTS AT: ",$P(GMTSSC,U,2)
 I $L($P($G(GMTSSC),U,4)) S X=$P(GMTSSC,U,4) D REGDTM4^GMTSU W !?34,"FOR: ",X
 I $L($P($G(GMTSSC),U,5)) S X=$P(GMTSSC,U,5) D REGDTM4^GMTSU W " TO ",X
 W ! I $D(GMTSTITL) W ?(IOM-$L(GMTSTITL)\2),GMTSTITL,!!
 I $D(GMTSPNM) W ?10,"Patient: ",GMTSPNM,!
 I $D(GMTSWDN) W "Hospital Location: ",GMTSWDN,!
 I $D(DUZ) W ?33,"User: ",$$GET1^DIQ(200,(+($G(DUZ))_","),.01),!
 W ! F GMTSLT=1:1:6 D B2
 W @IOF
 K GMTSLT1,GMTSLT2,GMTSLT3,GMTSLTX,GMTSLTY,GMTSLT,^UTILITY("GMTSLTR",$J)
 Q
B2 ; Write Banner
 W ! F GMTSLT1=9:-1:1 W ! F GMTSLT3=1:1:$L(GMTSLTX) S X=^UTILITY("GMTSLTR",$J,$E(GMTSLTX,GMTSLT3)) W "   " F GMTSLT2=1:1:5 W $S($E(X,(GMTSLT2-1*9+GMTSLT1)):GMTSXXX,1:GMTSBLNK)
 Q
