LRLABEL ;SLC/TGA - PRINTS STANDARD LABELS 3.5X15/16 ;2/6/91  08:18
 ;;5.2;LAB SERVICE;**161**;Sep 27, 1994
 ;FOR 3.5 X 15/16 LABEL
 U IO
 S LRTXT=$$LRTXT^LRLABLD(.LRTS,54)
 I $G(LRBAR(+$G(LRAA)),0) D BAR Q  ; Print barcode label
 W LRACC
 I $P(LRURGA,"^",2),$L(LRURGA,"^") W ?20,$P(LRURGA,"^")
 W:$L(LRINFW) ?27,LRINFW
 W !,$E(PNM,1,22),?24,$P(LRDAT," "),!,SSN,"  W:",LRLLOC,?20,$S(LRRB=0:"",1:" B:"_LRRB)
 W !
 I LRXL G SKIP:N-I<LRXL
 W LRPREF
SKIP W LRTOP,?15," Order:",LRCE,!
 W LRTXT,!!
 Q
 ;
BAR ;Print Bar code label
 W LRACC
 I $P(LRURGA,"^",2),$L(LRURGA,"^") W ?15,$P(LRURGA,"^")
 W:$L(LRINFW) ?21,LRINFW
 W !,$E(PNM,1,20),?22,$P(SSN,"-",3),!
 W:'$L(LRBAR1)!('$L(LRBAR0)) !
 I $L(LRBAR1),$L(LRBAR0) W @LRBAR1,LRBARID,@LRBAR0
 W:$D(LRTXT) LRTXT
 W !!
 Q
