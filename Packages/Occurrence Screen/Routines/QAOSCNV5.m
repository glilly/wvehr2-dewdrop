QAOSCNV5 ;HISC/DAD-CONVERT TEXT OF EXCEPTIONS ;9/9/93  13:54
 ;;3.0;Occurrence Screen;;09/14/1993
 W !!,"Converting text of exceptions"
 W !,"-----------------------------"
 I $O(^QA(741.1,0))'>0,$O(^QA(741.5,0)) D  Q
 . S DIU="^QA(741.5,",DIU(0)="DT" D EN^DIU2
 . Q
 F QAOEXCP=1:1 S QAOSDATA=$P($T(EXCEPT+QAOEXCP),";;",2) Q:QAOSDATA=""  D
 . S QAOSSCRN=+QAOSDATA,QAOSCODE=$P(QAOSDATA,"^",2)
 . S QAOSTEXT=$P(QAOSDATA,"^",3)
 . W !!,"Screen: ",QAOSSCRN,?20,"Code: ",QAOSCODE
 . S (QAOSD0,QAOSDONE)=0
 . F  S QAOSD0=$O(^QA(741.5,"C",QAOSSCRN,QAOSD0)) Q:QAOSD0'>0!QAOSDONE  D
 .. S QAOSZERO=$G(^QA(741.5,QAOSD0,0))
 .. Q:$P(QAOSZERO,"^",3)'=QAOSCODE
 .. W !?5,"From: ",$P(QAOSZERO,"^")
 .. W !?5,"To:   ",QAOSTEXT
 .. K DA,DIE,DR
 .. S DIE="^QA(741.5,",DA=QAOSD0,DR=".01///"_QAOSTEXT
 .. D ^DIE
 .. S QAOSDONE=1
 .. Q
 . W:'QAOSDONE !?5,"*** NOT FOUND ***"
 . Q
 K DA,DIE,DR,QAOEXCP,QAOSCODE,QAOSD0,QAOSDATA
 K QAOSDONE,QAOSSCRN,QAOSTEXT,QAOSZERO
 Q
EXCEPT ;;SCREEN ^ CODE ^ NEW_EXCEPTION_TEXT
 ;;107^2^TWO OPERATIONS SEPARATED BY MORE THAN 7 DAYS
