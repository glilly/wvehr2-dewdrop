PXBPCPT2 ;WASH/BDB - PROMPT PROCEDURE DIAGNOSES ;9/5/05
 ;;1.0;PCE PATIENT CARE ENCOUNTER;**124,170**;Aug 12, 1996
 ;
 Q  ;not an entry
 ;
CDX(PXN)    ;--Diagnosis for Procedure
 N TIMED,DIC,X,CPTDX,POS,PXDISV,PXD,PXC,CDX,VAL,PXCEAFTR,PXCEVIEN
CPT1 K PXBUT,EDATA
 S POS=PXN+11,CPTDX=$P($P(REQE,U,POS)," "),PXDISV="PXBCPTDX-"_POS
 S TIMED="I '$T!(DATA[""^"")",PXD=$P(REQI,U,POS),PXC=$P(REQI,U,3)
 S DIC("S")="I $P($$ICDDX^ICDCODE(Y,IDATE),""^"",10)"
PCPT1 ;SECOND ENTRY POINT
 W !," What is DIAGNOSIS "_PXN_" for this procedure: "_$S($G(CPTDX):CPTDX_"//",1:""),IOELEOL
 R DATA:DTIME S:DATA="" DATA=CPTDX S EDATA=DATA
P1CPT1 ;--
 X TIMED I  S PXBUT=1,LEAVE=1 G CDXX1
 I DATA="^D" G CDXX1
 I DATA="^"!(DATA="^^") S PXBEXIT=0 G CDXX1
 I DATA="@",'$G(PXD) S DATA="?"
 I DATA="@" K PXBREQ(PXD) S $P(REQI,U,POS)="@" G CDXX1
 ;I DATA="",PXN=1 W !,"PRIMARY DIAGNOSIS IS REQUIRED!" G CPT1
 I DATA="?" D EN1^PXBHLP0("PXB","POV",1,"",1) G CPT1
 ;I DATA="??" S DOUBLEQQ=1 D EN1^PXBHLP0("PXB","POV",1,"",2) S:$L(DATA,"^")>1 (Y,DATA,EDATA)=$P($P(DATA,"^",2),"--",1) G:Y>1 PFINCPT1 G:Y?1A1.NP PFINCPT1
 I DATA="??" D EN1^PXBHLP0("PXB","POV",1,"",2) G CPT1
 I $L(DATA)>200 S (DATA,EDATA)=$E(DATA,1,199)
 ;---SPACE BAR---
 I DATA=" ",$D(^DISV(DUZ,PXDISV)) S DATA=^DISV(DUZ,PXDISV) W DATA
 ;-----
 ;--Do a DIC lookup on data if a "?" is NOT entered
 K X,DIC
 S X=EDATA
 D CONFIG^LEXSET("ICD",,IDATE)
 S DIC("A")="Select Diagnosis:"
 S DIC="^LEX(757.01,",DIC(0)=$S('$L($G(X)):"A",1:"")_"EQM"
 D ^DIC
 I X="@" Q
 I Y=-1 S DATA="^P" G P1CPT1
 S WHAT=$G(Y(1))
 S X="`"_+$$CODEN^ICDCODE(WHAT,80)
 S (DATA,EDATA)=WHAT K Y
 S DIC=80,DIC(0)="MZ",DIC("S")="I $P($$ICDDX^ICDCODE(Y,IDATE),U,10)" D ^DIC
 ;
PFINCPT1 ;--Finish DIAGNOSIS
 I $L(Y,U)'>1 S X=Y,DIC=80,DIC(0)="IZM" D ^DIC
 I +Y<0 D HELP1^PXBUTL1("POV") G CPT1
 I $$DUP(+Y) W !,$P(Y,U,2)," IS ALREADY A DIAGNOSIS!" G PCPT1
 S CDX=Y(0),^DISV(DUZ,PXDISV)=DATA,$P(REQI,U,POS)=+Y
 S $P(REQE,U,POS)=$P(CDX,U,1)_" --"_$P(CDX,U,3)
 I $D(PXBREQ(+Y,"I")) G CDXX1
 I 'PXBDXPRI D
 .D PRI^PXBPPOV1 ;PRI/SEC
 .I '$D(DIRUT),$P(REQI,U,6)="P" S PXBDXPRI=+Y
 S PXCEVIEN=PXBVST,PXDX=Y
 D WIN17^PXBCC(PXBCNT),GET800^PXCEC800 ;CI's
 I $G(PXCEQUIT) S $P(REQE,U,POS)=""
 I '$G(PXCEQUIT) S PXBREQ(+PXDX,"I")=PXCEAFTR(800)
 I '$G(PXCEQUIT) D EN0^PXBSTOR(PXBVST,PATIENT,REQI,.PXMREQ),EN1^PXKMAIN
CDXX1 ;--EXIT AND CLEAN UP
 I '$D(REQE) S REQE=""
 I $P(REQE,U,POS)="" S $P(REQI,U,POS)=""
 Q
 ;
DUP(CD) ;DUPLICATE?
 N ANS,CTR
 S ANS=0
 F CTR=12:1:19 I CTR'=POS,$P(REQI,U,CTR)=CD S ANS=1 Q
 Q ANS
 ;
