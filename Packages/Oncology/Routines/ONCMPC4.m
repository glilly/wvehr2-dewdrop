ONCMPC4 ;HINES CIOFO/GWB - 1999 Melanoma Study - Table IV ; 1/22/99
 ;;2.11;ONCOLOGY;**22**;Mar 07, 1995
 K TABLE,HTABLE
 S TABLE("SURGERY")="S^ONCMPC4"
 S TABLE("NON CANCER-DIRECTED SURGERY")="NCS^ONCMPC4"
 S TABLE("CANCER-DIRECTED SURGERY")="CS^ONCMPC4"
 S TABLE("SENTINEL NODES")="SN^ONCMPC4A"
 S TABLE("RADIATION THERAPY")="RT^ONCMPC4A"
 S TABLE("CHEMOTHERAPY")="C^ONCMPC4A"
 S TABLE("HORMONE THERAPY")="HT^ONCMPC4A"
 S TABLE("IMMUNOTHERAPY")="I^ONCMPC4"
 S TABLE("OTHER THERAPY")="OT^ONCMPC4"
 S HTABLE(1)="SURGERY"
 S HTABLE(2)="NON CANCER-DIRECTED SURGERY"
 S HTABLE(3)="CANCER-DIRECTED SURGERY"
 S HTABLE(4)="SENTINEL NODES"
 S HTABLE(5)="RADIATION THERAPY"
 S HTABLE(6)="CHEMOTHERAPY"
 S HTABLE(7)="HORMONE THERAPY"
 S HTABLE(8)="IMMUNOTHERAPY"
 S HTABLE(9)="OTHER THERAPY"
 S CHOICES=9
 W @IOF D HEAD^ONCMPC0
 W !," TABLE IV - FIRST COURSE OF TREATMENT"
 W !," ------------------------------------"
 K DIQ,ONC S DIC="^ONCO(165.5,"
 S DR="23;49;50;51;51.2;53;53.2;54;54.2;55;55.2;57;57.2;58;58.1;58.2;58.3;58;59;74;75;138;139;140;346;560"
 S DA=ONCONUM,DIQ="ONC",DIQ(0)="IE" D EN^DIQ1
 S DIE="^ONCO(165.5,",DA=ONCONUM
 S CDS=ONC(165.5,ONCONUM,58.2,"E")
 S RAD=$P($G(^ONCO(165.5,ONCONUM,3)),U,6)
 S HOR=$P($G(^ONCO(165.5,ONCONUM,3)),U,16)
PES S DR="346 40. PROTOCOL ELIGIBILITY STATUS..." D ^DIE G:$D(Y) JUMP
 I (X'=1)&(X'=9) D  G DFCT
 .S $P(^ONCO(165.5,ONCONUM,"STS2"),U,31)="00"
 .W !," 41. PROTOCOL PARTICIPATION........: Not on/NA"
 I X=9 D  G DFCT
 .S $P(^ONCO(165.5,ONCONUM,"STS2"),U,31)=99
 .W !," 41. PROTOCOL PARTICIPATION........: Unknown"
PP S DR="560 41. PROTOCOL PARTICIPATION........" D ^DIE G:$D(Y) JUMP
DFCT W !," 42. DATE OF FIRST COURSE TREATMENT: ",ONC(165.5,ONCONUM,49,"E")
 W ! K DIR S DIR(0)="E" D ^DIR G:$D(DIRUT) EXIT W @IOF D HEAD^ONCMPC0
S W !," SURGERY",!
 W " -------"
NCS W !," NON CANCER-DIRECTED SURGERY",!
DNCDS W !," 43. DATE OF NON CA-DIR SURGERY....: ",ONC(165.5,ONCONUM,58.3,"E")
NCDS S NCDS=ONC(165.5,ONCONUM,58.1,"E")
 S (NCDS1,NCDS2)="",LOS=$L(NCDS) I LOS<42 S NCDS1=NCDS G NCDS1
 S NOP=$L($E(NCDS,1,41)," ")
 S NCDS1=$P(NCDS," ",1,NOP-1),NCDS2=$P(NCDS," ",NOP,999)
NCDS1 W !," 44. NON CANCER-DIRECTED SURGERY...: ",NCDS1 W:NCDS2'="" !,?37,NCDS2
TOB S DR="1109 45. TYPE OF BIOPSY................" D ^DIE G:$D(Y) JUMP
CS W !!," CANCER-DIRECTED SURGERY",!
DCDS W !," 46. DATE OF CANCER-DIR SURGERY....: ",ONC(165.5,ONCONUM,50,"E")
SA S SA=ONC(165.5,ONCONUM,74,"E")
 S (SA1,SA2)="",LOS=$L(SA) I LOS<42 S SA1=SA G SA1
 S NOP=$L($E(SA,1,41)," "),SA1=$P(SA," ",1,NOP-1),SA2=$P(SA," ",NOP,999)
SA1 W !," 47. SURGICAL APPROACH.............: ",SA1 W:SA2'="" !,?37,SA2
SOPS S (CDS1,CDS2)="",LOS=$L(CDS) I LOS<42 S CDS1=CDS G SOPS1
 S NOP=$L($E(CDS,1,41)," ")
 S CDS1=$P(CDS," ",1,NOP-1),CDS2=$P(CDS," ",NOP,999)
SOPS1 W !," 48. SURGERY OF PRIMARY SITE.......: ",CDS1 W:CDS2'="" !,?37,CDS2
SM S SM=ONC(165.5,ONCONUM,59,"E")
 S (SM1,SM2)="",LOS=$L(SM) I LOS<42 S SM1=SM G SM1
 S NOP=$L($E(SM,1,41)," "),SM1=$P(SM," ",1,NOP-1),SM2=$P(SM," ",NOP,999)
SM1 W !," 49. SURGICAL MARGINS..............: ",SM1 W:SM2'="" !,?37,SM2
 I $E(CDS,1,2)="00" D  G SLNS
 .S $P(^ONCO(165.5,D0,"MEL1"),U,21)=998
 .W !," 50. DISTANCE FROM TUMOR TO EDGE OF                                                  SPECIMEN......................: 998  NA, surgery not performed"
MD S DR="1120 50. DISTANCE FROM TUMOR TO EDGE OF                                                  SPECIMEN......................" D ^DIE G:$D(Y) JUMP
SLNS W !," 51. SCOPE OF LYMPH NODE SURGERY...: ",ONC(165.5,ONCONUM,138,"E")
 W !
NLNR W !," 52. NUMBER OF LYMPH NODES REMOVED.: ",ONC(165.5,ONCONUM,140,"E")
SORS S SORS=ONC(165.5,ONCONUM,139,"E")
 S (SORS1,SORS2)="",LOS=$L(SORS) I LOS<42 S SORS1=SORS G SORS1
 S NOP=$L($E(SORS,1,41)," ")
 S SORS1=$P(SORS," ",1,NOP-1),SORS2=$P(SORS," ",NOP,999)
SORS1 W !!," 53. SURGERY OF OTHER REGIONAL SITE(S), DISTANT SITE(S),"
 W !,"     OR DISTANT LYMPH NODE(S)......: ",SORS1 W:SORS2'="" !,?37,SORS2
RR W !," 54. RECONSTRUCTION/RESTORATION....: ",ONC(165.5,ONCONUM,23,"E")
 I $E(CDS,1,2)="00" D  G RFNS
 .S $P(^ONCO(165.5,D0,"MEL1"),U,22)=8
 .W !," 55. SURGICAL CLOSURE..............: NA, surgery not performed"
 I $E(CDS,1,2)=99 D  G RFNS
 .S $P(^ONCO(165.5,D0,"MEL1"),U,22)=9
 .W !," 55. SURGICAL CLOSURE..............: Unknown"
SC S DR="1121 55. SURGICAL CLOSURE.............." D ^DIE G:$D(Y) JUMP
RFNS W !," 56. REASON FOR NO SURGERY.........: ",ONC(165.5,ONCONUM,58,"E")
 W ! K DIR S DIR(0)="E" D ^DIR G:$D(DIRUT) EXIT W @IOF D HEAD^ONCMPC0
 G SN^ONCMPC4A
JUMP ;Jump to prompts
 S XX="" R !!," GO TO ITEM: ",X:DTIME I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !," ",HTABLE(I)
 I '$D(TABLE(X)) S:X?1.2N X=X_"." S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !," ",HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT S:$D(DIRUT) OUT="Y"
 K CHOICES,PIECE,HTABLE,TABLE
 K CDS,CDSOT,LOS,NCDS,NCDSOT,NOP,SURG,SURG1,SURG2,SURGDT,SA,SA1,SA2
 K SM,SM1,SM2,SOORS,SOORS1,SOORS2,PL,SNE
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
