OCXODSP3 ;SLC/RJS,CLA -  Rule Display (Display a Data Field) ;10/29/98  12:37
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**32**;Dec 17,1997
 ;;  ;;ORDER CHECK EXPERT version 1.01 released OCT 29,1998
 ;
EN(OCXD0,OCXTAB,OCXRM,OCXCON) ;
 ;
 N OCXD1,OCXD,OCXRD,OCXE,OCXSUB
 ;
 S OCXCON=$G(OCXCON),OCXTAB=+$G(OCXTAB) S:'$G(OCXD0) OCXD0=+$$DIC("^OCXS(860.4,","AEMQ") Q:'OCXD0
 ;
 S OCXRD="" D DIQ("^OCXS(860.4,",OCXD0,.OCXRD)
 F OCXSUB="LINK" S OCXD1=0 F  S OCXD1=$O(^OCXS(860.4,OCXD0,OCXSUB,OCXD1)) Q:'OCXD1  D
 .S OCXD(0)=OCXD0,OCXD=OCXD1 D DIQ("^OCXS(860.4,"_OCXD0_","""_OCXSUB_""",",.OCXD,.OCXRD)
 ;
 W !
 W ! D FIELD("Data Field Name:",$G(OCXRD(860.4,OCXD0,.01,"E")),OCXTAB,OCXRM)
 W ! D FIELD("   Abbreviation:",$G(OCXRD(860.4,OCXD0,1,"E")),OCXTAB,OCXRM)
 ;
 I $L(OCXCON),'$D(OCXRD(860.41,OCXCON)) S OCXCON=$O(^OCXS(860.6,"B","DATABASE LOOKUP",0)) I OCXCON,'$D(OCXRD(860.41,OCXCON)) S OCXCON=0
 ;
 I 'OCXCON W ! D FIELD("   Data Context:","  ** ERROR ** Data Context not found for this data field.",OCXTAB,OCXRM) Q
 ;
 W ! D FIELD("   Data Context:",$P($G(^OCXS(860.6,OCXCON,0)),U,1),OCXTAB,OCXRM)
 ;
 D EN^OCXODSP4($G(OCXRD(860.41,OCXCON,1,"E")),OCXTAB+OCXOFF,OCXRM)
 ;
 Q
 ;
PARNUM(OCXOPER) ;
 ;
 N OCXPF,OCXPFN
 S OCXPF=$O(^OCXS(863.9,+OCXOPER,"PAR","B","OCXO GENERATE CODE FUNCTION",0)) Q:'OCXPF 0
 S OCXPF=$G(^OCXS(863.9,+OCXOPER,"PAR",+OCXPF,"VAL"))
 Q:'$L(OCXPF) 0
 I OCXPF S OCXPFN=OCXPF
 E  S OCXPFN=0 F  S OCXPFN=$O(^OCXS(863.7,"B",$E(OCXPF,1,30),OCXPFN)) Q:'OCXPFN  Q:($P($G(^OCXS(863.7,+OCXPFN,0)),U,1)=OCXPF)
 Q:'OCXPFN 0 Q +$O(^OCXS(863.7,+OCXPFN,"PAR",999),-1)
 ;
FIELD(TITLE,STRING,TAB,MARGIN) ;
 ;
 W ?TAB,TITLE
 ;
 N PTR,SUBSTR,STRLEN
 ;
 S STRLEN=MARGIN-($L(TITLE)+TAB)-5
 S SUBSTR="" F PTR=1:1:$L(STRING," ") D
 .I ($L(SUBSTR)>STRLEN) W ?(TAB+$L(TITLE)+1),SUBSTR W:$L($P(STRING," ",PTR+1)) ! S SUBSTR=""
 .S:$L(SUBSTR) SUBSTR=SUBSTR_" " S SUBSTR=SUBSTR_$P(STRING," ",PTR)
 W:$L(SUBSTR) ?(TAB+$L(TITLE)+1),SUBSTR
 Q
 ;
DIC(OCXDIC,OCXDIC0,OCXDICA,OCXX,OCXDICS,OCXDR) ;
 ;
 N DIC,X,Y
 S DIC=$G(OCXDIC) Q:'$L(DIC) -1
 S DIC(0)=$G(OCXDIC0) S:$L($G(OCXX)) X=OCXX
 S:$L($G(OCXDICS)) DIC("S")=OCXDICS
 S:$L($G(OCXDICA)) DIC("A")=OCXDICA
 S:$L($G(OCXDR)) DIC("DR")=OCXDR
 D ^DIC Q:(Y<1) 0 Q Y
 ;
 ;
DIQ(DIC,DA,OCXARY) ;
 ;
 N DR,DIQ S DR=".01:99999",DIQ="OCXARY(",DIQ(0)="IEN" D EN^DIQ1
 Q
 ;
MULT(OCXD0,OCXTAB,OCXRM) ;
 ;
 N OCXCON
 S OCXCON=0 F  S OCXCON=$O(^OCXS(860.4,OCXD0,"LINK",OCXCON)) Q:'OCXCON  D EN(OCXD0,OCXTAB,OCXRM,OCXCON)
 Q
 ;
