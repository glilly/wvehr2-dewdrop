LACOLT3 ;SLC/RWF - FOR COULTER SR. PLUS II WITH QC MODULE ;7/20/90  07:54 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK  ID=SEQ #, TRAY=1, CUP=ID, IDE=0
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 S LRTOP=$P(^LAB(69.9,1,1),U,1) D ^LASET Q:'TSK  S SS="CH",X="TRAP^"_LANM,@^%ZOSF("TRAP")
 F I=1:1:TC S Y=+TC(I,2),E=+$P(TC(I,2),",",2),TC(I,2)="V=$E(Y("_Y_"),"_E_","_(E+5)_")"
 S IDT=0
LA2 S TOUT=0,BAD=0 F A=1:1:17 S Y(A)=""
 F A=1:1:17 D IN D QC G QUIT:TOUT Q:Y(1)'["/"
 G LA2:Y(1)'["/"
 F I=0:0 S I=$O(TC(I)) Q:I<1  X TC(I,2) S @TC(I,1)=V ;V COULD BE MODIFIED ON THIS LINE
 S ID=+Y(2),SPEC=$P(^LAB(69.9,1,1),U,1),CUP=ID,IDE=0
LA3 X LAGEN
 F I=0:0 S I=$O(TV(I)) Q:I<1  D LA4
 G LA2
LA4 S J=1,R=$S($D(TV(I,J)):TV(I,J),1:"")
 I R]"" S ^LAH(LWL,1,ISQN,I)=R Q
 Q
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S X="" F I=1:1:$L(IN) S:$A(IN,I)-32 X=X_$E(IN,I)
 S IN=X IF IN["/"&(A>1) S A=17,^LA(TSK,"I",0)=CNT-1 Q
 S IN=$S(A=1:IN,X["....":"",X?.N.".".N:+X,1:"")
 S Y(A)=IN Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
OUT S CNT=^LA(TSK,"O")+1,^("O")=CNT,^("O",CNT)=OUT
 LOCK ^LA("Q") S Q=^LA("Q")+1,^("Q")=Q,^("Q",Q)=TSK LOCK
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
