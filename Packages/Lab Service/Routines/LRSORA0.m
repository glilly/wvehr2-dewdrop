LRSORA0 ;DRH/DALISC -  Continuation of LRSORA  07-28-93
 ;;5.2;LAB SERVICE;;Sep 27, 1994
INIT ;
 S U="^"
 Q
STOP ;
 D:($E(IOST,1,2)="C-")&('LREND) WAIT^LRSORA
 W @IOF D:'$D(ZTQUEUED) ^%ZISC
 K ^TMP("LR",$J)
 K LRA,LRV,LRV2,LREND,LREDT,LRSDT,LRLONG,LRTST,LRTW,LRVX,T,W,X,Y,I,J,C
 K DIC,DIR,DFN,VA("BID"),VA("PID"),VAERR,LRAN,LRDASH,LREDAT,LRHDR2,LRQUE
 K LRLCS,LRMRK,LROLD,LRPAG,LRPTS,LRSDAT,LRSRT,LRSUB1,LRSUB2,LRTEST,LRVAL
 K LREDT,LRSDT,LRTST,LRSPEC,LRSUB,LRDFN,LRLDFN,LRLIDT,LRLLOC,LRLONG,LRVDT
 K LRWRD,LRLOG,LRCDT,LRIDT,LRIDT1,LRHI,LRLO,LRTHER,LRTOP,LRLOC,LRDPF
 K SSN,PNM,AGE,DOB,SEX,V,%,%DT,LRPREC,DTOUT,DUOUT,ZTDESC,ZTQUEUED,ZTRTN
 K ZTSK,ZTSAVE,%ZIS,POP,%X,%Y,LRSUB3,LRAA,LRAAA,LRDAT,LRTSTX,LRUNITS
 K LRCHNG,LRCOMX,LRSORTI,LRCNT,REFCK,LRANF,LRANL,LRSPCK
 K LRTIC,LRSPCK,NEWPG,LRTSTCK,LRCOUNT,LRPATCK,LRCHKSP,LRCDT,LRCHNG
 Q
GAA ;
 S LRAA=0 W !
 K DIR,X,Y S DIR(0)="S^Y:YES;N:NO",DIR("B")="NO"
 S DIR("A")="Do you want to select accession areas (YES or NO) "
 S DIR("?")="Enter 'YES' to limit report to one or more accession areas."
 D ^DIR
 Q:Y="N"
 I ($D(DTOUT))!($D(DUOUT)) S LREND=1 Q
 K DIC S DIC=68,DIC(0)="AEMQZ"
 F  D ^DIC Q:Y=-1  D
 .S LRAA=+Y,LRAA($P(Y(0),U,11))=+Y
 I ($D(DTOUT))!($D(DUOUT)) S LREND=1 Q
 Q
