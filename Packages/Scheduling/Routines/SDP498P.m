SDP498P ;ALB/TEH - PCMM Post Init for locks; 18 Apr 2003  9:36 AM ; 8/23/05 2:34pm  ; Compiled May 23, 2007 07:37:17
 ;;5.3;Scheduling;**498**;AUG 13, 1993;Build 23
 ;
 ;IA 4988 (APPROVED)
 ;
 ;The following OPTIONS will have the locks removed.
 ;
 ;     
 ;     SCMC EXTENDED REPORT
 ;     SCMC FLAGGED
 ;     SCMC INACTIVATED REPORT
 ;     SC PCMM DIRECT PC FTEE
 ;     SCMC PRACTITIONER FLAGGED
 ;     SCMC PC STAFF AUTO INACTIVATE
 ;     SCMC PCMM MAIN MENU
 ;     
 ;
 ;The following OPTIONS will have the lock SC PCMM SETUP.
 ;
 ;     SCMC PCMM NIGHTLY TASK 
 ;     SCMC RETRANSMIT
 ;     SCMC PCMM ERR CODE REPORT
 ;     SCMC EXTEND A PATIENT
 ;     SCMC HL7 MENU
 ;     SCMC CLEAN GHOST ENTRIES
 ;     SCMC CLEAN INSTITUTION
 ;     
 ;The following OPTION will have the lock SCMC PCMM RETRANSMIT. 
 ;     
 ;     SCMC PCMM TRANS ERROR PROC
 ;     SCMC PCMM TRANS ERROR REPORT
 ;     
 ;The following OPTION will be deleted from the SCMC PCMM MAIN MENU.
 ;
 ;SCMC PCMM EWL MENU
 ;     
 ;The following OPTION will be added the SCMC PCMM MAIN MENU.   
 ;     
 ;     SD WAIT LIST MENU     
 ;     
 ;     
 Q
EN ;ENTRY POINT
 ;REMOVE LOCKS
 N SDPI,SDPX,DA,DR,DIE,DIK,SDPLOCK,SDPMESS,SDPMM,SDPEWL,SDPNEWL,DIC,X,SDPIEN
 F SDPI=1:1:7 S SDPX=$P($T(RE+SDPI),";",2) D
 .S DA=$O(^DIC(19,"B",SDPX,0)) I DA="" Q
 .S DR="3///@",DIE="^DIC(19," D ^DIE
 .S SDPMESS=" LOCK REMOVED FROM "_SDPX D MESS
 ;ADD LOCK SC PCMM SETUP
 S SDPLOCK="SC PCMM SETUP"
 F SDPI=1:1:7 S SDPX=$P($T(LOCK+SDPI),";",2) D
 .S DA=$O(^DIC(19,"B",SDPX,0)) I DA="" Q
 .S DR="3///^S X=SDPLOCK",DIE="^DIC(19," D ^DIE
 .S SDPMESS="LOCK SC PCMM SETUP HAS BEEN APPLIED TO "_SDPX D MESS
 S SDPLOCK="SCMC PCMM RETRANSMIT"
 F SDPI=8,9 S SDPX=$P($T(LOCK+SDPI),";",2) D
 .S DA=$O(^DIC(19,"B",SDPX,0)) I DA="" Q
 .S DR="3///^S X=SDPLOCK",DIE="^DIC(19," D ^DIE
 .S SDPMESS="LOCK SCMC PCMM RETRANSMIT HAS BEEN APPLIED TO "_SDPX D MESS
OPT ;GET SCMC PCMM MAIN MENU INTERNAL IEN
 S SDPMM=$O(^DIC(19,"B","SCMC PCMM MAIN MENU",0)) Q:SDPMM=""
 I '$D(^DIC(19,SDPMM,0)) Q
 ;GET BAD OPTION IEN SCMC PCMM EWL MENU
 S SDPEWL=$O(^DIC(19,"B","SCMC PCMM EWL MENU",0)) Q:SDPEWL=""
 I '$D(^DIC(19,SDPEWL,0)) Q
 ;GET GOOD OPTION IEN SD WAIT LIST MENU
 S SDPNEWL=$O(^DIC(19,"B","SD WAIT LIST MENU",0)) Q:SDPNEWL=""
DEL ;DELETE BAD OPTION
 S DA=$O(^DIC(19,SDPMM,10,"B",SDPEWL,0))
 I 'DA S SDPMESS="The OPTION SCMC PCMM EWL MENU is not on the SCMC PCMM MAIN MENU." G DEL1
 S DA(1)=SDPMM
 S DIK="^DIC(19,"_SDPMM_",10," D ^DIK
 S SDPMESS="The OPTION "_"SCMC PCMM EWL MENU has been removed."
DEL1 D MES^XPDUTL(SDPMESS)
ADD ;ADD EWL WAIT LIST OPTION
 S SDPMM=$O(^DIC(19,"B","SCMC PCMM MAIN MENU",0)) Q:SDPMM=""
 S SDPNEWL=$O(^DIC(19,"B","SD WAIT LIST MENU",0)) Q:SDPNEWL=""
 I $D(^DIC(19,SDPMM,10,"B",SDPNEWL)) D  G ADDER
 .S SDPMESS="The OPTION "_"SD WAIT LIST MENU is already on the SCMC PCMM MAIN MENU."
 .S SDPMESS(1)="*** No update for SCMC PCMM MAIN MENU will be made."
 S DA(1)=SDPMM,DIC(0)="L",DA=SDPNEWL,X=SDPNEWL
 S DIC="^DIC(19,"_DA(1)_",10,",DIC("P")=$P(^DD(19,10,0),U,2) K D0
 D FILE^DICN
ADD0 S DA(1)=SDPMM,DR="2////^S X=""WL"""
 S DR(1)="10;"
 S DR(1,19)="10;"
 S DR(2,"19.01")="2",DIE="^DIC(19,"_DA(1)_",10,"
 D ^DIE
 S SDPMESS="The OPTION "_"SD WAIT LIST MENU has been added."
ADD1 S SDPMM=$O(^DIC(19,"B","SCMC PCMM MAIN MENU",0)) Q:SDPMM=""
 S SDPNEWL=$O(^DIC(19,"B","SD WAIT LIST MENU",0)) Q:SDPNEWL=""
 S SDPIEN=$O(^DIC(19,SDPMM,10,"B",SDPNEWL,0))
 S SDPMM=$O(^DIC(19,"B","SCMC PCMM MAIN MENU",0)) Q:SDPMM=""
 S DA(1)=SDPMM,DA=SDPIEN,DR="2////^S X=""WL"""
 S DR(1)="10;"
 S DR(1,19)="10;"
 S DR(2,"19.01")="2",DIE="^DIC(19,"_DA(1)_",10,"
 D ^DIE
 S SDPMESS="The NEW WAIT LIST OPTION has been place on the SCMC PCMM MAIN MENU."
ADDER D MES^XPDUTL(SDPMESS) I $D(SDPMESS(1)) D
 .S SDPMESS=SDPMESS(1) D MES^XPDUTL(SDPMESS)
 ;REPAIR REPORTS MENU IN EWL MENU
 Q
MESS D MES^XPDUTL(SDPMESS)
 Q
RE ;REMOVAL LIST
 ;SCMC EXTENDED REPORT
 ;SCMC FLAGGED
 ;SCMC INACTIVATED REPORT
 ;SC PCMM DIRECT PC FTEE
 ;SCMC PRACTITIONER FLAGGED
 ;SCMC PC STAFF AUTO INACTIVATE
 ;SCMC PCMM MAIN MENU
 ;
LOCK ;LOCK LIST
 ;SCMC PCMM NIGHTLY TASK
 ;SCMC PCMM ERR CODE REPORT
 ;SCMC EXTEND A PATIENT
 ;SCMC HL7 MENU
 ;SCMC CLEAN GHOST ENTRIES
 ;SCMC CLEAN INSTITUTION
 ;SCMC RETRANSMIT
 ;SCMC PCMM TRANS ERROR REPORT
 ;SCMC PCMM TRANS ERROR PROC
