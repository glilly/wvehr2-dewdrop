EEOEOI5 ;HISC/FDW,JWR - EEO Complaint File Printable Inquiry routine ;11/11/92  12:59
 ;;2.0;EEO Complaint Tracking;;Apr 27, 1995
SEC ;Sets up neccessary security variables
 D ^EEOEOSE I FAIL Q
COMPL ;Inquiry of EEO Complaint info
 S DIC("A")="Select Complainant: "
 K DR
 S (EEOYSCR,DIC("S"))="I $P($G(^EEO(785,+Y,1)),U,3)>0"
 S (DIE,DIC)="^EEO(785,",DIC(0)="AEQMZ"
DIC ;Gathers complaint names
 D NEW^EEOEEDIE
 I (X="^")!((Y'>0)&('$D(EEOYI))) G KILL
 ;I +Y>0 I $D(^EEO(785,+Y,12)) I $P($G(^EEO(785,+Y,12)),U,2)="D" D DMSG^EEOEOE2 G DIC
 S:Y>0 EEOYI(+Y)="" S:$D(EEOYI) DIC("A")="  ANOTHER: "
 I Y<0,$D(EEOYI) G ZIS
 G DIC
 ;
ZIS ;gets printer info
 K %ZIS S %ZIS="MNQ"
 D ^%ZIS K %ZIS G:POP KILL
 S (EEOYOP,IOP)=ION_";"_IOST_";"_IOM_";"_IOSL
 I $D(IO("Q")) S ZTDESC="EEO Inquiry",ZTRTN="START^EEOEOI5",ZTSAVE("EEOY*")="" D ^%ZTLOAD G KILL
 ;
START ;beginning of print
 S EEOYZ=0 F I=0:0 S EEOYZ=$O(EEOYI(EEOYZ)) Q:EEOYZ=""  S EEOYLAST=EEOYZ
 S EEOYLP=0,EEOIOST=IOST
 F  S EEOYLP=$O(EEOYI(EEOYLP)) Q:EEOYLP=""  D PRINT Q:EEOYLP=-1
 ;
KILL ;generic kill
 D ^%ZISC
 D HOME^%ZIS
 K %ZIS,EEOYI,EEOYLAST,EEOYLP,EEOYOP,EEOYZ,BY,DHD,DIR,DR,FLDS,FR,I,IOP,TO,Y,ZTDESC,ZTQUEUED,ZTRTN,ZTSAVE,EEOIOST,EEOYI
 Q
 ;
PRINT ;print the report
 S FLDS="""COMPLAINANT:"",.01;X"
 S FLDS(1)="""CASE#:"";C44,1.3;L;X"
 S FLDS(2)="""DATE OF INCIDENT  :"";C2,14.5;X"
 S FLDS(3)="""DATE INITIAL CONTACT:  "";C44,14.7;X"
 S FLDS(4)="""DT NOTICE OF FINAL INTER.:  "";C2,15;X"
 S FLDS(5)="""DATE REQ. ADD'L INFO:  "";C44,15.3;X"
 S FLDS(6)="""DATE INFORMAL RESOUL.:  "";C2,15.5;X"
 S FLDS(6.5)="""TOTAL COUNSELOR'S DAYS:  "";C2,14.1;L;X"
 S FLDS(7)="""FORMAL COMPLAINT DATE:  "";C44,16;X"
 S FLDS(8)="""DATE UNION GRIEVENCE:  "";C2,16.07;X"
 S FLDS(9)="""DATE APPEAL TO MSPB:  "";C44,16.07;X"
 S FLDS(10)="""COUNS. INFORMED OF F.C.:  "";C2,16.5;X"
 S FLDS(11)="""DT COUNS. FILED REPORT:  "";C44,16.7;X"
 S FLDS(11.5)="""TOTAL COUNSELOR REPORT DAYS:  "";C2,51;L;X"
 S FLDS(12)="""DT REC'D BY EEO OFFICER:  "";C2,16.75;X"
 S FLDS(13)="""DATE LETTER OF ACKNOWL.:  "";C44,20;X"
 S FLDS(14)="""DATE TO OGC FOR ACC/REJ:  "";C2,21;X"
 S FLDS(15)="""DATE ACCEPTED BY OGC:  "";C44,22;X"
 S FLDS(16)="""DATE DISMISSED BY OGC:  "";C2,22.3;X"
 S FLDS(16.5)="""TOTAL DAYS OGC ACC/REJ:  "";C2,23;X;L"
 S FLDS(17)="""COMPL. ACCEPT. BY STATION:  "";C2,24;X"
 S FLDS(17.5)="""TOTAL DAYS ACCEPTANCE:  "";C2,25;L;X"
 S FLDS(17.6)="""TOTAL DAYS TO ASSIGN INV.:  "";C2,L;42;X"
 S FLDS(17.7)="""TOTAL INVESTIGATION DAYS:  "";C2,33;L;X"
 S FLDS(18)="""COMPL. SENT ADVISE/RIGHTS:  "";C2,41;X"
 S FLDS(19)="""COMPL. REC'D ADV/RGHTS:  "";C44,40.5;X"
 S FLDS(20)="""COMPL. MAKES ELECTION:  "";C2,40.6;X"
 S FLDS(20.5)="""TOTAL DAYS FOR ADVISE/RIGHTS:  "";C2,53;L;X"
 S FLDS(20.7)="""TOTAL DAYS TO MAKE ELECTION:  "";C2,55;L;X"
 S FLDS(21)="""DATE EEOC HEARING REQ'D:  "";C44,44;X"
 S FLDS(21.5)="""TOTAL DAYS TO REQ. EEOC HEAR:  "";C2,54;L;X"
 S FLDS(22)="""DATE EEOC HEAR. CONDUCTED:  "";C2,45;X"
 S FLDS(23)="""TOTAL DAYS FOR EEOC HEAR.:  "";C44,46;L;X"
 S FLDS(24)="""EEOC APPEAL:  "";C2,46.2;X"
 S FLDS(25)="""EEOC APPEAL #2:  "";C44,46.4;X"
 S FLDS(26)="""DATE FINAL AGENCY DECIS. ISS.:  "";C2,46.5;X"
 S FLDS(26.5)="""TOTAL DAYS FOR OGC FINAL DEC.:  "";C2,23.6;L;X"
 S FLDS(28)="""DATE CIVIL ACTION FILED:  "";C2,47;X"
 S FLDS(28.1)="""TOTAL DAYS FOR F.A.D.:  "";C2,56;L;X"
 S FLDS(29)="""DATE CLOSED:  "";C2,48;X"
 S FLDS(30)="""REASON CLOSED:"";C2,49;X"
 S FLDS(48)="""180 DAYS:  "";C2,57;X;L"
 S FLDS(49)="""TOTAL PROCESSING DAYS:"";C44"
 S FLDS(50)="50;"""""
 S FLDS(52)=""" "";C2"
 S FLDS(53)="""         * Denotes ongoing computations which are still active"";C2;X"
 S BY="@NUMBER"
 S DHD="EEO COMPLAINANT INQUIRY"
 S (FR,TO)=EEOYLP
 S IOP=EEOYOP
 S (DIE,DIC)="^EEO(785,",DIC(0)="AEQMZ"
 S DIS(0)="I $$SCREEN^EEOEOSE(D0)" D EN1^DIP K DIS(0),IOP S DIC("S")=EEOYSCR
 Q:$E(EEOIOST)'="C"!$D(ZTQUEUED)
 I EEOYLP'=EEOYLAST D
 . S DIR(0)="E",DIR("A")="Hit return to continue" W *7
 . D ^DIR
 . I Y'>0 S EEOYLP=-1 Q
 . W "   ...continuing...one moment please "
 . Q
 Q
