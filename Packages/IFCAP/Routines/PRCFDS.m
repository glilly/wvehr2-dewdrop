PRCFDS ;WISC/LEM-HANDLE FMS ACCOUNTING LINES ;6/7/94  9:39 AM
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 N DA S DA(1)=$G(PRCF("CIDA")) Q:DA(1)=""
 N NODE S NODE=$G(^PRCF(421.5,DA(1),5,0))
 I NODE="" S ^PRCF(421.5,DA(1),5,0)=U_$P(^DD(421.5,41,0),U,2)
 N CTR,I S (CTR,I)=0 F  S I=$O(PRCFD(I)) Q:I'>0  D
 . S CTR=$S(I=991:CTR,1:CTR+1),CTR=$S(CTR=991:992,1:CTR)
 . N DIC S DIC="^PRCF(421.5,"_DA(1)_",5,",DIC(0)="L"
 . S X=$P(PRCFD(I),U,1),AMT=+$P(PRCFD(I),U,2)
 . K DD,DO D FILE^DICN I Y'>0 W "ERROR" Q
 . N DIE S DIE=DIC,DA=+Y,FMSL=$S(I=991:991,1:CTR)
 . N DR S DR="1////^S X=AMT;2////^S X=FMSL" D ^DIE
 . Q
 Q
PAY W ?5,"Up to three sets of PROMPT PAYMENT TERMS may be sent to FMS with each"
 W !?5,"Payment Voucher (PV) generated by the IFCAP Payment/Invoice Tracking"
 W !?5,"module.  Enter 1, 2, or 3 to indicate which set of PROMPT PAYMENT TERMS"
 W !?5,"you wish to enter or edit.",!
 Q
