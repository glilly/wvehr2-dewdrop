PRCHES1 ;WISC/RHD/AKS-ESIG MAINTENANCE ROUTINE ;3/18/93  11:42
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 ;ROUTINE FOR MAINTAINING SUBFIELD 7 (ELECTRONIC SIGNATURE)
 ; FIELD .6 (PARTIAL), FILE 442
DECODE(LEVEL0,LEVEL1) ;Extrinsic Function to return hashed esig in readable form.
 ;returns "" if unsuccessful
 NEW RECORD,RECORD1,VERSION,PERSON,SIG,CHECKSUM
 ;get record and check version
 S RECORD=$G(^PRC(442,LEVEL0,11,LEVEL1,0)) I RECORD="" Q ""
 S RECORD1=$G(^PRC(442,LEVEL0,11,LEVEL1,1))
 S VERSION=$P(RECORD1,"^",9)
 S PERSON=+$P(RECORD,"^",7)
 I VERSION'="",VERSION'=1 Q ""
 S SIG=$P(RECORD,"^",8)
D1 ;decode e signature for version 1
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_LEVEL1_"^"_$$STRING(RECORD))
 Q $$DECODE^PRCUESIG(SIG,PERSON,CHECKSUM)
ENCODE(LEVEL0,LEVEL1,USERNUM,Y) ;Encode esig for version 1 only
 NEW RECORD,RECORD1,SIGBLOCK,CHECKSUM,OLDUSER
 S USERNUM=+USERNUM
 I USERNUM=0 S Y=-3 Q  ;-3 no user number
 S SIGBLOCK=$P($G(^VA(200,USERNUM,20)),"^",2)
 I SIGBLOCK="" S Y=-2 Q  ;-2 no signature block
 S RECORD=$G(^PRC(442,LEVEL0,11,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,11,LEVEL1,1))
 I RECORD="" S Y=-1 Q  ;-1 no 2237 record
 I $P(RECORD,"^",8)'="" S Y=-4 Q  ;-4 cannot re-sign record
 S OLDUSER=+$P(RECORD,"^",7)
 I OLDUSER=0 S $P(RECORD,"^",7)=USERNUM
 I OLDUSER>0 S USERNUM=OLDUSER
 S:$P(RECORD,"^",11)="" $P(RECORD,"^",11)=$$NOW^PRCUESIG
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_LEVEL1_"^"_$$STRING(RECORD))
 S $P(RECORD,"^",8)=$$ENCODE^PRCUESIG(SIGBLOCK,USERNUM,CHECKSUM)
 S $P(RECORD1,"^",9,10)="1^"_$$SUM^PRCUESIG(SIGBLOCK)
 S ^PRC(442,LEVEL0,11,LEVEL1,0)=RECORD
 S ^PRC(442,LEVEL0,11,LEVEL1,1)=RECORD1
 S Y=1 Q
RECODE(LEVEL0,LEVEL1,CHECKSUM,Y) ;Recode esig for version 1 only
 NEW RECORD,RECORD1,SIGBLOCK,USERNUM
 S RECORD=$G(^PRC(442,LEVEL0,11,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,11,LEVEL1,1))
 I RECORD=""!(RECORD1="") S Y=-1 Q  ;-1 no record
 S USERNUM=+$P(RECORD,"^",7)
 I $P(RECORD,"^",8)=""!(USERNUM=0) S Y=-4 Q  ;-4 cannot recode record
 ;S SIGBLOCK=$$DECODE^PRCUESIG($P(RECORD,"^",8),USERNUM,CHECKSUM)
 S SIGBLOCK=$P($G(^VA(200,USERNUM,20)),"^",2)
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_LEVEL1_"^"_$$STRING(RECORD))
 S $P(RECORD,"^",8)=$$ENCODE^PRCUESIG(SIGBLOCK,USERNUM,CHECKSUM)
 S $P(RECORD1,"^",10)=$$SUM^PRCUESIG(SIGBLOCK)
 S ^PRC(442,LEVEL0,11,LEVEL1,0)=RECORD
 S ^PRC(442,LEVEL0,11,LEVEL1,1)=RECORD1
 S Y=1
 Q
REMOVE(LEVEL0,LEVEL1) ;Entry point to remove esig
 NEW I,RECORD,RECORD1
 S RECORD=$G(^PRC(442,LEVEL0,11,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,11,LEVEL1,1))
 F I=7,8,11 S $P(RECORD,"^",I)=""
 S $P(RECORD1,"^",10)=""
 S ^PRC(442,LEVEL0,11,LEVEL1,0)=RECORD
 S ^PRC(442,LEVEL0,11,LEVEL1,1)=RECORD1
 Q
VERIFY(LEVEL0,LEVEL1)      ;extrinsic function to verify esig  Returns 1 if valid, 0 if not valid
 NEW RECORD,RECORD1,VERSION,SIGBLOCK
 S RECORD=$G(^PRC(442,LEVEL0,11,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,11,LEVEL1,1))
 S VERSION=$P(RECORD1,"^",9),SIGBLOCK=$P(RECORD1,"^",10)
 I VERSION_SIGBLOCK="" Q 1
 Q $$SUM^PRCUESIG($$DECODE(LEVEL0,LEVEL1))=SIGBLOCK
STRING(X)          ;Build String of critical fields
 Q $P(X,"^")_"^"_$P(X,"^",3)_"^"_$P(X,"^",5)_"^"_$P(X,"^",11)_"^"_$P(X,"^",12)_"^"_$P(X,"^",14)
