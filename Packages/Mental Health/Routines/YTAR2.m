YTAR2 ;SLC/TGA-SAVE DISCONTINUED TESTS; ;03/10/94 15:05
 ;;5.01;MENTAL HEALTH;;Dec 30, 1994
 ;
 S YSTIN=1 D:'$D(YSCLERK) EN40^YTFILE
 S ^YTD(601.4,YSDFN,1,YSENT,0)=YSENT_"^"_YSHD_"^"_$G(YSDTA)_"^"_J_"^"_$G(C)_"^"_$G(YSCL)_"^"_YSORD_"^"_$G(YSBEGIN),^YTD(601.4,YSDFN,1,"B",YSENT,YSENT)=""
 I $D(B) S ^YTD(601.4,YSDFN,1,YSENT,"B")=B
 I '$D(YSCLERK) S YSXT(0)=$P(YSXT,U,YSXTP+1,99) S:YSXT(0)]"" ^YTD(601.4,YSDFN,1,YSENT,"R")=YSXT(0)
 S:J>0 ^YTD(601.4,YSDFN,1,YSENT,J+199\200)=YSRP
 I $D(YSCLERK) S ^YTD(601.4,YSDFN,1,"AC",YSCL,YSCLERK)=""
 ;
 ; 3/10/94 LJA   Following line added...
 I YSTESTN="MMPR" S ^YTD(601.4,+YSDFN,1,+YSENT,99)="MMPIR"
 ;
 L  W !!?2,"SESSION INTERRUPTED" S:'$D(YSCLERK) XMB(YSXTP+5)=$P(YSXT,U,YSXTP)_"*" S XMB(5)="(""*"" indicates not completed)"
 I YSQ S YSXT="" I '$D(YSCLERK) F K=6:1 Q:'$D(XMB(K))  Q:(XMB(K)["*")  S:$P(^YTT(601,XMB(K),0),U,9)'="B" YSXT=YSXT_YSHD_","_XMB(K)_"^"
 I YSQ,YSXT]"" S YSXTP=1,ZTRTN="RP1^YTDP",ZTSAVE("YS*")="",ZTDTH=$H,ZTDESC="YS MH DISC INST" D ^%ZTLOAD W !,"REQUEST QUEUED!"
 I DUZ'=YSORD,$D(YSCLERK) S XMB(6)="CLERK-"_YSCLN_"*"
 E  I DUZ'=YSORD F K=6:1 Q:'$D(XMB(K))  S XMB(K)=$P(^YTT(601,+XMB(K),0),U)_$S(XMB(K)["*":"*",1:"")
 I DUZ'=YSORD D ENBUL^YSUTL
 G KAR^YTS:$D(YSCL),H^XUS:'$D(YSM) Q
