TIUFJ ; SLC/MAM - LM Template J (Objects) INIT ;10/26/95  15:33
 ;;1.0;TEXT INTEGRATION UTILITIES;;Jun 20, 1997
 ;
EN ; -- main entry point for Options TIUFJ CREATE OBJECTS MGR/VIEW OBJECTS CLIN
 ; Requires TIUFWHO, set in above options
 N TIUF,TIUFTMPL,TIUFATTR,TIUFAVAL,TIUFSTRT,TIUFVCN1,TIUFXNOD,TIUFREDO
 N DTOUT,DIRUT,DIROUT,DUOUT,TIUFLFT
 S TIUFTMPL="J",TIUFREDO=0
 N TIUFPRIV D SETUP^TIUFL S:$D(DTOUT) VALMQUIT=1 G:$G(VALMQUIT) ENX
 S TIUFXNOD="^^Object^"
 S TIUFATTR="T^TYPE",TIUFAVAL="O^OBJECT" D SELSTART^TIUFLA
 G:$D(DTOUT)!'$D(TIUFSTRT) ENX
 I TIUFWHO="C" D EN^VALM("TIUFJ OBJECTS CLIN")
 I "MN"[TIUFWHO D EN^VALM("TIUFJ OBJECTS MGR")
ENX Q
 ;
HDR ; -- header code
 ; Requires Attribute TIUFATTR="T^TYPE"
 ; Requires Attribute Value TIUFAVAL="O^OBJECT"
 ; Requires TIUFSTRT = e.g. " ^ZZZZZZZZ" as set in SELSTART^TIUFLA.
 N FROM,TO,HDR3
 S FROM=$S($P(TIUFSTRT,U)=" ":"FIRST",1:$P(TIUFSTRT,U))
 S TO=$S($P(TIUFSTRT,U)=" ":"LAST",$P(TIUFSTRT,U,2)="ZZZZZZZZ":"LAST",1:$P(TIUFSTRT,U,2))
 S HDR3=$S(TIUFSTRT'=" ^ZZZZZZZZ":" from "_FROM_" to "_TO,1:"")
 S VALMHDR(1)=$$CENTER^TIUFL("Objects "_HDR3,79)
HDRX ;
 Q
 ;
INIT ; -- init variables and list array. Called by Template J
 D INIT^TIUFA
 Q
 ;
EXIT ; -- exit code
 K ^TMP("TIUF1",$J),^TMP("TIUFB",$J),^TMP("TIUF1IDX",$J),^TMP("TIUFBIDX",$J),^TMP("TIUF",$J),IOELALL
 D CLEAN^VALM10
 Q
 ;
