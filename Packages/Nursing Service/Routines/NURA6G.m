NURA6G ;HIRMFO/MD,RM,FT-LICENSE PROFILE BY SERVICE CATEGORY ;8/23/96  09:34
 ;;4.0;NURSING SERVICE;;Apr 25, 1997
 S X=$G(^DIC(213.9,1,"OFF")) Q:X=""!(X=1)
 S (NURPAGE,NURSW1,NURPLSW,NURMDSW,NURQUIT,NURQUEUE,NUROUT)=0,NRNLP=1
 D EN1^NURSAUTL G QUIT:NUROUT
 D EN9^NURSAGSP
 I NURMDSW S DIC(0)="AEMQZ",NURPLSCR=0 D EN5^NURSAGSP K NURPLSCR I $G(NUROUT) G QUIT
 I NURMDSW=0,NURPLSW=1 S NURPLSCR=0 D PRD^NURSAGSP K NURPLSCR I $G(NUROUT) G QUIT
 D EN3^NURSAGSP G:NUROUT QUIT
 D EN7^NURSAGP0 G QUIT:NUROUT
 W ! D EN10^NURSAGSP G QUIT:NUROUT
 W ! S ZTDESC="License Profile by Service Category",ZTRTN="START^NURA6G" D EN7^NURSUT0 G:POP!($D(ZTSK)) QUIT
START ;
 K ^TMP($J) U IO
 D SORT G:NUROUT QUIT D NPRINT
QUIT K ^TMP($J) D CLOSE^NURSUT1,^NURAKILL
 Q
NPRINT S NURFAC(2)="" F  S NURFAC(2)=$O(^TMP($J,"L",NURFAC(2))) Q:NURFAC(2)=""  D NM Q:NURQUIT
 Q
NM S NURPROG(4)="" F  S NURPROG(4)=$O(^TMP($J,"L",NURFAC(2),NURPROG(4))) Q:NURPROG(4)=""!NURQUIT  D NHDR Q:NURQUIT  D NN Q:NURQUIT
 Q
NN S NURCAT="" F  S NURCAT=$O(^TMP($J,"L",NURFAC(2),NURPROG(4),NURCAT)) Q:NURCAT=""  D BRK,NO Q:NURQUIT
 Q
NO S NLDT="" F  S NLDT=$O(^TMP($J,"L",NURFAC(2),NURPROG(4),NURCAT,NLDT)) Q:NLDT=""  D NP Q:NURQUIT
 Q
NP S N1="" F  S N1=$O(^TMP($J,"L",NURFAC(2),NURPROG(4),NURCAT,NLDT,N1)) Q:N1=""  S NURSORT=$G(^(N1)) I NURSORT S D0=$E(NLDT,8),NLDTPR=$E(NLDT,1,7),DA=$O(^TMP($J,"L1",NURSORT,"")) D NPPRINT Q:NURQUIT
 Q
NPPRINT I ($Y>(IOSL-6)) D NHDR Q:NURQUIT
 W !
 S:'NURSW1 NURSW1=1
 W:N1'="  BLANK" ?2,$E(N1,1,20)
 I $D(^VA(200,+^NURSF(210,DA,0),1)),$P(^(1),"^",9)'="" S NURSSN=$P(^(1),"^",9) W ?28,$E(NURSSN,1,3),"-",$E(NURSSN,4,5),"-",$E(NURSSN,6,9)
 S Y=NLDTPR D:+Y D^DIQ W:Y'="  BLANK" ?46,Y
 I D0'="",$D(^NURSF(210,+DA,4,D0,0)),$P(^(0),"^",1)'="",$D(^DIC(5,$P(^NURSF(210,DA,4,D0,0),"^",1),0)),$P(^(0),"^",2)'="" W ?60,$P(^(0),"^",2)
 I D0'="",$D(^NURSF(210,DA,4,D0,0)),$P(^(0),"^",2)'="" W ?68,$P(^(0),"^",2)
 Q
NHDR I 'NURQUEUE,NURSW1,$E(IOST)="C" D ENDPG^NURSUT1 S:NUROUT NURQUIT=+NUROUT Q:NURQUIT
 S NURPAGE=NURPAGE+1 W:$E(IOST)="C"!(NURPAGE>1) @IOF
 I $G(NURMDSW) W !,?$$CNTR^NURSUT2(NURFAC(2)),$S(NURFAC(2)="  BLANK":"NO FACILITY",1:NURFAC(2)),!
 W !,"LICENSE PROFILE" S X="T" D ^%DT D:+Y D^DIQ W ?56,Y,?72,"PAGE: ",NURPAGE
 W !!,?46,"EXPIRATION",?60,"STATE",?68,"PROFESSIONAL"
 W !,?46,"DATE OF",?60,"ISS.",?68,"LICENSE"
 W !,?2,"NAME",?28,"SSN",?46,"LICENSE",?60,"LIC.",?68,"NUMBER"
 W !,$$REPEAT^XLFSTR("-",80)
 D:$G(NURPLSW) BRK1 D BRK
 Q
SORT W ! S NRPT=8 D EN1^NURAAGS0
 S X=$O(^TMP($J,"")) I X="" S NURPROG(4)=$S($G(NURPROG)=0:NURPROG(1),1:""),NURFAC(2)=$S($G(NURFAC)=0:NURFAC(1),1:"") D NHDR W !,"THERE IS NO DATA FOR THIS REPORT" S NUROUT=1
 Q
BRK Q:$G(NURCAT)=""   W !,?10,"SERVICE CATEGORY: " W:NURCAT'="  BLANK" $$CAT^NURSUT2(NURCAT)
 Q
BRK1 I $G(NURPROG(4))'="" W !?$$CNTR^NURSUT2(NURPROG(4)),$S(NURPROG(4)=" NURSING":$E(NURPROG(4),2,99),$G(NURPROG(4))="  BLANK":"NO PRODUCT LINE",1:$G(NURPROG(4))) W !?$$CNTR^NURSUT2(NURPROG(4)),$$REPEAT^XLFSTR("-",$L(NURPROG(4))+1),!
 Q
