FSCLMM ;SLC/STAFF-NOIS List Manager - Modify ;1/13/98  12:38
 ;;1.1;NOIS;;Sep 06, 1998
 ;
ENTRY ; from list template - entry code, FSCLDR, FSCLDS, FSCLMPMS, FSCLMPS
 N LINE,QUERY K QUERY
 K ^TMP("FSC MODIFY LISTS",$J)
 I '$G(FSCDEV) W !
 S VALMCNT=0,VALMCAP="",$P(VALMCAP," ",80)=""
 S VALMCNT=VALMCNT+1,^TMP("FSC MODIFY LISTS",$J,VALMCNT,0)="Description:"
 S LINE=0 F  S LINE=$O(^FSC("LIST",FSCLNUM,2,LINE)) Q:LINE<1  S VALMCNT=VALMCNT+1,^TMP("FSC MODIFY LISTS",$J,VALMCNT,0)=^(LINE,0)
 I '$O(^FSC("LIST",FSCLNUM,3,0)) Q
 S VALMCNT=VALMCNT+1,^TMP("FSC MODIFY LISTS",$J,VALMCNT,0)=" "
 S VALMCNT=VALMCNT+1,^TMP("FSC MODIFY LISTS",$J,VALMCNT,0)="Query Description:"
 S LINE=0 F  S LINE=$O(^FSC("LIST",FSCLNUM,3,LINE)) Q:LINE<1  S VALMCNT=VALMCNT+1,^TMP("FSC MODIFY LISTS",$J,VALMCNT,0)=^(LINE,0)
 S VALMCNT=VALMCNT+1,^TMP("FSC MODIFY LISTS",$J,VALMCNT,0)=" "
 D QUERY^FSCGETQ(FSCLNUM,.QUERY)
 D FORM^FSCGETQ(FSCLNUM,VALMAR,.VALMCNT)
 Q
 ;
HEADER ; from list template - header code, FSCLDR, FSCLDS, FSCLMPMS, FSCLMPS
 I '$D(^FSC("LIST",FSCLNUM,0)) Q
 N EVENT,INDEX,INDEXFLD,METHOD,OWNER,REBUILD,TYPE
 S OWNER=$P(^FSC("LIST",FSCLNUM,0),U,2),TYPE=$P(^(0),U,3),INDEX=$P(^(0),U,4),INDEXFLD=$P(^(0),U,5),METHOD=$P(^(0),U,6),EVENT=$P(^(0),U,7),REBUILD=$P(^(0),U,10)
 I REBUILD="" S REBUILD="WEEKLY"
 S OWNER="Owner: "_$S(OWNER:$$VALUE^FSCGET(OWNER,7107.1,1),1:"PUBLIC")
 S TYPE="Type: "_$S(TYPE="A":"ACTIVE UPDATE",TYPE="M":"MANUAL UPDATE",TYPE="S":"STORAGE ONLY",TYPE="I":"INDEXED",1:"")
 I TYPE["ACTIVE UPDATE" S TYPE=TYPE_" (Rebuild: "_REBUILD_")"
 I $L(INDEX) S TYPE=TYPE_" (using "_INDEX_" index)"
 I $L(INDEXFLD) S INDEXFLD="Requires entering a "_$$VALUE^FSCGET(INDEXFLD,7107.1,4)_"."
 S VALMHDR(1)="List: "_FSCLNAME
 S VALMHDR(2)=$$SETSTR^VALM1(OWNER,TYPE,40,$L(OWNER))
 I $L(METHOD)!$L(EVENT) S VALMHDR(3)=$$SETSTR^VALM1("Notify Event: "_EVENT,"Notify Method: "_METHOD,40,$L("Notify Event: "_EVENT)) Q
 S VALMHDR(3)=INDEXFLD
 Q
 ;
EXIT ; from list template - exit code
 I $G(FSC1) D CLEAR^VALM1
 K ^TMP("FSC MODIFY LISTS",$J)
 Q
 ;
HELP ; from list template - help code
 I $G(X)'["?" Q
 S VALMBCK="R"
 N XQH
 I X="?" S XQH="FSC MENU MODIFY" D EN^XQH Q
 I X="???" S VALMANS="?" D CLEAR^VALM1 S XQH="FSC U1 NOIS" D EN^XQH Q
 Q
