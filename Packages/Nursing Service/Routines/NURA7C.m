NURA7C ;HIRMFO/MD,FT-INDIVIDUAL PHONE NUMBER ;8/8/96  13:50
 ;;4.0;NURSING SERVICE;;Apr 25, 1997
 Q:'$D(^DIC(213.9,1,"OFF"))  Q:$P(^DIC(213.9,1,"OFF"),"^",1)=1
 W ! S (NURQUEUE,NUROUT,NURQUIT)=0
 D EN1^NURSAUTL G QUIT:NUROUT
 S DIC("S")=" S DA=+Y I 'NURSZAP!(NURSZAP'>7)!(NURSZDA=DA)"
 D EN3^NURSAGP1 G QUIT:NUROUT
 W ! S ZTDESC="Nursing Individual Phone Number(s)",ZTRTN="START^NURA7C" D EN7^NURSUT0 G:POP!($D(ZTSK)) QUIT
START ;
 K ^TMP($J)
 U IO S (NURPAGE,NURSW1)=0 D HEADER,WRITE
QUIT K ^TMP($J) D CLOSE^NURSUT1,^NURAKILL
 Q
 ; DETAIL LINE PRINT ROUTINE
WRITE I ($Y>(IOSL-6)) D HEADER Q:NURQUIT
 S NURSW1=1
 W:+N2 !,$E($P(^VA(200,N2,0),"^",1),1,20)
 D EN2^NURSUT0 W ?36,NPSPOS
 F NS2=0:0 S NS2=$O(^NURSF(210,N1,2,NS2)) Q:NS2'>0  W:$D(^NURSF(210,N1,2,NS2,0)) ?51,$P(^(0),"^",1) S NO=$P(^(0),"^",2) W ?66,$S(NO="S":"SELF",NO="R":"RELATIVE",NO="N":"NEIGHBOR",NO="O":"OTHER",1:"") W !
 Q
HEADER I 'NURQUEUE,$E(IOST)="C",NURSW1 D ENDPG^NURSUT1 S:NUROUT NURQUIT=+NUROUT Q:NURQUIT
 S NURPAGE=NURPAGE+1 W:$E(IOST)="C"!(NURPAGE>1) @IOF
 W !!,"TELEPHONE NUMBERS OF NURSING EMPLOYEES" S X="T" D ^%DT D:+Y D^DIQ W ?44,Y,?59,"PAGE: ",NURPAGE
 W !!,"EMPLOYEE",?36,"SERVICE",?51,"TELEPHONE",?66,"OWNER OF" W !,"NAME",?36,"POSITION",?51,"NUMBER",?66,"PHONE NO."
 W !,$$REPEAT^XLFSTR("-",80),!
 Q
