ESPORM5 ;DALISC/CKA - OFFENSE REPORT IN MAIL MESSAGE CONT. ;4/93
 ;;1.0;POLICE & SECURITY;;Mar 31, 1994
EN ;CONTINUED FROME ESPORM4
NARR ;PRINT NARRATIVE
 D HDR S ESPX=""
 F I=1:1:18 S ESPX=ESPX_"* "
 S ESPX=ESPX_"NARRATIVE" F I=1:1:17 S ESPX=ESPX_" *"
 D MSG
 S IEN=0,ESPX=" " D MSG S ESPX=$E(SPACES,1,3)_"ORIGIN:" D MSG F ESPN=1:1:2 S ESPX=" " D MSG
 F ESPN=1:1 S IEN=$O(^ESP(912,ESPID,70,IEN)) Q:IEN'>0  S ESPX=$E(SPACES,1,3)_^(IEN,0) D MSG
 S IEN=0,ESPX=" " D MSG S ESPX=$E(SPACES,1,3)_"INITIAL OBSERVATION:" D MSG F ESPN=1:1:2 S ESPX=" " D MSG
 F ESPN=1:1 S IEN=$O(^ESP(912,ESPID,72,IEN)) Q:IEN'>0  S ESPX=$E(SPACES,1,3)_^(IEN,0) D MSG
 S IEN=0,ESPX=" " D MSG S ESPX=$E(SPACES,1,3)_"INVESTIGATION:" D MSG S ESPX=" " D MSG
 F ESPN=1:1 S IEN=$O(^ESP(912,ESPID,73,IEN)) Q:IEN'>0  S ESPX=$E(SPACES,1,3)_^(IEN,0) D MSG
 S IEN=0,ESPX=" " D MSG S ESPX=$E(SPACES,1,3)_"DISPOSITION:" D MSG F ESPN=1:1:2 S ESPX=" " D MSG
 F ESPN=1:1 S IEN=$O(^ESP(912,ESPID,74,IEN)) Q:IEN'>0  S ESPX=$E(SPACES,1,3)_^(IEN,0) D MSG
SIG D HDR:$Y+5>IOSL
 F ESPN=1:1:4 S ESPX=" " D MSG
 S DIC="^ESP(912,",DA=ESPID,DR=".06",DIQ(0)="I" D EN^DIQ1
 S ESPOFF=$G(^UTILITY("DIQ1",$J,912,DA,.06,"I")),DIC="^VA(200,",DA=ESPOFF,DR="20.2;910.1",DIQ(0)="E" D EN^DIQ1
 S ESPX=$G(^UTILITY("DIQ1",$J,200,DA,20.2,"E"))_"   # "_$G(^UTILITY("DIQ1",$J,200,DA,910.1,"E")) D MSG S ESPX=" " D MSG S ESPX="INVESTIGATING OFFICER" D MSG
 F ESPN=1:1:5 S ESPX=" " D MSG
FOL ;PRINT FOLLOW-UP NOTES
 G:'$D(^ESP(912,ESPID,130)) SEND
 D HDR
 F ESPN=0:0 S ESPN=$O(^ESP(912,ESPID,130,ESPN))  Q:ESPN'>0  D
 .  D HDR:$Y+5>IOSL&(ESPN'=1)
 .  S IEN=0,ESPX=" " D MSG S ESPX=$E(SPACES,1,3)_"FOLLOW-UP NOTES:" D MSG F ESPZ=1:1:2 S ESPX=" " D MSG
 .  F ESPN1=1:1 S IEN=$O(^ESP(912,ESPID,130,ESPN,10,IEN)) Q:IEN'>0  S ESPX=$E(SPACES,1,3)_^(IEN,0) D MSG
 .  S DIC="^ESP(912,"_ESPID_",130,",DA=ESPN,DR=".01",DIQ(0)="I" D EN^DIQ1
 .  S ESPOFF=$G(^UTILITY("DIQ1",$J,912.17,DA,.01,"I"))
 .  S DIC="^VA(200,",DA=ESPOFF,DR="20.2;910.1",DIQ(0)="E" D EN^DIQ1
 .  S ESPX=" " D MSG S ESPX=" " D MSG S ESPX=" " D MSG S ESPX=" " D MSG
 .  S ESPX=$G(^UTILITY("DIQ1",$J,200,DA,20.2,"E"))_"   # "_$G(^UTILITY("DIQ1",$J,200,DA,910.1,"E")) D MSG S ESPX="FOLLOW-UP INVESTIGATOR" D MSG
 .  S ESPX=" " D MSG S ESPX=" " D MSG S ESPX=" " D MSG S ESPX=" " D MSG
 W !,"COMPLETED."
SEND S XMSUB="UOR"_$E(ESPDTR,4,5)_"-"_$E(ESPDTR,6,7)_"-"_$TR($E($P(ESPDTR,".",2)_"ZZZZ",1,4),"Z",0)
 S XMTEXT="^TMP($J,""UORM"","
 S XMDUN="Police & Security Package"
 D ^XMD
EXIT K DA,DIC,DIQ,DIRUT,ESPDOB,ESPDOBP,DR,DTOUT,DUOUT,ESPDTR,ESPFN,ESPI,ESPID,ESPL,ESPN,ESPN1,ESPOFF,ESPX,ESPZ,I,IEN,PAGE,SPACES,SSN,X,X1,X2,XMDUN,XMSUB,XMTEXT,Y
 K ^UTILITY("DIQ1",$J),^TMP($J,"UORM")
 QUIT
HDR ;PRINT HEADING
 S PAGE=PAGE+1 S ESPX=$E(SPACES,1,25)_"DEPARTMENT OF VETERANS AFFAIRS"_$E(SPACES,1,IOM-10)_"PAGE:  "_$J(PAGE,3)
 D MSG
 S ESPX=$E(SPACES,1,35)_"VA POLICE"
 S ESPX=$E(SPACES,1,28)_"UNIFORM OFFENSE REPORT"
 D MSG
 S ESPX=$E(SPACES,1,30)_"UOR# "_$E(ESPDTR,2,3)_"-"_$E(ESPDTR,4,5)_"-"_$E(ESPDTR,6,7)_"-"_$TR($E($P(ESPDTR,".",2)_"ZZZZ",1,4),"Z",0)
 D MSG
 S ESPX="VA Facility" D MSG
 I $D(^ESP(912,ESPID,5)),$P(^(5),U)'="" S ESPX=$S($D(^DIC(4,$P(^(5),U),0)):$P(^(0),U),1:"") D MSG
 S ESPX="Automated VA Form 10-1393" D MSG
 F ESPI=1:1:2 S ESPX=" " D MSG
 QUIT
MSG S ^TMP($J,"UORM",ESPL)=ESPX,ESPL=ESPL+1
 QUIT
