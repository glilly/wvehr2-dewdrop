%ZVEMO ;DJB,VRROLD**VRoutine Reader [12/31/94]
 ;;7.0;VPE;;COPYRIGHT David Bolduc @1993
 ;
EN ;Entry Point
 I $G(DUZ)'>0 D ID^%ZVEMKU Q:$G(DUZ)=""
 NEW X S X="ERROR^%ZVEMOY",@($$TRAP^%ZVEMKU1) KILL X
START ;
 NEW BOTTOM,CODE,CODELN,CODETG,DX,DY,END,END1,I,LNUM,PGTOP,START,STNG,TAG
 NEW FLAGBLNK,FLAGDEL,FLAGL,FLAGQ,FLAGTAG
 NEW VRRACT,VRRCRSR,VRRHIGH,VRRLINE,VRRLN,VRRMARK,VRRPGM,VRRSP
 I $G(VEELINE)="" NEW VEEIOST,VEELINE,VEELINE1,VEELINE2,VEESIZE,VEEX,VEEY
 ;FLAGVPE="VEDD^VGL^VRR^EDIT"
 I '$D(FLAGVPE) NEW FLAGVPE
 I $G(FLAGVPE)'["VRR" NEW VEES,VRRS,FLAGCOL
 I $G(FLAGVPE)'["EDIT",$D(VEE("OS"))#2=0 NEW VEE ;Mumps system
 ;
 S FLAGQ=0 D INIT^%ZVEMOY G:FLAGQ EX
 X VEES("RM0")
 S $P(FLAGVPE,"^",3)="VRR" ;Marks VRR as running
 D EN^%ZVEMOS G:FLAGQ EX ;Get Program
 S FLAGCOL=0 ;Used by Collapse option
 ;FLAGBLNK: Marks when last screen is blank
 ;FLAGTAG: Used by FIND TAG and LOCATE STRING
 ;FLAGDEL: Allows blanket DELETE with <ESCT> & <ESCD>
TOP ;
 X VEES("RM0")
 S FLAGL=0 ;Marks last screen
 D FF^%ZVEMOU
 F LNUM=START:1 S FLAGQ=0 Q:'$D(^TMP("VEE","VRR",$J,VRRS,"TXT",LNUM))  S CODE=^(LNUM) D PRINT Q:FLAGQ=1
 S FLAGL=1 I FLAGQ'=1 D PAGE^%ZVEMOM I FLAGQ'=1 S START=LNUM+1 G TOP
EX ;
 S VRRS=VRRS-1 Q:VRRS  X VEES("RM80")
 KILL:$G(FLAGVPE)'["EDIT" ^TMP("VEE","VRR",$J)
 Q
PRINT ;Print routine lines
 S CODETG=$P(CODE,$C(9)),CODELN=$P(CODE,$C(9),2,999)
 I $G(FLAGTAG)?1"F".E S PGTOP=LNUM D  Q:FLAGTAG'=0  ;Find Tag,Strng
 . I FLAGTAG="FT" Q:$E(CODETG,1,$L(TAG))'=TAG
 . I FLAGTAG="FS" Q:CODE'[STNG
 . S FLAGTAG=0
 S FLAGBLNK=0 ;Surpress highlight on blank page
 S (END,END1)=VEE("IOM")-10 I $L(CODETG)>8 S END1=END1-($L(CODETG)-9)
 W !,@$S(CODETG]"":"$J(CODETG,8)",1:"LNUM"),?9,$E(CODELN,1,END1)
 S $P(VRRLOC,"^",LNUM)=$Y_"*"_$S($L(CODETG)>8:$E(CODETG,9),1:" ")
 D:$Y>VEESIZE PAGE^%ZVEMOM Q:FLAGQ  S CODELN=$E(CODELN,(END1+1),999) D PRINT1
 I $G(VRRSP)=2 W ! D:$Y>VEESIZE PAGE^%ZVEMOM Q:FLAGQ  ;Double space
 Q
PRINT1 ;Use GOTO to conserve stack levels
 Q:CODELN=""
 W !?9,$E(CODELN,1,END) D:$Y>VEESIZE PAGE^%ZVEMOM Q:FLAGQ  S CODELN=$E(CODELN,(END+1),999)
 G PRINT1
PARAM(X) ;Parameter Passing....X=Routine Name
 S ^TMP("VEE",$J)=$G(X)
 I $G(DUZ)'>0 D ID^%ZVEMKU I $G(DUZ)="" KILL ^TMP("VEE",$J) Q
 I ^TMP("VEE",$J)]"" NEW FLAGPRM,%1 S FLAGPRM=1,%1=^($J)
 KILL ^TMP("VEE",$J)
 G EN
