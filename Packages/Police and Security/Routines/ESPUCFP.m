ESPUCFP ;DALISC/CKA -PRINT UNIFORM CRIME REPORT BY FACILITY- 3/99
 ;;1.0;POLICE & SECURITY;**27,35**;Mar 31, 1994
EN ;
 I '$D(DUZ(2)) W !,"Site # is not defined!" G EX
DATE ;ASK BEGINNING DATE
 D DT^DICRW K BEGDATE
ASK ;ASK FOR CRIME DATA DATE TO PRINT
 S DIC="^ESP(912.3,",DIC(0)="AEMQ",DIC("A")="  Beginning DATE of Report to Print : " D ^DIC
 I Y<0 G EX
 S ESPIEN=+Y
DIV ;GET THE DIVISION
 D DIVISION^VAUTOMA
 I VAUTD=1 K ^ESP(912.3,ESPIEN,1,9999) S ^ESP(912.3,ESPIEN,1,"B",9999,9999)="",^ESP(912.3,ESPIEN,1,9999,0)=9999 D
 .  F ESPN=0:0 S ESPN=$O(^ESP(912.3,ESPIEN,1,ESPN)) Q:+ESPN=0!(ESPN=9999)  D
 ..   F CTR=0:0 S CTR=$O(^ESP(912.3,ESPIEN,1,ESPN,CTR)) Q:CTR=""  D
 ...    S CAMT=$G(^ESP(912.3,ESPIEN,1,ESPN,CTR))
 ...    S ^ESP(912.3,ESPIEN,1,9999,CTR)=$G(^ESP(912.3,ESPIEN,1,9999,CTR))+CAMT
 K:VAUTD=1 CAMT,CTR
PRT ;PRINT REPORT
Q S %ZIS="Q" D ^%ZIS G:POP EX I '$D(IO("Q")) U IO D START G EX
 S ZTRTN="START^ESPUCFP",ZTDESC="UNIFORM CRIME REPORT"
 S ZTSAVE("ESP*")="",ZTSAVE("VAUTD*")=""
 D ^%ZTLOAD,HOME^%ZIS G EX
START ; BEGINS THE PRINT OF THIS CRIME REPORT
 S HOF=0,TFAC=0
 K ^UTILITY("DIQ1",$J) S (END,PAGE)=0
 S DIC="^ESP(912.3,",DA=ESPIEN,DR=".01;.02",DIQ(0)="E" D EN^DIQ1 I '$D(^UTILITY("DIQ1",$J,912.3,DA)) W !,"RECORD DOESN'T EXIST!" G EX
 F ESPN=0:0 S ESPN=$O(^ESP(912.3,ESPIEN,1,ESPN)) Q:ESPN=""!(END)!(+ESPN=0)  D
 .I +$G(VAUTD)'=1,'$D(VAUTD(+$P(^ESP(912.3,ESPIEN,1,ESPN,0),U))) Q
 .I TFAC=1,ESPN=9999 Q
 .S TFAC=TFAC+1
 .D HDR Q:END
 .D PRT^ESPUCFP1
 W !!,"Done."
 QUIT
EX D ^%ZISC
 K %ZIS,BEGDATE,DA,DIC,DIQ,DR,END,ESPIEN,ESPN,HOF,PAGE,TFAC,TOT,X,Y,^UTILITY("DIQ1",$J)
 QUIT
HDR ;PRINT HEADING
 I $E(IOST,1,2)="C-" W !,"Press RETURN to continue or '^' to exit: " R X:DTIME S END='$T!(X="^") Q:END
 S PAGE=PAGE+1 W:HOF=1 @IOF W !?25,"DEPARTMENT OF VETERANS AFFAIRS",?70,"PAGE ",$J(PAGE,3)
 W !?35,"VA POLICE",!?33,"UNIFORM CRIME REPORT"
 S:ESPN=9999 ESPFACI="*** ALL DIVISIONS ***"
 S:ESPN'=9999 ESPFACI=$P(^ESP(912.3,ESPIEN,1,ESPN,0),U),ESPFACI=$P($G(^DG(40.8,ESPFACI,0)),U)_" ("_$P($G(^ESP(912.3,ESPIEN,1,ESPN,0)),U,2)_")"
 W !!,"VA Facility: ",ESPFACI
 W ?52,"BEGINNING DATE: ",$G(^UTILITY("DIQ1",$J,912.3,ESPIEN,.01,"E"))
 W !,"Date/Time Printed",?52,"ENDING DATE: ",$G(^UTILITY("DIQ1",$J,912.3,ESPIEN,.02,"E"))
 D NOW^%DTC S Y=% X ^DD("DD") W !,$P(Y,":",1,2)
 S HOF=1
 QUIT
