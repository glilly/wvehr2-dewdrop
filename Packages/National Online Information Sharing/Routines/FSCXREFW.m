FSCXREFW ;SLC/STAFF-NOIS Xrefs Workload ;1/10/96  14:02
 ;;1.1;NOIS;;Sep 06, 1998
 ;
AK(OP,FIELD,VALUE,WKLD) ; from dd 7103.5
 N CALL,DATE,USER
 I OP="SET" D  Q
 .I FIELD="CALL" D  Q
 ..I VALUE S DATE=$P(^FSCD("WKLD",WKLD,0),U,3),USER=$P(^(0),U,2) I DATE,USER S ^FSCD("WKLD","AK",VALUE,USER,DATE)=WKLD
 .I FIELD="USER" D  Q
 ..I VALUE S CALL=$P(^FSCD("WKLD",WKLD,0),U),DATE=$P(^(0),U,3) I CALL,DATE S ^FSCD("WKLD","AK",CALL,VALUE,DATE)=WKLD
 .I FIELD="DATE" D  Q
 ..I VALUE S CALL=$P(^FSCD("WKLD",WKLD,0),U),USER=$P(^(0),U,2) I CALL,USER S ^FSCD("WKLD","AK",CALL,USER,VALUE)=WKLD
 I OP="KILL" D  Q
 .I FIELD="CALL" D  Q
 ..I VALUE S DATE=$P(^FSCD("WKLD",WKLD,0),U,3),USER=$P(^(0),U,2) I DATE,USER K ^FSCD("WKLD","AK",VALUE,USER,DATE)
 .I FIELD="USER" D  Q
 ..I VALUE S CALL=$P(^FSCD("WKLD",WKLD,0),U),DATE=$P(^(0),U,3) I CALL,DATE K ^FSCD("WKLD","AK",CALL,VALUE,DATE)
 .I FIELD="DATE" D  Q
 ..I VALUE S CALL=$P(^FSCD("WKLD",WKLD,0),U),USER=$P(^(0),U,2) I CALL,USER K ^FSCD("WKLD","AK",CALL,USER,VALUE)
 Q
 ;
AUD(OP,FIELD,VALUE,WKLD) ; from dd 7103.5
 N DATE,USER
 I OP="SET" D  Q
 .I FIELD="DATE" D  Q
 ..I VALUE S USER=$P(^FSCD("WKLD",WKLD,0),U,2) I USER S ^FSCD("WKLD","AUD",USER,VALUE,WKLD)=""
 .I FIELD="USER" D  Q
 ..I VALUE S DATE=$P(^FSCD("WKLD",WKLD,0),U,3) I DATE S ^FSCD("WKLD","AUD",VALUE,DATE,WKLD)=""
 I OP="KILL" D  Q
 .I FIELD="DATE" D  Q
 ..I VALUE S USER=$P(^FSCD("WKLD",WKLD,0),U,2) I USER K ^FSCD("WKLD","AUD",USER,VALUE,WKLD)
 .I FIELD="USER" D  Q
 ..I VALUE S DATE=$P(^FSCD("WKLD",WKLD,0),U,3) I DATE K ^FSCD("WKLD","AUD",VALUE,DATE,WKLD)
 Q
