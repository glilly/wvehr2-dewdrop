FSCLME ;SLC/STAFF-NOIS List Manager - Edit ;1/13/98  22:03
 ;;1.1;NOIS;;Sep 06, 1998
 ;
ENTRY ; from list template - entry code
 N CALLNUM,CALLLINE,REFLINE,LASTLINE,LISTCNT,LISTNUM,STYLE
 D KILLTMP
 I '$G(FSCDEV) W "."
 S FSCSTYLE=$$EFORMAT^FSCUF(DUZ)
 S VALMAR="^TMP(""FSC MULT "_FSCSTYLE_""",$J,+$G(FSCCNT))" ;*** use current value or user default??
 S VALMCNT=0
 S (LISTCNT,LISTNUM)=0 F  S LISTNUM=$O(^TMP("FSC SELECT",$J,FSCCALLS,LISTNUM)) Q:LISTNUM<1  D
 .S LISTCNT=LISTCNT+1
 .S CALLLINE=+$O(^TMP("FSC LIST CALLS",$J,"IDX",LISTNUM,0))
 .S CALLNUM=+$O(^TMP("FSC LIST CALLS",$J,"ICX",CALLLINE,0))
 .S REFLINE=^TMP("FSC LIST CALLS",$J,CALLLINE,0)
 .S ^TMP("FSC MULT",$J,LISTNUM)=CALLNUM_U_REFLINE
 .I LISTCNT=1 S LASTLINE=0 D BUILD^FSCFORM(LISTNUM,CALLNUM,.FSCSTYLE,0,"FSC MULT ") S FSCCNT=LISTNUM
 S STYLE="FSC MULT "_FSCSTYLE
 S VALMCAP=$$CAP^FSCU("E",.FSCSTYLE,FSCCNT)
 S VALMCNT=+$P(^TMP(STYLE,$J,FSCCNT),U,2),VALMHDR(1)=$P(^TMP("FSC MULT",$J,FSCCNT),U,2,99)
 I '$O(^TMP("FSC MULT",$J,0)) S FSCCNT=1,@VALMAR@(1,0)=" ",@VALMAR@(2,0)="    No calls meet criteria.",VALMCNT=2
 Q
 ;
HEADER ; from list template - header code
 S VALMHDR(1)=""
 Q
 ;
EXIT ; from list template - exit code
 I $G(FSC1) D CLEAR^VALM1
 D KILLTMP
 K ^TMP("FSC SELECT",$J,"EVALUES")
 Q
 ;
KILLTMP ;
 K ^TMP("FSC MULT",$J)
 K ^TMP("FSC MULT BRIEF",$J)
 K ^TMP("FSC MULT DETAIL",$J)
 K ^TMP("FSC MULT FORMAT",$J)
 K ^TMP("FSC MULT STAT",$J)
 K ^TMP("FSC MULT CUSTOM",$J)
 K ^TMP("FSC TEXT",$J)
 Q
 ;
HELP ; from list template - help code
 I $G(X)'["?" Q
 S VALMBCK="R"
 N XQH
 I X="?" S XQH="FSC MENU EDIT" D EN^XQH Q
 I X="???" S VALMANS="?" D CLEAR^VALM1 S XQH="FSC U1 NOIS" D EN^XQH Q
 Q
