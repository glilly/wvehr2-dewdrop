LEXQID3 ;ISL/KER - Query - ICD Diagnosis - Extract (cont) ;10/30/2008
 ;;2.0;LEXICON UTILITY;**62**;Sep 23, 1996;Build 16
 ;               
 ; Global Variables
 ;    ^ICD(               ICR   4487
 ;    ^ICD9(              ICR   4485
 ;    ^TMP("LEXQID")      SACC 2.3.2.5.1
 ;               
 ; External References
 ;    $$ICDDX^ICDCODE     ICR   3990
 ;    DRGD^ICDGTDRG       ICR   4052
 ;    $$DT^XLFDT          ICR  10103
 ;    $$FMTE^XLFDT        ICR  10103
 ;    $$UP^XLFSTR         ICR  10104
 ;               
 Q
NOT(X,LEXVDT,LEXLEN) ; Include ICD Codes not to use with ***.**
 ; 
 ; ^TMP("LEXQID",$J,"NOT",0)=<total>
 ; ^TMP("LEXQID",$J,"NOT",1,1)=<header>
 ; ^TMP("LEXQID",$J,"NOT",2,#)=<header text>
 ; ^TMP("LEXQID",$J,"NOT",3,<code >)=<code>_"  "_<diagnosis>
 ; 
 K ^TMP("LEXQID",$J,"NOT") N LEX,LEXI,LEXC,LEXICD,LEXIEN,LEXISO,LEXSO,LEXSD,LEXD,LEXT,LEXSTR S LEXIEN=+($G(X)) Q:+LEXIEN'>0  Q:'$D(^ICD9(+LEXIEN,0))
 S LEXVDT=+($G(LEXVDT)) S:LEXVDT'?7N LEXVDT=$$DT^XLFDT S LEXLEN=+$G(LEXLEN) S:+LEXLEN>62 LEXLEN=62
 S LEXISO=$P($G(^ICD9(+LEXIEN,0)),"^",1) Q:'$L(LEXISO)  S LEXI=0 F  S LEXI=$O(^ICD9(+LEXIEN,"N",LEXI)) Q:+LEXI'>0  D
 . S LEXICD=+($G(^ICD9(+LEXIEN,"N",+LEXI,0))),LEXD=$$ICDDX^ICDCODE(+LEXICD,LEXVDT)
 . S LEXSO=$P(LEXD,"^",2),LEXSD=$$UP^XLFSTR($P(LEXD,"^",4)) Q:'$L(LEXSO)  Q:'$L(LEXSD)
 . S LEXT=LEXSO,LEXT=LEXT_$J(" ",(9-$L(LEXT)))_LEXSD
 . S ^TMP("LEXQID",$J,"NOT",3,(LEXSO_" "))=LEXT
 S LEXC=0,LEXI="" F  S LEXI=$O(^TMP("LEXQID",$J,"NOT",3,LEXI)) Q:'$L(LEXI)  S LEXC=LEXC+1
 S ^TMP("LEXQID",$J,"NOT",0)=+($G(LEXC))
 S LEXI=+($G(^TMP("LEXQID",$J,"NOT",0))) I LEXI>0 D
 . N LEX,LEXC,LEXSTR,LEXT S LEXSTR="The following code"_$S(LEXI>1:"s ",1:" ")_"cannot be used in conjunction with "
 . S:$L($G(LEXISO)) LEXSTR=LEXSTR_"ICD Code "_LEXISO S:'$L($G(LEXISO)) LEXSTR=LEXSTR_"this ICD Code"
 . S LEX(1)=LEXSTR D PR^LEXQM(.LEX,(LEXLEN-7)) S (LEXC,LEXT)=0 F  S LEXT=$O(LEX(LEXT)) Q:+LEXT'>0  D
 . . S LEXSTR=$$TM^LEXQM($G(LEX(LEXT))) S:$L(LEXSTR) LEXC=LEXC+1,^TMP("LEXQID",$J,"NOT",2,LEXC)=LEXSTR
 S:$D(^TMP("LEXQID",$J,"NOT",2)) ^TMP("LEXQID",$J,"NOT",1,1)="Not used"
 Q
REQ(X,LEXVDT,LEXLEN) ; Include ICD Codes required with ***.**
 ; 
 ; ^TMP("LEXQID",$J,"REQ",0)=<total>
 ; ^TMP("LEXQID",$J,"REQ",1,1)=<header>
 ; ^TMP("LEXQID",$J,"REQ",2,#)=<header text>
 ; ^TMP("LEXQID",$J,"REQ",3,<code >)=<code>_"  "_<diagnosis>
 ; 
 K ^TMP("LEXQID",$J,"REQ") N LEX,LEXC,LEXI,LEXICD,LEXIEN,LEXISO,LEXSO,LEXSD,LEXD,LEXSTR,LEXT S LEXIEN=+($G(X)) Q:+LEXIEN'>0  Q:'$D(^ICD9(+LEXIEN,0))
 S LEXVDT=+($G(LEXVDT)) S:LEXVDT'?7N LEXVDT=$$DT^XLFDT S LEXLEN=+$G(LEXLEN) S:+LEXLEN>62 LEXLEN=62
 S LEXISO=$P($G(^ICD9(+LEXIEN,0)),"^",1) Q:'$L(LEXISO)  S LEXI=0 F  S LEXI=$O(^ICD9(+LEXIEN,"R",LEXI)) Q:+LEXI'>0  D
 . S LEXICD=+($G(^ICD9(+LEXIEN,"R",+LEXI,0))),LEXD=$$ICDDX^ICDCODE(+LEXICD,LEXVDT)
 . S LEXSO=$P(LEXD,"^",2),LEXSD=$$UP^XLFSTR($P(LEXD,"^",4)) Q:'$L(LEXSO)  Q:'$L(LEXSD)
 . S LEXT=LEXSO,LEXT=LEXT_$J(" ",(9-$L(LEXT)))_LEXSD
 . S ^TMP("LEXQID",$J,"REQ",3,(LEXSO_" "))=LEXT
 S LEXC=0,LEXI="" F  S LEXI=$O(^TMP("LEXQID",$J,"REQ",3,LEXI)) Q:'$L(LEXI)  S LEXC=LEXC+1
 S ^TMP("LEXQID",$J,"REQ",0)=+($G(LEXC))
 S LEXI=+($G(^TMP("LEXQID",$J,"REQ",0))) I LEXI>0 D
 . N LEX,LEXC,LEXSTR,LEXT S:LEXI>1 LEXSTR="One of the following codes is required when "
 . S:LEXI>1 LEXSTR="One of the following codes is required when " S:LEXI'>1 LEXSTR="The following code is required when "
 . S:$L($G(LEXISO)) LEXSTR=LEXSTR_"ICD Code "_LEXISO_" "
 . S:'$L($G(LEXISO)) LEXSTR=LEXSTR_"this ICD Code " S LEXSTR=LEXSTR_"is used"
 . S LEX(1)=LEXSTR D PR^LEXQM(.LEX,(LEXLEN-7)) S (LEXC,LEXT)=0 F  S LEXT=$O(LEX(LEXT)) Q:+LEXT'>0  D
 . . S LEXSTR=$$TM^LEXQM($G(LEX(LEXT))) S:$L(LEXSTR) LEXC=LEXC+1,^TMP("LEXQID",$J,"REQ",2,LEXC)=LEXSTR
 S:$D(^TMP("LEXQID",$J,"REQ",2)) ^TMP("LEXQID",$J,"REQ",1,1)="Required with"
 Q
NCC(X,LEXVDT,LEXLEN) ; Include the codes that ***.** is not CC with
 ; 
 ; ^TMP("LEXQID",$J,"NCC",0)=<total>
 ; ^TMP("LEXQID",$J,"NCC",1,1)=<header>
 ; ^TMP("LEXQID",$J,"NCC",2,#)=<header text>
 ; ^TMP("LEXQID",$J,"NCC",3,<code >)=<code>_"  "_<diagnosis>
 ; 
 K ^TMP("LEXQID",$J,"NCC") N LEX,LEXI,LEXC,LEXICD,LEXIEN,LEXISO,LEXSO,LEXSD,LEXD,LEXSTR,LEXT S LEXIEN=+($G(X)) Q:+LEXIEN'>0  Q:'$D(^ICD9(+LEXIEN,0))
 S LEXVDT=+($G(LEXVDT)) S:LEXVDT'?7N LEXVDT=$$DT^XLFDT S LEXLEN=+$G(LEXLEN) S:+LEXLEN>62 LEXLEN=62
 S LEXISO=$P($G(^ICD9(+LEXIEN,0)),"^",1) Q:'$L(LEXISO)  S LEXI=0 F  S LEXI=$O(^ICD9(+LEXIEN,2,LEXI)) Q:+LEXI'>0  D
 . S LEXICD=+($G(^ICD9(+LEXIEN,2,+LEXI,0))),LEXD=$$ICDDX^ICDCODE(+LEXICD,LEXVDT)
 . S LEXSO=$P(LEXD,"^",2),LEXSD=$$UP^XLFSTR($P(LEXD,"^",4)) Q:'$L(LEXSO)  Q:'$L(LEXSD)
 . S LEXT=LEXSO,LEXT=LEXT_$J(" ",(9-$L(LEXT)))_LEXSD
 . S ^TMP("LEXQID",$J,"NCC",3,(LEXSO_" "))=LEXT
 S LEXC=0,LEXI="" F  S LEXI=$O(^TMP("LEXQID",$J,"NCC",3,LEXI)) Q:'$L(LEXI)  S LEXC=LEXC+1
 S ^TMP("LEXQID",$J,"NCC",0)=+($G(LEXC))
 S LEXI=+($G(^TMP("LEXQID",$J,"NCC",0))) I LEXI>0 D
 . N LEX,LEXC,LEXSTR,LEXT S LEXSTR="ICD Code " S:$L($G(LEXISO)) LEXSTR=LEXSTR_LEXISO_" "
 . S LEXSTR=LEXSTR_"is not considered as Complication Comorbidity (CC) with the following code"_$S(LEXI>1:"s",1:"")
 . S LEX(1)=LEXSTR D PR^LEXQM(.LEX,(LEXLEN-7)) S (LEXC,LEXT)=0 F  S LEXT=$O(LEX(LEXT)) Q:+LEXT'>0  D
 . . S LEXSTR=$$TM^LEXQM($G(LEX(LEXT))) S:$L(LEXSTR) LEXC=LEXC+1,^TMP("LEXQID",$J,"NCC",2,LEXC)=LEXSTR
 S:$D(^TMP("LEXQID",$J,"NCC",2)) ^TMP("LEXQID",$J,"NCC",1,1)="Not CC with"
 Q
DRG(X,LEXVDT,LEXLEN) ; Diagnosis Related Group
 ;               
 ; ^TMP("LEXQID",$J,"DRG",0)=<total>
 ; ^TMP("LEXQID",$J,"DRG",1,1)=<header>
 ; ^TMP("LEXQID",$J,"DRG",1,2)=<effective date>
 ; ^TMP("LEXQID",$J,"DRG",2,1)=<header text>
 ; ^TMP("LEXQID",$J,"DRG",3,#)=<DRG list>
 ;               
 N LEXC,LEXDDD,LEXDDE,LEXDEF,LEXDDI,LEXDDT,LEXDRG,LEXDRG1,LEXDRG2,LEXDRGC,LEXDRGD,LEXDRP,LEXI,LEXIEN,LEXL,LEXN,LEXN0,LEXT S LEXIEN=+($G(X)) Q:+LEXIEN'>0  Q:'$D(^ICD9(+LEXIEN,3))
 S LEXVDT=+($G(LEXVDT)) S:LEXVDT'?7N LEXVDT=$$DT^XLFDT S LEXLEN=+$G(LEXLEN) S:+LEXLEN>62 LEXLEN=62 S (LEXDEF,LEXDDT)=$O(^ICD9(+LEXIEN,3,"B",(LEXVDT+.99999)),-1)
 S LEXDDI=$O(^ICD9(+LEXIEN,3,"B",+LEXDDT," "),-1) I $O(^ICD9(+LEXIEN,3,+LEXDDI,1,0))'>0 D  Q
 . S ^TMP("LEXQID",$J,"DRG",0)=0,^TMP("LEXQID",$J,"DRG",1,1)="DRG Groups"
 . S ^TMP("LEXQID",$J,"DRG",2,1)="No DRG Groups found to be active for the date provided"
 . S:LEXVDT?7N ^TMP("LEXQID",$J,"DRG",2,1)="No DRG Groups found to be active on "_$$SD^LEXQM(LEXVDT)
 S LEXDRGC=0,LEXDDE="" F  S LEXDDE=$O(^ICD9(+LEXIEN,3,+LEXDDI,1,"B",LEXDDE)) Q:'$L(LEXDDE)  D
 . N LEXDDD S LEXDDD=0 F  S LEXDDD=$O(^ICD9(+LEXIEN,3,+LEXDDI,1,"B",LEXDDE,LEXDDD)) Q:+LEXDDD'>0  D
 . . N LEXNO,LEXDRP,LEXDRG,LEXDRGD,LEXI,LEXT,LEXDRG1,LEXDRG2,LEXC S LEXN0=$G(^ICD9(+LEXIEN,3,+LEXDDI,1,+LEXDDD,0)) S LEXDRP=$P(LEXN0,"^",1)
 . . S LEXDRG=$P($G(^ICD(+LEXDRP,0)),"^",1) K LEXDRGD D DRGD^ICDGTDRG(LEXDRG,"LEXDRGD",,+LEXVDT) S LEXDRG=$TR(LEXDRG,"DRG",""),LEXDRG=+LEXDRG Q:+LEXDRG'>0
 . . S LEXI=0 F  S LEXI=$O(LEXDRGD(LEXI)) Q:+LEXI'>0  D
 . . . N LEXT S LEXT=$$TM^LEXQM($G(LEXDRGD(LEXI))) I '$L(LEXT)!(LEXT["CODE TEXT MAY BE INACCURATE") K LEXDRGD(LEXI) Q
 . . . S LEXDRGD(LEXI)=LEXT
 . . S LEXDRG1=LEXDRG,LEXDRG1=LEXDRG1_$J(" ",(6-$L(LEXDRG1))),LEXDRG2=$J(" ",6) D PR^LEXQM(.LEXDRGD,(LEXLEN-8))
 . . S (LEXC,LEXI)=0 F  S LEXI=$O(LEXDRGD(LEXI)) Q:+LEXI'>0  D
 . . . N LEXT,LEXL,LEXN S LEXT=$$TM^LEXQM($G(LEXDRGD(LEXI))) Q:'$L(LEXT)  S LEXC=LEXC+1 S:LEXC=1 LEXL=LEXDRG1_LEXT,LEXDRGC=+($G(LEXDRGC))+1
 . . . S:LEXC>1 LEXL=LEXDRG2_LEXT S LEXN=$O(^TMP("LEXQID",$J,"DRG",3," "),-1)+1 S ^TMP("LEXQID",$J,"DRG",3,LEXN)=LEXL
 S ^TMP("LEXQID",$J,"DRG",0)=+($G(LEXDRGC)),^TMP("LEXQID",$J,"DRG",1,1)="DRG Groups"
 S:$G(LEXDEF)?7N ^TMP("LEXQID",$J,"DRG",1,2)=$$SD^LEXQM(LEXDEF)
 S:+($G(LEXDRGC))>0 ^TMP("LEXQID",$J,"DRG",2,1)=+($G(LEXDRGC))_" Diagnosis Related Group"_$S(+($G(LEXDRGC))>1:"s",1:"")_" (DRG)"
 Q
CC(X,LEXVDT,LEX) ; Complication/Comorbidity
 N LEXCCE,LEXCCI,LEXCH,LEXEF,LEXN0 K LEX S LEX=0,LEXIEN=+($G(X)) Q:+LEXIEN'>0  Q:'$D(^ICD9(+LEXIEN,3))  S LEXVDT=+($G(LEXVDT)) S:LEXVDT'?7N LEXVDT=$$DT^XLFDT
 S LEXEF=$O(^ICD9(+LEXIEN,69,"B",(LEXVDT+.9999)),-1),LEXCH=$O(^ICD9(+LEXIEN,69,"B",+LEXEF," "),-1),LEXN0=$G(^ICD9(+LEXIEN,69,+LEXCH,0)),LEXCCI=$P(LEXN0,"^",2)
 Q:LEXEF'?7N  Q:+LEXCCI'?1N  S LEXCCE=$S(+LEXCCI=0:"Non-Complication/Comorbidity (Non-CC)",+LEXCCI=1:"Complication/Comorbidity (CC)",+LEXCCI=2:"Major Complication/Comorbidity (MCC)",1:"")
 Q:'$L(LEXCCE)  S LEX=1,LEX(0)=$$SD^LEXQM(LEXEF),LEX(1)=LEXCCE
 Q
 ; 
 ; Miscellaneous            
SD(X) ;   Short Date
 Q $TR($$FMTE^XLFDT(+($G(X)),"5DZ"),"@"," ")
IA(X) ;   Inaccurate
 N LEXBRD,LEXVDT,LEXSYS S LEXVDT=+($G(X)),LEXSYS=1,LEXVDT=$S($G(LEXVDT)="":$$DT^XLFDT,1:$$DBR(LEXVDT)),LEXBRD=3021001,X=$S(LEXVDT<LEXBRD:1,1:0)
 Q X
DBR(X) ;   Date Business Rules
 N LEXVDT S LEXVDT=$G(X) Q:'$G(LEXVDT)!($P(LEXVDT,".")'?7N) $$DT^XLFDT
 S:LEXVDT#10000=0 LEXVDT=LEXVDT+101 S:LEXVDT#100=0 LEXVDT=LEXVDT+1 S X=$S(LEXVDT<2781001:2781001,1:LEXVDT)
 Q X
