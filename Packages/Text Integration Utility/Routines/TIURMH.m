TIURMH ; SLC/JER - MIS Review Screen Header ;4/20/94  16:18
 ;;1.0;TEXT INTEGRATION UTILITIES;;Jun 20, 1997
HDR ; Initialize header for clinician's review screen
 N BY,TIUI,TIUX,SCREEN,STATUS,RANGE,TIUBY,TIUDOCS,XREF
 S TIUX=$G(^TMP("TIUR",$J,0)),STATUS=$P(TIUX,U,2),SCREEN=$P(TIUX,U,3,99)
 S VALM("TITLE")=$S($L(STATUS,",")>1:$S(VALM("ENTITY")="Document":"Clinical",1:""),1:STATUS)
 S:$L(VALM("TITLE")) VALM("TITLE")=VALM("TITLE")_" "
 S VALM("TITLE")=VALM("TITLE")_$S($L(VALM("ENTITY")):$$NTTNM(VALM("ENTITY")),1:"Document")_"s"
 I +TIUEDT>1 S RANGE=" from "_$$DATE^TIULS(TIUEDT,"MM/DD/YY")_" to "_$$DATE^TIULS(TIULDT,"MM/DD/YY")
 S VALMHDR(1)=$$CENTER^TIULS($G(RANGE))
 S TIUDOCS=$J(+$G(^TMP("TIUR",$J,0)),4)_" documents"
 S VALMHDR(1)=$$SETSTR^VALM1(TIUDOCS,VALMHDR(1),(IOM-$L(TIUDOCS)),$L(TIUDOCS))
 Q
NTTNM(NAME) ; Handle plural names appropriately
 I "Yy"[$E(NAME,$L(NAME)) S NAME=$E(NAME,1,($L(NAME)-1))_"ie"
 Q NAME
