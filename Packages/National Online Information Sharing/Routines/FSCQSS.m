FSCQSS ;SLC/STAFF-NOIS Query Search Setup ;8/18/98  12:07
 ;;1.1;NOIS;;Sep 06, 1998
 ;
SETUP ; from FSCQS
 N COND,FFSEQ,FIELD,FQUERY,FSEQ,NODE,VALUE
 S FSEQ=+$O(CRITERIA("O",0)),STEP=+CRITERIA("O",FSEQ)
 S FFSEQ=+$O(CRITERIA("O",FSEQ,0)),FQUERY=+CRITERIA("O",FSEQ,FFSEQ)
 S QUERY=CRITERIA(STEP,FQUERY)
 F  S FFSEQ=$O(CRITERIA("O",FSEQ,FFSEQ)) Q:FFSEQ<1  S FQUERY=+CRITERIA("O",FSEQ,FFSEQ) D
 .S FQUERY=CRITERIA(STEP,FQUERY)
 .S FIELD=$P(FQUERY,U),COND=$P(FQUERY,U,2),VALUE=$P(FQUERY,U,3)
 .I "PND"'[$E(CRITERIA("F",FIELD)) S VALUE=""""_$$UP^XLFSTR(VALUE)_""""
 .I $P(CRITERIA("F",FIELD),U)="W" D  Q
 ..S VALUE=$E(VALUE,2,$L(VALUE)-1) D QUOTES(.VALUE)
 ..I FIELD=59 D PERWP Q
 ..I COND="[" D  Q
 ...S CRITERIA("O",FSEQ,"AND",FFSEQ)="N GOOD,LINE1,SUBNUM I 1"
 ...S CRITERIA("O",FSEQ,"AND",FFSEQ+.1)="S LINE1=""F  S SUBNUM=$O(^FSCD(""""CALL"""",CALL,"_$S(FIELD=12:30,FIELD=13:50,FIELD=43:110,FIELD=54:103,1:80)_",SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))["_VALUE_" S GOOD=1 Q"" I 1"
 ...S CRITERIA("O",FSEQ,"AND",FFSEQ+.2)="S (GOOD,SUBNUM)=0 X LINE1 I GOOD"
 ..I COND="exists" S CRITERIA("O",FSEQ,"AND",FFSEQ)="I $O(^FSCD(""CALL"",CALL,"_$S(FIELD=12:30,FIELD=13:50,FIELD=43:110,FIELD=54:103,1:80)_",0))" Q
 ..I COND="'[" D  Q
 ...S CRITERIA("O",FSEQ,"AND",FFSEQ)="N LINE1,GOOD,SUBNUM I 1"
 ...S CRITERIA("O",FSEQ,"AND",FFSEQ+.1)="I $O(^FSCD(""CALL"",CALL,"_$S(FIELD=12:30,FIELD=13:50,FIELD=43:110,FIELD=54:103,1:80)_",0))"
 ...S CRITERIA("O",FSEQ,"AND",FFSEQ+.2)="S LINE1=""F  S SUBNUM=$O(^FSCD(""""CALL"""",CALL,"_$S(FIELD=12:30,FIELD=13:50,FIELD=43:110,FIELD=54:103,1:80)_",SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))["_VALUE_" S GOOD=1 Q"" I 1"
 ...S CRITERIA("O",FSEQ,"AND",FFSEQ+.3)="S (GOOD,SUBNUM)=0 X LINE1 I 'GOOD"
 ..I COND="not exists" S CRITERIA("O",FSEQ,"AND",FFSEQ)="I '$O(^FSCD(""CALL"",CALL,"_$S(FIELD=12:30,FIELD=13:50,FIELD=43:110,FIELD=54:103,1:80)_",0))" Q
 .S NODE=$P(CRITERIA("F",FIELD),U,2)
 .I NODE["7100.6," D PER Q
 .I NODE[";",$E(NODE,1,2)'="1;" D  Q
 ..S CRITERIA("O",FSEQ,"AND",1+(NODE*.00001))="S X("_+NODE_")=$G(^FSCD(""CALL"",CALL,"_+NODE_")) I 1"
 ..I COND["exist" D  Q
 ...I COND="exists" S CRITERIA("O",FSEQ,"AND",FFSEQ)="I $L($P(X("_+NODE_"),U,"_+$P(NODE,";",2)_"))" Q
 ...I COND="not exists" S CRITERIA("O",FSEQ,"AND",FFSEQ)="I '$L($P(X("_+NODE_"),U,"_+$P(NODE,";",2)_"))" Q
 ..I COND["range" D  Q
 ...S CRITERIA("O",FSEQ,"AND",FFSEQ+.1)="S X(""BEG"")="""_$P(VALUE,"-")_""",X(""END"")="""_$P(VALUE,"-",2)_""" I X(""BEG"")=+X(""BEG""),X(""END"")=+X(""END"")"
 ...I COND="range" S CRITERIA("O",FSEQ,"AND",FFSEQ+.2)="I $P(X("_+NODE_"),U,"_+$P(NODE,";",2)_")'<X(""BEG""),$P(X("_+NODE_"),U,"_+$P(NODE,";",2)_")'>X(""END"")"
 ...I COND="not range" S CRITERIA("O",FSEQ,"AND",FFSEQ+.2)="I $P(X("_+NODE_"),U,"_+$P(NODE,";",2)_")<X(""BEG"")!($P(X("_+NODE_"),U,"_+$P(NODE,";",2)_")>X(""END""))"
 ..S CRITERIA("O",FSEQ,"AND",FFSEQ)="I $$UP^XLFSTR($P(X("_+NODE_"),U,"_+$P(NODE,";",2)_"))"_COND_VALUE
 .I $E(NODE,1,2)="1;" D  Q
 ..S CRITERIA("O",FSEQ,"AND",1.1)="S Y=$G(^FSCD(""CALL"",CALL,1)) I 1"
 ..I COND["exist" D  Q
 ...I COND="exists" S CRITERIA("O",FSEQ,"AND",FFSEQ)="I $L(Y)" Q
 ...I COND="not exists" S CRITERIA("O",FSEQ,"AND",FFSEQ)="I '$L(Y)" Q
 ..S CRITERIA("O",FSEQ,"AND",FFSEQ)="I $$UP^XLFSTR(Y)"_COND_VALUE
 S CRITERIA("O",FSEQ,"AND",1+$O(CRITERIA("O",FSEQ,"A"),-1))="S ^TMP(""FSC NEWLIST"",$J,CALL)="""""
 Q
 ;
PERWP ;
 I COND="[" D  Q
 .S CRITERIA("O",FSEQ,"AND",FFSEQ)="N GOOD,IEN,LINE1,SUBNUM I 1"
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.1)="S IEN=+$G(^FSCD(""CALL USER"",""AUC"",DUZ,CALL)) I IEN"
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.2)="S LINE1=""F  S SUBNUM=$O(^FSCD(""""CALL USER"""",IEN,2,SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))["_VALUE_" S GOOD=1 Q"" I 1"
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.3)="S (GOOD,SUBNUM)=0 X LINE1 I GOOD"
 I COND="exists" D  Q
 .S CRITERIA("O",FSEQ,"AND",FFSEQ)="N GOOD,IEN,LINE1,SUBNUM I 1"
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.1)="S IEN=+$G(^FSCD(""CALL USER"",""AUC"",DUZ,CALL)) I IEN"
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.2)="I $O(^FSCD(""CALL USER"",IEN,2,0))"
 I COND="'[" D  Q
 .S CRITERIA("O",FSEQ,"AND",FFSEQ)="N GOOD,IEN,LINE1,SUBNUM I 1"
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.1)="S IEN=+$G(^FSCD(""CALL USER"",""AUC"",DUZ,CALL)) I IEN"
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.2)="I $O(^FSCD(""CALL USER"",IEN,2,0))"
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.3)="S LINE1=""F  S SUBNUM=$O(^FSCD(""""CALL USER"""",IEN,2,SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))["_VALUE_" S GOOD=1 Q"" I 1"
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.4)="S (GOOD,SUBNUM)=0 X LINE1 I 'GOOD"
 I COND="not exists" D  Q
 .S CRITERIA("O",FSEQ,"AND",FFSEQ)="N GOOD,IEN,LINE1,SUBNUM I 1"
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.1)="S IEN=+$G(^FSCD(""CALL USER"",""AUC"",DUZ,CALL)) I 1"
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.2)="I '$O(^FSCD(""CALL USER"",IEN,2,0))" Q
 Q
 ;
PER ;
 S NODE=$P(NODE,",",2)
 S CRITERIA("O",FSEQ,"AND",1+((NODE+5000)*.00001))="S X("_(5000+NODE)_")=$G(^FSCD(""CALL USER"",+$G(^FSCD(""CALL USER"",""AUC"",DUZ,CALL)),"_+NODE_")) I 1"
 I COND["exist" D  Q
 .I COND="exists" S CRITERIA("O",FSEQ,"AND",FFSEQ)="I $L($P(X("_(5000+NODE)_"),U,"_+$P(NODE,";",2)_"))" Q
 .I COND="not exists" S CRITERIA("O",FSEQ,"AND",FFSEQ)="I '$L($P(X("_(5000+NODE)_"),U,"_+$P(NODE,";",2)_"))" Q
 I COND["range" D  Q
 .S CRITERIA("O",FSEQ,"AND",FFSEQ+.1)="S X(""BEG"")="""_$P(VALUE,"-")_""",X(""END"")="""_$P(VALUE,"-",2)_""" I X(""BEG"")=+X(""BEG""),X(""END"")=+X(""END"")"
 .I COND="range" S CRITERIA("O",FSEQ,"AND",FFSEQ+.2)="I $P(X("_(5000+NODE)_"),U,"_+$P(NODE,";",2)_")'<X(""BEG""),$P(X("_(5000+NODE)_"),U,"_+$P(NODE,";",2)_")'>X(""END"")"
 .I COND="not range" S CRITERIA("O",FSEQ,"AND",FFSEQ+.2)="I $P(X("_(5000+NODE)_"),U,"_+$P(NODE,";",2)_")<X(""BEG"")!($P(X("_(5000+NODE)_"),U,"_+$P(NODE,";",2)_")>X(""END""))"
 S CRITERIA("O",FSEQ,"AND",FFSEQ)="I $$UP^XLFSTR($P(X("_(5000+NODE)_"),U,"_+$P(NODE,";",2)_"))"_COND_VALUE
 Q
 ;
QUOTES(VALUE) ; from FSCQSSM
 N CHAR,CNT,TEMP
 I VALUE["""" D
 .S TEMP=""
 .F CNT=1:1:$L(VALUE) S CHAR=$E(VALUE,CNT) D
 ..I CHAR="""" S TEMP=TEMP_"""""""""" Q
 ..S TEMP=TEMP_CHAR
 .S VALUE=""""""_TEMP_""""""
 E  S VALUE=""""""_VALUE_""""""
 Q
