IMRUTST ;HCIOFO/FAI-UPDATE LAB TEST VALUES ;08/31/00  09:48;
 ;;2.1;IMMUNOLOGY CASE REGISTRY;**5**;Feb 09, 1998
 ;  Category logic below
 ;CATEGORY 1 SET TO 2 - CD4 COUNT IS 200 - 499
 ;CATEGORY 1 SET TO 3 - CD4 COUNT IS LESS THAN 200
 ;CATEGORY 2 SET TO 3 - CD4 COUNT IS LESS THAN 200
 ;CATEGORY 1 SET TO 3 - CD4 % IS LESS THAN 14
 ;CATEGORY 2 SET TO 3 - CD4 % IS LESS THAN 14
 ;
BGIN D ICRPT,KILL
 Q
ICRPT F IMRFN=0:0 S IMRFN=$O(^IMR(158,IMRFN)) Q:IMRFN'>0  S X=+^(IMRFN,0),IMRCAT=$P($G(^(0)),U,42) D ^IMRXOR S DFN=X I $D(^DPT(DFN,0)) D DEMOG,SETLR
 Q
DEMOG D DEM^VADPT S IMRDOD=$P(VADM(6),U),(IMRRACE,X)=$P(VADM(8),U)
 I X>0 S X=$S($D(^DIC(10,X,0)):$P(^(0),U,2),1:0) I X>0 S $P(^IMR(158,IMRFN,0),U,2)=$S(X=1:3,X=2:3,X=3:5,X=4:2,X=5:4,X=6:1,1:9) K VA,VADM,X
 S IMRXCAT=$P($G(^IMR(158,IMRFN,0)),U,42)
 I IMRXCAT="" S $P(^IMR(158,IMRFN,0),U,42)=1,$P(^IMR(158,IMRFN,0),U,36)=DT
 I (IMRXCAT="1")!(IMRXCAT="2")!(IMRXCAT="3") S $P(^IMR(158,IMRFN,0),U,23)=""
 Q:$P($G(^IMR(158,IMRFN,5)),U,19)'=""
 S:$G(IMRDOD)="" $P(^IMR(158,IMRFN,1),U,34)="1"
 S:$G(IMRDOD)'="" $P(^IMR(158,IMRFN,1),U,34)="2",$P(^IMR(158,IMRFN,5),U,19)=IMRDOD,$P(^IMR(158,IMRFN,5),U,20)="1"
 ;patient status above [1;34]=(1:alive,2:dead,9:unknown)
 K IMRDOD,IMRRACE
 Q
SETLR S PNAM=$P($G(^DPT(DFN,0)),U,1),SSN=$P($G(^DPT(DFN,0)),U,9),IMRTSTLR=$P($G(^DPT(DFN,"LR")),U,1) D DATA
 Q
DATA K IMRCD
 Q:$G(IMRTSTLR)=""
 S (IMRTSTI,IMRTSTII)="",ILR=IMRTSTLR
 S LGN="" F  S LGN=$O(^IMR(158.95,"B",LGN)),LIG="" Q:LGN=""  S LIG=$O(^IMR(158.95,"B",LGN,LIG)) Q:LIG=""  D LOCAL,LOCAL2
 Q
LOCAL Q:LGN'="CD4"
 S IMRCD="" F  S IMRCD=$O(^IMR(158.9,1,3,"B",LIG,IMRCD)),IMS="" Q:IMRCD=""  F  S IMS=$O(^IMR(158.9,1,3,IMRCD,1,"B",IMS)),IMLM="" Q:IMS=""  F  S IMLM=$O(^IMR(158.9,1,3,IMRCD,1,"B",IMS,IMLM)) Q:IMLM=""  D LLT
 Q
LOCAL2 Q:LGN'="VIRAL LOAD"
 S IMRCD="" F  S IMRCD=$O(^IMR(158.9,1,3,"B",LIG,IMRCD)),IMS="" Q:IMRCD=""  F  S IMS=$O(^IMR(158.9,1,3,IMRCD,1,"B",IMS)),IMLM="" Q:IMS=""  F  S IMLM=$O(^IMR(158.9,1,3,IMRCD,1,"B",IMS,IMLM)) Q:IMLM=""  D LLT
 Q
LLT S IMLO="" F  S IMLO=$O(^IMR(158.9,1,3,IMRCD,1,IMLM,1,"B",IMLO)),TNN="" Q:IMLO=""  F  S TNN=$O(^IMR(158.9,1,3,IMRCD,1,IMLM,1,"B",IMLO,TNN)) Q:TNN=""  D LWK
 Q
LWK S IMWK=$P($G(^IMR(158.9,1,3,IMRCD,1,IMLM,1,TNN,0)),U,2),LNM=$P($G(^LAB(60,IMLO,0)),U,1),LLOC=$P($G(^LAB(60,IMLO,0)),U,5)
 I LLOC'="" S UNN=$P($G(^LAB(60,IMLO,1,0)),U,3),LDAT=$P(LLOC,";",2) S:UNN'="" UNS=$P($G(^LAB(60,IMLO,1,UNN,0)),U,7) D CHEMS Q
 I LLOC="" D PANEL Q
 Q
PANEL F PN=0:0 S PN=$O(^LAB(60,IMLO,2,PN)) Q:PN'>0  S LPN=$P($G(^LAB(60,IMLO,2,PN,0)),U,1),LNM=$P($G(^LAB(60,LPN,0)),U,1),LLOC=$P($G(^LAB(60,LPN,0)),U,5) D PAN2
 Q
PAN2 S UNN=$P($G(^LAB(60,LPN,1,0)),U,3)
 S:UNN'="" UNS=$P($G(^LAB(60,LPN,1,UNN,0)),U,7)
 S:LLOC'="" LDAT=$P(LLOC,";",2)
 D CHEMS
 Q
CHEMS S LDT="" F  S LDT=$O(^LR(ILR,"CH",LDT)),DNAM="" Q:LDT=""  F  S DNAM=$O(^LR(ILR,"CH",LDT,DNAM)) Q:DNAM=""  S LRES=$P($G(^LR(ILR,"CH",LDT,LDAT)),U,1),DTRC=$P($G(^LR(ILR,"CH",LDT,0)),U,1),Y=DTRC D DD^%DT S DTAA=Y D PLBS,CDTWO,VIRONE
 Q
PLBS Q:(LRES["CANC")!(LRES["canc")
 Q:(DTRC["CANC")!(DTRC["canc")
 S DTR1=$E(DTAA,1,3),DTR2=$E(DTAA,9,12),DTRD=DTR1_","_DTR2,DTAA=$E(DTAA,1,12),LDO=$E(LDT,1,7)
 I LGN="CD4" Q:DNAM'=LDAT  Q:UNS["%"  D CHRG,CHLV
 Q
CDTWO Q:(LRES["CANC")!(LRES["canc")
 Q:(DTRC["CANC")!(DTRC["canc")
 S DTR1=$E(DTAA,1,3),DTR2=$E(DTAA,9,12),DTRD=DTR1_","_DTR2,DTAA=$E(DTAA,1,12),LDO=$E(LDT,1,7)
 I LGN="CD4" Q:DNAM'=LDAT  Q:UNS'["%"  D CHPR
 Q
VIRONE Q:(LRES["CANC")!(LRES["canc")
 Q:(DTRC["CANC")!(DTRC["canc")
 S DTR1=$E(DTAA,1,3),DTR2=$E(DTAA,9,12),DTRD=DTR1_","_DTR2,DTAA=$E(DTAA,1,12),LDO=$E(LDT,1,7)
 I LGN="VIRAL LOAD" Q:DNAM'=LDAT  D STORE
 Q
CHRG ; Update CD4
 Q:(LRES["CANC")!(LRES["canc")
 Q:(LRES["COMMENT")!(LRES["comment")
 Q:LRES=""
 Q:(DTRC["CANC")!(DTRC["canc")
 S IMRXCAT=$P($G(^IMR(158,IMRFN,0)),U,42)
 I $D(IMRXCAT),LRES>199,LRES<500,IMRXCAT=1 S $P(^IMR(158,IMRFN,0),U,42)=2,$P(^IMR(158,IMRFN,0),U,44)=DT
 I $D(IMRXCAT),LRES<200,IMRXCAT=1 S $P(^IMR(158,IMRFN,0),U,42)=3,$P(^IMR(158,IMRFN,0),U,35)=DT
 I $D(IMRXCAT),LRES<200,IMRXCAT=2 S $P(^IMR(158,IMRFN,0),U,42)=3,$P(^IMR(158,IMRFN,0),U,35)=DT
 S LCDD=$P($G(^IMR(158,IMRFN,102)),U,2)
 S:(LCDD'="")&(DTRC>LCDD) $P(^IMR(158,IMRFN,102),U,2)=DTRC,$P(^IMR(158,IMRFN,102),U,1)=LRES
 S:LCDD="" $P(^IMR(158,IMRFN,102),U,2)=DTRC,$P(^IMR(158,IMRFN,102),U,1)=LRES
 Q
 ; lowest CD4 value and date
CHLV Q:(LRES["CANC")!(LRES["canc")
 Q:(LRES["COMMENT")!(LRES["comment")
 Q:LRES=""
 Q:(DTRC["CANC")!(DTRC["canc")
 S PLOW=$P($G(^IMR(158,IMRFN,102)),U,5) ;get lowest CD4 value in File 158 for comparison
 S:(PLOW'="")&(LRES<PLOW) $P(^IMR(158,IMRFN,102),U,5)=LRES,$P(^IMR(158,IMRFN,102),U,6)=DTRC
 S:PLOW="" $P(^IMR(158,IMRFN,102),U,5)=LRES,$P(^IMR(158,IMRFN,102),U,6)=DTRC
 Q
CHPR ; Update CD4 Percentage & Current Category if Applicable
 Q:(LRES["CANC")!(LRES["canc")
 Q:(LRES["COMMENT")!(LRES["comment")
 Q:LRES=""
 Q:(DTRC["CANC")!(DTRC["canc")
 S IMRXCAT=$P($G(^IMR(158,IMRFN,0)),U,42)
 I $D(IMRXCAT),LRES<14,IMRXCAT=1 S $P(^IMR(158,IMRFN,0),U,42)=3,$P(^IMR(158,IMRFN,0),U,35)=DT
 I $D(IMRXCAT),LRES<14,IMRXCAT=2 S $P(^IMR(158,IMRFN,0),U,42)=3,$P(^IMR(158,IMRFN,0),U,35)=DT
 S LOWP=$P($G(^IMR(158,IMRFN,112)),U,9)
 S:(LOWP'="")&(LRES<LOWP) $P(^IMR(158,IMRFN,112),U,9)=LRES,$P(^IMR(158,IMRFN,112),U,10)=DTRC
 S:LOWP="" $P(^IMR(158,IMRFN,112),U,9)=LRES,$P(^IMR(158,IMRFN,112),U,10)=DTRC
 Q
STORE ; Store viral load data in 113 nodes
 Q:(LRES["CANC")!(LRES["canc")
 Q:(LRES["COMMENT")!(LRES["comment")
 Q:LRES=""
 S:LRES["C" LRES=$P(LRES,"C",1)
 S:LRES["c" LRES=$P(LRES,"c",1)
 S:LRES["<" LRES=$P(LRES,"<",2)
 S:LRES[">" LRES=$P(LRES,">",2)
 S:LRES["(" LRES=$P(LRES,"(",1)
 Q:(DTRC["CANC")!(DTRC["canc")
 Q:$G(IMLO)=""  S IMRLTEST=IMLO
 I '$D(^IMR(158,IMRFN,113,"B",IMRLTEST)) D  ;add viral test results if that test not in field 113
 .K DA,DD,DO
 .S:'$D(^IMR(158,IMRFN,113,0)) DIC("P")=$P(^DD(158,122,0),U,2)
 .S X=IMRLTEST,DA(1)=IMRFN,DIC="^IMR(158,IMRFN,113,",DIC(0)="L"
 .D FILE^DICN
 .K DD,DO
 .Q:Y'>0
 .S DA=+Y
 .S DA(1)=IMRFN,DIE="^IMR(158,IMRFN,113,",DR="1////"_LRES_";2////"_DTRC_";3////"_LRES_";4////"_DTRC
 .D ^DIE
 .Q
 S IMRVLIEN=$O(^IMR(158,IMRFN,113,"B",IMRLTEST,0)) Q:'IMRVLIEN
 S IMRLNODE=$G(^IMR(158,IMRFN,113,IMRVLIEN,0)) Q:IMRLNODE=""
 I LRES>$P(IMRLNODE,U,2) S $P(IMRLNODE,U,2)=LRES,$P(IMRLNODE,U,3)=DTRC ;save the highest score and its date/time
 I DTRC>$P(IMRLNODE,U,5) S $P(IMRLNODE,U,4)=LRES,$P(IMRLNODE,U,5)=DTRC ;save most recent date/time and its value
 S ^IMR(158,IMRFN,113,IMRVLIEN,0)=IMRLNODE ;update global node
 Q
KILL ; kill variables
 K CDAR,CDP,HVR,IMDATE,IMRCD4,IMRCD4D,IMRCD4E,IMRCD4X,IMRCDX,IMRCDXD,IMREDIT,IMRESULT,IMRLOOP,IMRPN,X,Y
 K IMRLNODE,IMRLTEST,IMRP103,IMRSTN,IMRTSTI,IMRTSTII,IMRVLIEN,IMRXCAT,LCDD,LLOC,MDT,PLOW,RC
 Q
