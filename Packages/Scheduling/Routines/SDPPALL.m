SDPPALL ;ALB/CAW - Patient Profile - All ; 29 Jun 99  04:11PM
 ;;5.3;Scheduling;**6,41,177**;AUG 13, 1993
 ;
 ;
EN ;
 K ^TMP("SDPPALL",$J)
 S VALMBCK=""
 W ! D WAIT^DICD,EN^VALM("SDPP PATIENT PROFILE ALL")
 S VALMBCK="R"
 Q
 ;
INIT ;
 S SDLN=0,SDLN1=1,$P(SDASH,"-",IOM+1)=""
INIT1 I SDTYP=2!(SDTYP=6)!($D(SDTYP(2))) D
 .D SET^SDPPAPP1("                        *** Appointments ***") D:'SDPRINT CNTRL^VALM10(SDLN,25,20,IOINHI,IOINORM)
 .D EN1^SDPPAPP1 ; Appointments
 .I SDLN1=SDLN D SET^SDPPADD1(""),SET^SDPPADD1("     No appointments found.")
 I SDTYP=1!(SDTYP=6)!($D(SDTYP(1))) D
 .D SET^SDPPADD1("                          *** Add/Edits ***") D:'SDPRINT CNTRL^VALM10(SDLN,27,17,IOINHI,IOINORM)
 .S SDLN1=SDLN
 .D EN1^SDPPADD1 ; Add/Edits
 .I SDLN1=SDLN D SET^SDPPADD1(""),SET^SDPPADD1("     No add/edits found.")
 I SDTYP=4!(SDTYP=6)!($D(SDTYP(4))) D
 .D SET^SDPPENR1("                         *** Enrollments ***") D:'SDPRINT CNTRL^VALM10(SDLN,26,19,IOINHI,IOINORM)
 .S SDLN1=SDLN
 .D EN1^SDPPENR1 ; Enrollments
 .I SDLN1=SDLN D SET^SDPPENR1(""),SET^SDPPENR1("  No enrollments found.")
 I SDTYP=5!(SDTYP=6)!($D(SDTYP(5))) D
 .D SET^SDPPMT1("                      *** Means Test Info ***") D:'SDPRINT CNTRL^VALM10(SDLN,23,23,IOINHI,IOINORM)
 .S SDLN1=SDLN
 .D EN1^SDPPMT1 ;  Means test Info
 .I SDLN1=SDLN D SET^SDPPMT1(""),SET^SDPPMT1("  No Means Test Info found.")
 I SDTYP=3!(SDTYP=6)!($D(SDTYP(3))) D
 .D SET^SDPPDIS1("                        *** Dispositions ***") D:'SDPRINT CNTRL^VALM10(SDLN,25,20,IOINHI,IOINORM)
 .S SDLN1=SDLN
 .D EN1^SDPPDIS1 ; Dispositions
 .I SDLN1=SDLN D SET^SDPPDIS1(""),SET^SDPPDIS1("  No dispositions found.")
 I SDTYP=7!(SDTYP=6)!($D(SDTYP(7))) D
 .D:'SDPRINT CNTRL^VALM10(SDLN,20,35,IOINHI,IOINORM)
 .S SDLN1=SDLN
 .D TDATA^SDPPTEM(DFN,.VALMCNT)
 D:'SDPRINT HDR^SDPP
 Q
 ;
QUIT ;
 S VALMBCK="R"
 K BEGDATE,ENDDATE,DIC,DGINC,DGINR,DGREL,SD,SDASH,SDATA,SDDT,SDFLG,SDFST,SDIFN,SDJ,SDL,SDLD,SDPR,SDSEC,X,SD1,SDACT,SDAP,SDBEN,SDBENE,SDCAR,SDCARE,SDLN1
 K SDALL,SDCI,SDCL,SDDIS,SDFLN,SDLEN,SDLN,SDM,SDM1,SDMT,SDOB,SDPDATA,SDPOV,SDPV,SDSC,SDSEC,SDSTAT,SDAP,SDWHEN,SDSTATUS,SDVER,SDYN,SDOPT,SDACT,SDMT1,^TMP("SDPPALL",$J)
 Q
