YTMATE ;SLC/DKG-TEST PKG: MATE TEST ; 10/20/88  09:02 ;
 ;;5.01;MENTAL HEALTH;;Dec 30, 1994
 ;
 S X=^YTD(601.2,YSDFN,1,YSET,1,YSED,1) F I=1:1:10 D FSCR
 S YSRR="" F I=1:1:10 S YSRR=YSRR_R(I)_"^"
 D DTA^YTREPT W !!?28,$P(^YTT(601,YSTEST,"P"),U),!!!?28,"S E L F",?49,"M A T E",!!?24,"INC   CON   AFF      INC   CON   AFF"
 W !!?11,"BEHAVIOR",$J(R(1),7,0),$J(R(3),6,0),$J(R(5),6,0),$J(R(6),9,0),$J(R(8),6,0),$J(R(10),6,0)
 W !?11,"FEELINGS",$J(R(2),7,0),$J(R(4),6,0),$J(R(7),15,0),$J(R(9),6,0)
 ;W !!!?25,"--- ITEM RESPONSES ---",!! S YSIT=1,YSLFT=0 F I=1:1:9 D:IOST?1"C-".E WAIT:$Y>(IOSL-4) Q:YSLFT  D RLN
 K I,YSIT,YSIX,J,YSKK,R,YSRR,X,Y Q
FSCR ;
 S R(I)=0,J=I S:I>5 J=J-5 S Y=^YTT(601,YSTEST,"S",J,"K",1,0) F J=1:2:17 S YSIT=$P(Y,U,J),YSIX=$P(Y,U,J+1) S:I>5 YSIT=YSIT+45 S:YSIX[$E(X,YSIT) R(I)=R(I)+1
 Q
RLN ;
 W ?1 F YSKK=1:1:10 W $J(YSIT,3,0)," ",$E(X,YSIT),"  " S YSIT=YSIT+1
 W ! Q
WAIT ;
 ;  Added 5/6/94 LJA
 N A,B,B1,C,D,E,E1,F,F1,G,G1,H,I,J,J1,J2,J3,J4,K,L,L1,L2,M,N
 N N1,N2,N3,N4,P,P0,P1,P3,R,R1,S,S1,T,T1,T2,TT,V,V1,V2,V3
 N V4,V5,V6,W,X,X0,X1,X2,X3,X4,X7,X8,X9,Y,Y1,Y2,Z,Z1,Z3
 ;
 F I0=1:1:(IOSL-$Y-2) W !
 N DTOUT,DUOUT,DIRUT
 S DIR(0)="E" D ^DIR K DIR S YSTOUT=$D(DTOUT),YSUOUT=$D(DUOUT),YSLFT=$D(DIRUT)
 W @IOF Q
