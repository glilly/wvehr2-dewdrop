NURA6E1 ;HIRMFO/JH/MD,FT-CATEGORY, GENDER REPORT BY SERVICE ;8/8/96  13:23
 ;;4.0;NURSING SERVICE;**13**;Apr 25, 1997
CAT D HSKEEP G QUIT:$G(NUROUT)
 D EN3^NURSAGSP G QUIT:$G(NUROUT)
 D EN1^NURSAGP0 G QUIT:$G(NUROUT)
 D DEV G:POP!($D(ZTSK)) QUIT
START ;
 K ^TMP($J),^TMP("NURA",$J) U IO S (NURPAGE,NURSW1,NURQUIT,NMCNT,NFCNT)=0,NURSSP="",$P(NURSSP," ",132)=" "
 S NURSTIL="GENDER BY SERVICE CATEGORY",NURSTIL1=$E(NURSSP,1,10)_"SERVICE",NURSTIL2="NO.       CATEGORY     NAME"_$E(NURSSP,1,22)_"GENDER"
 S NURROU=13,NRPT=4,NURSLEV=3 W ! D EN1^NURAAGS0,CHK G:$G(NUROUT) QUIT
 D PRINT^NURAGEN G:$G(NX)="^" QUIT
 S NURSORT(1)=0,NURSORT(2)=2,NURSORT(3)="NL1",NURSORT(4)="NL(1)" D GENDER^NURAGEN2,TOTALS
 G QUIT
POS ;POSITION, GENDER REPORT BY SVC.
 D HSKEEP G QUIT:$G(NUROUT)
 W ! D EN2^NURSAGSP G QUIT:$G(NUROUT)
 D EN1^NURSAGP0 G QUIT:$G(NUROUT)
 D DEV G:POP!($D(ZTSK)) QUIT
START1 ;
 K ^TMP($J),^TMP("NURA",$J) U IO S (NMCNT,NFCNT,NURPAGE,NURSW1,NURQUIT)=0,NURSSP="",$P(NURSSP," ",132)=" "
 S NURSTIL="GENDER BY SERVICE POSITION",NURSTIL1=$E(NURSSP,1,10)_"SERVICE",NURSTIL2="NO.       POSITION     NAME"_$E(NURSSP,1,22)_"GENDER"
 S NURROU=14,NRPT=4,NURSLEV=4 W ! D EN2^NURAAGS0,CHK G:$G(NUROUT) QUIT
 D PRINT^NURAGEN G:$G(NX)="^" QUIT
 S NURSORT(1)=0,NURSORT(2)=2,NURSORT(4)="NL(2)",NURSORT(3)="NL1(1)" D GENDER^NURAGEN2,TOTALS
 G QUIT
DEV ;
 S ZTDESC=$S(ANS2=1:"Gender Report by Service Category",1:"Gender Report by Service Position")
 W ! S ZTRTN=$S(ANS2=1:"START",1:"START1")_"^NURA6E1" D EN7^NURSUT0
 Q
QUIT K ^TMP("NURA",$J),^TMP($J) D CLOSE^NURSUT1,^NURAKILL
 Q
CHK D NODATA^NURAGEN
 Q
HSKEEP ;
 D EN1^NURSAUTL Q:$G(NUROUT)
 I NURMDSW D EN12^NURSAGSP Q:$G(NUROUT)  S DIC(0)="AEQZ",NURPLSCR=0 D EN5^NURSAGSP Q:$G(NUROUT)
 I NURMDSW=0,NURPLSW=1 S NURPLSCR=0 D PRD^NURSAGSP K NURPLSCR
 Q
TOTALS ;
 I $Y>(IOSL-6) D HEADER^NURAGEN
 W !,?47,$$REPEAT^XLFSTR("-",33)
 D EN4^NURAED3 W !!,"TOTAL NUMBER OF ASSIGNMENTS ",?47,$J(NTCT,4,0),!!,"TOTAL NUMBER OF MALES ",?47,$J(NMCNT,4,0),!!,"TOTAL NUMBER OF FEMALES ",?47,$J(NFCNT,4,0),!!,"TOTAL NUMBER OF PERSONNEL",?47,$J(NTOT,4,0)
 Q
