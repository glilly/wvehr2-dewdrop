FSCRPCGI ;SLC/STAFF-NOIS RPC Install Info ;1/11/98  15:24
 ;;1.1;NOIS;;Sep 06, 1998
 ;
MOD(MOD,DATE) ; from FSCRPCG
 N CNT,DAY,LINE,NUM,NUM1,PACKAGE
 S PACKAGE=+$P($G(^FSC("MOD",+$G(MOD),0)),U,8) I 'PACKAGE Q
 S DATE=+$G(DATE) I 'DATE Q
 S CNT=0
 S DAY="" F  S DAY=$O(^FSCD("STU MSG","APD",PACKAGE,DAY),-1) Q:DAY=""  Q:DAY<DATE  D
 .S NUM="" F  S NUM=$O(^FSCD("STU MSG","APD",PACKAGE,DAY,NUM),-1) Q:NUM=""  D
 ..S CNT=CNT+1,^TMP("FSCRPC",$J,"OUTPUT",CNT)=""
 ..S NUM1=0 F  S NUM1=$O(^FSCD("STU MSG",NUM,1,NUM1)) Q:NUM1<1  S LINE=$G(^(NUM1,0)) D
 ...S CNT=CNT+1,^TMP("FSCRPC",$J,"OUTPUT",CNT)=LINE
 I 'CNT S ^TMP("FSCRPC",$J,"OUTPUT",1)="      No installs in this date range"
 Q
 ;
SITE(SITE,DATE) ; from FSCRPCG
 N CNT,DAY,LINE,NUM,NUM1
 S SITE=+$G(SITE) I 'SITE Q
 S DATE=+$G(DATE) I 'DATE Q
 S CNT=0
 S DAY="" F  S DAY=$O(^FSCD("STU MSG","ASD",SITE,DAY),-1) Q:DAY=""  Q:DAY<DATE  D
 .S NUM="" F  S NUM=$O(^FSCD("STU MSG","ASD",SITE,DAY,NUM),-1) Q:NUM=""  D
 ..S CNT=CNT+1,^TMP("FSCRPC",$J,"OUTPUT",CNT)=""
 ..S NUM1=0 F  S NUM1=$O(^FSCD("STU MSG",NUM,1,NUM1)) Q:NUM1<1  S LINE=$G(^(NUM1,0)) D
 ...S CNT=CNT+1,^TMP("FSCRPC",$J,"OUTPUT",CNT)=LINE
 I 'CNT S ^TMP("FSCRPC",$J,"OUTPUT",1)="      No installs in this date range"
 Q
