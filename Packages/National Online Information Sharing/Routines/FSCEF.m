FSCEF ;SLC/STAFF-NOIS List Edit File ;1/13/98  11:25
 ;;1.1;NOIS;;Sep 06, 1998
 ;
NOTE(CALL,TYPE) ; from FSCEB, FSCEDC, FSCEL, FSCEN, FSCLMPES
 N CNT,LINE,NUM
 S NUM=$P(^FSCD("CALL",CALL,120),U,7)+1,$P(^(120),U,7)=NUM
 S LINE="("_NUM_") "_$$FMTE^XLFDT($$NOW^XLFDT),LINE=$$SETSTR^VALM1($$VALUE^FSCGET(DUZ,7107.1,1),LINE,35,$L(LINE))
 L +^FSCD("CALL",CALL,50):30 I '$T Q  ; *** needs ok
 I '$D(^FSCD("CALL",CALL,50,0)) S ^(0)="^^0^0^"_DT_U
 S CNT=1+$O(^FSCD("CALL",CALL,50,"A"),-1)
 S $P(^FSCD("CALL",CALL,120),U,6)=CNT
 S ^FSCD("CALL",CALL,50,CNT,0)=LINE
 S LINE=0 F  S LINE=$O(^TMP("FSC TEXT",$J,TYPE,LINE)) Q:LINE<1  S CNT=CNT+1,^FSCD("CALL",CALL,50,CNT,0)=^(LINE,0)
 S CNT=CNT+1,^FSCD("CALL",CALL,50,CNT,0)=""
 S $P(^FSCD("CALL",CALL,50,0),U,3,4)=CNT_U_CNT
 L -^FSCD("CALL",CALL,50)
 D MRE^FSCMR(DUZ,CALL)
 Q
 ;
CLOSE(CALL,TYPE,CDATE,FUNC,TASK) ; from FSCEB, FSCEC, FSCEDC
 N CNT,DA,DIE,DR,LINE
 S DA=CALL,DIE="^FSCD(""CALL"",",DR="81///`"_DUZ_";82///"_CDATE_";8///`"_FUNC_";9///`"_TASK
 L +^FSCD("CALL",CALL):30 I '$T Q  ; *** needs ok
 D ^DIE
 S (CNT,LINE)=0 F  S LINE=$O(^TMP("FSC TEXT",$J,TYPE,LINE)) Q:LINE<1  S CNT=CNT+1,^FSCD("CALL",CALL,80,CNT,0)=^(LINE,0)
 S ^FSCD("CALL",CALL,80,0)="^^"_CNT_U_CNT_U_DT_U
 L -^FSCD("CALL",CALL)
 D MRE^FSCMR(DUZ,CALL)
 Q
