SCCVCDS1 ; ALB/TMP - SCHED VSTS CONV - SCREEN BUILD ; 25-NOV-97
 ;;5.3;Scheduling;**211**;Aug 13, 1993
 ;
BLD ; -- build selected template display
 N SCCV0,SCCV1,SCCV2,SCCVLRQ,SCCVZ,SCCVZ1
 S VALMBG=1,SCCVSCRN=2
 Q:'$D(SCCVDA)
 K ^TMP("SCCV.CST.EDIT",$J),^TMP("SCCV.CST.EDIT.DX",$J)
 S VALMCNT=0
 S SCCV0=$G(^SD(404.98,SCCVDA,0)),SCCV1=$G(^(1)),SCCV2=$G(^(2))
 S SCCVLRQ=+$$LSTREQ^SCCVLOG(SCCVDA),SCCVLRQ=$G(^SD(404.98,SCCVDA,"R",SCCVLRQ,0))
 S VALMCNT=VALMCNT+1
 S SCCVZ="Conversion Template Date Range:"
 D SET^VALM10(VALMCNT,SCCVZ),REV^SCCVDSP2(SCCVZ,VALMCNT,1)
 S VALMCNT=VALMCNT+1
 D SET^VALM10(VALMCNT,"Start Date: "_$$FMTE^XLFDT($P(SCCV0,U,3),"1D"))
 S VALMCNT=VALMCNT+1
 D SET^VALM10(VALMCNT,"End Date  : "_$$FMTE^XLFDT($P(SCCV0,U,4),"1D"))
 S VALMCNT=VALMCNT+1
 D SET^VALM10(VALMCNT," ")
 I $P(SCCVLRQ,U,3) D
 .S VALMCNT=VALMCNT+1
 .S SCCVZ="Conversion Results:"
 .D SET^VALM10(VALMCNT,SCCVZ),REV^SCCVDSP2(SCCVZ,VALMCNT,1)
 .S VALMCNT=VALMCNT+1
 .D SET^VALM10(VALMCNT,$$LAST($P(SCCV1,U)))
 .S VALMCNT=VALMCNT+1
 .D SET^VALM10(VALMCNT,"# Converted    : "_+$P(SCCV1,U,2))
 .S VALMCNT=VALMCNT+1
 .D SET^VALM10(VALMCNT,"# Not Converted: "_+$P(SCCV2,U,6))
 .S VALMCNT=VALMCNT+1
 .D SET^VALM10(VALMCNT,"# Errors Logged: "_+$P(SCCV1,U,5))
 I '$P(SCCVLRQ,U,3) D
 .S VALMCNT=VALMCNT+1
 .S SCCVZ="Estimate Results:"
 .D SET^VALM10(VALMCNT,SCCVZ),REV^SCCVDSP2(SCCVZ,VALMCNT,1)
 .S VALMCNT=VALMCNT+1
 .D SET^VALM10(VALMCNT,$E("# Encounters to be added  : "_+$P(SCCV2,U,7)_$J("",39),1,39)_"# Add/Edits Found   : "_+$P(SCCV2,U))
 .S VALMCNT=VALMCNT+1
 .D SET^VALM10(VALMCNT,$E("# Visits to be added      : "_+$P(SCCV2,U,8)_$J("",39),1,39)_"# Ancillaries Found : "_+$P(SCCV2,U,2))
 .S VALMCNT=VALMCNT+1
 .D SET^VALM10(VALMCNT,$E("# V PROVIDERs to be added : "_+$P(SCCV2,U,9)_$J("",39),1,39)_"# Appointments Found: "_+$P(SCCV2,U,3))
 .S VALMCNT=VALMCNT+1
 .D SET^VALM10(VALMCNT,$E("# V POVs to be added      : "_+$P(SCCV2,U,10)_$J("",39),1,39)_"# Credit Stops Found: "_+$P(SCCV2,U,4))
 .S VALMCNT=VALMCNT+1
 .D SET^VALM10(VALMCNT,$E("# V CPTs to be added      : "_+$P(SCCV2,U,11)_$J("",39),1,39)_"# Dispositions Found: "_+$P(SCCV2,U,5))
 S VALMCNT=VALMCNT+1
 D SET^VALM10(VALMCNT," ")
 S VALMCNT=VALMCNT+1
 S SCCVZ="Last Event:",SCCVZ1="Last Action Request:"
 D SET^VALM10(VALMCNT,$E(SCCVZ_$J("",39),1,39)_SCCVZ1),REV^SCCVDSP2(SCCVZ,VALMCNT,1),REV^SCCVDSP2(SCCVZ1,VALMCNT,40)
 S VALMCNT=VALMCNT+1
 D SET^VALM10(VALMCNT,$E("Event : "_$$EXPAND^SCCVDSP2(404.98,.05,$P(SCCV0,U,5))_$J("",39),1,39)_"Action       : "_$$NONE^SCCVDSP2(404.98,.08,$P(SCCV0,U,8),"<No action requested>"))
 S VALMCNT=VALMCNT+1
 D SET^VALM10(VALMCNT,$E("Status: "_$$EXPAND^SCCVDSP2(404.98,.07,$P(SCCV0,U,7))_$J("",39),1,39)_"Start Date/Tm: "_$$FMTE^XLFDT($P(SCCVLRQ,U,4)))
 S VALMCNT=VALMCNT+1
 D SET^VALM10(VALMCNT,$J("",39)_"Stop Date/Tm : "_$$FMTE^XLFDT($P(SCCVLRQ,U,5)))
 S VALMCNT=VALMCNT+1
 D SET^VALM10(VALMCNT,$E("Bulletin Upon Completion: "_$$EXPAND^SCCVDSP2(404.98,.06,+$P(SCCV0,U,6))_$J("",39),1,39)_$E("Task #       : "_$P(SCCV1,U,3)_$J("",28),1,28)_"Volume: "_$P(SCCV1,U,4))
 I '$P(SCCVLRQ,U,3) D
 .N SCGRAPH,SCCT,Z
 .S SCCT=$$DSPGLBL^SCCVEGD1(SCCVDA,"SCGRAPH",0)
 .I SCCT D
 ..S VALMCNT=VALMCNT+1
 ..D SET^VALM10(VALMCNT," ")
 ..S Z=0 F  S Z=$O(SCGRAPH(Z)) Q:'Z  I $D(SCGRAPH(Z,0)) D
 ...S VALMCNT=VALMCNT+1
 ...D SET^VALM10(VALMCNT,$J("",10)_SCGRAPH(Z,0))
 Q
 ;
HDR ;Template screen header
 N SCCV0,X
 S SCCV0=$G(^SD(404.98,SCCVDA,0))
 S X="List #: "_+$O(VALMY(0))_"  "
 S X=X_$$FMTE^XLFDT($P(SCCV0,U,3),"1D")_" - "_$$FMTE^XLFDT($P(SCCV0,U,4),"1D")_"  "
 S X=X_$$EXPAND^SCCVDSP2(404.98,.05,$P(SCCV0,U,5))_"  "_$$EXPAND^SCCVDSP2(404.98,.07,$P(SCCV0,U,7))_"  "
 S X=X_"Template #: "_$P(SCCV0,U)
 S VALMHDR(1)=X
 Q
 ;
LAST(SCOE) ; -- build last encounter info string
 N SCOE0,SCORG,X
 S SCOE0=$G(^SCE(+SCOE,0)),SCORG=$P(SCOE0,U,8)
 S X="Last ^SCE Entry: "_SCOE
 S X=X_"    DFN: "_$P(SCOE0,U,2)
 S X=X_"   Type: "_$S(SCORG=1:"Appointment",SCORG=2:"Stop Code Addition",SCORG=3:"Disposition",SCORG=4:"Credit Stop",1:"")
 Q X
