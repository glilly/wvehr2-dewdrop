OCXOCMP7 ;SLC/RJS,CLA - ORDER CHECK CODE COMPILER (Build Local Extrinsic Functions) ;10/29/98  12:37
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**32**;Dec 17,1997
 ;;  ;;ORDER CHECK EXPERT version 1.01 released OCT 29,1998
 ;
 ;
EN(OCXFUNC,OCXCODE) ;
 ;
 N OCXLN,OCXTXT,OCXRTN,OCXRLN,OCXTR,OCXFLN,OCXRNAM
 S OCXRTN=$O(^OCXS(860.8,"C",OCXFUNC,0)) Q:'OCXRTN
 S OCXCODE=$G(OCXCODE),OCXFLN=$O(OCXCODE(""),-1)+1,OCXRNAM=$P($G(^OCXS(860.8,+OCXRTN,0)),U,1)
 ;
 S OCXLN=0 F  S OCXLN=$O(^OCXS(860.8,OCXRTN,"CODE",OCXLN)) Q:'OCXLN  D
 .S OCXTXT=$G(^(OCXLN,0))
 .S OCXRLN=$P(OCXTXT,";",2,999)
 .S OCXTR=$P(OCXTXT,";",1)
 .S:'$L(OCXRLN) OCXRLN=" ;"
 .;
 .F  Q:'(OCXRLN["%%%%")  S OCXRLN=$P(OCXRLN,"""%%%%""",1)_"||LNTAG||"_$P(OCXRLN,"""%%%%""",2,999)
 .;
 .I '(OCXTR["T"),'(OCXTR["L") S OCXCODE=OCXCODE+$L(OCXRLN),OCXCODE($O(OCXCODE(""),-1)+1)=OCXRLN Q
 .;
 .I (OCXTR["T"),(OCXTR["-"),'$G(OCXTRACE) S OCXCODE=OCXCODE+$L(OCXRLN),OCXCODE($O(OCXCODE(""),-1)+1)=OCXRLN Q
 .I (OCXTR["T"),(OCXTR["+"),$G(OCXTRACE) S OCXCODE=OCXCODE+$L(OCXRLN),OCXCODE($O(OCXCODE(""),-1)+1)=OCXRLN Q
 .;
 .I (OCXTR["L"),(OCXTR["-"),'$G(OCXTLOG) S OCXCODE=OCXCODE+$L(OCXRLN),OCXCODE($O(OCXCODE(""),-1)+1)=OCXRLN Q
 .I (OCXTR["L"),(OCXTR["+"),$G(OCXTLOG) S OCXCODE=OCXCODE+$L(OCXRLN),OCXCODE($O(OCXCODE(""),-1)+1)=OCXRLN Q
 ;
 I $L(OCXRNAM),$D(OCXCODE(OCXFLN)) S OCXCODE(OCXFLN)=OCXCODE(OCXFLN)_" ; "_OCXRNAM
 ;
 Q
 ;
METACODE(OCXFUNC,OCXCODE) ;
 ;
 N OCXELE,INDEX
 S OCXELE=+$P(OCXFUNC,"E",2) Q:'OCXELE
 D FILE(OCXFUNC_"() ;  Verify Event/Element: '"_$P($G(^OCXS(860.3,+OCXELE,0)),U,1)_"'")
 D FILE(" ;")
 S INDEX1=$O(^TMP("OCXCMP",$J,"METACODE","B",OCXELE,0))
 I INDEX1 S INDEX2=0 F  S INDEX2=$O(^TMP("OCXCMP",$J,"METACODE",INDEX1,INDEX2)) Q:'INDEX2  D FILE(" "_^(INDEX2))
 D FILE(" ;")
 Q
 ;
FILE(X) ;
 S OCXCODE($O(OCXCODE(999999),-1)+1)=X,OCXCODE=$G(OCXCODE)+$L(X)
 Q
 ;
DFLIST(OCXELE) ;
 ;
 N LIST,DF
 S LIST="",DF=0 F  S DF=$O(^TMP("OCXCMP",$J,"ELEMENT",OCXELE,"DATA",DF)) Q:'DF  S:$L(LIST) LIST=LIST_"," S LIST=LIST_DF
 Q LIST
 ;
