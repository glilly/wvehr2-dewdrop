FSCQSW ;SLC/STAFF-NOIS Query Search Word-Processing ;8/8/98  22:10
 ;;1.1;NOIS;;Sep 06, 1998
 ;
CON ; from FSCQS
 I FIELD=59 D PCON Q
 N CALL,SUB,SUBNUM
 S SUB=$S(FIELD=12:30,FIELD=13:50,FIELD=43:110,FIELD=54:103,FIELD=14:80,1:0) Q:'SUB
 S VALUE=$$UP^XLFSTR(VALUE)
 I LIST=1!(LIST=0) D  Q
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..S SUBNUM=0 F  S SUBNUM=$O(^FSCD("CALL",CALL,SUB,SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))[VALUE X ACTION Q
 I LIST="" D  Q
 .S CALL=0 F  S CALL=$O(^FSCD("CALL",CALL)) Q:CALL<1  D
 ..S SUBNUM=0 F  S SUBNUM=$O(^FSCD("CALL",CALL,SUB,SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))[VALUE X ACTION Q
 Q
 ;
NCON ; from FSCQS
 I FIELD=59 D PNCON Q
 N CALL,OK,SUB,SUBNUM
 S SUB=$S(FIELD=12:30,FIELD=13:50,FIELD=43:110,FIELD=54:103,FIELD=14:80,1:0) Q:'SUB
 S VALUE=$$UP^XLFSTR(VALUE)
 I LIST=1!(LIST=0) D  Q
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..I '$O(^FSCD("CALL",CALL,SUB,0)) Q
 ..S (OK,SUBNUM)=0 F  S SUBNUM=$O(^FSCD("CALL",CALL,SUB,SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))[VALUE S OK=1 Q
 ..I 'OK X ACTION
 I LIST="" D  Q
 .S CALL=0 F  S CALL=$O(^FSCD("CALL",CALL)) Q:CALL<1  D
 ..I '$O(^FSCD("CALL",CALL,SUB,0)) Q
 ..S (OK,SUBNUM)=0 F  S SUBNUM=$O(^FSCD("CALL",CALL,SUB,SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))[VALUE S OK=1 Q
 ..I 'OK X ACTION
 Q
 ;
EX ; from FSCQS
 I FIELD=59 D PEX Q
 N CALL,SUB
 S SUB=$S(FIELD=12:30,FIELD=13:50,FIELD=43:110,FIELD=54:103,FIELD=14:80,1:0) Q:'SUB
 I LIST=1!(LIST=0) D  Q
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..I $O(^FSCD("CALL",CALL,SUB,0)) X ACTION
 I LIST="" D  Q
 .S CALL=0 F  S CALL=$O(^FSCD("CALL",CALL)) Q:CALL<1  I $O(^(CALL,SUB,0)) X ACTION
 Q
 ;
NEX ; from FSCQS
 I FIELD=59 D PNEX Q
 N CALL,SUB
 S SUB=$S(FIELD=12:30,FIELD=13:50,FIELD=43:110,FIELD=54:103,FIELD=14:80,1:0) Q:'SUB
 I LIST=1!(LIST=0) D  Q
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..I '$O(^FSCD("CALL",CALL,SUB,0)) X ACTION
 I LIST="" D  Q
 .S CALL=0 F  S CALL=$O(^FSCD("CALL",CALL)) Q:CALL<1  I '$O(^(CALL,SUB,0)) X ACTION
 Q
PCON ;
 N CALL,IEN,SUB,SUBNUM
 S SUB=2
 S VALUE=$$UP^XLFSTR(VALUE)
 I LIST=1!(LIST=0) D  Q
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..S IEN=+$G(^FSCD("CALL USER","AUC",DUZ,CALL)) I 'IEN Q
 ..S SUBNUM=0 F  S SUBNUM=$O(^FSCD("CALL USER",IEN,SUB,SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))[VALUE X ACTION Q
 I LIST="" D  Q
 .S CALL=0 F  S CALL=$O(^FSCD("CALL USER","AUC",DUZ,CALL)) Q:CALL<1  D
 ..S IEN=+$G(^FSCD("CALL USER","AUC",DUZ,CALL)) I 'IEN Q
 ..S SUBNUM=0 F  S SUBNUM=$O(^FSCD("CALL USER",IEN,SUB,SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))[VALUE X ACTION Q
 Q
 ;
PNCON ;
 N CALL,IEN,OK,SUB,SUBNUM
 S SUB=2
 S VALUE=$$UP^XLFSTR(VALUE)
 I LIST=1!(LIST=0) D  Q
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..S IEN=+$G(^FSCD("CALL USER","AUC",DUZ,CALL)) I 'IEN Q
 ..I '$O(^FSCD("CALL USER",IEN,SUB,0)) Q
 ..S (OK,SUBNUM)=0 F  S SUBNUM=$O(^FSCD("CALL USER",IEN,SUB,SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))[VALUE S OK=1 Q
 ..I 'OK X ACTION
 I LIST="" D  Q
 .S CALL=0 F  S CALL=$O(^FSCD("CALL USER","AUC",DUZ,CALL)) Q:CALL<1  D
 ..S IEN=+$G(^FSCD("CALL USER","AUC",DUZ,CALL)) I 'IEN Q
 ..I '$O(^FSCD("CALL USER",IEN,SUB,0)) Q
 ..S (OK,SUBNUM)=0 F  S SUBNUM=$O(^FSCD("CALL USER",IEN,SUB,SUBNUM)) Q:SUBNUM<1  I $$UP^XLFSTR($G(^(SUBNUM,0)))[VALUE S OK=1 Q
 ..I 'OK X ACTION
 Q
 ;
PEX ;
 N CALL,IEN,SUB
 S SUB=2
 I LIST=1!(LIST=0) D  Q
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..S IEN=+$G(^FSCD("CALL USER","AUC",DUZ,CALL)) I 'IEN Q
 ..I $O(^FSCD("CALL USER",IEN,SUB,0)) X ACTION
 I LIST="" D  Q
 .S CALL=0 F  S CALL=$O(^FSCD("CALL USER","AUC",DUZ,CALL)) Q:CALL<1  D
 ..S IEN=+$G(^FSCD("CALL USER","AUC",DUZ,CALL)) I 'IEN Q
 ..I $O(^FSCD("CALL USER",IEN,SUB,0)) X ACTION
 Q
 ;
PNEX ;
 N CALL,IEN,SUB
 S SUB=2
 I LIST=1!(LIST=0) D  Q
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..S IEN=+$G(^FSCD("CALL USER","AUC",DUZ,CALL))
 ..I '$O(^FSCD("CALL USER",IEN,SUB,0)) X ACTION
 I LIST="" D  Q
 .S CALL=0 F  S CALL=$O(^FSCD("CALL USER","AUC",DUZ,CALL)) Q:CALL<1  D
 ..S IEN=+$G(^FSCD("CALL USER","AUC",DUZ,CALL))
 ..I '$O(^FSCD("CALL USER",IEN,SUB,0)) X ACTION
 Q
