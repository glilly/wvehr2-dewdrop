LRDIST3 ;SLC/CJS - DATA DISTRIBUTION ; 2/22/87  1:53 PM ;
 ;;5.2;LAB SERVICE;;Sep 27, 1994
Q3 ;from LRDIST1
 S Y=^TMP("LR",$J,"X",LRII,1) D DD^LRX W !,Y W:LRSPEC=-1&(^TMP("LR",$J,"X",LRII,3)>0) "  ",$S($D(^LAB(61,^TMP("LR",$J,"X",LRII,3),0)):$P(^(0),U,1),1:"")
 S X=^TMP("LR",$J,"X",LRII,2) W !,?4,@$S(X'=""&$L(LRCHM(.1)):LRCHM(.1),1:"$J(X,LRCW)"),?13
 G:LRCTRL WX
 I ^TMP("LR",$J,"X",LRII,2)<LRLM1 W "*" Q
 I ^TMP("LR",$J,"X",LRII,2)>LRLM2 W ?76,"*" Q
 W ?(^TMP("LR",$J,"X",LRII,2)-LRLM1/LRSTS\1+15),"X"
 Q
WX S LRXF="X" I ^TMP("LR",$J,"X",LRII,2)<LRLM1F S LRFOOT=LRFOOT+1,LRFOOT(LRFOOT)=LRII,LRXF=LRFOOT
 I ^TMP("LR",$J,"X",LRII,2)>LRLM2F S LRFOOT=LRFOOT+1,LRFOOT(LRFOOT)=LRII,LRXF=LRFOOT
 IF (^TMP("LR",$J,"X",LRII,2)-LRLM1/LRSTS\1)<0 W:$L(LRFOOT)=1 " " W LRFOOT
 E  W "  "
 S LRTAB=^TMP("LR",$J,"X",LRII,2)-LRLM1/LRSTS\1+15 F I=15:1:74 W $S(I=LRTAB:LRXF,I=15:".",I=25:"|",I=35:".",I=45:"*",I=55:".",I=65:"|",1:" ") I I=LRTAB,$L(LRXF)'=1 S I=I+$L(LRXF)-1
 W $S(^TMP("LR",$J,"X",LRII,2)=LRLM2:"X",1:".") IF ^TMP("LR",$J,"X",LRII,2)>LRLM2 W ?76,LRFOOT
 Q
