PRCHES6 ;WISC/RHD/AKS-ESIG MAINTENANCE ROUTINE ;8/18/94  3:30 PM
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 ;ROUTINE FOR MAINTAINING SUBFIELD 7 (ELECTRONIC SIGNATURE)
 ; FIELD 50 (AMMENDMENT) FILE 442
DECODE(LEVEL0,LEVEL1) ;Extrinsic Function to return hashed electronic sig to readable form.
 ;returns "" if unsuccessful
 NEW RECORD,RECORD1,VERSION,PERSON,SIG,CHECKSUM
 S RECORD=$G(^PRC(442,LEVEL0,6,LEVEL1,0)) I RECORD="" Q ""
 S RECORD1=$G(^PRC(442,LEVEL0,6,LEVEL1,1))
 S VERSION=$P(RECORD,"^",9)
 S PERSON=+$P(RECORD1,"^")
 I VERSION'="",VERSION'=1 Q ""
 S SIG=$P(RECORD1,"^",2)
D1 ;decode e signature for version 1
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_LEVEL1_"^"_$$STRING(RECORD,RECORD1))
 Q $$DECODE^PRCUESIG(SIG,PERSON,CHECKSUM)
ENCODE(LEVEL0,LEVEL1,USERNUM,Y) ;Encode e signature for version 1 only
 NEW RECORD,RECORD1,SIGBLOCK,CHECKSUM,OLDUSER
 S USERNUM=+USERNUM
 I USERNUM=0 S Y=-3 Q  ;-3 no user number
 S SIGBLOCK=$P($G(^VA(200,USERNUM,20)),"^",2)
 I SIGBLOCK="" S Y=-2 Q  ;-2 no signature block
 S RECORD=$G(^PRC(442,LEVEL0,6,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,6,LEVEL1,1))
 I RECORD="" S Y=-1 Q  ;-1 no 2237 record
 I $P(RECORD1,"^",2)'="" S Y=-4 Q  ;-4 cannot re-sign record
 S OLDUSER=+$P(RECORD1,"^",1)
 I OLDUSER=0 S $P(RECORD1,"^",1)=USERNUM
 I OLDUSER>0 S USERNUM=OLDUSER
 S:$P(RECORD1,"^",3)="" $P(RECORD1,"^",3)=$$NOW^PRCUESIG
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_LEVEL1_"^"_$$STRING(RECORD,RECORD1))
 S $P(RECORD1,"^",2)=$$ENCODE^PRCUESIG(SIGBLOCK,USERNUM,CHECKSUM)
 S $P(RECORD,"^",9,10)="1^"_$$SUM^PRCUESIG(SIGBLOCK)
 S ^PRC(442,LEVEL0,6,LEVEL1,0)=RECORD
 S ^PRC(442,LEVEL0,6,LEVEL1,1)=RECORD1
 S Y=1 Q
RECODE(LEVEL0,LEVEL1,Y) ;Recode ESIG for version 1 only
 N RECORD,RECORD1,SIGBLOCK,CHECKSUM,USERNUM
 S RECORD=$G(^PRC(442,LEVEL0,6,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,6,LEVEL1,1))
 I RECORD=""!(RECORD1="") S Y=-1 Q  ;-1 no record
 S USERNUM=+$P(RECORD1,"^")
 I $P(RECORD1,"^",2)=""!(USERNUM=0) S Y=-4 Q  ;-4 cannot recode record
 S SIGBLOCK=$P($G(^VA(200,USERNUM,20)),"^",2)
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_LEVEL1_"^"_$$STRING(RECORD,RECORD1))
 S $P(RECORD1,"^",2)=$$ENCODE^PRCUESIG(SIGBLOCK,USERNUM,CHECKSUM)
 S $P(RECORD,"^",9,10)="1^"_$$SUM^PRCUESIG(SIGBLOCK)
 S ^PRC(442,LEVEL0,6,LEVEL1,0)=RECORD
 S ^PRC(442,LEVEL0,6,LEVEL1,1)=RECORD1
 S Y=1 ;1 record recoded properly
 Q
REMOVE(LEVEL0,LEVEL1) ;Entry point to remove e signature from record
 NEW I,RECORD,RECORD1
 S RECORD=$G(^PRC(442,LEVEL0,6,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,6,LEVEL1,1))
 F I=10 S $P(RECORD,"^",I)=""
 F I=1,2,3 S $P(RECORD1,"^",I)=""
 S ^PRC(442,LEVEL0,6,LEVEL1,0)=RECORD
 S ^PRC(442,LEVEL0,6,LEVEL1,1)=RECORD1
 Q
VERIFY(LEVEL0,LEVEL1)      ;extrinsic function to verify version 1 signature.  Returns 1 if valid
 NEW RECORD,RECORD1,VERSION,SIGBLOCK
 S RECORD=$G(^PRC(442,LEVEL0,6,LEVEL1,0))
 S VERSION=$P(RECORD,"^",9),SIGBLOCK=$P(RECORD,"^",10)
 I VERSION_SIGBLOCK="" Q 1
 Q ($$SUM^PRCUESIG($$DECODE(LEVEL0,LEVEL1))=SIGBLOCK)
STRING(X,X1)          ;Build String of critical fields
 Q $P(X,"^",1,4)_"^"_$P(X1,"^",3)
