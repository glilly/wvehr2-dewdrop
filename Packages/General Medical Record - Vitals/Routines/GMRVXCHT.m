GMRVXCHT ;HIRMFO/RM-CONVERSION TABLE FOR QUALIFIERS ;7/22/96
 ;;4.0;Vitals/Measurements;;Apr 25, 1997
BLDTABL ; THIS PROCEDURE WILL CREATE THE CONVERSION TABLE IN THE ^TMP($J
 ; ARRAY.
 ;    Output Variables:
 ;      ^TMP($J,"GMRVCHAR",CHAR)=VP
 ;      ^TMP($J,"GMRVCHAR",CHAR,TYPE)=CATEGORY
 ;      ^TMP($J,"GMRVCAT",CATEGORY,TYPE)=^MAX^^PORD^EORD^DEFCH
 ;      where CHAR=Qualifier name (free text)
 ;            TYPE=Vital Type (ptr to 120.51 file)
 ;            VP=Variable ptr address of this qualifier in
 ;               pre-converted system, i.e., 1;GMRD(120.52,
 ;            CATEGORY=Category of Qualifier (free text)
 ;            MAX=Maximum Entries (numeric)
 ;            PORD=Print Order (numeric)
 ;            EORD=Edit Order (numeric)
 ;            DEFCH=Default Qualifier (free text)
 ;
 N GMRVFILE,GMRVCAT,GMRVCHAR,GMRVLINE,GMRVP,GMRVPTR,GMRVTXT,GMRVTYP
 F GMRVTXT=1:1 S GMRVLINE=$P($T(CHRTABL+GMRVTXT),";;",2) Q:GMRVLINE=""  D
 .  S GMRVTYP=$P(GMRVLINE,";",2) Q:GMRVTYP=""
 .  S GMRVTYP=$O(^GMRD(120.51,"B",GMRVTYP,"")) Q:GMRVTYP'>0
 .  S GMRVCHAR=$P(GMRVLINE,";")
 .  Q:GMRVCHAR=""
 .  S GMRVP="" F GMRVFILE=120.52,120.53 D  Q:+GMRVP
 .  .  S GMRVPTR=$O(^GMRD(GMRVFILE,"B",GMRVCHAR,"")) Q:GMRVPTR'>0
 .  .  S GMRVP=GMRVPTR_";GMRD("_GMRVFILE_","
 .  .  Q
 .  S ^TMP($J,"GMRVCHAR",GMRVCHAR)=GMRVP
 .  S ^TMP($J,"GMRVCHAR",GMRVCHAR,GMRVTYP)=$P(GMRVLINE,";",3)
 .  Q
 F GMRVTXT=1:1 S GMRVLINE=$P($T(CATTABL+GMRVTXT),";;",2) Q:GMRVLINE=""  D
 .  S GMRVCAT=$P(GMRVLINE,";") Q:GMRVCAT=""
 .  S GMRVTYP=$P(GMRVLINE,";",2) Q:GMRVTYP=""
 .  S GMRVTYP=$O(^GMRD(120.51,"B",GMRVTYP,"")) Q:GMRVTYP'>0
 .  S ^TMP($J,"GMRVCAT",GMRVCAT,GMRVTYP)=$P(GMRVLINE,";",3)
 .  Q
 Q
CHRTABL ;;CHACTERISTIC;TYPE;CATEGORY
 ;;L ARM;BLOOD PRESSURE;LOCATION
 ;;L LEG;BLOOD PRESSURE;LOCATION
 ;;R ARM;BLOOD PRESSURE;LOCATION
 ;;R LEG;BLOOD PRESSURE;LOCATION
 ;;LYING;BLOOD PRESSURE;POSITION
 ;;SITTING;BLOOD PRESSURE;POSITION
 ;;STANDING;BLOOD PRESSURE;POSITION
 ;;APICAL;PULSE;LOCATION
 ;;BILATERAL PERIPHERALS;PULSE;LOCATION
 ;;CAROTID;PULSE;LOCATION
 ;;OTHER;PULSE;LOCATION
 ;;PERIPHERAL;PULSE;LOCATION
 ;;RADIAL;PULSE;LOCATION
 ;;ASSISTED VENTILATOR;RESPIRATION;METHOD
 ;;CONTROLLED VENTILATOR;RESPIRATION;METHOD
 ;;SPONTANEOUS;RESPIRATION;METHOD
 ;;AXILLARY;TEMPERATURE;LOCATION
 ;;CORE;TEMPERATURE;LOCATION
 ;;ORAL;TEMPERATURE;LOCATION
 ;;RECTAL;TEMPERATURE;LOCATION
 ;;SKIN;TEMPERATURE;LOCATION
 ;;TYMPANIC;TEMPERATURE;LOCATION
 ;;ACTUAL;WEIGHT;QUALITY
 ;;DRY;WEIGHT;QUALITY
 ;;ESTIMATED;WEIGHT;QUALITY
 Q
CATTABL ;;CATEGORY;TYPE;^MAX^^PRINT ORDER^EDIT ORDER^DEFAULT CHAR.
 ;;LOCATION;BLOOD PRESSURE;^1^^1^1
 ;;LOCATION;PULSE;^1^^2^1^RADIAL
 ;;LOCATION;TEMPERATURE;^1^^1^1^ORAL
 ;;METHOD;RESPIRATION;^1^^1^1^SPONTANEOUS
 ;;POSITION;BLOOD PRESSURE;^1^^2^2
 ;;QUALITY;WEIGHT;^1^^1^1^ACTUAL
 Q
