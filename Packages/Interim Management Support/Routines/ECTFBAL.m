ECTFBAL ;B'ham ISC/PTD-Fund Control Point Balance Sheet ;01/29/91 08:00
V ;;1.05;INTERIM MANAGEMENT SUPPORT;;
 I '$D(^PRC(420)) W *7,!!?29,"OPTION IS UNAVAILABLE!",!,"The 'Fund Control Point' File - #420 is not loaded on your system.",!! S XQUIT="" Q
 I '$O(^PRC(420,0)) W *7,!!,"'Fund Control Point' File - #420 has not been populated on your system.",!! S XQUIT="" Q
 S CNT=0 F J=0:0 S J=$O(^PRC(420,J)) Q:'J  S CNT=CNT+1
DIC I CNT>1 W !! S DIC="^PRC(420,",DIC(0)="QEANM",DIC("A")="Select STATION number: " D ^DIC G:Y<0 EXIT S STNUM=+Y
 I CNT=1 S STNUM=$O(^PRC(420,0))
YR W !,"You may select the fiscal year for this report.",!
 S %DT="AE",%DT("A")="Enter FISCAL YEAR: ",%DT(0)=2850000 D ^%DT K %DT G:$D(DTOUT)!("^"[X) EXIT S YRDA=$E(Y,1,3),YR=$E(Y,2,3)
QTR S CYR=$E(DT,1,3),CFY=$E(DT,4)+CYR,QTR=$E(222333444111,+$E(DT,4,5)) I YRDA<CFY S QTR=4
 I YRDA>CFY W *7,!!?10,"FUTURE fiscal years NOT allowed.",! K YRDA,YR,CYR,CFY,CMO G YR
DIP S DIC="^PRC(420,",BY="@.01,1;S,.01,1,15,@.01",(FR,TO)=STNUM_",,"_YR,L=0
 I QTR=1 S FLDS="1,.01,.5,15,1;L10;C35;""1QTR UNCOM BAL"",5;L10;C50;""1QTR UNOBL BAL"",9;L10;C65;""1QTR SCP UNCOM BAL"""
 I QTR=2 S FLDS="1,.01,.5,15,2;L10;C35;""2QTR UNCOM BAL"",6;L10;C50;""2QTR UNOBL BAL"",10;L10;C65;""2QTR SCP UNCOM BAL"""
 I QTR=3 S FLDS="1,.01,.5,15,3;L10;C35;""3QTR UNCOM BAL"",7;L10;C50;""3QTR UNOBL BAL"",11;L10;C65;""3QTR SCP UNCOM BAL"""
 I QTR=4 S FLDS="1,.01,.5,15,4;L10;C35;""4QTR UNCOM BAL"",8;L10;C50;""4QTR UNOBL BAL"",12;L10;C65;""4QTR SCP UNCOM BAL"""
 S DHD="FUND CONTROL POINT REPORT FOR FY '"_YR D EN1^DIP
EXIT K %,%DT,%Y,B,BY,CFY,CNT,CYR,DHD,DIC,DTOUT,FLDS,FR,J,L,QTR,STNUM,TO,X,Y,YR,YRDA
 Q
 ;
