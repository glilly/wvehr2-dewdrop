MCARPAC ;WISC/TJK-PRINT ROUTINES FOR PACEMAKER ;1/22/98  10:33
 ;;2.3;Medicine;**15**;09/13/1996
GENERIC ;
 D MCPPROC^MCARP
 S DIC="^MCAR(699.5,"
 I '$D(MCARPPS) S DIC(0)="AEMQZ",DIC("S")="I '$P(^MCAR(699.5,+Y,0),U,3)" D ^DIC G:Y<0 EXIT S (MCARGDA,DA)=+Y
 S MCARGNUM=$P(^MCAR(699.5,MCARGDA,0),U,6),MCARZ=$P(^MCAR(697.2,MCARGNUM,0),U,8)_" REPORT",MCARGRTN=$S($G(MCBP)=1:"GENERICB",1:"GENERIC") G DEVQUE
ALEAD S DIC="^MCAR(698.2,",MCARZ="A-LEAD REPORT",MCARGRTN=$S($G(MCBP)=1:"ALEADB",1:"ALEAD"),MCFILE=698.2 G LOOK
VLEAD S DIC="^MCAR(698.1,",MCARZ="V-LEAD REPORT",MCARGRTN=$S($G(MCBP)=1:"VLEADB",1:"VLEAD"),MCFILE=698.1 G LOOK
GEN S DIC="^MCAR(698,",MCARZ="GENERATOR IMPLANT REPORT",MCARGRTN=$S($G(MCBP)=1:"GENIMPB",1:"GENIMP"),MCFILE=698 G LOOK
SURV S DIC="^MCAR(698.3,",MCARZ="PACEMAKER SURVEILLANCE REPORT",MCARGRTN=$S($G(MCBP)=1:"SURVB",1:"SURV"),MCFILE=698.3
LOOK ;
 I '$D(MCARPPS) S DIC(0)="AEMQ" D ^DIC G:Y<0 EXIT S (MCARGDA,DA)=+Y
DEVQUE ; Device Control and Queuing Control
 I $D(ORHFS) U IO G PRINT ;dcm/slc added for CPRS
 K IO("Q") S %ZIS="MQ" D ^%ZIS G EXIT:POP
 I $D(IO("Q")) S (ZTSAVE("MC*"),ZTSAVE("DIC"))="",ZTRTN="PRINT^MCARPAC",ZTDESC=MCARZ D ^%ZTLOAD K ZTSK G EXIT
 U IO
PRINT ; Print Report
 G PRINT^MCARP
EXIT I IOST'?1"P-".E,'$D(MCOUT) R !!,"* END * Press return to continue: ",X:DTIME
 K ^UTILITY($J),IO("Q"),MCARGDA,MCARGDT,SSN,MCOUT
 K MCARGNM,MCARGRTN,X,DFN,SSN,MCARGNUM,MCARGNAM,MCARZ,DN,D0,FLDS,MCARCODE
 K DIOEND,DIOBEG,DI,DICS,DJ,BY,A,DICSS,DIC
 K DIEDT,DIQ,DIWF,DIPZ,DIL,DXS,DALL,DSC,DCL,DPP,DPQ,DIC,DU,DQI,DY,S,DC
 K DL,DV,DE,DA,DK,Y,R,C,D,I,J,Q,M,P,N,D1,DIW,DIWL,DIWR,DIWT,PG,Z,L
 W:IOST?1"P-".E @IOF D ^%ZISC Q
