LAFARA2 ;SLC/RAF- COBAS FARA II;8/1/94  06:45 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID,IDE OR CUP
LA1 S:$D(ZTQUEUED) ZTREQ="@"
 S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 K TV S TOUT=0 D IN G QUIT:TOUT,LA2:$E(IN,1,2)'="20" G QUIT:TOUT
LA2A G:'$L(IN) LA2
 S TRAY=+$E(IN,14,15),CUP=+$E(IN,17,20),(ID,IDE)=+$E(IN,22,31),V=$E(IN,9,12) D NUM S TEST=V
 F I=0:0 S I=$O(TC(I)) Q:I<1  I TEST=TC(I,4),$E(IN,54)="N" S REMARK=$E(IN,56,57) S V=+$E(IN,33,44) X:$D(TC(I,2)) TC(I,2) S @TC(I,1)=V
 X LAGEN G LA2A:'ISQN
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK) D
 .K CNT,CTRL,CUP,I,ID,IDE,IN,ISQN,JJ,LAGEN,LANM,LWL,REMARK,T
 .K TC,TEST,TOUT,TRAY,TSK,TV,V,S
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
