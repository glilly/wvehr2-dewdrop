NURA6J1 ;HIRMFO/JH/MD,FT-NURSING EDUCATION PROFILE BY SVC. CATEGORY ;8/8/96  13:40
 ;;4.0;NURSING SERVICE;**13**;Apr 25, 1997
CAT D HSKEEP G QUIT:$G(NUROUT)
 S ^TMP("NURSCAT",$J,"R")=""
 D EN2^NURSAGP1 G QUIT:$G(NUROUT)
 D DEV G:POP!($D(ZTSK)) QUIT
START ;
 K ^TMP($J),^TMP("NURA",$J) U IO S (NURPAGE,NURSW1,NURQUIT)=0,NURSSP="",$P(NURSSP," ",132)=" "
 S NURSTIL="NURSING EDUCATION PROFILE BY SERVICE CATEGORY",NURSTIL1=$E(NURSSP,1,10)_"SERVICE      EMPLOYEE"_$E(NURSSP,1,18)_"HIGHEST",NURSTIL2="NO.       CATEGORY     NAME"_$E(NURSSP,1,22)_"NURSING DEGREE"
 S NURROU=19,NRPT=6,NURSLEV=3 W ! D EN1^NURAAGS0,CHK G:$G(NUROUT) QUIT D PRINT^NURAGEN G:NX="^" QUIT
 D TOTALS
 G QUIT
POS ;NURSING EDUCATION PROFILE BY SVC. POSITION
 D HSKEEP G QUIT:$G(NUROUT)
 S NRNS=1 W ! D EN2^NURSAGSP G:$G(NUROUT) QUIT
 D EN2^NURSAGP1 G QUIT:$G(NUROUT)
 D DEV G:POP!($D(ZTSK)) QUIT
START1 ;
 K ^TMP($J),^TMP("NURA",$J) U IO S (NURPAGE,NTCT,NURSW1,NURQUIT)=0,NURSSP="",$P(NURSSP," ",132)=" "
 S NURSTIL="NURSING EDUCATION PROFILE BY SERVICE POSITION",NURSTIL1=$E(NURSSP,1,6)_"SERVICE    ENPLOYEE"_$E(NURSSP,1,23)_"HIGHEST",NURSTIL2="NO.   POSITION   NAME"_$E(NURSSP,1,27)_"NURSING DEGREE"
 S NURROU=20,NRPT=6,NURSLEV=4 W ! D EN2^NURAAGS0,CHK G:$G(NUROUT) QUIT D PRINT^NURAGEN G:NX="^" QUIT
 S NURSORT(1)=0,NURSORT(2)=2,NURSORT(3)="N(1)" D NURA^NURAGEN2,TOTALS
 G QUIT
DEV ;
 S ZTSAVE("^TMP(""NURSCAT"",$J,")="",ZTDESC="Nursing Education Profile by Service "_$S(ANS2=1:"Category",1:"Position")
 W ! S ZTRTN=$S(ANS2=1:"START",1:"START1")_"^NURA6J1" D EN7^NURSUT0
 Q
QUIT K ^TMP("NURA",$J),^TMP($J) D CLOSE^NURSUT1,^NURAKILL
 Q
CHK D NODATA^NURAGEN
 Q
HSKEEP ;
 D EN1^NURSAUTL Q:$G(NUROUT)
 I NURMDSW D EN12^NURSAGSP Q:$G(NUROUT)  S DIC(0)="AEQZ",NURPLSCR=0 D EN5^NURSAGSP Q:$G(NUROUT)
 I NURMDSW=0,NURPLSW=1 S NURPLSCR=0 D PRD^NURSAGSP K NURPLSCR Q:$G(NUROUT)
 Q
TOTALS ;
 I $Y>(IOSL-6) D HEADER^NURAGEN
 S X="",$P(X,"-",33)="" W !,?47,X D EN2^NURAED3 W !!,"TOTAL NUMBER OF ASSIGNMENTS ",?47,$J(NTCT,4,0),!!,"TOTAL NUMBER OF PERSONNEL",?47,$J(NTOT,4,0)
 Q
