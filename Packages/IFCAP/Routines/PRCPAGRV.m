PRCPAGRV ;WISC/RFJ-autogen print items with vendors not selected    ;01 Dec 92
 ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 Q
 ;
 ;
NOVEND ;  print vendors not selected
 N %,%H,%I,DESCNSN,ITEMDA,NOW,PAGE,SCREEN,VDA,VNM,X,Y
 D NOW^%DTC S Y=% D DD^%DT S NOW=Y,PAGE=1,SCREEN=$$SCRPAUSE^PRCPUREP U IO D H
 S VNM="" F  S VNM=$O(^TMP($J,"PRCPAG","NOV",VNM)) Q:VNM=""!($G(PRCPFLAG))  S VDA=0 F  S VDA=$O(^TMP($J,"PRCPAG","NOV",VNM,VDA)) Q:'VDA!($G(PRCPFLAG))  D
 .   W !!?7,"VENDOR: ",$S(VDA=0:"<< VENDOR NOT DEFINED >>",1:VNM),!
 .   S DESCNSN="" F  S DESCNSN=$O(^TMP($J,"PRCPAG","NOV",VNM,VDA,DESCNSN)) Q:DESCNSN=""  S ITEMDA=0 F  S ITEMDA=$O(^TMP($J,"PRCPAG","NOV",VNM,VDA,DESCNSN,ITEMDA)) Q:'ITEMDA!($G(PRCPFLAG))  D
 .   .   W !,ITEMDA,?7,$E($$DESCR^PRCPUX1(PRCP("I"),ITEMDA),1,54) W:PRCP("DPTYPE")="W" ?63,$$NSN^PRCPUX1(ITEMDA)
 .   .   I $Y>(IOSL-5) D:SCREEN P^PRCPUREP Q:$G(PRCPFLAG)  D H
 I $G(PRCPFLAG) Q
 D END^PRCPUREP
 Q
 ;
 ;
H S %=NOW_"  PAGE "_PAGE,PAGE=PAGE+1 I PAGE'=2!(SCREEN) W @IOF
 W $C(13),"AUTO-GENERATION VENDORS AND ITEMS NOT SELECTED",?(80-$L(%)),%,!?5,"FOR INVENTORY POINT: ",PRCP("IN"),!,"MI#",?7,"DESCRIPTION" W:PRCP("DPTYPE")="W" ?63,"NSN" S %="",$P(%,"-",81)="" W !,%
 Q
