LAAIMX ;INDY/PLS;SLC/RAF- IMX ROUTINE FOR MEIA QUAN. DATA; 06/18/93  11:01 ;;;PLS 55693,31670
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;This routine was written by Phillip Salmon at the Indianapolis VAMC
 ;  This routine will handle
 ;datastreams which use ASSAY NUMBERS, %GHb & %A1C tests, and also
 ;allows linearity checking.
 ;CROSS LINK BY IDE
LA1 S:$D(ZTQUEUED) ZTREQ="@"
 S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 K TV S TOUT=0 D IN G QUIT:TOUT,LA2:$E(IN,1,5)'="ASSAY" S V=$E(IN,7,9) D NUM S TEST=+V
 F III=0:0 S III=$O(TC(III)) Q:III<1  I TEST=TC(III,4) D LA2A Q
 Q:TOUT  G LA2
LA2A S TRAY=1 X TC(III,2)
 ;TC(III,2) SET TEST FLAG (LATFLG) TO INDICATE WHICH POSITION TO EXTRACT DATA
 F I=0:0 S TOUT=0 D IN Q:IN["]"!(IN["!!")  G QUIT:TOUT I $E(IN,1,9)="LOC    ID" D IN Q:IN["]"!(IN["!!")  G QUIT:TOUT D LA2B
 K LATFLG
 Q
LA2B F I=0:0 S TOUT=0 D IN G QUIT:TOUT Q:IN']""!(IN["]")!(IN["!!")  D
 .S V=$E(IN,1,2) D NUM S (CUP,ID)=+V,IDE=+$E(IN,5,13) S V=$S($D(LATFLG):$E(IN,27,35),1:$E(IN,17,26)) D NUM
 .I V=+V!(V[".") S V=$J(V,0,+TC(III,3)),@TC(III,1)=$S(V<LRLN(III):"<"_LRLN(III),1:V) D LA3
 Q
 ;
LA3 X LAGEN Q:'ISQN
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 K TV Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>300  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK) D
 .K CNT,CTRL,CUP,I,ID,IDE,III,IN,ISQN,JJ,LAGEN,LANM,LRLN,LWL,T,TC,TEST
 .K TOUT,TRAY,TSK,TV,V,X
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
