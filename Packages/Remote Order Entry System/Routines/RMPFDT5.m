RMPFDT5 ;DDC/KAW-PATIENT ORDER INFORMATION [ 06/16/95   3:06 PM ]
 ;;2.0;REMOTE ORDER/ENTRY SYSTEM;;JUN 16, 1995
 ;;input:  RMPFX
 ;;output: RMPFADP,RMPFAPD,RMPFAPP,RMPFDC,RMPFDR,RMPFHAT,RMPFODP,RMPFDDC,RMPFRMK,RMPFST,RMPFSTP,RMPFTF,RMPFTYP,RMPFURP,RMPFUS,RMPFAD
 Q:'$D(^RMPF(791810,RMPFX,0))
 D ^RMPFET3 S Y=DT D DD^%DT S RMPFDAT=Y
 S S0=^RMPF(791810,RMPFX,0),Y=$P(S0,U,1) D DD^%DT S RMPFTDP=Y
 S DFN=$P(S0,U,4)
 S (RMPFTYP,RMPFHAT)="",RMPFTYP=$P(S0,U,2) I RMPFTYP,$D(^RMPF(791810.1,RMPFTYP,0)) S RMPFTYPP=$P(^(0),U,1),RMPFHAT=$P(^(0),U,2),RMPFTP=$P(^(0),U,3)
 I RMPFTYP=""!(RMPFHAT="") W $C(7),!!,"ERROR IN ORDER TYPE" G END
 S RMPFSTP="",RMPFST=$P(^RMPF(791810,RMPFX,0),U,3) I RMPFST,$D(^RMPF(791810.2,RMPFST,0)) S RMPFSTP=$P(^(0),U,1)
 S RMPFUR=$P(S0,U,5),RMPFURP="" I RMPFUR,$D(^VA(200,RMPFUR,0)) S RMPFURP=$P(^(0),U,1)
 S RMPFAD=$P(S0,U,8),RMPFADP="" I RMPFAD,$D(^VA(200,RMPFAD,0)) S RMPFADP=$P(^(0),U,1)
 S RMPFOD=$P(S0,U,9),RMPFODP="" I RMPFOD?7N S Y=RMPFOD D DD^%DT S RMPFODP=Y
 S RMPFAP=$P(S0,U,10),RMPFAPP="" I RMPFAP,$D(^VA(200,RMPFAP,0)) S RMPFAPP=$P(^(0),U,1)
 S RMPFAPD="",X=$P(S0,U,11) I X S Y=X D DD^%DT S RMPFAPD=Y
 S RMPFPO=$P(S0,U,7),RMPFINV=$P(S0,U,13),Y=$P(S0,U,14)
 D DD^%DT S RMPFRDC=Y
 S S1=$S($D(^RMPF(791810,RMPFX,11)):^(11),1:"")
 S RMPFTF=$P(S1,U,1),RMPFTF=$S(RMPFTF="M":"MONAURAL",RMPFTF="B":"BINAURAL",RMPFTF="C":"CROS",RMPFTF="BC":"BI-CROS",1:"")
 S RMPFUS=$P(S1,U,2),RMPFUS=$S(RMPFUS="M":"MONAURAL",RMPFUS="B":"BINAURAL",1:"")
 S RMPFDDC=$P(S1,U,3),RMPFDDC=$S(RMPFDDC:"YES",RMPFDDC=0:"NO",1:"")
 S S2=$G(^RMPF(791810,RMPFX,2)),RMPFDC=$P(S2,U,1) I RMPFDC,$D(^RMPR(662,RMPFDC,0)) S RMPFDC=$P(^(0),U,1)
 S RMPFTE=$P(S2,U,2) I RMPFTE,$D(^RMPF(791810.4,RMPFTE,0)) S RMPFTE=$P(^(0),U,1)
 S X=$G(^RMPF(791810,RMPFX,10)),RMPFCAT=$P(X,U,5),RMPFCAR=$P(X,U,4)
 S RMPFCAT=$S(RMPFCAT="R":"ROUTINE",RMPFCAT="E":"EMERGENCY",RMPFCAT="P":"PRIORITY",1:"")
 I RMPFCAR S Y=RMPFCAR D DD^%DT S RMPFCAR=Y
 S RMPFRMK=$P(X,U,1),RMPFCUR=$P(X,U,3),RMPFCARE=$P(X,U,8)
 I RMPFCARE S Y=RMPFCARE D DD^%DT S RMPFCARE=Y
 S RMPFDR=$P(X,U,2)
 K RMPFTA I $D(^RMPF(791810,RMPFX,1)) S SA=^(1),RMPFTA="" F I=4:1:6 I $P(SA,U,I)="" K RMPFTA Q
END K RMPFAP,RMPFUR,S1,SA,A,S0,Y,X
 Q
