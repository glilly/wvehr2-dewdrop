LRCAPML1 ;SLC/AM/DALISC/FHS/J0 - WKLD COST REP BY MAJ SCTN; 2/6/91@16:04
 ;;5.2;LAB SERVICE;;Sep 27, 1994
EN ;
 D INITMAN^LRCAPMR1
 S LRGETIN=$S(LRIN:0,1:1)
 F  D BUILD Q:(LREND)!('LRLOOP)!(LRBLDONE)
 Q
BUILD ; BUILD DATA SUBSET IN ^TMP
 D GTIN
 I 'LRIN S LRBLDONE=1 Q
 D GENCOM^LRCAPMR1,CAPCOM^LRCAPMR1
 S (LRAPIGT,LRAPIGTU,LRCPIGT,LRCPIGTU)=0
 S LRCDT=LRCDTB-1
 F  S LRCDT=$O(^LRO(64.1,LRIN,1,LRCDT)) Q:(LRCDT>LRCDTE)!(LRCDT<1)  D
 . D DATCOM^LRCAPMR1
 . W:$E(IOST,1,2)="C-" "."
 . S LRCC=0
 . F  S LRCC=$O(^LRO(64.1,LRIN,1,LRCDT,1,LRCC)) Q:(LRCC<1)  D CC
 S LRGTOTS=$G(^TMP("LR-WL",$J,0))
 S $P(LRGTOTS,U)=$P(LRGTOTS,U)+LRAPIGT+LRCPIGT
 S $P(LRGTOTS,U,2)=$P(LRGTOTS,U,2)+LRAPIGTU+LRCPIGTU
 S ^TMP("LR-WL",$J,0)=LRGTOTS
 S LRGTOTS=$G(^TMP("LR-WL",$J,"DIV","AP",LRIN,0))
 S $P(LRGTOTS,U)=$P(LRGTOTS,U)+LRAPIGT
 S $P(LRGTOTS,U,2)=$P(LRGTOTS,U,2)+LRAPIGTU
 S ^TMP("LR-WL",$J,"DIV","AP",LRIN,0)=LRGTOTS
 S LRGTOTS=$G(^TMP("LR-WL",$J,"DIV","CP",LRIN,0))
 S $P(LRGTOTS,U)=$P(LRGTOTS,U)+LRCPIGT
 S $P(LRGTOTS,U,2)=$P(LRGTOTS,U,2)+LRCPIGTU
 S ^TMP("LR-WL",$J,"DIV","CP",LRIN,0)=LRGTOTS
 Q
CC ;
 S LRCAPNAM=$$WKLDNAME^LRCAPU(LRCC)
 D BMPMANL^LRCAPMR1
 S LRCTM=$S(LRCTMB=0:"",1:LRCTMB-.001),LRFIRST=1
 F  S LRCTM=$O(^LRO(64.1,LRIN,1,LRCDT,1,LRCC,1,LRCTM)) Q:(LRCTM>LRCTME)!(LRCTM="")  D TM
 Q
TM ;
 Q:'($D(^LRO(64.1,LRIN,1,LRCDT,1,LRCC,1,LRCTM,0))#2)
 S LRX=^(0)
 S LRMA=$P(LRX,U,7)
 I LRAA Q:'$D(LRAA(LRMA))
 S LRUC=+$P(LRX,U,3),LRLDIV=$P(LRX,U,6),LRLSS=$P(LRX,U,8),LRSPEC=+$P(LRX,U,14)
 S:'LRUC LRUC=1
 S LRUW=+$P($G(^LAM(LRCC,0)),U,10),LRFIRST=0 S LRUWSP=+$P($G(^LAM(LRCC,5,LRSPEC,0)),U,2) S:LRUWSP LRUW=LRUWSP_"*" K LRUWSP
 S LRWC=LRUC*LRUW
 ;
 ;
 I +LRMA D
 . S LRREC=$G(^LRO(68,LRMA,0))
 . S LRMAA=$S($P(LRREC,U,11)]"":$P(LRREC,U,11),1:LRNDFN)
 . S LRMAN=$S($P(LRREC,U)]"":$P(LRREC,U),1:LRNDFN)
 . S LRMAN(LRMAA)=LRMAN
 I '+LRMA S (LRMAA,LRMAN)=LRNDFN,LRMAN(LRMAA)=LRMAN
 I +LRLSS D
 . S LRREC=$G(^LRO(68,LRLSS,0))
 . S LRLSSA=$S($P(LRREC,U,11)]"":$P(LRREC,U,11),1:LRNDFN)
 . S LRLSSN=$S($P(LRREC,U)]"":$P(LRREC,U),1:LRNDFN)
 . S LRLSSN(LRLSSA)=LRLSSN
 I '+LRLSS S (LRLSSA,LRLSSN)=LRNDFN,LRLSSN(LRLSSA)=LRLSSN
 ;
 ;
 I $D(^TMP("LR-WL",$J,"DIV",LRLDIV,LRIN,LRMAA,LRLSSA,LRCAPNAM))#2 S LRX=^(LRCAPNAM),LRX1=LRUC+$P(LRX,"^"),LRX2=LRWC+$P(LRX,"^",2),^(LRCAPNAM)=LRX1_"^"_LRX2_"^"_LRUW_"^"_LRCAPNUM
 E  S ^TMP("LR-WL",$J,"DIV",LRLDIV,LRIN,LRMAA,LRLSSA,LRCAPNAM)=LRUC_"^"_LRWC_"^"_LRUW_"^"_LRCAPNUM
 I LRLDIV="AP" S LRAPIGT=LRAPIGT+LRWC,LRAPIGTU=LRAPIGTU+LRUC
 E  S LRCPIGT=LRCPIGT+LRWC,LRCPIGTU=LRCPIGTU+LRUC
 I $D(^TMP("LR-WL",$J,"DIV",LRLDIV,LRIN,LRMAA,LRLSSA,0))#2 S LRX1=+$P(^(0),"^")+LRWC,LRX2=+$P(^(0),"^",2)+LRUC,^(0)=LRX1_"^"_LRX2
 E  S ^TMP("LR-WL",$J,"DIV",LRLDIV,LRIN,LRMAA,LRLSSA,0)=LRWC_"^"_LRUC
 I $D(^TMP("LR-WL",$J,"AA",LRMAA,LRLSSA,0))#2 S LRX1=+$P(^(0),"^")+LRWC,LRX2=+$P(^(0),"^",2)+LRUC,^(0)=LRX1_"^"_LRX2
 E  S ^TMP("LR-WL",$J,"AA",LRMAA,LRLSSA,0)=LRWC_"^"_LRUC
 Q
GTIN ;
 S:LRGETIN LRIN=+$O(^LRO(64.1,LRIN))
 S:LRIN LRINN=$S($D(^DIC(4,LRIN,0))#2:$P(^DIC(4,LRIN,0),U),1:LRNDFN)
 S LRGETIN=1
 Q
