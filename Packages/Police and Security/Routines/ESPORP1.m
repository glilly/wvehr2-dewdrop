ESPORP1 ;DALISC/CKA - PRINT OFFENSE REPORT CONT. ;10/92
 ;;1.0;POLICE & SECURITY;**14,17**;Mar 31, 1994
EN ;
COMP ;PRINT COMPLAINANTS
 F ESPN=0:0 S ESPN=$O(^ESP(912,ESPID,20,ESPN)) Q:ESPN'>0  D  G EXIT:END
 .  S DIC="^ESP(912,"_ESPID_",20,",DA=ESPN,DR=".02;.03;1.01:1.06;2.01:2.06",DIQ(0)="E" D EN^DIQ1 Q:'$D(^UTILITY("DIQ1",$J,912.03,DA))
 .  D LINES^ESPORP(2) Q:END
 .  F I=1:1:16 W "* "
 .  W "COMPLAINANT  DATA" F I=1:1:15 W " *"
 .  D LINES^ESPORP(2) Q:END
 .  W "COMPLAINANT NAME: ",$G(^UTILITY("DIQ1",$J,912.03,DA,.02,"E"))
 .  D LINES^ESPORP() Q:END
 .  W "STATUS: ",$G(^UTILITY("DIQ1",$J,912.03,DA,.03,"E"))
 .  D LINES^ESPORP() Q:END
 .  W "HOME ADDRESS: ",$G(^UTILITY("DIQ1",$J,912.03,DA,1.01,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.03,DA,1.02,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.03,DA,1.02,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.03,DA,1.03,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.03,DA,1.03,"E")),","
 .  W $G(^UTILITY("DIQ1",$J,912.03,DA,1.04,"E"))," ",$G(^UTILITY("DIQ1",$J,912.03,DA,1.05,"E"))
 .  D LINES^ESPORP() Q:END
 .  W "HOME PHONE: ",$G(^UTILITY("DIQ1",$J,912.03,DA,1.06,"E"))
 .  D LINES^ESPORP() Q:END
 .  W "WORK ADDRESS: ",$G(^UTILITY("DIQ1",$J,912.03,DA,2.01,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.03,DA,2.02,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.03,DA,2.02,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.03,DA,2.03,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.03,DA,2.03,"E")),","
 .  W $G(^UTILITY("DIQ1",$J,912.03,DA,2.04,"E"))," ",$G(^UTILITY("DIQ1",$J,912.03,DA,2.05,"E"))
 .  D LINES^ESPORP() Q:END
 .  W "WORK PHONE: ",$G(^UTILITY("DIQ1",$J,912.03,DA,2.06,"E"))
VIC ;PRINT VICTIMS
 G:'$D(^ESP(912,ESPID,30)) CONT
 F ESPN=0:0 S ESPN=$O(^ESP(912,ESPID,30,ESPN)) Q:ESPN'>0!(END)  D
 .  S DIC="^ESP(912,"_ESPID_",30,",DA=ESPN,DR=".02:.07;1.01:1.06;2.01:2.06",DIQ(0)="E" D EN^DIQ1 Q:'$D(^UTILITY("DIQ1",$J,912.04,DA))
 .  D MIN^ESPORP(15) Q:END
 .  D LINES^ESPORP(2) Q:END  F I=1:1:17 W "* "
 .  W "VICTIM DATA"
 .  F I=1:1:17 W " *"
 .  D LINES^ESPORP(2) Q:END  W "VICTIM NAME: ",$G(^UTILITY("DIQ1",$J,912.04,DA,.02,"E"))
 .  D LINES^ESPORP() Q:END  W "SEX: ",$G(^UTILITY("DIQ1",$J,912.04,DA,.03,"E"))
 .  W ?20,"RACE: ",$G(^UTILITY("DIQ1",$J,912.04,DA,.04,"E"))
 .  D LINES^ESPORP() Q:END  W "STATUS: ",$G(^UTILITY("DIQ1",$J,912.04,DA,.05,"E"))
 .  D LINES^ESPORP() Q:END  W "DRIVER'S LICENSE & STATE: ",$G(^UTILITY("DIQ1",$J,912.04,DA,.06,"E"))," ",$G(^UTILITY("DIQ1",$J,912.04,DA,.07,"E"))
 .  D LINES^ESPORP() Q:END  W "HOME ADDRESS: ",$G(^UTILITY("DIQ1",$J,912.04,DA,1.01,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.04,DA,1.02,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.04,DA,1.02,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.04,DA,1.03,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.04,DA,1.03,"E")),","
 .  W $G(^UTILITY("DIQ1",$J,912.04,DA,1.04,"E"))," ",$G(^UTILITY("DIQ1",$J,912.04,DA,1.05,"E"))
 .  D LINES^ESPORP() Q:END  W "HOME PHONE: ",$G(^UTILITY("DIQ1",$J,912.04,DA,1.06,"E"))
 .  D LINES^ESPORP() Q:END  W "WORK ADDRESS: ",$G(^UTILITY("DIQ1",$J,912.04,DA,2.01,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.04,DA,2.02,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.04,DA,2.02,"E"))
 .  I $G(^UTILITY("DIQ1",$J,912.04,DA,2.03,"E"))]"" D LINES^ESPORP() Q:END  W ?14,$G(^UTILITY("DIQ1",$J,912.04,DA,2.03,"E")),","
 .  W $G(^UTILITY("DIQ1",$J,912.04,DA,2.04,"E"))," ",$G(^UTILITY("DIQ1",$J,912.04,DA,2.05,"E"))
 .  D LINES^ESPORP() Q:END  W "WORK PHONE: ",$G(^UTILITY("DIQ1",$J,912.04,DA,2.06,"E"))
 .  K ^UTILITY($J,"W") S DIWL=10,DIWR=70,DIWF="W",IEN=0
 .  D LINES^ESPORP() Q:END  W "MEDICAL TREATMENT:",!
 .  F ESPN1=1:1 S IEN=$O(^ESP(912,ESPID,30,ESPN,10,IEN)) Q:IEN'>0  S X=^(IEN,0) D  Q:END
 .  .  D MIN^ESPORP(2) Q:END
 .  .  D ^DIWP
 .  Q:END
 .  D ^DIWW
CONT G:END EXIT G ^ESPORP2
EXIT K DA,DIC,DIQ,DIWF,DIWL,DIWR,ESPDOB,ESPDOBP,DR,END,ESPDTR,ESPID,ESPFN,ESPN,ESPN1,IEN,PAGE,SSN,X
 K ^UTILITY("DIQ1",$J)
 Q
