OCXOED08 ;SLC/RJS,CLA - Rule Editor (Element Options) ;10/29/98  12:37
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**32**;Dec 17,1997
 ;;  ;;ORDER CHECK EXPERT version 1.01 released OCT 29,1998
 ;
 ;
S ;
 ;
 Q
EN(OCXD0,OCXRD,OCXACT) ;
 ;
 ;
 N OCXTHLN,OCXTNLN,OCXTRLN,OCXTULN,OCXTNLN
 ;
 S OCXOPT=$$GETOPT^OCXOEDT(.OCXACT) Q:(OCXOPT=U) 1 X:$L(OCXOPT) OCXOPT
 ;
 Q:'$D(^OCXS(860.3,OCXD0)) 1
 ;
 Q 0
 ;
 ;
EDELE(OCXD0) ;
 N X S X=$$DIE("^OCXS(860.3,",OCXD0,".01;.02")
 Q:'$D(^OCXS(860.3,OCXD0))
 Q
 ;
EDEXP(OCXD0,OCXD1) ;
 D EN^OCXOED09(OCXD0,OCXD1)
 Q
 ;
EOPT(OCXMODE,OCXD0) ;
 ;
 I OCXMODE="ADD" D  Q
 .S:'$D(^OCXS(860.3,OCXD0,"COND",0)) ^OCXS(860.3,OCXD0,"COND",0)="^860.31^^"
 .N OCXD1 S OCXD1=$O(^OCXS(860.3,OCXD0,"COND","@"),-1)+1
 .S ^OCXS(860.3,OCXD0,"COND",OCXD1,0)=OCXD1,^OCXS(860.3,OCXD0,"COND","B",OCXD1,OCXD1)=""
 .D EDEXP(OCXD0,OCXD1)
 I OCXMODE="DEL" D  Q
 .S:'$D(^OCXS(860.3,OCXD0,"COND",0)) ^OCXS(860.3,OCXD0,"COND",0)="^860.31^^"
 .N OCXD1,DA S DA(1)=OCXD0,OCXD1=+$$DIC("^OCXS(860.3,"_OCXD0_",""COND"",","AEMQN","Select Expression Index Number: ") Q:(OCXD1<0)
 .Q:'$$READ("Y","Are you sure you want to Delete ?","YES")
 .Q:'$$DIE("^OCXS(860.3,"_(+OCXD0)_",""COND"",",OCXD1,"S DA(1)="_(+OCXD0)_";.01///@")
 .W !!,"Deleted..." H 1
 Q
 ;
COMP ;
 D ^OCXOCMP
 Q
 ;
 ;
READ(OCXZ0,OCXZA,OCXZB,OCXZL) ;
 N OCXLINE,DIR,DTOUT,DUOUT,DIRUT,DIROUT
 Q:'$L($G(OCXZ0)) U
 S DIR(0)=OCXZ0
 S:$L($G(OCXZA)) DIR("A")=OCXZA
 S:$L($G(OCXZB)) DIR("B")=OCXZB
 F OCXLINE=1:1:($G(OCXZL)-1) W !
 D ^DIR
 I $D(DTOUT)!$D(DUOUT)!$D(DIRUT)!$D(DIROUT) Q U
 Q Y
 ;
DIE(DIE,DA,DR) ;
 ;
 D RM(IOM) N DUOUT,DTOUT,DIC S DIC=DIE D ^DIE D RM(0) Q:$G(DTOUT) 0 Q:$G(DUOUT) 0 Q 1
 ;
RM(X) X ^%ZOSF("RM") Q
 ;
DIC(OCXDIC,OCXDIC0,OCXDICA,OCXX,OCXDICS,OCXDR,DA) ;
 ;
 N DIC,X,Y
 S DIC=$G(OCXDIC) Q:'$L(DIC) -1
 S DIC(0)=$G(OCXDIC0) S:$L($G(OCXX)) X=OCXX
 S:$L($G(OCXDICS)) DIC("S")=OCXDICS
 S:$L($G(OCXDICA)) DIC("A")=OCXDICA
 S:$L($G(OCXDR)) DIC("DR")=OCXDR
 D ^DIC Q:(Y<1) 0 Q Y
 ;
