FSCRPCOP ;SLC/STAFF-NOIS RPC Other Pofile ;11/1/97  17:33
 ;;1.1;NOIS;;Sep 06, 1998
 ;
PROFILE(IN,OUT) ; from FSCRPX (RPCCallProfileDefaults)
 N CALL,SITE,MODULE,PATCH,CONTACT,SPEC,LINE,ZERO
 S LINE=""
 S CALL=+$G(^TMP("FSCRPC",$J,"INPUT",1))
 I CALL D
 .S ZERO=^FSCD("CALL",CALL,0)
 .S SITE=$P(ZERO,U,5)
 .S MODULE=$P(ZERO,U,8)
 .S PATCH=$$PATCH(CALL)
 .S CONTACT=$P(ZERO,U,6)
 .S SPEC=$P(ZERO,U,9)
 .S LINE=SITE_U_MODULE_U_PATCH_U_CONTACT_U_SPEC
 S ^TMP("FSCRPC",$J,"OUTPUT",1)=LINE
 Q
 ;
PATCH(CALL) ;
 N PATCH,PATCHIEN
 S PATCH=$P($G(^FSCD("CALL",+CALL,120)),U,14)
 I '$L(PATCH) Q ""
 S PATCHIEN=+$O(^A1AE(11005,"B",PATCH,0))
 I 'PATCHIEN S $P(PATCH,"*",2)=+$P(PATCH,"*",2),PATCHIEN=+$O(^A1AE(11005,"B",PATCH,0))
 I 'PATCHIEN Q ""
 Q PATCHIEN
