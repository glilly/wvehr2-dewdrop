LASMAC4 ;SLC/RWF - SMAC RUN CONTROL FOR SMAC (LASMACA) ;7/20/90  10:10 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 D PICK
LA2 D TSK I TSK<1!('$D(LWL)) W !," Auto Instrument file not setup right, OR already running!" Q
 G LA3:LAGEN'["LWLIST" W !!,$C(7),"Have you BUILT the LOAD LIST? N//" R X:DTIME Q:'$T  I "Nn"[X D ^LRLL G LASMAC4
 I $S('$D(^LRO(68.2,LWL,2)):1,1:$P(^(2),U,1)'=DT) W !,$C(7)," LOAD LIST IS NOT CURRENT!" Q
LA3 S IOP="HOME" D ^%ZIS K IOT,IOSL,IOBS
 D ASK^LA1103
 W @IOF,!,"START OF SMAC RUN"
 D TSK
 S T=TSK D SET^LAB
D4 R !,"PRESS RETURN TO START PROCESSING",X:DTIME,! G D4:'$T
LA1 K ^LA("LOCK",TSK),^LA("STOP",TSK) S ^LRO(68.2,LWL,8,0)="",LRMODE=1 D @("^"_LANM) O IO(0) U IO(0)
 I '$D(^LA("STOP",TSK)) W !!,$C(7),"IS THE SMAC RUN FINISHED" S %=2 D YN^DICN G LA1:%'=1
 W !,$C(7),"Finished collecting SMAC data."
 ;W @IOF D MEAN^LACRIT
 W !!,"SMAC RUN OVER",@IOF
 K ^LA("STOP",TSK),LWL,TC,TV,LRMODE,^TMP($J),^TMP("LA",$J)
END L
 Q
EN ;
HALT R !!,"DO YOU REALLY WANT TO STOP THE SMAC RUN? N//",X:DTIME G HALT:'$T Q:X'["Y"
HA2 D PICK
 D TSK Q:TSK<1  S ^LA("STOP",TSK)=1 Q
TSK S TSK=$O(^LAB(62.4,"C",LANM,0)) W:TSK<1 !,"NOT IN AUTO INSTRUMENT FILE."
 K ^LA("LOCK",TSK),LRTOP D ^LASET Q
PICK S U="^",A=$O(^LAB(62.4,"C","LASMAC")),B=$O(^(A)),A=$P(A,"LASMAC",2),B=$P(B,"LASMAC",2)
PC2 S X=A I $L(B) W !,"SMAC '",A,"' OR '",B,"': " R X:DTIME G PC2:'$T Q:X[U  S:X="" X=" " S X=$E(X,1) G PC2:A_B'[X
 S LANM="LASMAC"_X Q
