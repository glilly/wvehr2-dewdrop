LRMITSRH ;SLC/STAFF - MICRO TREND REPORT HEADER ;10/12/92  20:26
 ;;5.2;LAB SERVICE;**257,339**;Sep 27, 1994
 ; from LRMITSR
 ; writes heading information
 ;
 I $$S^%ZTLOAD S (LREND,ZTSTOP)=1 Q
 W @IOF S LRPAGE=LRPAGE+1
 S LRHDRT=$S(LRTYPE="O":"ORGANISM",LRTYPE="S":"SITE/SPECIMEN",LRTYPE="L":"LOCATION",LRTYPE="P":"PATIENT",LRTYPE="C":"COLLECTION SAMPLE",LRTYPE="DIV":"DIVISION",1:"PHYSICIAN")
 I LRPAGE=1 D
 .S LRLINE(1)=LRLINE1_LRHDRT,LRLINE(1)=$J(LRLINE(1),IOM+$L(LRLINE(1))\2)
 .W LRLINE(1),!,LRLINE(2)
 .I $D(LRM(LRTYPE,"S")) D
 ..W !,"This report is restricted to the following ",$$LOW^XLFSTR(LRHDRT),"s:"
 ..K LRTEMP D HLIST^LRMITSRS("LRM(LRTYPE,""S"")","LRTEMP",", ",IOM)
 ..S LRN="" F  S LRN=$O(LRTEMP(LRN)) Q:LRN=""  W !,LRTEMP(LRN)
 ..K LRTEMP
 .S LRCNT=2 F  S LRCNT=$O(LRLINE(LRCNT)) Q:LRCNT<1  W !,LRLINE(LRCNT)
 S LRHDR(1)=LRHDR1_LRPATNUM_" patients)           BY "_LRHDRT_"    Page "_$J(LRPAGE,1)
 S LRCNT=0 F  S LRCNT=$O(LRHDR(LRCNT)) Q:LRCNT<1  W !,LRHDR(LRCNT)
 Q
