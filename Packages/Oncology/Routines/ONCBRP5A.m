ONCBRP5A ;HINES CIOFO/GWB - 1998 Breast Cancer Study - Table V (continued) ;6/1/98
 ;;2.11;ONCOLOGY;**18**;Mar 07, 1995
RT W @IOF D HEAD^ONCBRP0
 W !," RADIATION THERAPY"
 W !," -----------------"
RT1 W !," 67. RADIATION THERAPY.............: ",ONC(165.5,ONCONUM,51.2)
 I RAD=0 D  G DRS
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,53)=0
 .W !," 68. PRE-RADIATION THERAPY"
 .W !,"     MAMMOGRAM OF PATIENT..........: Not done"
 I RAD=9 D  G DRS
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,53)=9
 .W !," 68. PRE-RADIATION THERAPY"
 .W !,"     MAMMOGRAM OF PATIENT..........: Unknown if done"
PRTMP S DR="952 68. PRE-RADIATION THERAPY                                                           MAMMOGRAM OF PATIENT.........." D ^DIE G:$D(Y) JUMP
DRS W !," 69. DATE RADIATION STARTED........: ",ONC(165.5,ONCONUM,51)
 I RAD=0 D  G RADWRT
 .S $P(^ONCO(165.5,ONCONUM,"BLA2"),U,16)="0000000"
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,54)=0
 .I $$GET1^DIQ(160,ONCOPA,10,"I")=2 S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,55)="00000",$P(^ONCO(165.5,ONCONUM,"THY1"),U,44)="00000"
 .E  S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,55)="",$P(^ONCO(165.5,ONCONUM,"THY1"),U,44)=""
 I RAD=9 D  G RADWRT
 .S $P(^ONCO(165.5,ONCONUM,"BLA2"),U,16)="9999999"
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,54)=9
 .I $$GET1^DIQ(160,ONCOPA,10,"I")=2 S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,55)="99999",$P(^ONCO(165.5,ONCONUM,"THY1"),U,44)="99999"
 .E  S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,55)="",$P(^ONCO(165.5,ONCONUM,"THY1"),U,44)=""
RADWRT I (RAD=0)!(RAD=9) D  G:$D(DIRUT) EXIT G RFNR
 .K DIQ S DIC="^ONCO(165.5,",DR="361;953;954;443",DA=ONCONUM,DIQ="ONC"
 .D EN^DIQ1
 .W !," 70. DATE RADIATION ENDED..........: ",ONC(165.5,ONCONUM,361)
 .W !," 71. SITES IRRADIATED..............: ",ONC(165.5,ONCONUM,953)
 .W !," 72. (F) cGy DOSE TO WHOLE BREAST"
 .W !,"     OR CHEST WALL.................: ",ONC(165.5,ONCONUM,954)
 .W !," 73. (F) BOOST RADIATION...........: ",ONC(165.5,ONCONUM,443)
DRE S DR="361 70. DATE RADIATION ENDED.........." D ^DIE G:$D(Y) JUMP
SI S DR="953 71. SITES IRRADIATED.............." D ^DIE G:$D(Y) JUMP
DWBCW I $$GET1^DIQ(160,ONCOPA,10,"I")=1 D  W ! G RFNR
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,55)=""
 .S $P(^ONCO(165.5,ONCONUM,"THY1"),U,44)=""
 .W !," 72. (F) cGy DOSE TO WHOLE BREAST"
 .W !,"     OR CHEST WALL.................: (Data Item for Females Only)"
 .W !," 73. (F) BOOST RADIATION...........: (Data Item for Females Only)"
 S DR="954 72. (F) cGy DOSE TO WHOLE BREAST                                                    OR CHEST WALL................." D ^DIE G:$D(Y) JUMP
BR S DR="443 73. (F) BOOST RADIATION..........." D ^DIE G:$D(Y) JUMP
RFNR W !," 74. REASON FOR NO RADIATION ......: ",ONC(165.5,ONCONUM,75)
 W ! K DIR S DIR(0)="E" D ^DIR G:$D(DIRUT) EXIT
HOT W @IOF D HEAD^ONCBRP0
 W !," HORMONE THERAPY"
 W !," ---------------"
HT W !," 75. HORMONE THERAPY...............: ",ONC(165.5,ONCONUM,54.2)
DEHTB W !," 76. DATE HORMONE THERAPY STARTED..: ",ONC(165.5,ONCONUM,54)
SHT I $$GET1^DIQ(160,ONCOPA,10,"I")=2 D  G:$D(DIRUT) EXIT W ! G CT
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,56)=""
 .W !," 77. (M) SPECIFIC HORMONE THERAPY..: (Data Item for Males Only)"
 .W ! K DIR S DIR(0)="E" D ^DIR Q:$D(DIRUT)  D HEAD^ONCBRP0
 I HOR=0 D  G:$D(DIRUT) EXIT G CT
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,56)=0
 .W !," 77. (M) SPECIFIC HORMONE THERAPY...: None"
 .W ! K DIR S DIR(0)="E" D ^DIR Q:$D(DIRUT)  D HEAD^ONCBRP0
 I HOR=9 D  G:$D(DIRUT) EXIT G CT
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,56)=9
 .W !," 77. (M) SPECIFIC HORMONE THERAPY...: Unknown"
 .W ! K DIR S DIR(0)="E" D ^DIR Q:$D(DIRUT)  D HEAD^ONCBRP0
 S DR="955 77. (M) SPECIFIC HORMONE THERAPY.." D ^DIE G:$D(Y) JUMP
 W ! K DIR S DIR(0)="E" D ^DIR G:$D(DIRUT) EXIT
CT W @IOF D HEAD^ONCBRP0
 W !," CHEMOTHERAPY"
 W !," ------------"
C W !," 78. CHEMOTHERAPY..................: ",ONC(165.5,ONCONUM,53.2)
DCS W !," 79. DATE CHEMOTHERAPY STARTED.....: ",ONC(165.5,ONCONUM,53)
CRCD I CHE=0 D  G PRTC
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,57)=8
 .W !," 80. CHEMOTHERAPEUTIC REGIME"
 .W !,"     CONTAINING DOXORUBICIN.......: NA, no chemotherapy"
 I CHE=9 D  G PRTC
 .S $P(^ONCO(165.5,ONCONUM,"BRE1"),U,57)=9
 .W !," 80. CHEMOTHERAPEUTIC REGIME"
 .W !,"     CONTAINING DOXORUBICIN.......: Unknown"
 S DR="956 80. CHEMOTHERAPEUTIC REGIME                                                         CONTAINING DOXORUBICIN........" D ^DIE G:$D(Y) JUMP
PRTC W ! K DIR S DIR(0)="E" D ^DIR S:$D(DIRUT) OUT="Y"
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!," GO TO ITEM: ",X:DTIME I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !," ",HTABLE(I)
 I '$D(TABLE(X)) S:X?1.2N X=X_"." S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !," ",HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT S:$D(DIRUT) OUT="Y"
 K CHOICES,PIECE,HTABLE,TABLE
 K CDS,CDSOT,LOS,NCDS,NCDSOT,NOP,SURG,SURG1,SURG2,SURGDT,SA,SA1,SA2
 K SM,SM1,SM2,SOORS,SOORS1,SOORS2,HOR,RAD
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
