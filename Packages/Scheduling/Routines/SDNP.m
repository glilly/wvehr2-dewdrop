SDNP ;MAN/GRR - AUTO-REBOOK NO-SHOW CLINIC REPORT ; 22 MAR 84  12:27 pm
 ;;5.3;Scheduling;;Aug 13, 1993
 S:'$D(^UTILITY($J,+A,GDATE)) ^(GDATE)=SC_"^"_NDATE
EN1 I '$D(FSW) S FSW="" D HED
 D:$Y>(IOSL-2) HED
 S P=^DPT(+A,0) W !!,$P(P,"^",1),?32,$E($P(P,"^",9),6,9) S X=GDATE D TM W ?37,$J(X,8) S X=NDATE D:X]"" TM
 S Y=NDATE D DTS^SDUTL W:Y]"" ?49,Y," ",$J(X,8) I NDATE F TST=3,4,5 S X=$P(^DPT(+A,"S",NDATE,0),"^",TST) I X]"" D TM W !,?58,$J(X,8)," ",$P("LAB^X-RAY^EKG","^",(TST-2))
 W ! S:DUPE MESS=MESS_$S(MESS]"":",",1:"")_" MULTIPLE APPTS. ON NO-SHOW DATE" W:MESS]"" !,?5,MESS,! W:$D(MESS1) ?5,MESS1,! K MESS1 S MESS=""
 Q
HED S Y=CDATE D DTS^SDUTL S Y1=Y,Y=DT D DTS^SDUTL W @IOF,?27,"NO-SHOW AUTO-REBOOK REPORT",!!,"NO-SHOW CLINIC : ",$P(^SC(+SC,0),"^",1),!,"DATE OF NO-SHOW: ",Y1,?30,"DATE PRINTED: ",Y,!
 W !,"PATIENT NAME",?32,"SSN",?39,"TIME",?50,"NEW DATE/TIME" Q
TM S X=$E($P(X,".",2)_"0000",1,4),%=X>1159 S:X>1259 X=X-1200 S X=X\100_":"_$E(X#100+100,2,3)_" "_$E("AP",%+1)_"M" Q
