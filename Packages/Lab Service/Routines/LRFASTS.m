LRFASTS ;DALOI/FHS - ENHANCED LRFAST RTN ACCESSION/VERIFY PROCESS ; Jun 3, 2003
 ;;5.2;LAB SERVICE;**30,95,121,271,286**;Sep 27, 1994
EN ;
 N DIC,DIR,DIRUT,DTOUT,DUOUT,LRPER,X,Y
 D ^LRPARAM
 S LRFASTS=""
 I '$D(LRLABKY) W !!?10,"Not authorized to use this option " Q
 S LRCW=8,LREND=0,LRPANEL=0
 S DIR(0)="YO",DIR("A")="Do you want to review the data before and after you edit",DIR("B")="YES"
 D ^DIR
 I $D(DIRUT) D QUIT Q
 I Y=0 S LRPER=""
 S X=$$SELPL^LRVERA(DUZ(2))
 I X<1 D QUIT Q
 I X'=DUZ(2) N LRPL S LRPL=X
 ;
 K LRCDEF0,LRCDEF
 D ^LRORD
 ;
QUIT ;
 I $D(LRCSQ),'$O(^TMP("LRCAP",LRCSQ,DUZ,0)) K ^TMP("LRCAP",LRCSQ,DUZ),LRCSQ
 I $D(LRCSQ),$P(LRPARAM,U,14) D STD^LRCAPV K LRIDIV
 ;
 K I12,LRCDEF,LRCDEF0,LRCDEF0X,LRCSQ,LRCW,LRFASTS,LRNTN,LRNX,LRPANEL,LRSSCX,LRDUF0,LRTEC,LRVF,LRXDP,X9,%,L1,LRAD,LREND,LRSN,QUOUT
 K LRAL,LRALL,LRCAPMS,LRMA,SEX,S2,T1,AGE,N,D0,D1,DOB,I,LRFASTS,LRSLOW,DIR,X3,LRORDXS,LRADXS,LRSNXS,LRWP,LRWPC
 K LRALERT,LRCSQQ,LRT,LRNOW,LRODTSV,LRSNSV,LRSUF0,LRTSNV,NOW,LRI,LRTNSV
 ; ORVP,ORIFN Killed for OE/RR 2.5
 K ORVP,ORIFN
 ;
 D SLOWK,^%ZISC
 ;
 Q
 ;
 ;
LRWU4 ;
 N L,LRI,LRADXS,LRSNXS
 Q:'$G(LRORD)
 S LRORDXS=LRORD,LRADXS=0
 F  S LRADXS=$O(^LRO(69,"C",LRORDXS,LRADXS)) Q:LRADXS<1  D
 . S LRSNXS=0
 . F  S LRSNXS=$O(^LRO(69,"C",LRORDXS,LRADXS,LRSNXS)) Q:LRSNXS<1  D
 . . K LRSLOW
 . . S LRSN=+LRSNXS,LRAD=+LRADXS,LRORD=+LRORDXS
 . . Q:'LRSN!('LRAD)!('$O(^LRO(69,LRAD,1,LRSN,2,0)))
 . . S LRI=0
 . . F  S LRI=$O(^LRO(69,LRAD,1,LRSN,2,LRI)) Q:LRI<1  D
 . . . S L=$G(^LRO(69,LRAD,1,LRSN,2,LRI,0))
 . . . I $P(L,U,3),$P(L,U,4),$P(L,U,5) S LRSLOW($P(L,U,3,5))=""
 . . S LRI=""
 . . F  S LRI=$O(LRSLOW(LRI)) Q:LRI=""  D GO
 ;
 D SLOWK
 Q
 ;
 ;
GO ;
 ; Protect variables
 N LRAA,LRAD,LRAN,LRADXS,LRSNXS
 S LRAD=$P(LRI,U,1),LRAA=$P(LRI,U,2),LRAN=$P(LRI,U,3)
 ;
 ; Protect subscript variable
 N LRI
 ;
 I $P(LRPARAM,U,14),$P($G(^LRO(68,LRAA,0)),U,16) D ^LRCAPV Q:$G(LREND)
 ;
 ; Check for different performing lab.
 I $G(LRPL) N LRDUZ S LRDUZ(2)=LRPL
 ;
 D SLOW^LRVER
 Q
 ;
 ;
SLOWK ;
 K I5,LRCSN,LRORIFN,LRWPC,X4
 K K,LRACN,LRACN0,LRDAX,LRDOC,LRCDEF,LRCDEF0
 K LRLBL,LRLBLBP,LRLL,LRLWC,LRMACH,LROD0,LROD1,LROD3,LROOS,LRORD,LROSD,LRYR
 K LRAA,LRACD,LRAN,LRAOD,LRCAPLOC,LRAOD,LRCDT,LRCFL,LRCODEN,LRCS,LRDAT,LRDEL,LRDFN,LRDPF,LRDV,LRDVF,LREAL,LREDO,LRFFLG,LRFP,LRIDIV,LRIDT,LRIX,LRJ,LRK,LRBLBP,LRLCT,LRLDT,LRLLOC,LRM,LRMAX1
 K LRMAX2,LRMAXX,LRMETH,LRMX,LRNAME,LRNOCODE,LROLLOC,LROT,LRPR,LRPRAC,LRRB,LRSAMP,LRSAVE,LRSPN,LRSS,LRSSX,LRST,LRSUB,LRSUM,LRSX,LRSXN,LRTEST,LRTN,LRTREA,LRTS,LRTX,LRTY,LRVRM,LRWL0,LRWLC,LRWRD,LRX,LRXD,LRWRD,SSN
 K DR,GLB,H8,L,S5,T,TT
 Q
