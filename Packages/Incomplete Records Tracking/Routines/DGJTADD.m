DGJTADD ;MAC/ALB - ENTER/EDIT DEFICIENCIES FOR IRT ; MAY 13,1992@11:00
 ;;1.0;Incomplete Records Tracking;;Jun 25, 2001
EN N DA,DIC,DIE,DIR,DLAYGO,DGJTABR,DGJTCTG,DGJTTRK,DGJTSTD D FULL^VALM1
 R !!,"Enter New Deficiency: ",X:DTIME G Q1:X="^"!('$T) S DGJTX=X I DGJTX["?"!($L(DGJTX)>30)!($L(DGJTX)<3)!'(DGJTX'?1P.E) W !!,"Enter a new Deficiency Name ... 3-30 characters OR '^' to EXIT" G EN
 S DIR(0)="393.3,.02" D ^DIR G Q1:X["^"!$D(DTOUT) S DGJTABR=$P(Y,"^",1)
 S DIR(0)="393.3,.06" D ^DIR G Q1:X["^"!$D(DTOUT) S DGJTCTG=+Y
 S DIR(0)="393.3,.07" D ^DIR G Q1:X["^"!$D(DTOUT) S DGJTTRK=+Y
 S DIR(0)="393.3,.08" D ^DIR G Q1:X["^"!$D(DTOUT) S DGJTSTD=+Y
 S X=DGJTX,DIC="^VAS(393.3,",DIC(0)="L",DLAYGO=393.3 D ^DIC G:Y'>0 Q1
 S DIE=DIC,DA=+Y,DR=".02////"_DGJTABR_";.06////"_DGJTCTG_";.07////"_DGJTTRK_";.08////"_DGJTSTD D ^DIE
 G EN
Q1 K DR D EVDT^DGJPAR1 S VALMBG=1,VALMBCK="R" Q
