NURA9C2 ;HIRMFO/MD,FT-SERVICE POSITION CERTIFICATION REPORT BY LOCATION ;8/9/96  10:13
 ;;4.0;NURSING SERVICE;**13**;Apr 25, 1997
 Q:'$D(^DIC(213.9,1,"OFF"))  Q:$P(^DIC(213.9,1,"OFF"),"^",1)=1
 S (NURQUEUE,NURQUIT,NUROUT)=0
 D EN1^NURSAUTL G QUIT:$G(NUROUT)
 I NURMDSW S DIC(0)="AEQZ",NURPLSCR=1 D EN5^NURSAGSP G QUIT:$G(NUROUT)
 I NURMDSW=0,NURPLSW=1 S NURPLSCR=1 D PRD^NURSAGSP K NURPLSCR I $G(NUROUT) G QUIT
 W ! D EN1^NURSAGSP G QUIT:$G(NUROUT) W !
 S NRNS=1 D EN2^NURSAGSP G QUIT:$G(NUROUT)
 D EN4^NURSAGP0 W ! G QUIT:$G(NUROUT)
 S ZTDESC="Nursing Service Position Certification Report by Location",ZTRTN="START^NURA9C2" D EN7^NURSUT0 G:POP!($D(ZTSK)) QUIT
START ;
 K ^TMP($J) U IO S (NURPAGE,NURSW1)=0,NSPN(1)=""
 D SORT G:$G(NUROUT) QUIT
 D PRINT
QUIT K ^TMP($J) D CLOSE^NURSUT1,^NURAKILL
 Q
 ; DETAIL LINE PRINT ROUTINE
PRINT ;
 S NURFAC="" F  S NURFAC=$O(^TMP($J,"L",NURFAC)) Q:NURFAC=""!(NURQUIT)  D PA Q:NURQUIT
 Q
PA S NURPROG="" F  S NURPROG=$O(^TMP($J,"L",NURFAC,NURPROG)) Q:NURPROG=""!(NURQUIT)  D PB Q:NURQUIT
 Q
PB S NL1="" F  S NL1=$O(^TMP($J,"L",NURFAC,NURPROG,NL1)) Q:NL1=""  D:NURSW1 HEADER,BRK Q:NURQUIT  D P0 Q:NURQUIT
 Q
P0 S NPRI="" F  S NPRI=$O(^TMP($J,"L",NURFAC,NURPROG,NL1,NPRI)) Q:NPRI=""  D P1 Q:NURQUIT
 Q
P1 S NSPN="" F  S NSPN=$O(^TMP($J,"L",NURFAC,NURPROG,NL1,NPRI,NSPN)) Q:NSPN=""  S NURSORT=$G(^TMP($J,"L",NURFAC,NURPROG,NL1,NPRI,NSPN)) I NURSORT D:NURSW1 BRK1 D P3 Q:NURQUIT
 Q
P3 S NCRT="" F  S NCRT=$O(^TMP($J,"L1",NURSORT,NCRT)) Q:NCRT=""  D P4 Q:NURQUIT
 Q
P4 S NCDT="" F  S NCDT=$O(^TMP($J,"L1",NURSORT,NCRT,NCDT)) Q:NCDT=""  D P5 Q:NURQUIT
 Q
P5 S N1="" F  S N1=$O(^TMP($J,"L1",NURSORT,NCRT,NCDT,N1)) Q:N1=""  W ! D:NCRT'="  BLANK" PRINT1 Q:NURQUIT
 Q
PRINT1 I ($Y>(IOSL-6)!'NURSW1) D HEADER,BRK,BRK1 Q:NURQUIT
 S NURSW1=1 S NURCTA="" S NSUB=$O(^NURSF(212.2,"C",$E(NCRT,1,30),"")),NURCTA=$S('$D(^NURSF(212.2,NSUB,0)):"",1:$P(^(0),"^",4))
 W:N1'="  BLANK" !,$E(N1,1,20)
 W ?24,$E(NCRT,1,30)
 W:NURCTA'=" BLANK" ?60,NURCTA
 S Y=NCDT D:+Y D^DIQ W:Y'="  BLANK" ?68,Y
 S NSPN(1)=NSPN Q
BRK Q:NURQUIT  W !!?31,"LOCATION:  ",NL1
 Q
BRK1 Q:NURQUIT  W !!?31,"SERVICE POSITION: ",NSPN W !
 Q
HEADER I 'NURQUEUE,$E(IOST)="C",NURSW1 D ENDPG^NURSUT1 S:NUROUT NURQUIT=+NUROUT Q:NURQUIT
 S NURPAGE=NURPAGE+1 W:$E(IOST)="C"!(NURPAGE>1) @IOF
 I NURMDSW,$L($G(NURFAC))>1 W ?$$CNTR^NURSUT2(NURFAC),$$FACL^NURSUT2(NURFAC)
 W !!,"CERTIFICATION PROFILE BY LOCATION/SVC. POSITION" S X="T" D ^%DT D:+Y D^DIQ W ?58,Y,?72,"PAGE: ",NURPAGE
 W !!?60,"CERT.",?68,"DATE CERT." W !,"NAME",?24,"CERTIFICATION",?60,"AGENCY",?68,"EXPIRES" W !,$$REPEAT^XLFSTR("-",80)
 I $G(NURPLSW),$L($G(NURPROG))>1 N Z S Z=$$PROD^NURSUT2(NURPROG) W !?$$CNTR^NURSUT2(Z),$G(Z),!,?$$CNTR^NURSUT2(Z),$$REPEAT^XLFSTR("-",$L(Z)+1)
 Q
SORT W ! S NRPT=2 D EN4^NURAAGS0
 D NODATA^NURA9C1
 Q
