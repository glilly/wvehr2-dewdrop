SROCANUP ;B'HAM ISC/ADM - UPDATE CANCELLED CASE ; 26 MAY 1992  2:10 PM [ 03/07/97  12:00 PM ]
 ;;3.0; Surgery ;**63**;24 Jun 93
 S SRSOUT=0 K DIC W @IOF,!,"Update Cancelled Case",!!
PAT S DIC("A")="Select Patient: ",DIC=2,DIC(0)="QEAM" D ^DIC I Y<0 S SRSOUT=1 G END
 S DFN=+Y D DEM^VADPT D HDR
 W ! S (SRDT,CNT)=0 F  S SRDT=$O(^SRF("ADT",DFN,SRDT)) Q:'SRDT!(SRSOUT)  S SROP=0 F  S SROP=$O(^SRF("ADT",DFN,SRDT,SROP)) Q:'SROP!($D(SRTN))!(SRSOUT)  D LIST
 Q:$D(SRTN)!SRSOUT
 I 'CNT W !!,"No cancelled cases exist on this patient.",!! K DFN G PAT
OPT W !!!,"Select Operation: " R X:DTIME I '$T!("^"[X) S SRSOUT=1 G END
 I '$D(SRCASE(X)) W !!,"Enter the number of the desired operation." G OPT
 S SRTN=+SRCASE(X)
 Q
LIST ; list cases
 I $P($G(^SRF(SROP,30)),"^")="" Q
 I $P($G(^SRF(SROP,.2)),"^")!($P($G(^SRF(SROP,.2)),"^",10)) Q
 I $Y+5>IOSL S SRBACK=0 D SEL^SROPER Q:$D(SRTN)!(SRSOUT)  D:'SRBACK HDR I SRBACK S CNT=0,SROP=SRCASE(1)-1,SRDT=$P(SRCASE(1),"^",2) W @IOF,!,?1,VADM(1)_"   "_VA("PID"),! Q
 S CNT=CNT+1,SRSDATE=$P(^SRF(SROP,0),"^",9)
 W !,CNT_". "
CASE W $E(SRSDATE,4,5)_"-"_$E(SRSDATE,6,7)_"-"_$E(SRSDATE,2,3)
 S SROPER=$P(^SRF(SROP,"OP"),"^") I $O(^SRF(SROP,13,0)) S SROTHER=0 F I=0:0 S SROTHER=$O(^SRF(SROP,13,SROTHER)) Q:'SROTHER  D OTHER
 D ^SROP1 K SROPS,MM,MMM S:$L(SROPER)<65 SROPS(1)=SROPER I $L(SROPER)>64 S SROPER=SROPER_"  " F M=1:1 D LOOP Q:MMM=""
 W ?14,SROPS(1) I $D(SROPS(2)) W !,?14,SROPS(2) I $D(SROPS(3)) W !,?14,SROPS(3) W:$D(SROPS(4)) !,?14,SROPS(4)
 W ! S SRCASE(CNT)=SROP_"^"_SRDT
 Q
OTHER ; other operations
 S SRLONG=1 I $L(SROPER)+$L($P(^SRF(SROP,13,SROTHER,0),"^"))>235 S SRLONG=0,SROTHER=999,SROPERS=" ..."
 I SRLONG S SROPERS=$P(^SRF(SROP,13,SROTHER,0),"^")
 S SROPER=SROPER_$S(SROPERS=" ...":SROPERS,1:", "_SROPERS)
 Q
LOOP ; break procedures
 S SROPS(M)="" F LOOP=1:1 S MM=$P(SROPER," "),MMM=$P(SROPER," ",2,200) Q:MMM=""  Q:$L(SROPS(M))+$L(MM)'<65  S SROPS(M)=SROPS(M)_MM_" ",SROPER=MMM
 Q
END K SRTN D ^SRSKILL W @IOF
 Q
RT ; start RT logging
 I $D(XRTL) S XRTN="SROP" D T0^%ZOSV
 Q
HDR ; print heading
 W @IOF,!,?1,VADM(1)_"   "_VA("PID"),!
 Q
