%ZISP ;AC/SFISC - Collect screen parameters(Graphic set) ;11/04/97  14:41
 ;;8.0;KERNEL;**69**;JUL 10, 1995
 Q
PSET D PKILL F %ZISI=1:1 S %ZISZ=$T(Z+%ZISI) Q:%ZISZ=""  D
 . I $P(%ZISZ,";",6)="E" S %ZISX=$G(^%ZIS(2,IOST(0),$P(%ZISZ,";",5)))
 . E  S %ZISX=$P($G(^%ZIS(2,IOST(0),$P(%ZISZ,";",5))),"^",$P(%ZISZ,";",6))
 . S @$P(%ZISZ,";",3)=%ZISX
 Q
PKILL K IOBAROFF,IOBARON,IOCLROFF,IOCLRON,IODPLXL,IODPLXS,IOITLOFF,IOITLON,IOSMPLX,IOSPROFF,IOSPRON,IOSUBOFF,IOSUBON
 Q
 ;The following OLDPSET entry point is no longer used.
OLDPSET D PKILL F %ZISI=1:1 S %ZISZ=$T(Z+%ZISI) Q:%ZISZ=""  D SETDR^%ZISS
 D SET2^%ZISS1 G KV^%ZISS
 Q
Z ;;Variable name;Element number;Global subscript;Piece position;1=input key
IOBARON ;;IOBARON;60;BAR1;E
IOBAROFF ;;IOBAROFF;61;BAR0;E
IOCLRON ;;IOCLRON;67.21;CLR1;E
IOCLROFF ;;IOCLROFF;67.22;CLR0;E
IOSMPLX ;;IOSMPLX;1001;1001;1
IODPLXL ;;IODPLXL;1002;1001;2
IODPLXS ;;IODPLXS;1003;1001;3
IOSUBON ;;IOSUBON;65;SUB1;E
IOSUBOFF ;;IOSUBOFF;65.1;SUB0;E
IOSPRON ;;IOSPRON;65.2;SPR1;E
IOSPROFF ;;IOSPROFF;65.3;SPR0;E
IOITLON ;;IOITLON;66;I1;E
IOITLOFF ;;IOITLOFF;67;I0;E
