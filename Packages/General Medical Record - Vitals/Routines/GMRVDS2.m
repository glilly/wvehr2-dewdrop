GMRVDS2 ;HIRMFO/RM,YH-VITAL SIGNS DISPLAY ;3/9/99
 ;;4.0;Vitals/Measurements;**11**;Apr 25, 1997
PRNT ;
 S GMRI=0 F  S GMRI=$O(GMRLIN(GMRI)) Q:GMRI'>0  D WRT2^GMRVDS1:$D(GLOC)&(IOSL<($Y+6)) W ! F GMRJ=1:1:5 S GMRSP=$S(GMRJ'=1:7+((GMRJ-2)*16),1:0) D
 .I GMRI=1 W ?GMRSP,$P(GMRLIN(GMRI),"^",GMRJ)
 .E  D
 ..I GMRJ=1 W:'$D(GLIN($P(GMRLIN(GMRI),"^"))) ?GMRSP,$P(GMRLIN(GMRI),"^") S:'$D(GLIN($P(GMRLIN(GMRI),"^"))) GLIN($P(GMRLIN(GMRI),"^"))=""
 ..E  W ?GMRSP,$P(GMRLIN(GMRI),"^",GMRJ)
 Q
SETBP ;
 S GDT=GMRDAT,GDATE=GMRDAT+.00000014
 F  S GMRDAT=$O(^GMR(120.5,"AA",DFN,GMR(X),GMRDAT)) Q:GMRDAT'>0!(GMRDAT>GDATE)  S Y=0 F  S Y=$O(^GMR(120.5,"AA",DFN,GMR(X),GMRDAT,Y)) Q:Y'>0  I '$D(^GMR(120.5,+Y,2)) D:X="BP" SETNODE^GMRVDS0 D:X="P" SETP
 S GMRDAT=GDT K GDT,GDATE Q
STOP ;
 W !,"Return to continue " K X R X:DTIME I '$T!(X["^") S GMROUT=1
 W @IOF Q
SETP ;dISPLAY MULTIPLE PULSE
 S GMRL=$S($D(^GMR(120.5,Y,0)):^(0),1:"")
 N GG S GG=$P(GMRL,"^",8),OK=0 D  Q:'OK
 . I "REFUSEDPASSUNAVAILABLE"[$$UP^XLFSTR(GG) Q
 . I '$D(^GMR(120.5,Y,5,"B")) S OK=1 Q
 . I $D(^GMR(120.5,Y,5,"B",GAPICAL)) S OK=1 Q
 . I $D(^GMR(120.5,Y,5,"B",GBRACHI)) S OK=1 Q
 . I $D(^GMR(120.5,Y,5,"B",GRADIAL)) S OK=1
 S GMRL1=$P(GMRL,"^") ;adding trailing zeros to time if necessary
 S $P(GMRL1,".",2)=$P(GMRL1,".",2)_"0000"
 S $P(GMRL1,".",2)=$E($P(GMRL1,".",2),1,4)
 S $P(GMRL,"^")=GMRL1
 K GMRL1
 I GMRL'="" S GMRDATA(X,$P(GMRL,"^"),Y)=$P(GMRL,"^",8),GMRDATS=1 I $P($G(^GMR(120.5,Y,5,0)),"^",4)>0 D CHAR^GMRVCHAR(Y,.GMRVARY,GMR(X))
 Q
