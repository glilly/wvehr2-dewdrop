RMPFDX ;DDC/KAW-STATION ORDER INFORMATION [ 06/16/95   3:06 PM ]
 ;;2.0;REMOTE ORDER/ENTRY SYSTEM;;JUN 16, 1995
RMPFSET I '$D(RMPFMENU) D MENU^RMPFUTL I '$D(RMPFMENU) W !!,$C(7),"*** A MENU SELECTION MUST BE MADE ***" Q  ;;RMPFMENU must be defined
 I '$D(RMPFSTAN)!'$D(RMPFDAT)!'$D(RMPFSYS) D ^RMPFUTL Q:'$D(RMPFSTAN)!'$D(RMPFDAT)!'$D(RMPFSYS)
 W @IOF,!,"STATION ORDER INFORMATION" H 1
LIST S RMPFP="*",(RMPFORD,RMPFTP)="S" D ^RMPFDS1 K RMPFX D SEL:RMPFCX
 G END:$D(RMPFOUT)!'$D(RMPFX)
START D DISP,CONT G END:$D(RMPFOUT),START:$D(RMPFX),LIST
END K RMPFO,RMPFP,RMPFORD,RMPFTP,RMPFX,RMPFCX,RMPFOUT,RMPFQUT,RMPFS
 K T,J,L,Y,DFN,RMPFERR Q
CONT F I=1:1 Q:$Y>(IOSL-4)  W !
 W !!,"Type <P>rint,"
 I $O(^RMPF(791810,RMPFX,201,0)) W " <M>essages,"
 W " <RETURN> to continue or <^> to exit: " D READ K I
 I $D(RMPFOUT) K Y Q
 I $D(RMPFQUT) D MSG^RMPFDT Q
 I Y="" K RMPFX Q
 S Y=$E(Y,1) I "Pp"[Y D QUE K RMPFX Q
 I "mM"[Y D ^RMPFDT4 K RMPFX Q
QUE W ! S %ZIS="NPQ" D ^%ZIS G END:POP I IO=IO(0),'$D(IO("S")) D DISP,CONT G QUEE
 I $D(IO("S")) S %ZIS="",IOP=ION D ^%ZIS G DISP^RMPFDX
 S ZTRTN="DISP^RMPFDX",ZTSAVE("RMPF*")="",ZTIO=ION
 S ZTDESC="STATION ORDER INFO" D ^%ZTLOAD D HOME^%ZIS
 W:$D(ZTSK) !!,"*** Request Queued ***" H 2
QUEE K %,%T,%ZIS,POP,Y1,ZTSK,ZTDESC,ZTRTN,ZTIO,ZTSAVE Q
READ K RMPFOUT,RMPFQUT
 R Y:DTIME I '$T W $C(7) R Y:5 G READ:Y="." S:'$T Y=U
 I Y?1"^".E S (RMPFOUT,Y)="" Q
 S:Y?1"?".E (RMPFQUT,Y)=""
 Q
DISP D ^RMPFDT1
 Q
SEL W !!,"Select Order Number or <RETURN> to exit: " D READ
 Q:$D(RMPFOUT)
SEL1 I $D(RMPFQUT) W !!,"Type the number of the order you wish to view or <RETURN> to continue." G SEL
 Q:Y=""  I '$D(RMPFS(Y)) S RMPFQUT="" G SEL1
 S RMPFX=RMPFS(Y) Q
