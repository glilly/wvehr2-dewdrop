ABSVDLE2 ;EAP ALTOONA VOLUNTARY DONATIONS PROGRAM  ; 26 Sep 2001  2:04 PM
V ;;4.0;VOLUNTARY TIMEKEEPING;**25,26**;JULY 6, 1994
 ;CREATE MANUAL ENTRY IN FILE #503344
CREATE ;
 S DIC="^ABS(503344,",DLAYGO=503344,DIC(0)="LM" D NOW^%DTC S DT=X
GET L ^ABS("RECEIPT") S X=$S($D(^ABS("RECEIPT")):+^("RECEIPT")+1,1:1),^("RECEIPT")=X L  G:$D(^ABS(503344,X)) GET S DINUM=X D FILE^DICN G:+Y<0 GET
 W !!,"THIS TEMPORARY RECEIPT LOG ENTRY HAS BEEN ASSIGNED NUMBER: ",+Y S ABSVDA=+Y
EDIT S DIE=DIC,DA=+Y,DR="[ABSV DON MANUAL ENTRY]" D ^DIE I $D(Y) G END1
END1 ;
 Q
WRITE ;
 I '$D(ZHDATE) S ZHDATE=""
 S U="^" I '$D(IOM) S IOM=80
 W ! F I=1:1:IOM W "="
 I '$D(^ABS(503344,ABSVDA,0)) W !,"ENTRY IS NOT DEFINED IN FILE 503344" Q
 S ZH=^ABS(503344,ABSVDA,0) S ZHORG=$P(ZH,U,11) S ZHBY=$P(ZH,U,7) S ZHAMOU=$P(ZH,U,6) S ABSVDOLA=ZHAMOU D DOLL^ABSVDLE1 S ZHAMOU=ABSVDOLA
 I ZHORG="" W !,"NO ORGANIZATION NAME FOR THIS ENTRY" Q
 S ZHNAM=$P(^ABS(503334,ZHORG,0),U,2) S ZHNUM=$P(ZH,U,1) S ZHCITY=$P(ZH,U,4)
 W !,ZHNAM,?31,"| TYPE OF FUNDS | AMOUNT: ",ZHAMOU
 S ZHPOST="",ZHFUND="",ZHPURP=""
 I $D(^ABS(503344,ABSVDA,1)) S ZH1=^ABS(503344,ABSVDA,1) S ZHPOST=$P(ZH1,U,1) S ZHFUND=$P(ZH1,U,2) S ZHPURP=$P(ZH1,U,3)
 S ZHDON=$P(ZH,U,2) S ZHADD1=$P(ZH,U,3) S ZHADD2=$P(ZH,U,10)
 S $P(^ABS(503344,ABSVDA,0),U,5)="C" S $P(^ABS(503344,ABSVDA,0),U,8)=ZHDATE1 S $P(^ABS(503344,ABSVDA,0),U,9)="MANUAL ENTRY" I $D(ABSV("SITE")) S $P(^ABS(503344,ABSVDA,0),U,12)=ABSV("SITE")
 W !,ZHDON,?31,"|  CASH/CHECK   | ISSUED BY: ",ZHBY
 W !,ZHADD1,?31,"|               | DATE ISSUED: ",ZHDATE
 I ZHADD2'="" D ADDRESS G SKIP
 I ZHADD2="" D NOADD
SKIP W ! F I=1:1:IOM W "="
 I $D(^ABS(503342,ZHFUND,0)) S ZHFNUM=$P(^ABS(503342,ZHFUND,0),U,3) S ZHFUND=$P(^ABS(503342,ZHFUND,0),U,1)
 W !,"POST: ",ZHPOST,?31,"| FUND: ",ZHFUND," #",ZHFNUM
 W ! F I=1:1:IOM W "="
 W !,"PURPOSE OF DONATION: ",ZHPURP
 Q
END ;
 K ZH,ZH1,ZHFUND,ZHFNUM,ZHPOST,ZHADD1,ZHADD2,ZHDON,ZHBY,ZHDATE,ZHAMOU,ZHNAM
 K ZHDATE1,ABSVDOLA,ZHPURP,ZHNUM,ZHCITY,ZHORG,ABSVDA
 Q
ADDRESS ;
 W !,ZHADD2,?31,"|               | LOG FILE#: ",ZHNUM
 W !,ZHCITY,?31,"|               |"
 Q
NOADD ;
 W !,ZHCITY,?31,"|               | LOG FILE#: ",ZHNUM
 Q
