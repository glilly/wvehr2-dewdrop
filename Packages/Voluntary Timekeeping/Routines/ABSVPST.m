ABSVPST ;VAMC ALTOONA/CTB PRE/POST INIT ;10/22/01  3:41 PM
V ;;4.0;VOLUNTARY TIMEKEEPING;**25,28**;JULY 6, 1994
VHVS S DIC=503332,DIC(0)="MNZL",DLAYGO=1,X="135V",DIC("DR")="1///VETERANS HISTORY VOLUNTARY SERVICE;2///VHVS" D ^DIC
 I $P(Y,"^",3) S X="RECORD ADDED!" D MSG^ABSVQ QUIT
 I +Y S X="RECORD FOR '135V' ALREADY EXISTS, NO ACTION TAKEN!" D MSG^ABSVQ QUIT
 S X="UNABLE TO ADD RECORD '135V'.  PLEASE USE EXISTING TIMEKEEPING OPTION TO ADD RECORD MANUALLY." D MSG^ABSVQ QUIT
 QUIT
POST ;POST INIT TO DELETE 503340 IF IT EXISTS AND TO TRANSFER THE DATA FROM FILE 503461
 S X="Updating Cross References on File 503334 - VOLUNTEER ORGANIZATION CODES" D MSG^ABSVQ
 D WAIT^ABSVYN
 S DIK="^ABS(503334," D IXALL^DIK
 I $D(^DIZ(503461,0)) D XFER
 QUIT
XFER S ABSVXA="I See that you have data in the Class III NEW DONATIONS file.",ABSVXA(1)="Do you wish to transfer this data NOW",%=2 D ^ABSVYN
 I %=1 D XFER1 QUIT
 S X="You may transfer the data at anytime by executing the following:" D MSG^ABSVQ S X="     D XFER^ABSVPST" D MSG^ABSVQ
 QUIT
XFER1 I $P(^ABS(503340,0),"^",3)>0 D  I %'=1 S X="No action taken.*" D MSG^ABSVQ QUIT
 . S ABSVXA="This option will DELETE any existing entries!",ABSVXA(1)="Do you wish to continue",%=2 D ^ABSVYN
 . Q:%'=1
 . S ABSVXA="ARE YOU SURE YOU WANT TO CONTINUE",%=2 D ^ABSVYN
 . QUIT
 S X="TRANSFER INITIATED." D MSG^ABSVQ
 K ^ABS(503340)
 S %X="^DIZ(503461,",%Y="^ABS(503340," D %XY^%RCR
 S $P(^ABS(503340,0),"^",1,2)="DONATIONS^503340I"
 S X="TRANSFER COMPLETED." D MSG^ABSVQ
X S X="UPDATING TRANSFERRED DATA." D MSG^ABSVQ D WAIT^ABSVYN
 S DIC=503338,DIC(0)="AEMNZ",DIC("A")="Select VOLUNTARY STATION to be assigned to ALL records: " D ^DIC I Y<0 S X="NO ACTION TAKEN. PLEASE TRANSFER DATA LATER." D MSG^ABSVQ QUIT
 S ABSV("INST")=+Y,ABSV("SITE")=$P(Y(0),"^",9)
 S DA=0 F  S DA=$O(^ABS(503340,DA)) Q:'DA  D
 .Q:$P(^ABS(503340,DA,0),"^",2)=""
 .S X=$P(^(0),"^",2),ORG=$P($G(^ABS(503334,X,0)),"^") Q:ORG=""
 .S Y=$$NEXTORG^ABSVDENT(X)
 .S Z=$$LZF^ABSVU2(Y,4),Z=ABSV("SITE")_"-"_ORG_"-"_Z
 .S $P(^ABS(503340,DA,0),"^",1)=Z,$P(^(0),"^",15)=ABSV("INST")
 .QUIT
 S DIK="^ABS(503340," D IXALL^DIK
 QUIT
ASKFOR ;;;;RETURNS % WITH EITHER 1 FOR YES OR 0 FOR NO;;;;;
 S %A="Do you want to continue",%B="Enter Yes or No."
 W !,%A_"? (Y/N) // " R ANS:$S($D(DTIME):DTIME,1:300)
 I (ANS["?")!(ANS="") W !,%B G ASKFOR
 S ANS=$E(ANS,1) S %=$S(ANS="Y":1,ANS="y":1,1:0)
 K %A,%B,ANS
 Q
