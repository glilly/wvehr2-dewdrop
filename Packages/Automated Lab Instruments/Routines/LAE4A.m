LAE4A ;SLC/RWF - BECKMAN E4A ELECROTLYTE ANALYZER ;8/16/90  14:15 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK ID=TRAY_CUP
 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:'$D(^LA(TSK,"I",0))
 S SS="CH",IDT=0 D ^LASET Q:TSK<1  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
 F I=1:1:TC S LA(TC(I,4))=TC(I,1)
 S TOUT=0,ID=0,ERR=0,SAM="S"
LA2 D IN G QUIT:TOUT,LA9:IN'?1"[".ANP1"]".AN
 S TAG=+$E(IN,2,3) G NEW:TAG=1,DATA:TAG=2,ET:TAG=99,DATA:TAG=3,LA2
DATA D CS S ERR=ERR+ER,TEST=$E(IN,5,7) IF '$D(LA(TEST)) G LA2
 S X=$E(IN,9,14) D PACK S @LA(TEST)=X G LA2
CALC G DATA ;PROCESS SAME AS DATA
NEW S CUP=+$E(IN,17,18),TRAY=+$E(IN,20,21),ERR=0,ID=TRAY_CUP
 F I=1:1:TC S @TC(I,1)=""
 S TYPE=$E(IN,23,24),SAM=$E(IN,26)
IDQC D CS S ERR=ERR+ER
 G LA9:TYPE="CA",LA2
ET IF ERR>1!('ID) G LA9
 S SPEC=$S(SAM="S":$P(^LAB(69.9,1,1),U,3),SAM="U":$P(^LAB(69.9,1,1),U,2),1:0)
 X LAGEN
 F I=0:0 S I=$O(TV(I)) Q:I<1  D LA4
LA9 S REC="",IDE=0,ID=0 G LA2
LA4 S R=$S($D(TV(I,1)):TV(I,1),1:"")
 S:R]"" ^LAH(LWL,1,ISQN,I)=R Q
 Q
CS S CS=0,Y=$L(IN)-1 F I=1:1:Y-1 S CS=$A(IN,I)+CS
 S X=$A(IN,Y)-48 S:X>9 X=X-7 S ER=X*16,X=$A(IN,Y+1)-48 S:X>9 X=X-7 S X=X+ER
 S ER=CS+X#256
 Q
PACK S Y=X,X="" F I=1:1:$L(Y) S:$A(Y,I)-32 X=X_$E(Y,I)
 Q:X=""  S:X'?.P1N.NP X="*" Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) Q:TOUT>9  S TOUT=TOUT+1 H 9 G IN
 S IN=^(CNT),^(0)=CNT,TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
Z ;01A^4^01B^5^02A^7^03A^3^04A^6^05A^2^06A^1^10A^10^
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
