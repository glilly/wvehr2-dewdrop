LEXDD2 ; ISL Display Defaults - Build List        ; 09-23-96
 ;;2.0;LEXICON UTILITY;;Sep 23, 1996
 ;
BUILD ; Display defaults
 Q:+($G(LEXDUZ))<1  Q:'$D(^VA(200,LEXDUZ,0))
 I '$D(LEXLC) N LEXLC S LEXLC=0
 I '$D(LEXC) N LEXC S LEXC=""
APPS ; Get applications authorizing user defaults
 K LEXD S LEXD(0)=0 N LEXA,LEXAP
 D BLANK^LEXDD4,BLANK^LEXDD4,NAME^LEXDD4,BLANK^LEXDD4
 D SEV^LEXDD4,LOC^LEXDD4
 S LEXA="" F  S LEXA=$O(^LEXT(757.2,"ADEF",LEXA)) Q:LEXA=""  D
 . N LEXAP S LEXAP=0
 . F  S LEXAP=$O(^LEXT(757.2,"ADEF",LEXA,LEXAP)) Q:+LEXAP=0  D
 . . N LEXOK S LEXOK=$$FLD
 . . I LEXOK D
 . . . D BLANK^LEXDD4,APP^LEXDD4,BLANK^LEXDD4
 . . . S LEXDICS=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,1))
 . . . S LEXDICS(0)=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,1.5))
 . . . S LEXSHOW=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,2))
 . . . S LEXSHOW(0)=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,2.5))
 . . . S LEXSUB=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,3))
 . . . S LEXSUB(0)=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,3.5))
 . . . S LEXCTX=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,4))
 . . . S LEXCTX(0)=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,4.5))
 . . . D DEF^LEXDD4
 D DSPLY^LEXDD3
 Q
FLD(X) ;    User Defaults
 N LEX1,LEX2,LEX3,LEX4 S X=0
 S LEX1=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,1))
 S LEX2=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,2))
 S LEX3=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,3))
 S LEX4=$G(^LEXT(757.2,LEXAP,200,LEXDUZ,4))
 S:($L(LEX1)+$L(LEX2)+$L(LEX3)+$L(LEX4))>0 X=1
 Q X
