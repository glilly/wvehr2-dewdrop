NURSFMU ;HIRMFO/RM-NURSING FILEMAN ROUTINE ;10/05/95
 ;;4.0;NURSING SERVICE;;Apr 25, 1997
EN1 ; PRINT FILE ENTRIES
 D DICRW G:NURSOUT QUIT S L=1 D EN1^DIP
 G QUIT
EN2 ; SEARCH FILE ENTRIES
 D DICRW G:NURSOUT QUIT D EN^DIS
 G QUIT
EN3 ; INQUIRE FILE ENTRIES
 D DICRW G:NURSOUT QUIT D CHOOSE G:NURSOUT QUIT D SELECT G:NURSOUT QUIT D CAPT:NURSCH=1,NOCAPT:NURSCH=2 G QUIT
 G QUIT
EN4 ; LIST FILE ATTRIBUTES
 D DICRW G:NURSOUT QUIT D EN^DID
 G QUIT
QUIT D ^NURSKILL
 Q
DICRW ;
 D QUIT S NURSOUT=0,DIC=1,DIC(0)="AEQM",DIC("S")="I DUZ(0)=""@""!(+Y>124&(+Y<124.4))!(+Y>120.4&(+Y<120.58))!(+Y>209.999&(+Y<220))!(+Y=200)",DIC("A")="OUTPUT FROM WHAT FILE: " D ^DIC K DIC I +Y'>0 S NURSOUT=1
 E  S NURSDIC=+Y,DIC=$S($D(^DIC(+Y,0,"GL")):^("GL"),1:""),NURSOUT=$S(DIC']"":1,1:NURSOUT)
 Q
CHOOSE ;
 S DIC(0)="AEQM" S:$D(NURSCHOS) DIC("A")="ANOTHER ONE: " D ^DIC K DIC("A") I +Y'>0 S:$D(DTOUT)!$D(DUOUT)!'$D(NURSCHOS) NURSOUT=1 Q
 S NURSCHOS(+Y)=""
 G CHOOSE
SELECT ;
 W !,"STANDARD CAPTIONED OUTPUT" S %=1 D YN^DICN S:%=-1 NURSOUT=1 S NURSCH=% Q:NURSCH'=0  W !,$C(7),?3,"Answer YES if you want the report in standard captioned form,",!?3,"else answer NO."
 G SELECT
CAPT ;
 W !,"DISPLAY COMPUTED FIELDS" S %=2 D YN^DICN Q:%=-1
 I '% W !,$C(7),?3,"Answer YES if you wish to see the computed fields for this file,",!?3,"else answer NO." G CAPT
 S DIQ(0)=$S(%=1:"C",1:"")
 F DA=0:0 S DA=$O(NURSCHOS(DA)) Q:DA'>0  W ! D EN^DIQ,NEXT:$O(NURSCHOS(DA))>0 Q:NURSOUT
 Q
NEXT ;
 W !,"DO YOU WANT TO CONTINUE" S %="" D YN^DICN I '% W !,$C(7),?3,"Answer YES if you wish to see the rest,",!?3,"or NO if you wish to stop" G NEXT
 S:%=2!(%=-1) NURSOUT=1
 Q
NOCAPT ;
 S DHD=$S($D(^DIC(NURSDIC,0)):$P(^(0),"^")_" ",1:"")_"LIST",L=0,BY=".01",(FR,TO)="",DIS(0)="I $D(NURSCHOS(D0))" D EN1^DIP
 Q
