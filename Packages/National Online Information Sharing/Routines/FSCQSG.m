FSCQSG ;SLC/STAFF-NOIS Query Search Greater Than ;1/11/98  15:50
 ;;1.1;NOIS;;Sep 06, 1998
 ;
GT ; from FSCQS
 I LIST=1 D  Q
 .S NODE=+$P(CODE,";"),PIECE=$P(CODE,";",2)
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..S CVALUE=$P($G(^FSCD("CALL",CALL,NODE)),U,PIECE) I $L(CVALUE),CVALUE>VALUE X ACTION
 I LIST=0 D  Q
 .S CVALUE=VALUE F  S CVALUE=$O(^FSCD("CALL",INDEX,CVALUE)) Q:CVALUE=""  D
 ..S CALL=0 F  S CALL=$O(^FSCD("CALL",INDEX,CVALUE,CALL)) Q:CALL<1  D
 ...I $D(^TMP("FSC USELIST",$J,CALL)) X ACTION
 I LIST="" D  Q
 .S CVALUE=VALUE F  S CVALUE=$O(^FSCD("CALL",INDEX,CVALUE)) Q:CVALUE=""  D
 ..S CALL=0 F  S CALL=$O(^FSCD("CALL",INDEX,CVALUE,CALL)) Q:CALL<1  X ACTION
 Q
NGT ; from FSCQS
 I LIST=1 D  Q
 .S NODE=+$P(CODE,";"),PIECE=$P(CODE,";",2)
 .S CALL=0 F  S CALL=$O(^TMP("FSC USELIST",$J,CALL)) Q:CALL<1  D
 ..S CVALUE=$P($G(^FSCD("CALL",CALL,NODE)),U,PIECE) I $L(CVALUE),CVALUE'>VALUE X ACTION
 I LIST=0 D  Q
 .S CVALUE="" F  S CVALUE=$O(^FSCD("CALL",INDEX,CVALUE)) Q:CVALUE=""  Q:CVALUE>VALUE  D
 ..S CALL=0 F  S CALL=$O(^FSCD("CALL",INDEX,CVALUE,CALL)) Q:CALL<1  D
 ...I $D(^TMP("FSC USELIST",$J,CALL)) X ACTION
 I LIST="" D  Q
 .S CVALUE="" F  S CVALUE=$O(^FSCD("CALL",INDEX,CVALUE)) Q:CVALUE=""  Q:CVALUE>VALUE  D
 ..S CALL=0 F  S CALL=$O(^FSCD("CALL",INDEX,CVALUE,CALL)) Q:CALL<1  X ACTION
 Q
