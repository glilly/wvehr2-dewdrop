OCXOED09 ;SLC/RJS,CLA - Rule Editor (Element Expression Display) ;10/29/98  12:37
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**32**;Dec 17,1997
 ;;  ;;ORDER CHECK EXPERT version 1.01 released OCT 29,1998
 ;
 ;
S ;
 ;
 Q
EN(OCXD0,OCXD1,OCXR1) ;
 ;
 N OCXACT,OCXRD
 F  K OCXRD,OCXACT S (OCXRD,OCXACT)="" D DISP(OCXD0,OCXD1,.OCXRD,.OCXACT) Q:$$EN^OCXOED10(OCXD0,OCXD1,.OCXRD,.OCXACT)
 ;
 Q
 ;
DISP(OCXD0,OCXD1,OCXRD,OCXACT) ;
 ;
 N OCXTHLN,OCXTNLN,OCXTRLN,OCXTULN,OCXTNLN,OCXOPER
 ;
 S OCXTNLN=$C(27,91,48,109),OCXTRLN=$C(27,91,55,109),OCXTULN=$C(27,91,52,109),OCXTHLN=$C(27,91,49,109)
 ;
 D GETDATA(OCXD0,OCXD1,.OCXRD)
 ;
 W @IOF,OCXTNLN
 W !,$$CENTER($$FIELD("Element Expression Edit Screen"),80),!!
 I $D(OCXRD("ELE",OCXD0,"C",OCXD1,1,"I")) D
 .W "  ",$$OPT^OCXOEDT("DF"_OCXRD("ELE",OCXD0,"C",OCXD1,1,"I"),"EDDF","10",.OCXACT,$G(OCXRD("ELE",OCXD0,"C",OCXD1,1,"I")))
 W "  ",$$FIELD("Data Field:")," ",$$DATA($G(OCXRD("ELE",OCXD0,"C",OCXD1,1,"E")),60)
 W !
 S OCXOPER=$G(OCXRD("ELE",OCXD0,"C",OCXD1,2,"I")) I +OCXOPER S OCXOPER=$P($G(^OCXS(863.9,+OCXOPER,0)),U,4) I $L(OCXOPER) W "              ",$$FIELD("  Operator:")," ",$$DATA(OCXOPER,30) I 1
 E  W "              ",$$FIELD("  Operator:")," ",$$DATA($G(OCXRD("ELE",OCXD0,"C",OCXD1,2,"E")),30)
 I (+$G(OCXRD("ELE",OCXD0,"C",OCXD1,"PARNUM","I"))=2) D
 .W !
 .I $L($G(OCXRD("ELE",OCXD0,"C",OCXD1,3,"E"))) D  Q
 ..W "                   ",$$FIELD("    Value:")," ",$$DATA($G(OCXRD("ELE",OCXD0,"C",OCXD1,3,"E")),60)
 .W "                   ",$$OPT^OCXOEDT("DF"_$G(OCXRD("ELE",OCXD0,"C",OCXD1,4,"I")),"EDDF","10",.OCXACT,$G(OCXRD("ELE",OCXD0,"C",OCXD1,4,"I")))
 .W "  ",$$FIELD(" Data Field:")," ",$$DATA($G(OCXRD("ELE",OCXD0,"C",OCXD1,4,"E")),60)
 I (+$G(OCXRD("ELE",OCXD0,"C",OCXD1,"PARNUM","I"))=3) D
 .W !
 .W !,$$CENTER("AND",80)
 .W !
 .I $L($G(OCXRD("ELE",OCXD0,"C",OCXD1,3.1,"E"))) D  Q
 ..W "  ",$$FIELD("    Value:")," ",$$DATA($G(OCXRD("ELE",OCXD0,"C",OCXD1,3.1,"E")),60)
 .W "  ",$$OPT^OCXOEDT("DF"_OCXRD("ELE",OCXD0,"C",OCXD1,5,"I"),"EDDF","10",.OCXACT,$G(OCXRD("ELE",OCXD0,"C",OCXD1,5,"I")))
 .W "  ",$$FIELD(" Data Field:")," ",$$DATA($G(OCXRD("ELE",OCXD0,"C",OCXD1,5,"E")),60)
 W !!,"  ",$$OPT^OCXOEDT("Edit","EDEXP","10",.OCXACT,OCXD0_","_OCXD1)
 ;
 Q
 ;
CENTER(X,M) ;
 N SP S SP="",$P(SP," ",80)=" " Q $E(SP,1,((M\2)-($L(X)\2)))_X
 ;
SEP(OCXHDR) ;
 ;
 N SPACES S SPACES="",$P(SPACES," ",80-$L(OCXHDR))=" " Q OCXTNLN_OCXTHLN_OCXTULN_$G(OCXHDR)_SPACES_OCXTNLN
 ;
FIELD(OCXHDR) ;
 ;
 Q OCXTHLN_$G(OCXHDR)_OCXTNLN
 ;
DATA(OCXVAL,OCXLEN) ;
 ;
 N SPACES S SPACES="",$P(SPACES," ",OCXLEN+5)=" ",OCXVAL=$G(OCXVAL)
 I ($L(OCXVAL)>OCXLEN) Q $E(OCXVAL,1,OCXLEN-3)_"..."
 Q $E((OCXVAL_SPACES),1,OCXLEN)
 ;
GETDATA(OCXD0,OCXD1,OCXD) ;
 ;
 N OCXDIQ,OCXX,OCXDA,OCXOPER,OCXPF,OCXPFN
 S OCXDIQ="",OCXDA=OCXD0 D DIQ("^OCXS(860.3,",.OCXDA,"IEN",.OCXDIQ)
 M OCXD("ELE")=OCXDIQ(860.3) K OCXDIQ S OCXDIQ=""
 S OCXDIQ="",OCXDA=OCXD1,OCXDA(1)=OCXD0 D DIQ("^OCXS(860.3,"_OCXD0_",""COND"",",.OCXDA,"IEN",.OCXDIQ)
 M OCXD("ELE",OCXD0,"C")=OCXDIQ(860.31) K OCXDIQ S OCXDIQ=""
 S OCXD("ELE",OCXD0,"C",OCXD1,"PARNUM","I")=$$PARNUM(+$G(OCXD("ELE",OCXD0,"C",OCXD1,2,"I")))
 Q
PARNUM(OCXOPER) ;
 ;
 N OCXPF,OCXPFN
 S OCXPF=$O(^OCXS(863.9,+OCXOPER,"PAR","B","OCXO GENERATE CODE FUNCTION",0)) Q:'OCXPF 0
 S OCXPF=$G(^OCXS(863.9,+OCXOPER,"PAR",+OCXPF,"VAL"))
 Q:'$L(OCXPF) 0
 I OCXPF S OCXPFN=OCXPF
 E  S OCXPFN=0 F  S OCXPFN=$O(^OCXS(863.7,"B",$E(OCXPF,1,30),OCXPFN)) Q:'OCXPFN  Q:($P($G(^OCXS(863.7,+OCXPFN,0)),U,1)=OCXPF)
 Q:'OCXPFN 0 Q +$O(^OCXS(863.7,+OCXPFN,"PAR",999),-1)
 ;
DIC(OCXDIC,OCXDIC0,OCXDICA,OCXX,OCXDICS,OCXDR) ;
 ;
 N DIC,X,Y
 S DIC=$G(OCXDIC) Q:'$L(DIC) -1
 S DIC(0)=$G(OCXDIC0) S:$L($G(OCXX)) X=OCXX
 S:$L($G(OCXDICS)) DIC("S")=OCXDICS
 S:$L($G(OCXDICA)) DIC("A")=OCXDICA
 S:$L($G(OCXDR)) DIC("DR")=OCXDR
 D ^DIC Q:(Y<1) 0 Q Y
 ;
DIQ(DIC,DA,OCXDIQ0,OCXARY) ;
 N DR,DIQ S DR=".01:99999",DIQ="OCXARY(",DIQ(0)=$G(OCXDIQ0) D EN^DIQ1
 Q
 ;
