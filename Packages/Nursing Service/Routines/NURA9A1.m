NURA9A1 ;HIRMFO/JH/MD,FT-ACADEMIC DEGREE REPORT BY SVC. CATEGORY AND LOCATION ;8/8/96  13:53
 ;;4.0;NURSING SERVICE;**13**;Apr 25, 1997
CAT D HSKEEP G QUIT:$G(NUROUT)
 D EN3^NURSAGSP G QUIT:$G(NUROUT)
 D EN2^NURSAGP0 G QUIT:$G(NUROUT)
 D DEV G:POP!($D(ZTSK)) QUIT
START ;
 K ^TMP($J),^TMP("NURA",$J),^TMP("NURLOC",$J) U IO S (NURPAGE,NURSW1,NURQUIT)=0,NURSSP="",$P(NURSSP," ",80)=" "
 S NURSTIL="ACADEMIC EDUC. PROFILE BY LOCATION/SVC. CATEGORY",NURSTIL1=$E(NURSSP,1,17)_"SERVICE"_"    "_"EMPLOYEE"_$E(NURSSP,1,15)_" HIGHEST"
 S NURSTIL2="NO.   LOCATION   CATEGORY   NAME"_$E(NURSSP,1,19)_" ACAD. DEGREE"
 S NURROU=1,NRPT=1,NURSLEV=4 W ! D EN3^NURAAGS0,CHK G:$G(NUROUT) QUIT
 D PRINT^NURAGEN G:NX="^" QUIT
 S NURSORT(2)=3,NURSORT(3)="N(1)" D TOTALS
 G QUIT
POS ;ACADEMIC DEGREE REPORT BY SERVICE POSITION AND LOCATION
 D HSKEEP G QUIT:$G(NUROUT)
 W ! D EN2^NURSAGSP G QUIT:$G(NUROUT)
 D EN2^NURSAGP0 G QUIT:$G(NUROUT)
 D DEV G:POP!($D(ZTSK)) QUIT
START1 ;
 K ^TMP($J),^TMP("NURA",$J) U IO S (NURPAGE,NURSW1,NURQUIT)=0,NURSSP="",$P(NURSSP," ",132)=""
 S NURSTIL="ACADEMIC EDUC. PROFILE BY LOCATION/SVC. POSITION",NURSTIL1=$E(NURSSP,1,17)_"SERVICE    EMPLOYEE"_$E(NURSSP,1,16)_"HIGHEST"
 S NURSTIL2="NO.   LOCATION   POSITION   NAME"_$E(NURSSP,1,20)_"ACAD. DEGREE"
 S NURROU=2,NRPT=1,NURSLEV=5 W ! D EN4^NURAAGS0,CHK G:$G(NUROUT) QUIT
 D PRINT^NURAGEN G:NX="^" QUIT
 D TOTALS
 G QUIT
DEV ;
 S ZTDESC="Nursing Academic Education Profile by Location/Service "_$S(ANS2=1:"Category",1:"Position")
 S ZTRTN=$S(ANS2=1:"START^NURA9A1",ANS2=2:"START1^NURA9A1") D EN7^NURSUT0
 Q
QUIT D CLOSE^NURSUT1,^NURAKILL
 Q
CHK D NODATA^NURAGEN
 Q
HSKEEP ;
 D EN1^NURSAUTL Q:$G(NUROUT)
 I NURMDSW S DIC(0)="AEQZ",NURPLSCR=1 D EN5^NURSAGSP Q:$G(NUROUT)
 I NURMDSW=0,NURPLSW=1 S NURPLSCR=1 D PRD^NURSAGSP K NURPLSCR Q:$G(NUROUT)
 W ! D EN1^NURSAGSP
 Q
TOTALS ;
 I $Y>(IOSL-6) D HEADER^NURAGEN
 W !,?47,$$REPEAT^XLFSTR("-",33) D EN2^NURAED3 W !!,"TOTAL NUMBER OF ASSIGNMENTS ",?47,$J(NTCT,4,0),!!,"TOTAL NUMBER OF PERSONNEL",?47,$J(NTOT,4,0)
 Q
