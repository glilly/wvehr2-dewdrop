TIUSRVP2 ; SLC/JER - More RPCs related to note actions ;8/15/05
 ;;1.0;TEXT INTEGRATION UTILITIES;**184**;Jun 20, 1997
MAKEADD(TIUDADD,TIUDA,TIUX,SUPPRESS) ; Create addendum
 N DIE,DR,DA,DIC,X,Y,DLAYGO,TIUATYP,TIUCAN,TIUFPRIV,TIU S TIUFPRIV=1
 N PTIEN,SUCCESS
 S TIUCAN=$$CANDO^TIULP(TIUDA,"MAKE ADDENDUM")
 I TIUCAN'>0 S TIUDADD="0^You may not MAKE AN ADDENDUM for this "_$$STATUS^TIULC(TIUDA)_" "_$$PNAME^TIULC1(+$G(^TIU(8925,+TIUDA,0))) Q
 S TIUATYP=+$$WHATITLE^TIUPUTU("ADDENDUM")
 S (DIC,DLAYGO)=8925,DIC(0)="L",X=""""_"`"_TIUATYP_""""
 D ^DIC
 S TIUDADD=+Y
 I +Y'>0 S TIUDADD=TIUDADD_"^Could not create addendum." Q
 D GETTIU^TIULD(.TIU,TIUDA)
 S TIU("DOCTYP")=TIUATYP_U_$$PNAME^TIULC1(TIUATYP)
 S PTIEN=$P(^TIU(8925,TIUDA,0),U,2)
 D STUFREC^TIUSRVP1(+TIUDADD,.TIUX,PTIEN,+$G(TIUDA),TIUATYP,.TIU)
 K ^TIU(8925,+TIUDADD,"TEMP")
 M ^TIU(8925,+TIUDADD,"TEMP")=TIUX("TEXT") K TIUX("TEXT")
 D SETXT0^TIUSRVP(+TIUDADD)
 D FILE^TIUSRVP(.SUCCESS,+TIUDADD,.TIUX,+$G(SUPPRESS))
 I +SUCCESS'>0 D DIK^TIURB2(TIUDADD) S TIUDADD="-1^Could not create addendum." Q
 I +$O(^TIU(8925,+TIUDADD,"TEMP",0)) D MERGTEXT^TIUEDI1(+TIUDADD,.TIU)
 I '+$G(SUPPRESS) D RELEASE^TIUT(+TIUDADD,1)
 K ^TIU(8925,+TIUDADD,"TEMP")
 Q
SETCOS(TIUDA,TIUX,TIUD0,TIUD12) ; set cosig req
 N TIUDAD,TIUEXS,TIUNCS,TIUEXCS,TIURCS,TIUATT,TIUTTL,TIUDAD0
 S TIUEXS=$S(+$G(TIUX(1202)):+$G(TIUX(1202)),1:$P(TIUD12,U,4))
 S TIUNCS=$S(+$G(TIUX(1208)):+$G(TIUX(1208)),+$G(TIUX(1209)):+$G(TIUX(1209)),1:0)
 I TIUNCS S TIUX(1506)=$S(TIUNCS=TIUEXS:0,1:1) G SETCOSX
 S TIUEXCS=$P(TIUD12,U,8),TIUATT=$P(TIUD12,U,9)
 S TIUDAD=+$P(TIUD0,U,6),TIUDAD0=$G(^TIU(8925,+TIUDAD,0))
 I +$$ISDS^TIULX($S(+TIUDAD:+TIUDAD0,1:+TIUD0)) D  G SETCOSX
 . S TIUX(1506)=$S(TIUEXS=TIUEXCS:0,1:1)
 S TIUTTL=$S(+$G(TIUX(.01)):+$G(TIUX(.01)),1:+TIUD0)
 S TIUX(1506)=+$$REQCOSIG^TIULP(TIUTTL,TIUDA,TIUEXS)
SETCOSX S:'TIUX(1506) TIUX(1208)="@"
 Q
SIGN(ERR,TIUDA,TIUX) ; API for /es/
 N X,TIUACT,TIUSIGN,TIUD0,TIUD12,TIUSTAT,SIGNER,COSIGNER,VALID,XTRASGNR
 N TIUES S ERR=0
 S TIUD0=$G(^TIU(8925,+TIUDA,0)),TIUD12=$G(^TIU(8925,+TIUDA,12))
 S SIGNER=$P(TIUD12,U,4),COSIGNER=$P(TIUD12,U,8)
 I (DUZ'=SIGNER),(DUZ'=COSIGNER) S XTRASGNR=+$O(^TIU(8925.7,"AE",+TIUDA,+DUZ,0))
 I $G(XTRASGNR),+$P($G(^TIU(8925.7,$G(XTRASGNR),0)),U,4) S XTRASGNR=""
 I '$G(XTRASGNR) S XTRASGNR=$$ASURG^TIUADSIG(TIUDA)
 S TIUSTAT=+$P(TIUD0,U,5)
 S TIUACT=$S(TIUSTAT'>5:"SIGNATURE",+$G(XTRASGNR):"SIGNATURE",1:"COSIGNATURE")
 S TIUSIGN=$$CANDO^TIULP(TIUDA,TIUACT)
 I +TIUSIGN'>0 S ERR="89250004^"_$P(TIUSIGN,U,2) Q
 S VALID=$$VALIDATE($$DECRYP^XUSRB1(TIUX))
 I +VALID'>0 S ERR="89250005^"_$$EZBLD^DIALOG(89250005) Q
 S TIUES=1_U_$$GET1^DIQ(200,+DUZ,20.2)_U_$$GET1^DIQ(200,+DUZ,20.3)
 I '+$G(XTRASGNR) D ES^TIURS(TIUDA,TIUES)
 I +$G(XTRASGNR) D ADDSIG^TIURS1(TIUDA,XTRASGNR)
 I +$G(^TIU(8925,TIUDA,21)),(TIUACT="SIGNATURE") D AUDLINK^TIUGR1(TIUDA,"a",+$G(^TIU(8925,TIUDA,21)))
 Q
VALIDATE(X) ; Validate /es/-code
 N TIUY S TIUY=0
 D HASH^XUSHSHP I X]"",(X=$P($G(^VA(200,+DUZ,20)),U,4)) S TIUY=1
 Q TIUY
