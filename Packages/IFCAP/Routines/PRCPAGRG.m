PRCPAGRG ;WISC/RFJ-autogen print items with groups not selected     ;01 Dec 92
 ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 Q
 ;
 ;
NOGROUP ;  print groups not selected
 N %,%H,%I,DESCNSN,GNM,ITEMDA,NOW,PAGE,SCREEN,X,Y
 D NOW^%DTC S Y=% D DD^%DT S NOW=Y,PAGE=1,SCREEN=$$SCRPAUSE^PRCPUREP U IO D H
 S GNM="" F  S GNM=$O(^TMP($J,"PRCPAG","NOG",GNM)) Q:GNM=""!($G(PRCPFLAG))  D
 .   W !!?7,"GROUP: ",$S(GNM=" ":"<< GROUP NOT DEFINED >>",1:GNM),!
 .   S DESCNSN="" F  S DESCNSN=$O(^TMP($J,"PRCPAG","NOG",GNM,DESCNSN)) Q:DESCNSN=""  S ITEMDA=0 F  S ITEMDA=$O(^TMP($J,"PRCPAG","NOG",GNM,DESCNSN,ITEMDA)) Q:'ITEMDA!($G(PRCPFLAG))  D
 .   .   W !,ITEMDA,?7,$E($$DESCR^PRCPUX1(PRCP("I"),ITEMDA),1,54) W:PRCP("DPTYPE")="W" ?63,$$NSN^PRCPUX1(ITEMDA)
 .   .   I $Y>(IOSL-5) D:SCREEN P^PRCPUREP Q:$G(PRCPFLAG)  D H
 I $G(PRCPFLAG) Q
 D END^PRCPUREP
 Q
 ;
 ;
H S %=NOW_"  PAGE "_PAGE,PAGE=PAGE+1 I PAGE'=2!(SCREEN) W @IOF
 W $C(13),"AUTO-GENERATION GROUPS AND ITEMS NOT SELECTED",?(80-$L(%)),%,!?5,"FOR INVENTORY POINT: ",PRCP("IN"),!,"MI#",?7,"DESCRIPTION" W:PRCP("DPTYPE")="W" ?63,"NSN" S %="",$P(%,"-",81)="" W !,%
 Q
