QAOSPAUD ;HISC/DAD-OCCURRENCE SCREEN AUDIT TRAIL DISPLAY ;10/19/92  14:58
 ;;3.0;Occurrence Screen;;09/14/1993
ASKPAT ;
 K DIC S DIC="^QA(741,",DIC(0)="AEMNQ",DIC("A")="Select PATIENT: " W ! D ^DIC K DIC G:Y'>0 EXIT S QAOSD0=+Y
 D LOOP G ASKPAT
EXIT ;
 K DIC,QAOSD0,Y,QAOSAUDT,QAOSWHAT,L,BY,FR,TO,FLDS,DHD
 Q
LOOP ;
 D ENDISP^QAOUTL0
 S QAOSAUDT=+$G(^QA(741,QAOSD0,"AUDIT"))
 I $S(QAOSAUDT'>0:1,$D(^QA(740.5,QAOSAUDT,0))[0:1,1:0) W *7,!!?5,"*** NO AUDIT TRAIL EXISTS FOR THIS RECORD ***",!,*7 Q
 S L=0,DIC="^QA(740.5,",BY="@NUMBER",(FR,TO)=QAOSAUDT,FLDS=".02;""PATIENT"",1,.01;S1;C5,.03;C33,.02;C47,.04;C5;W70",DHD="OCCURRENCE SCREEN AUDIT TRAIL"
 D EN1^DIP
 Q
