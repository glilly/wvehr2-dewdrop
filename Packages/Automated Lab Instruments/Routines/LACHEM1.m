LACHEM1 ;SLC/DLG - TECHNICON CHEM1 UNIDIRECTIONAL AUTOMATED DATA ;7/20/90  07:46 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID OR IDE; USING LIS FORMAT 12
LA1 S:$D(ZTQUEUED) ZTREQ="@"
 S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 K TV,Y S TOUT=0,A=1 D IN G QUIT:TOUT,LA2:IN'["TT/CC" D QC
 S TOUT=0,BAD=0 F A=2:1 D IN,QC G QUIT:TOUT Q:IN=""
 I Y(1)["CONTROL" G LA2 ; ADDED SO NO CONTROLS WOULD BE SENT TO ^LAH
 F J=3:1 Q:'$D(Y(J))  F K=2:11:57 S IN=$E(Y(J),K,K+10),I=$P(IN,"=",1),V=+$P(IN,"=",2) S:I<10 I=$E(I,2) Q:I<1  Q:'$D(TC(I,1))  S @TC(I,1)=V ;V COULD BE MODIFIED ON THIS LINE
 S V=$E(Y(1),46,52) D NUM S ID=+V,V=$E(Y(1),28,30) D NUM S IDE=+V
LA3 X LAGEN G LA2:'ISQN ;Can be changed by the cross-link code
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S Y(A)=IN Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
OUT S CNT=^LA(TSK,"O")+1,^("O")=CNT,^("O",CNT)=OUT
 LOCK ^LA("Q") S Q=^LA("Q")+1,^("Q")=Q,^("Q",Q)=TSK LOCK
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ; ERROR TRAP
