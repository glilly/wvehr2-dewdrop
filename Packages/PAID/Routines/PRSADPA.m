PRSADPA ; HISC/REL-Display Data for Payroll ;9/15/95  09:42
 ;;4.0;PAID;;Sep 21, 1995
LV ; Display Leave Request
 K DIC S DIC("A")="Select EMPLOYEE: ",DIC(0)="AEQM",DIC="^PRST(458.1,",D="C" W ! D IX^DIC S DA=+Y K DIC
 G:DA<1 EX
 W ! K IOP,%ZIS S %ZIS("A")="Select Device: ",%ZIS="MQ" D ^%ZIS K %ZIS,IOP G:POP EX
 I $D(IO("Q")) S PRSAPGM="L1^PRSADPA",PRSALST="DA" D QUE^PRSAUTL G LV
 U IO D L1 D ^%ZISC K %ZIS,IOP G LV
L1 S DIC="^PRST(458.1," K DR D EN^DIQ Q
OT ; Display OT Request
 K DIC S DIC("A")="Select EMPLOYEE: ",DIC(0)="AEQM",DIC="^PRST(458.2,",D="C" W ! D IX^DIC S DA=+Y K DIC
 G:DA<1 EX
 W ! K IOP,%ZIS S %ZIS("A")="Select Device: ",%ZIS="MQ" D ^%ZIS K %ZIS,IOP G:POP EX
 I $D(IO("Q")) S PRSAPGM="O1^PRSADPA",PRSALST="DA" D QUE^PRSAUTL G OT
 U IO D O1 D ^%ZISC K %ZIS,IOP G OT
O1 S DIC="^PRST(458.2," K DR D EN^DIQ Q
ED ; Display Envir. Diff. Request
 K DIC S DIC("A")="Select EMPLOYEE: ",DIC(0)="AEQM",DIC="^PRST(458.3,",D="C" W ! D IX^DIC S DA=+Y K DIC
 G:DA<1 EX
 W ! K IOP,%ZIS S %ZIS("A")="Select Device: ",%ZIS="MQ" D ^%ZIS K %ZIS,IOP G:POP EX
 I $D(IO("Q")) S PRSAPGM="E1^PRSADPA",PRSALST="DA" D QUE^PRSAUTL G ED
 U IO D E1 D ^%ZISC K %ZIS,IOP G ED
E1 S DIC="^PRST(458.3," K DR D EN^DIQ Q
PP ; Display Pay Period
 K DIC S DIC("A")="Select EMPLOYEE: ",DIC(0)="AEQM",DIC="^PRSPC("
 W ! D ^DIC S DFN=+Y K DIC G:DFN<1 EX
 S TLE=$P($G(^PRSPC(DFN,0)),"^",8)
 S DIC="^PRST(458,",DIC(0)="AEQM",DIC("A")="Select PAY PERIOD: ",DIC("S")="I $D(^PRST(458,+Y,""E"",DFN))"
 W ! D ^DIC K DIC G:Y<1 EX S PPI=+Y
 W ! K IOP,%ZIS S %ZIS("A")="Select Device: ",%ZIS="MQ" D ^%ZIS K %ZIS,IOP G:POP EX
 I $D(IO("Q")) S PRSAPGM="P1^PRSADPA",PRSALST="DFN^PPI" D QUE^PRSAUTL G PP
 U IO D P1 D ^%ZISC K %ZIS,IOP G PP
P1 S DIC="^PRST(458,PPI,""E"",",(DA(1),D0)=PPI,(DA,D1)=DFN K DR D EN^DIQ Q
EX G KILL^XUSCLEAN
