LBRYRPT0 ;ISC2/DJM-LIBRARY TECHNICIAN REPORTS ;[ 05/23/97  12:13 PM ]
 ;;2.5;Library;**2**;Mar 11, 1996
MED ;MEDICAL TITLES BY SUBJECT OR WITHOUT & BY DETAILED OR BRIEF HOLDINGS
 S TEXT="Local Medical Holdings" D HEAD G EXIT:$G(DUOUT)=1
MED0 W "Do you want MEDICAL HOLDINGS listed by TITLE" S %=1 D YN^DICN
 G:%=1 MED1 G:%=2 MED4 G:%=-1 EXIT
 W !!,"Yes will list medical holdings by title."
 W !,"No will list medical holdings by subject.",!!
 G MED0
MED1 W !,"Do you want DETAILED HOLDINGS listed" S %=1 D YN^DICN
 S DIS(0)="I $P($G(^LBRY(680,D0,0)),U,4)=LBRYPTR"
 S FLDS=$S(%=1:"[LBRY MEDICAL HOLDINGS]",%=2:"[LBRY BRIEF MEDICAL HOLDINGS]",1:"") G:%=-1 EXIT G:%=0 MED3
MED2 S L=0,DIC="^LBRY(680,",BY="[LBRY MEDICAL HOLDINGS]" D EN1^DIP G EXIT
MED3 D YN G MED1
MED4 W !,"Do you want DETAILED HOLDINGS listed" S %=1 D YN^DICN
 S FLDS=$S(%=1:"[LBRY MEDICAL BY SUBJECT]",%=2:"[LBRY BRIEF MEDICAL/SUBJECT]",1:"") G:%=-1 EXIT G:%=0 MED5
 S L=0,DIC="^LBRY(680,",BY="[LBRY MEDICAL BY SUBJECT]"
 S DIS(0)="I $P($G(^LBRY(680,D0,0)),U,4)=LBRYPTR"
 D EN1^DIP G EXIT
MED5 D YN G MED4
MSC ;CURRENT MEDICAL SUBSCRIPTIONS BY SUBJECT WITH COST
 S TEXT="Medical Holdings with Cost" D HEAD G EXIT:$G(DUOUT)=1
 S L=0,DIC="^LBRY(680,",FLDS="[LBRY MEDICAL SUBSCRIPTIONS]"
 S DIS(0)="I $P($G(^LBRY(680,D0,0)),U,4)=LBRYPTR"
 S BY="[LBRY MEDICAL SUBSCRIPTIONS]" D EN1^DIP
 G EXIT
LDT ;LOCAL DONOR TITLES
 S TEXT="Local Donor Titles" D HEAD G EXIT:$G(DUOUT)=1
 S L=0,DIC="^LBRY(681,",FLDS="[LBRY DONOR TITLES]"
 S DIS(0)="I $P($G(^LBRY(681,D0,0)),U,4)=LBRYPTR"
 S BY="[LBRY DONOR TITLES]" D EN1^DIP
 G EXIT
LPS ;LISTING OF ALL CURRENT PATIENTS' SUBSCRIPTIONS
 S TEXT="Patients' Subscriptions" D HEAD G EXIT:$G(DUOUT)=1
 S L=0,DIC="^LBRY(680,",FLDS="[LBRY PATIENT HOLDINGS]"
 S BY="[LBRY PATIENT HOLDINGS]"
 S DIS(0)="I $S($O(^LBRY(680,D0,4,""B"",""G"",0)):1,$O(^LBRY(680,D0,4,""B"",""P"",0)):1,1:0)"
 S DIS(1)="I $P($G(^LBRY(680,D0,0)),U,4)=LBRYPTR"
 D EN1^DIP
 G EXIT
LAT ;LOCAL ADMINISTRATIVE TITLES BY LOCATION
 S TEXT="Local Administrative Titles" D HEAD G EXIT:$G(DUOUT)=1
 S L=0,DIC="^LBRY(680,",FLDS="[LBRY ADMIN TITLES]"
 S DIS(0)="I $P($G(^LBRY(680,D0,0)),U,4)=LBRYPTR"
 S BY="[LBRY ADMIN TITLES]" D EN1^DIP
 G EXIT
LPT ;ALL PATIENT HOLDINGS, BRIEF OR DETAILED HOLDINGS
 S TEXT="Patient Library Holdings" D HEAD G EXIT:$G(DUOUT)=1
LPT0 W "Do you want all general and patient health education titles"
 S %=1 D YN^DICN G:%=1 LPT1 G:%=2 LPT4 G:%=-1 EXIT
 W !!,"Yes will list all non-medical titles."
 W !,"No will list all patient health education titles only.",!!
 G LPT0
LPT1 W !,"Do you want DETAILED HOLDINGS listed" S %=1 D YN^DICN
 S FLDS=$S(%=1:"[LBRY PATIENT TITLES]",%=2:"[LBRY PATIENT TITLES BRIEF]",1:"") G:%=-1 EXIT G:%=0 LPT3
LPT2 S L=0,DIC="^LBRY(680,",BY="[LBRY PATIENT TITLES]"
 S DIS(0)="I $S($O(^LBRY(680,D0,4,""B"",""G"",0)):1,$O(^LBRY(680,D0,4,""B"",""P"",0)):1,1:0)"
 S DIS(1)="I $P($G(^LBRY(680,D0,0)),U,4)=LBRYPTR"
 D EN1^DIP G EXIT
LPT3 D YN G LPT1
LPT4 W !,"Do you want DETAILED HOLDINGS listed" S %=1 D YN^DICN
 S FLDS=$S(%=1:"[LBRY PATIENT TITLES]",%=2:"[LBRY PATIENT TITLES BRIEF]",1:"") G:%=-1 EXIT G:%=0 LPT5
 S L=0,DIC="^LBRY(680,",BY="[LBRY PATIENT TITLES BRIEF]"
 S DIS(0)="I $P($G(^LBRY(680,D0,0)),U,4)=LBRYPTR"
 S DHD=$S(%=1:"Local Patient Holdings, PHE only",1:"Local Patient Brief Holdings, PHE only")
 D EN1^DIP G EXIT
LPT5 D YN G LPT4
ART ;LISTING OF ADMINISTRATIVE TITLES WITH RECIPIENTS
 S TEXT="Administrative Recipients" D HEAD G EXIT:$G(DUOUT)=1
 S L=0,DIC="^LBRY(681,",FLDS="[LBRY ADMIN RECIPIENTS]"
 S DIS(0)="I $P($G(^LBRY(681,D0,0)),U,4)=LBRYPTR"
 S BY="[LBRY ADMIN RECIPIENTS]" D EN1^DIP
 G EXIT
TUL ;UNION LISTS WITH COMPLETE OR BRIEF HOLDINGS
 S TEXT="Union List with Holdings" D HEAD G EXIT:$G(DUOUT)=1
TUL0 W "Do you want DETAILED HOLDINGS listed" S %=1 D YN^DICN
 S FLDS=$S(%=1:"[LBRY UNION LIST CODE]",%=2:"[LBRY UNION LIST BRIEF]",1:"") G:%=-1 EXIT G:%=0 TUL1
 S DIS(0)="I $P($G(^LBRY(680,D0,0)),U,4)=LBRYPTR"
 S L=0,DIC="^LBRY(680,",BY="[LBRY UNION LIST CODE]" D EN1^DIP
 G EXIT
TUL1 D YN G TUL0
NLT ;NEW LOCAL TITLES IN TITLE AUTHORITY FILE
 S TEXT="New Local Titles" D HEAD G EXIT:$G(DUOUT)=1
 S L=0,DIC="^LBRY(680.5,",FLDS="[LBRY NEW LOCAL TITLES]"
 S BY="[LBRY NEW LOCAL TITLES]" D EN1^DIP G EXIT
HEAD I $G(LBRYPTR)="" S DUOUT=1 Q
 W @IOF,"VA Library Serials "_TEXT_" for "_LBRYNAM_"   "
 S Y=DT X ^DD("DD") W Y,!!
 Q
YN W !!,"Yes will present a complete description of the title's holdings."
 W !,"No will present a brief description of the title's holdings.",!
 Q
EXIT I IOT="TRM" R !!,"Press return to continue: ",LBRYC:DTIME
 K L,DIC,FLDS,BY,TEXT,%,Y,DIS,DHD,LBRYC,DUOUT
 Q
