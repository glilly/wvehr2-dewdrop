FBNHRCS3 ;ACAMPUS/dmk-RCS 10-0168 (18-3) ;1/14/98
 ;;3.5;FEE BASIS;**12**;JAN 30, 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
 ; print out report with data in ^TMP($J,"FBTOT",
 ;
 Q:'$O(^TMP($J,"FBTOT",0))
 N I,J,FBOUT
 S I=0
 D HED
 F  S I=$O(^TMP($J,"FBTOT",I)) Q:'I!$G(FBOUT)  D  Q:$G(FBOUT)
 . F J=1:1:12 W !,$P($T(FIELD+J),";;",2),?55,$S(J=12:$E($P(^(I),U,J),4,5)_$E($P(^(I),U,J),2,3),1:$P(^(I),U,J))
 .I $E(IOST,1,2)="C-" S DIR(0)="E" D ^DIR K DIR S:'Y!($D(DIRUT)) FBOUT=1 Q:$G(FBOUT)  D HED:$O(^TMP($J,"FBTOT",+$G(I))) Q
 .W !!
 .I ($Y+23)>IOSL,$O(^TMP($J,"FBTOT",+$G(I))) W @IOF D HED
 Q
 ;
FIELD ;fields to be printed
 ;; [1] THREE DIGIT STATION NUMBER
 ;; [2] NAME OF COMMUNITY NURSING HOME
 ;; [3] NAME OF CITY WHERE NURSING HOME IS LOCATED
 ;; [4] STATE CODE WHERE NURSING HOME IS LOCATED
 ;; [5] COUNTY WHERE NURSING HOME IS LOCATED
 ;; [6] NUMBER OF BEDS IN NURSING HOME (Skilled)
 ;; [7] NURSING HOME INSPECTED OR ACCREDITED
 ;; [8] PER DIEM RATE (High)
 ;; [9] PER DIEM RATE (Low)
 ;;[10] CERTIFIED FOR MEDICARE/MEDICAID
 ;;[11] NUMBER OF VETERANS IN HOME
 ;;[12] DATE OF LAST ASSESSMENT
 Q
 ;
HED I $E(IOST,1,2)="C-" W @IOF
 W !!,$$CJ^XLFSTR("COMMUNITY NURSING HOME 10-0168 (18-3) REPORT",80)
 W !?20,"FROM DATE: ",$$DATX^FBAAUTL(FBBEG),"    TO DATE: ",$$DATX^FBAAUTL(FBEND)
 W !?16,">>> NOTE: FIELDS 7, 10, 12 are current data <<<"
 W !,$$CJ^XLFSTR($$REPEAT^XLFSTR("=",46),80),!!
 Q
