LAMIVTKC ;SLC/DLG - VITEK PROTOCOL CONTROLLER ;7/20/90  09:40 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;;
 ;Call with T set to Instrument data is to/from
 ; P1= RESET POINT FOR INCOMING RECORDS, P3=Reset point FOR RECORDS SENT
RCHK K LATYPE S:IN'["~" LATYPE="X" S:'$D(LATYPE) LATYPE=$E(IN,$F(IN,"~")) Q:"BCDEFUX^]"'[LATYPE  S LATYPE=$S(LATYPE="]":"GS",LATYPE="^":"RS",1:LATYPE) D @LATYPE
 Q
B Q  ;RECIEVED STX
C Q  ;RECIEVED ETX
D I $D(^LA(T,"O",0)),^LA(T,"O")'=^LA(T,"O",0) S K=1 D OUT Q  ;RECIEVED EOT
 Q
E S ^LA(T,"P1")=CNT+2,OUT=$C(6),%=OUT
 ;I ^LA(T,"O",^LA(T,"P3"))[$C(29) S ^LA(T,"O",0)=^LA(T,"P2") L ^LA(T) S Q=^LA("Q")+1,^("Q")=Q,^LA("Q",Q)=T L  ;OUTPUT WAS HUNG RESET FOR RETRANSMISSION
 S T=T-BASE Q  ;RECIEVED ENQ
F S O=^LA(T,"O",0),^LA(T,"P3")=$S(^LA(T,"O",O)[$C(2):O+1,1:O) S K=1 D OUT Q  ;RECIEVED ACK
GS D CKSUM Q  ;GS RECORD NEXT RECORD SHOULD BE X TYPE LENGTH 2
RS D CKSUM Q  ;RECIEVED RS DATA PACKET
U S ^LA(T,"O",0)=^LA(T,"P3"),K=1 D OUT Q  ;RECIEVED NAK
X D CKSUM I $L(IN)=2 S OUT=$S(LASUM=LASUM1:$C(6),1:$C(21)),%=OUT S:LASUM=LASUM1 ^LA(T,"P1")=CNT+1 S T=T-BASE K LASUM,LASUM1 Q  ;RECIEVED GS CKSUM PACKET
 Q
CKSUM S:'$D(LASUM) LASUM=0
 S LASUM=$S(LATYPE="RS":30,LATYPE="GS":29,LATYPE="X":23,1:0)+LASUM
 I LATYPE="X",($L(IN)>2) F I=1:1:$L(IN) S LASUM=LASUM+$A(IN,I)
 I LATYPE="X",($L(IN)=2) S LASUM=LASUM-23,LASUM=LASUM#256,LASUM1=$F("0123456789abcdef",$E(IN,1))-2*16+($F("0123456789abcdef",$E(IN,2))-2)
 Q
OUT D NEXT Q:'$D(^LA(T,"O",O))  Q:%[$C(29)  ;Q:%[$C(4)  Q:%[$C(5)
 S K=K+1 G OUT Q
NEXT S O=^LA(T,"O",0)+K Q:'$D(^(O))  S %=^(O)
 L ^LA("Q") S Q=^LA("Q")+1,^("Q")=Q,^("Q",Q)=T L  Q
ACK S LASUM1=$F("0123456789abcdef",$E(IN,121))-2*16+($F("0123456789abcdef",$E(IN,122))-2)
 S LASUM=0 F I=1:1:120 S LASUM=LASUM+(255-$A(IN,I)+1)
 S LASUM=LASUM#256,OUT=$S(LASUM=LASUM1:$C(6),1:$C(21)),%=OUT S T=T-BASE Q
