ABSV04 ;VAMC ALTOONA/CTB - CREATE SINGLE TT04 FOR ONE VOL ;5/27/93  10:37 AM
V ;;4.0;VOLUNTARY TIMEKEEPING;;JULY 6, 1994
ONE(DA,INST,SITE) ;CREATE TT04 FOR ONE VOLUNTEER
 ;DA=INTERNAL NUMBER IN 503330
 ;INST=POINTER TO INSTITUTION FILE
 ;SITE=ENTERNAL VALUE OF VOLUNTARY SITE NUMBER
 ;
 ;GET VOLUNTEER & STATION LEVEL VARIABLES LEVEL VARIABLES
 ;
 N I,X,N,LNAME,FNAME,FULLNAME,SSN,STATION,SITEYEAR,TOTHOURS,LSTAWD,DTLSTAWD,COMB,STRING,PSEUDO
 ;
 S DIC=503330,DR=".01;1;23;.1",X="FULLNAME;SSN,I;PSEUDO;STATION"
 S DR(503330.01)="14;15;16;17",DA(503330.01)=INST,X(503330.01)="SITEYEAR;TOTHOURS;LSTAWD;DTLSTAWD,I"
 D EXT^ABSVU2(DIC,.DA,.DR,.X,"") K DIC,DR,X
 S LNAME=$E($P(FULLNAME,","),1,14),FNAME=$E($P(FULLNAME,",",2),1,10)
 S DTLSTAWD=$E(DTLSTAWD,4,5)_$E(DTLSTAWD,2,3)
 ;BUILD STRING
 S STRING="04"_$$RBF^ABSVU2(SITE,4)_$$RBF^ABSVU2(PSEUDO,1)_$$LBF^ABSVU2(SSN,9)_$$LBF^ABSVU2(" ",24)_$$LZF^ABSVU2(SITEYEAR,2)
 S STRING=STRING_$$LZF^ABSVU2(TOTHOURS,5)_$$LZF^ABSVU2(LSTAWD,5)_$$LBF^ABSVU2(DTLSTAWD,4)_$$RBF^ABSVU2(LNAME,14)_$$RBF^ABSVU2(FNAME,10)
 Q STRING_"$"
