QAMPINQ2 ;HISC/DAD-INQUIRE: GROUP FILE ;11/13/92  08:19
 ;;1.0;Clinical Monitoring System;;09/13/1993
EN1 D XIT S QAQDIC="^QA(743.5,",QAQDIC(0)="AEMNQ",QAQDIC("A")="Select GROUP: ",QAQUTIL=743.5 D ^QAQSELCT G:QAQQUIT EXIT
 K %ZIS,IOP S %ZIS="MQ",%ZIS("B")="HOME" W ! D ^%ZIS G:POP EXIT I $D(IO("Q")) K IO("Q") S ZTRTN="ENTSK^QAMPINQ2",ZTSAVE("^UTILITY($J,")="",ZTDESC="Group file inquire" D ^%ZTLOAD G EXIT
ENTSK ;
 S QAMQUIT=0,QAMGTXT="" K QAMUNDL S $P(QAMUNDL,"=",81)="" U IO
 F  S QAMGTXT=$O(^UTILITY($J,743.5,QAMGTXT)) Q:QAMGTXT=""!QAMQUIT  F QAMD0=0:0 S QAMD0=$O(^UTILITY($J,743.5,QAMGTXT,QAMD0)) Q:QAMD0'>0!QAMQUIT  D LOOP0
EXIT ;
 W ! D ^%ZISC
XIT K %ZIS,DIR,POP,QAM,QAMD0,QAMD1,QAMEMBR,QAMFILE,QAMGROUP,QAMGTXT,QAMPAGE,QAMQUIT,QAMUNDL,QAQDIC,QAQQUIT,QAQUTIL,X,Y,ZTRTN,ZTSAVE,^UTILITY($J,743.5)
 S:$D(ZTQUEUED) ZTREQ="@"
 Q
LOOP0 ;
 S QAM=$S($D(^QA(743.5,QAMD0,0))#2:^(0),1:"") Q:QAM=""  S QAMGROUP=$P(QAM,"^"),QAMFILE=$P(QAM,"^",2),QAMFILE=$S($D(^DIC(+QAMFILE,0))#2:$P(^(0),"^"),1:QAMFILE)
 S QAMPAGE=1 D HEAD W !,"GROUP: ",QAMGROUP,?39,"FILE: ",QAMFILE,!!,"MEMBERS:"
 S QAMEMBR="" F  S QAMEMBR=$O(^QA(743.5,QAMD0,"GRP","B",QAMEMBR)) Q:QAMEMBR=""!QAMQUIT  F QAMD1=0:0 S QAMD1=$O(^QA(743.5,QAMD0,"GRP","B",QAMEMBR,QAMD1)) Q:QAMD1'>0!QAMQUIT  D LOOP1
 I 'QAMQUIT W !!,QAMUNDL D WAIT
 Q
LOOP1 ;
 S QAM=$S($D(^QA(743.5,QAMD0,"GRP",QAMD1,0))#2:$P($P(^(0),"^"),";"),1:"") W:QAM]"" !?5,QAM D:$O(^QA(743.5,QAMD0,"GRP","B",QAMEMBR))]"" PAUSE
 Q
PAUSE Q:$Y'>(IOSL-4)  D WAIT,HEAD:'QAMQUIT
 Q
HEAD W:(QAMPAGE>1)!($E(IOST)="C") @IOF W !,QAMUNDL,! S QAMPAGE=QAMPAGE+1
 Q
WAIT I $E(IOST)="C" K DIR S DIR(0)="E" D ^DIR S QAMQUIT=$S(Y'>0:1,1:0)
 Q
