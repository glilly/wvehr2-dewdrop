YTMCMI ;SLC/DKG-TEST PKG: MILLON SCALES ; 10/20/88  09:03 ;
 ;;5.01;MENTAL HEALTH;;Dec 30, 1994
 ;
 S YSTY="TL" D ^YTREPT,YSSRT
 I $E(YSSR)=4!($E(YSSR,1,2)["7") F J=9:1:13,15 S YSRR(J)=YSRR(J)+$S(J=9!(J=11):8,J=10:16,1:28),YSAS(J)=$S(J=9!(J=11):8,J=10:16,1:28)
 I $E(YSSR,1,2)["2"!($E(YSSR,1,2)["8") F J=9:1:11 S YSRR(J)=YSRR(J)-9 S:$D(YSAS(J)) YSAS(J)=YSAS(J)-9 S:'$D(YSAS(J)) YSAS(J)=-9
 S K=0 F J=1:1:8 S K=K+$P(R,U,J)
 S YSAD=$S(K<110:110-K,K<132:0,1:131-K/3) I YSAD#1 S YSAD=YSAD\1-1
 I YSAD F J=9:1:20 S YSRR(J)=YSRR(J)+YSAD,YSWF(J)=YSAD
BOTH ;
 S S="" F J=1:1:21 S S=S_YSRR(J)_"^"
 S YSII="I       I           I       I    I      I"
 S YSXX="XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
 S X=$P(^YTT(601,YSTEST,"P"),U) D DTA^YTREPT W !!?(72-$L(X)\2),X,!!
 I K<94!(K>165) W !,"*** THIS PROFILE IS INVALID  - TOTAL RAW SCORE SCALES 1-8 ",$S(K<94:"LESS THAN 94",K>165:"GREATER THAN 165",1:"")," ***",!
 E  S K=$P(R,U,21) W "VALIDIY SCORE = ",K W ?20 W:K "***" W " THIS PROFILE IS ",$S(K=0:"VALID",K=1:"UNRELIABLE",1:"INVALID") W:K " ***" W !
 W !?7,"S C A L E",?23,"RAW WF AS BR",?47,"PROFILE OF BR SCORES",!?35,"20      35          60      75   85     100",!?36,YSII,!
 S YSLFT=0 F YSSC=1:1:20 D YSSCL Q:YSLFT
 G:YSLFT END
 ;I IOST?1"C-".E D WAIT I YSLFT G END
 ;W !?36,YSII,!!?25,"--- ITEM RESPONSES ---",!! S YSIT=1,X=^YTD(601.2,YSDFN,1,YSET,1,YSED,1)
 ;S A=$L(X),B=A\10 I B S K=10 F I=1:1:B D RLN
 ;S K=-10*B+A I K D RLN
END ;
 K A,B,I,J,K,R,S,X,YSAD,YSAS,YSBR,YSII,YSIT,YSKK,YSNAM,YSRR,YSRS,YSSC,YSSR,YSTY,YSXR,YSXX,YSWF Q
RLN ;
 W ?1 F YSKK=1:1:K W $J(YSIT,3,0)," ",$E(X,YSIT),"  " S YSIT=YSIT+1
 W ! Q
YSSRT ;
 F J=1:1:21 S YSRR(J)=$P(S,U,J)
 K YSRS F J=1:1:8 S K=130-YSRR(J) S:'$D(YSRS(K)) YSRS(K)="" S YSRS(K)=YSRS(K)_J
 S YSSR="",K=0 F  S K=$O(YSRS(K)) Q:'K  S YSSR=YSSR_YSRS(K)
 K YSRS Q
YSSCL ;
 S YSBR=$P(S,U,YSSC),YSRS=$P(R,U,YSSC),YSNAM=$P(^YTT(601,YSTEST,"S",YSSC,0),U,2)
 S YSXR=$S(YSBR<20:20,YSBR<101:YSBR,1:100),YSXR=YSXR-19\2+1
 D:IOST?1"C-".E WAIT:$Y>(IOSL-4) Q:YSLFT  W !,YSNAM,?24,$J(YSRS,2,0),$S($D(YSWF(YSSC)):$J(YSWF(YSSC),3,0),1:"   "),$S($D(YSAS(YSSC)):$J(YSAS(YSSC),3,0),1:"   "),$J(YSBR,3,0)," ",$E(YSXX,1,YSXR),$E(YSII,YSXR+1,41)
 W:YSSC=8!($P(^YTT(601,YSTEST,0),U)="MCMI"&(YSSC=11))!($P(^(0),U)="MILL"&((YSSC=14)!(YSSC=17))) !?36,YSII Q
WAIT ;
 ;  Added 5/6/94 LJA
 N A,B,B1,C,D,E,E1,F,F1,G,G1,H,I,J,J1,J2,J3,J4,K,L,L1,L2,M,N
 N N1,N2,N3,N4,P,P0,P1,P3,R,R1,S,S1,T,T1,T2,TT,V,V1,V2,V3
 N V4,V5,V6,W,X,X0,X1,X2,X3,X4,X7,X8,X9,Y,Y1,Y2,Z,Z1,Z3
 ;
 F I0=1:1:(IOSL-$Y-2) W !
 N DTOUT,DUOUT,DIRUT
 S DIR(0)="E" D ^DIR K DIR S YSTOUT=$D(DTOUT),YSUOUT=$D(DUOUT),YSLFT=$D(DIRUT)
 W @IOF Q
