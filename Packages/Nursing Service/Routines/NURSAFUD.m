NURSAFUD ;HIRMFO/RM,MD-SITE FILES UPDATE ;1/24/96
 ;;4.0;NURSING SERVICE;;Apr 25, 1997
EN3 ; ENTRY FROM NURSFL-LOC NURS LOCATION EDIT
 S X=$G(^DIC(213.9,1,"OFF")) Q:X=""!(X=1)
 S DLAYGO=211.4,DIC="^NURSF(211.4,",DIC(0)="AEMQLZ" W ! D EN1^NURSAFLL G:+Y'>0 Q3 S NURSREV=0 D EN1^NURSALED G EN3
Q3 D ^NURSKILL Q
EN4 ; ENTRY FROM NUREFL-MI NURS MI FILE EDIT
 S X=$G(^DIC(213.9,1,"OFF")) Q:X=""!(X=1)
 W !!,"TO EDIT MANDATORY INSERVICE DATA, USE THE EDUCATION TRACKING SOFTWARE."
 Q
EN5 ; ENTRY FROM NURAFL-CERT CERTIFICATION FILE EDIT
 S X=$G(^DIC(213.9,1,"OFF")) Q:X=""!(X=1)
 W ! S DLAYGO=212.2,DIC="^NURSF(212.2,",DIC(0)="AELQMNZ",DIC("A")="Select Type of Certification: " D ^DIC K DIC G:+Y'>0 Q5 S DIE="^NURSF(212.2,",DR=".01;1;2;3",DA=+Y D ^DIE K DIE G EN5
Q5 D ^NURSKILL Q
EN6 ; ENTRY FROM NURAFL-CLBK CLINICAL BACKGROUND FILE EDIT
 S X=$G(^DIC(213.9,1,"OFF")) Q:X=""!(X=1)
 W ! S DLAYGO=211.5,DIC="^NURSF(211.5,",DIC(0)="AELQMNZ",DIC("A")="Enter Type of Background/Experience: " D ^DIC K DIC G:+Y'>0 Q6 S DIE="^NURSF(211.5,",DR=".01T~;",DA=+Y D ^DIE K DIE G EN6
Q6 D ^NURSKILL Q
EN7 ; ENTRY FROM NURAFL-GS-COD OPTION (LOAD GRADE/STEP CODES)
 S X=$G(^DIC(213.9,1,"OFF")) Q:X=""!(X=1)
 W !,"Select GRADE/STEP Code: " R NURX:DTIME I "^"[NURX G Q7
 I NURX["?" D EN2^NURSUT4 G EN7
 S X=NURX,DIC=211.1,DIC(0)="EQZ",DIC=211.1 D ^DIC K DIC
 K:$L(X)>10!($L(X)<4)!'(X'?1P.E)!(X'?.ANP) X ;repeat input transform to kill X
 I +Y'>0,$G(X)'="" D  G:$G(NUROUT) Q7
 .  S Y=$P($G(^NURSF(211.1,0)),U,4) W !,$C(7),"Are you adding '"_X_"' AS A NEW NURS PAY SCALE (the "_(Y+1)_"nd)" K % D YN^DICN I %=2 S NUROUT=1 Q
 .  K DD,DO S DLAYGO=211.1,DIC="^NURSF(211.1,",DIC(0)="EQL" D FILE^DICN I '+Y S NUROUT=1 Q
 .  Q
 G:+Y'>0 Q7 S DA=+Y,DIE=211.1,DR=".01" D ^DIE G:$G(DTOUT)!($G(DUOUT)) Q7 G EN7
Q7 D ^NURSKILL Q
