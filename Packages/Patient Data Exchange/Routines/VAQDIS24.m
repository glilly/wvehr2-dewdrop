VAQDIS24 ;ALB/JFP - PDX, BUILDS DISPLAY ARRAY FOR MAS DATA ;01MAR93
 ;;1.5;PATIENT DATA EXCHANGE;**5**;NOV 17, 1993
SCR2 ; -- Builds MAS DATA SCREEN 2,PATIENT DATA
ROW0 ; -- HEADER
 D BLANK^VAQDIS20
 S VAQLN=$$REPEAT^VAQUTL1(" ",79)
 S VAQCTR=" -- PATIENT DATA -- "
 S X=$$CENTER^VAQDIS20(VAQLN,VAQCTR)
 D TMP,BLANK^VAQDIS20
 K VAQLN,VAQCTR
ROW1 ;
 S X=$$SETSTR^VALM1("Sex: "_$G(@XTRCT@("VALUE",2,.02,0)),"",9,45)
 S X=$$SETSTR^VALM1("POB: "_$G(@XTRCT@("VALUE",2,.092,0))_", "_$G(@XTRCT@("VALUE",2,.093,0)),X,46,33)
 D TMP
ROW2 ;
 S X=$$SETSTR^VALM1("Marital: "_$G(@XTRCT@("VALUE",2,.05,0)),"",5,42)
 S X=$$SETSTR^VALM1("Father: "_$G(@XTRCT@("VALUE",2,.2401,0)),X,43,36)
 D TMP
ROW3 ;
 S X=$$SETSTR^VALM1("Religion: "_$G(@XTRCT@("VALUE",2,.08,0)),"",4,42)
 S X=$$SETSTR^VALM1("Mother: "_$G(@XTRCT@("VALUE",2,.2402,0)),X,43,36)
 D TMP
ROW4 ;
 S VAQTMP=$G(@XTRCT@("VALUE",2,57.4,0))
 S VAQINF=$S($P(VAQTMP,"-",1)["PARA":"PARA,",$P(VAQTMP,"-",1)["QUAD":"QUAD,",VAQTMP="":"UNANSWERED",1:"NOT APPLICABLE")_$P(VAQTMP,"-",2)
 S X=$$SETSTR^VALM1("SCI: "_VAQINF,"",9,35)
 S X=$$SETSTR^VALM1("Mom's Maiden: "_$G(@XTRCT@("VALUE",2,.2403,0)),X,37,42)
 D TMP K VAQTMP,VAQINF
EXIT ;
 QUIT
TMP ; -- Sets up display array
 S VALMCNT=VALMCNT+1
 S @ROOT@(VALMCNT,0)=$E(X,1,79)
 QUIT
 ;
END ; -- End of code
 QUIT
