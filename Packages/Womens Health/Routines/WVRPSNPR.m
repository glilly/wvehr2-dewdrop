WVRPSNPR ;HCIOFO/FT,JR IHS/ANMC/MWR - REPORT: RETRIEVE A SNAPSHOT;
 ;;1.0;WOMEN'S HEALTH;;Sep 30, 1998
 ;;* MICHAEL REMILLARD, DDS * ALASKA NATIVE MEDICAL CENTER *
 ;;  CALLED BY OPTION: "WV PRINT/RETRIEVE SNAPSHOT" TO DISPLAY A
 ;;  PREVIOUS SNAPSHOT.
 ;
 D SETVARS^WVUTL5 S WVPOP=0,WVFAC=DUZ(2) K ^TMP("WVF",$J)
 N A,B,C,D,E,F,G,H,J,K,L,M,N,P,Q,R,S,X,Y
 D TITLE^WVUTL5("RETRIEVE A PROGRAM SNAPSHOT")
 D RETRIEVE G:WVPOP EXIT
 D DEVICE   G:WVPOP EXIT
 D LOADVARS
 D ^WVRPSNP1
 ;
EXIT ;EP
 D KILLALL^WVUTL8
 Q
 ;
 ;
RETRIEVE ;EP
 ;---> RETRIEVE A SNAPSHOT
 N DIC,Y
 W !!?3,"Select the Snapshot you wish to display or print."
 D DIC^WVFMAN(790.71,"QEMA",.Y,"   Select Snapshot: ")
 I Y<0 S WVPOP=1 Q
 S WVDTIEN=+Y
 Q
 ;
DEVICE ;EP
 ;---> GET DEVICE AND POSSIBLY QUEUE TO TASKMAN.
 S ZTRTN="DEQUEUE^WVRPSNPR"
 F WVSV="DT","DTIEN" D
 .I $D(@("WV"_WVSV)) S ZTSAVE("WV"_WVSV)=""
 D ZIS^WVUTL2(.WVPOP,1)
 Q
 ;
DEQUEUE ;EP
 ;---> QUEUED REPORT
 N A,B,C,D,E,F,G,H,J,K,L,M,N,P,Q,R,S,X,Y
 D SETVARS^WVUTL5,LOADVARS,^WVRPSNP1,EXIT
 Q
 ;
LOADVARS ;EP
 S Y=^WV(790.71,WVDTIEN,0)
 S WVDT=+Y
 S WVFAC=$P(Y,U,2),A=$P(Y,U,3),B=$P(Y,U,4),C=$P(Y,U,5),D=$P(Y,U,6)
 S E=$P(Y,U,7),F=$P(Y,U,8),G=$P(Y,U,9),H=$P(Y,U,10),S=$P(Y,U,11)
 S J=$P(Y,U,12),K=$P(Y,U,13),L=$P(Y,U,14),P=$P(Y,U,15),Q=$P(Y,U,16)
 S R=$P(Y,U,17),M=$P(Y,U,18)
 Q
