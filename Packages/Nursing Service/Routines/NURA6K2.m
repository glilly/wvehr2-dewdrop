NURA6K2 ;HIRMFO/MD,FT-NURSING SERVICE PROFICIENCY REPORT BY SERVICE ; 1/3/03 1:14pm
 ;;4.0;NURSING SERVICE;**38**;Apr 25, 1997
 Q:'$D(^DIC(213.9,1,"OFF"))  Q:$P(^DIC(213.9,1,"OFF"),"^",1)=1
 S (NURQUIT,NURQUEUE,NUROUT)=0,NURHOSP=1
 D EN1^NURSAUTL G QUIT:NUROUT
 I NURMDSW S DIC(0)="AEQMZ" S NURPLSCR=0 D EN5^NURSAGSP G QUIT:$G(NUROUT)
 I NURMDSW=0,NURPLSW=1 S NURPLSCR=0 D PRD^NURSAGSP K NURPLSCR I $G(NUROUT) G QUIT
 D EN4^NURSAGP1 G QUIT:NUROUT
 W ! S ZTDESC="Nursing Service Proficiency Report by Service",ZTRTN="START^NURA6K2",NURS132=1 D EN7^NURSUT0 G:POP!($D(ZTSK)) QUIT
START ;
 K ^TMP($J)
 S X="",(NURQUIT,NURSW1,NURPAGE)=0 F  S X=$O(^NURSF(210,"AC",X)) Q:X=""  I X'="R" F DA=0:0 S DA=$O(^NURSF(210,"AC",X,DA)) Q:DA'>0  I $D(^NURSF(210,DA,0)),+$P(^(0),U) S DA(1)=$P(^(0),U) W:$R(100)&($E(IOST)="C") "." D SORT
 S X=$O(^TMP($J,"")) I X="" D NHDR W !,"THERE IS NO DATA FOR THIS REPORT" S DIR(0)="E" D:$E(IOST)="C" ^DIR S NUROUT=1 G QUIT
 U IO D NPRINT
QUIT K ^TMP($J) D CLOSE^NURSUT1,^NURAKILL
 Q
SORT ; POSITION SORT
 F NURNODE4=0:0 S NURNODE4=$O(^NURSF(211.8,"C",DA(1),NURNODE4)) Q:NURNODE4'>0  F NURNODE5=0:0 S NURNODE5=$O(^NURSF(211.8,"C",DA(1),NURNODE4,NURNODE5)) Q:NURNODE5'>0  D
 .I $D(^NURSF(211.8,NURNODE4,1,NURNODE5,0)),$P(^(0),U)'>DT&(('$P(^(0),U,6))!($P(^(0),U,6)'<DT)) D SORT1
 .Q
 Q
SORT1 Q:NURSZAP>7&(NURSZDA'=DA)  S NURSZORT=1 D EN3^NURSAUTL:NURSZAP>6,EN2^NURSAUTL:NURSZORT&NURSZAP Q:'NURSZORT
 S NURNEN=1 D SETFAC^NURAAGS1,SETPROG^NURAAGS1
 I $D(^VA(200,DA(1),0)) S N1=$S($P(^(0),"^",1)'="":$P(^(0),"^",1),1:"  BLANK")
 I $D(^VA(200,DA(1),1)) S SSN=$S($P(^(1),"^",9)'="":$P(^(1),"^",9),1:"  BLANK")
 S NLO=$S($D(^NURSF(211.8,NURNODE4,0))&$P(^NURSF(211.8,NURNODE4,0),"^")'="":$P(^(0),"^"),1:"  BLANK")
 I $D(^NURSF(211.4,"B",+NLO)) S NLO(1)=$O(^NURSF(211.4,"B",+NLO,0)) I $D(^NURSF(211.4,+NLO(1),"I")),$E($P(^("I"),"^"))="I" Q
 D EN2^NURSUT0 S NURSER=$G(NPSPOS(1))
 S NPWARD=NLO D EN7^NURSAUTL S NL1=$S(NPWARD'="":NPWARD,1:"  BLANK")
 Q:NURSER=""!'(NURSER="R")
 I '$D(^NURSF(210,DA,14,0)) Q
 S NURPROG("TST")=$G(NURPROG(2))
 S NN=0 F II=0:0 S NN=$O(^NURSF(210,DA,14,NN)) Q:NN'>0  D NGO
 K NURPROG("TST")
 Q
NGO I '$D(^NURSF(210,DA,14,NN,0)) Q
 S NDATA=^NURSF(210,DA,14,NN,0),NDD=$S($P(NDATA,"^")'="":$P(NDATA,"^"),1:"  BLANK"),NEV=$S($P(NDATA,"^",2)>0:$P(^VA(200,+$P(NDATA,"^",2),0),"^"),1:"  BLANK")
 I 'NURHOSP,'$D(NURSNLOC(NLO)) Q
 I 'NSP,NDD<NSPC!(NDD>NSPC(2)) Q
 I NURMDSW,'$G(NURFAC),$G(NURFAC(1))'=$G(NURFAC(2)) Q
 I NURPLSW,'$G(NURPROG),$G(NURPROG(1))'=NURPROG("TST") Q
 S:NURPROG(2)="NURSING" NURPROG(2)=" "_NURPROG(2)
 S ^TMP($J,NURFAC(2),NURPROG(2),NDD,NL1,N1,SSN,NEV,DA,NN)=""
 Q
NPRINT S NURFAC(2)="" F  S NURFAC(2)=$O(^TMP($J,NURFAC(2))) Q:NURFAC(2)=""!NURQUIT  D NM Q:NURQUIT
 Q
NM S NURPROG(2)="" F  S NURPROG(2)=$O(^TMP($J,NURFAC(2),NURPROG(2))) Q:NURPROG(2)=""!NURQUIT  D:NURSW1 NHDR Q:NURQUIT  D NN Q:NURQUIT
 Q
NN S NDD="" F  S NDD=$O(^TMP($J,NURFAC(2),NURPROG(2),NDD)) Q:NDD=""  D NO Q:NURQUIT
 Q
NO S NL1="" F  S NL1=$O(^TMP($J,NURFAC(2),NURPROG(2),NDD,NL1)) Q:NL1=""  D NR Q:NURQUIT
 Q
NR S N1="" F  S N1=$O(^TMP($J,NURFAC(2),NURPROG(2),NDD,NL1,N1)) Q:N1=""  S SSN=$O(^(N1,"")),NEV=$O(^(SSN,"")),DA=$O(^(NEV,0)),D1=$O(^(DA,0)) D NPPRINT Q:NURQUIT
 Q
NPPRINT I ($Y>(IOSL-10))!'(NURSW1) D NHDR Q:NURQUIT
 W !
 W:N1'="  BLANK" $E(N1,1,20)
 W ?23,$E(SSN,1,3),"-",$E(SSN,4,5),"-",$E(SSN,6,9)
 W:NL1'="  BLANK" ?40,$E(NL1,1,10)
 S Y=NDD D:Y'="  BLANK" D^DIQ W:Y'="  BLANK" ?51,Y
 W:NEV'="  BLANK" ?65,NEV
 S DATA=$G(^NURSF(210,DA,14,D1,0)) F I=4,5 I +$P(DATA,U,I) S Y=$P(DATA,U,I) D D^DIQ S ZZ=$S(I=4:91,1:105) W ?ZZ,Y
 Q
NHDR I 'NURQUEUE,NURSW1,$E(IOST)="C" D ENDPG^NURSUT1 S:NUROUT NURQUIT=+NUROUT Q:NURQUIT
 I 'NURSW1 S NURSW1=1
 S NURPAGE=NURPAGE+1 W:$E(IOST)="C"!(NURPAGE>1) @IOF
 I $G(NURMDSW) W !,?$$CNTR^NURSUT2(NURFAC(2)),$$FACL^NURSUT2(NURFAC(2))
 W !,"NURSING SERVICE PROFICIENCY PROFILE" S X="T" D ^%DT D:+Y D^DIQ W ?90,Y,?105,"PAGE: ",NURPAGE
 W !!,?51,"DATE",?91,"WORK COPY",?105,"COPY RET'D"
 W !,"NAME",?23,"SSN",?40,"LOCATION",?51,"DUE",?65,"EVALUATOR",?91,"SENT OUT",?105,"FOR TYPING",!
 S X="",$P(X,"-",132)="" W X,!
 I $G(NURPLSW),$G(NURPROG(2))'="" N Z S Z=$$PROD^NURSUT2(NURPROG(2)) W !?$$CNTR^NURSUT2(Z),Z,!?$$CNTR^NURSUT2(Z),$$REPEAT^XLFSTR("-",$L(Z)+1),!
 Q
