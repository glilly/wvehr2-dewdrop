SROICU2 ;B'HAM ISC/MAM - UNSCHEDULED ADMISSION TO ICU ; 2 OCT 1990  12:45 PM
 ;;3.0; Surgery ;**48,106**;24 Jun 93
 S (SRCT,SRSOUT)=0,SRSDATE=SRSD-.0001,SREDT=SRED+.9999
 D HDR
 F  S SRSDATE=$O(^SRF("AC",SRSDATE)) Q:'SRSDATE!(SRSDATE>SREDT)!SRSOUT  S SRTN=0 F  S SRTN=$O(^SRF("AC",SRSDATE,SRTN)) Q:'SRTN!SRSOUT  I $D(^SRF(SRTN,0)),$$DIV^SROUTL0(SRTN) D SET
 I SRCT=0 W !!,"No unscheduled admissions to ICU for the selected date range."
 Q
SET ; case information
 S X=$P(^SRF(SRTN,0),"^",4),SRSS=$S(X:$P(^SRO(137.45,X,0),"^"),1:"")
 I '$D(^SRF(SRTN,.2)) Q
 I $P(^SRF(SRTN,.2),"^",12)="" Q
 S SR(.4)=$G(^SRF(SRTN,.4)),SRDISP=$P(SR(.4),"^",3),SRDISP1=$P(SR(.4),"^",6),Y=SRDISP,C=$P(^DD(130,.43,0),"^",2) D:Y'="" Y^DIQ S SRDISP=Y I Y["ICU" Q
 S Y=SRDISP1,C=$P(^DD(130,.46,0),"^",2) D:Y'="" Y^DIQ S SRDISP1=Y I Y=""!(Y'["ICU") Q
 S SRDIS=SRDISP_"/"_SRDISP1
 I $Y+7>IOSL D PAGE I SRSOUT Q
 S DFN=$P(^SRF(SRTN,0),"^") D DEM^VADPT S SRODATE=$E(SRSDATE,4,5)_"/"_$E(SRSDATE,6,7)_"/"_$E(SRSDATE,2,3)
 S SRDIAG=$S($D(^SRF(SRTN,34)):$P(^(34),"^"),1:"DIAGNOSIS NOT ENTERED"),SR(.1)=$S($D(^SRF(SRTN,.1)):^(.1),1:""),SRSUR=$P(SR(.1),"^",4),SRATT=$P(SR(.1),"^",13)
 I SRSUR S SRSUR=$P(^VA(200,SRSUR,0),"^")
 I SRATT S SRATT=$P(^VA(200,SRATT,0),"^") I $L(SRATT)>18 S SRATT=$P(SRATT,",")_","_$E($P(SRATT,",",2))
OPS S SROPER=$P(^SRF(SRTN,"OP"),"^"),OPER=0 F I=0:0 S OPER=$O(^SRF(SRTN,13,OPER)) Q:OPER=""  D OTHER
 K SROP,MM,MMM S:$L(SROPER)<50 SROP(1)=SROPER I $L(SROPER)>49 S SROPER=SROPER_"  " S SROPER=SROPER_"  " F M=1:1 D LOOP Q:MMM=""
 W !,SRODATE,?17,VADM(1),?55,SRSS,?110,SRSUR,!,?17,VA("PID"),?55,SRDIAG,?110,SRATT,!,?17,SRDIS,?55,SROP(1) I $D(SROP(2)) W !,?55,SROP(2) I $D(SROP(3)) W !,?55,SROP(3) I $D(SROP(4)) W !,?55,SROP(4)
 W ! S SRCT=SRCT+1
 Q
PAGE I $E(IOST)="P" D HDR Q
 W !!,"Press RETURN to continue, or '^' to quit:  " R X:DTIME I '$T!(X="^") S SRSOUT=1 Q
 I X["?" W !!,"Press RETURN to continue listing cases, or '^' to exit from this option." G PAGE
HDR ; print heading
 I $D(ZTQUEUED) D ^SROSTOP I SRHALT S SRSOUT=1 Q
 I $Y W @IOF
 W !,?(132-$L(SRINST)\2),SRINST,!,?58,"SURGICAL SERVICE",?100,"REVIEWED BY: ",!,?52,"UNSCHEDULED ADMISSIONS TO ICU",?100,"DATE REVIEWED: "
 W !,?53,"FROM ",$E(SRSD,4,5)_"/"_$E(SRSD,6,7)_"/"_$E(SRSD,2,3),"  TO ",$E(SRED,4,5)_"/"_$E(SRED,6,7)_"/"_$E(SRED,2,3)
 W !!,"DATE",?17,"PATIENT",?55,"SURGICAL SPECIALTY",?110,"SURGEON",!,?17,"PATIENT ID",?55,"POSTOPERATIVE DIAGNOSIS",?110,"ATTENDING SURGEON",!,?17,"REQ DISPOSITION/POSTOP DISPOSITION",?55,"OPERATIVE PROCEDURE(S)"
 W ! F LINE=1:1:132 W "="
 Q
OTHER ; other operations
 S SRLONG=1 I $L(SROPER)+$L($P(^SRF(SRTN,13,OPER,0),"^"))>240 S SRLONG=0,OPER=999,SROPERS=" ..."
 I SRLONG S SROPERS=$P(^SRF(SRTN,13,OPER,0),"^")
 S SROPER=SROPER_$S(SROPERS=" ...":SROPERS,1:", "_SROPERS)
 Q
LOOP ; break procedure if greater than 50 characters
 S SROP(M)="" F LOOP=1:1 S MM=$P(SROPER," "),MMM=$P(SROPER," ",2,200) Q:MMM=""  Q:$L(SROP(M))+$L(MM)'<50  S SROP(M)=SROP(M)_MM_" ",SROPER=MMM
 Q
