ABSVBGMF ;VAMC ALTOONA/CTB - UPDATE SEX OF VOLUNTEER ;9/1/99  11:04 PM
V ;;4.0;VOLUNTARY TIMEKEEPING;**17**;JULY 6, 1994
 ;UPDATE SEX CODE FOR ALL VOLUNTEERS
 N N,DA,%T,CHANGE
 S (CHANGE,N)="" F  S N=$O(^ABS(503330,"B",N)) Q:N=""  D
 . S DA="" F  S DA=$O(^ABS(503330,"B",N,DA)) Q:'DA  D
 . . N X,X1,SEX,NODE,BIRTHDAY,AGE
 . . W "."
 . . S AGE=21 ;VOLUNTEERS WITHOUT BIRTHDAYS ARE ASSUMED TO BE > 20
 . . S NODE=$G(^ABS(503330,DA,0)) Q:NODE=""
 . . S SEX=$P(NODE,"^",7) Q:SEX=""
 . . S BIRTHDAY=$P(NODE,"^",8)
 . . I BIRTHDAY S AGE=$$AGE^ABSVU2(BIRTHDAY,$G(DT))
 . . I SEX="M",AGE<21 S X=SEX,X1="B" D CHANGE QUIT
 . . I SEX="B",AGE>20 S X=SEX,X1="M" D CHANGE QUIT
 . . I SEX="F",AGE<21 S X=SEX,X1="G" D CHANGE QUIT
 . . I SEX="G",AGE>20 S X=SEX,X1="F" D CHANGE QUIT
 . . QUIT
 . QUIT
 W !,$S(+CHANGE:+CHANGE,1:"NO")," CHANGE",$S(CHANGE=1:"",1:"S"),$S(+CHANGE:" MADE.",1:" REQUIRED.")
 W !!,"DONE" QUIT
CHANGE ;change sex for one volunteer
 K ^ABS(503330,"AD",X,DA) S ^ABS(503330,"AD",X1,DA)="",$P(^ABS(503330,DA,0),"^",7)=X1
 W !,$P(NODE,"^",1),$S(BIRTHDAY="":"",1:" AGE: "_AGE),"   CHANGED TO "_$S(X1="F":"FEMALE",X1="G":"GIRL",X1="B":"BOY",1:"MALE")
 S CHANGE=CHANGE+1
