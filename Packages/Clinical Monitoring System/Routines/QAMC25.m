QAMC25 ;HISC/DAD-CONDITION: PATIENT GROUP ;9/28/92  13:13
 ;;1.0;Clinical Monitoring System;;09/13/1993
EN1 ; *** CONDITION CODE
 S QAMGRP=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P1"))#2:+^("P1"),1:0)
 F QAMDFN=0:0 S QAMDFN=$O(^QA(743.5,QAMGRP,"GRP","AB",QAMDFN)) Q:QAMDFN'>0  D
 . S ^UTILITY($J,"QAM CONDITION",QAMD1,QAMDFN)=""
 . S ^UTILITY($J,"QAM CONDITION",QAMD1,QAMDFN,QAMTODAY)=QAMDFN
 . Q
 Q
 ;
EN2 ; *** PARAMETER CODE
 K DIC,DIR,DIRUT S DIC=743.5,DIC(0)="EMNQZ",DIC("A")="PATIENT GROUP: ",DIC("B")=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P1"))#2:$P(^("P1"),"^",2),1:""),DIC("S")="I $P(^(0),""^"",2)=2" K:DIC("B")="" DIC("B")
 S DIR("?")="Enter the PATIENT GROUP you wish to monitor."
 S QAMPARAM="P1" D EN2^QAMUTL1 I $D(DIRUT) S Y=-1 G Y
 S:Y]"" ^QA(743,QAMD0,"COND",QAMD1,"P1")=+Y_"^"_Y(0,0)
 ;
EXIT K Y
 K QAMPARAM
Y Q
