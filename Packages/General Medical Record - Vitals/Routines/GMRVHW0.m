GMRVHW0 ;HIRMFO/YH-HP LASER WEIGHT CHART - DATA ARRAY ;5/14/97
 ;;4.0;Vitals/Measurements;**1**;Apr 25, 1997
EN1 ;
 S GMRHT=0 K ^TMP($J,"GMR"),^TMP($J,"GMRK"),^TMP($J,"GDT"),^TMP($J,"GTNM"),GWT S GWT=0
 S GSTART1=(9999999-GMRFIN)-.0001,GEND1=9999999-GMRSTRT
 F GTYPE="H","W" D SETT^GMRVWT0
 F I=1:1:8 S GWT(I)=""
 I GWT>0 S GWT=GWT\10*10-40,J=0 F I=1:1:8 S GWT(I)=GWT+(10*J),J=J+1,GWT(I)=GWT(I)_"/"_$J(GWT(I)/2.2,0,2)
 U IO D GRAPH
Q1 D Q1^GMRVWT0 K GRAPHW Q
GRAPH ;
 D DEM^VADPT,INP^VADPT S SNN=$P(VADM(2),"^",2)
 S GPG=0 D SETV^GMRVWT1 F GK="H","W" S ^TMP($J,"GTNM",GK)=0 F GI=0:0 S GI=$O(^TMP($J,"GMRVG",GK,GI)) Q:GI'>0  S GJ="" F X=0:0 S GJ=$O(^TMP($J,"GMRVG",GK,GI,GJ)) Q:GJ=""  S ^TMP($J,"GTNM",GK)=^TMP($J,"GTNM",GK)+1,^TMP($J,"GDT",GI)=""
 S GTNM=0 F X=0:0 S X=$O(^TMP($J,"GDT",X)) Q:X'>0  S GTNM=GTNM+1
 S GPG=$S(GTNM=0:1,1:GTNM\10+''(GTNM#10)),GDT1=0
 F GPGS=1:1:GPG K GMRQUAL S ^TMP($J,"GMRK","G199")="Page "_GPGS D SETP^GMRVWT1,DATE,PAGE D ^GMRVHW1
 D KVAR^VADPT K VA Q
PAGE ;
 ;DATA FOR WEIGHT GRAPH
 K GRAPHW S GRAPHW=0,I=1,J=161,GPA=0 F GI=141:1:150 S GRAPHW=0.6+(1.6*(I-1)) D:^TMP($J,"GMRK","G"_GI)'=""  S I=I+1,J=J+1
 .S GRAPHW(I)=$S(GPA=0:"PA",1:"PD")_GRAPHW_","_^TMP($J,"GMRK","G"_GI)_";LB"_^TMP($J,"GMRK","G"_J)_"#;",GPA=1
 I $D(GMRQUAL) D LEGEND^GMRVLGQU
 K GPA Q
DATE F GCNTD=1:1:10 S:$L(GDT1) GDT1=$O(^TMP($J,"GDT",GDT1)) S ^TMP($J,"GMRK","G"_GCNTD)=$S($L(GDT1):$E(GDT1,4,5)_"-"_$E(GDT1,6,7)_"-"_$E(GDT1,2,3),1:"") D DATE1
 Q
DATE1 S Y=$E($P(GDT1,".",2)_"0000",1,4),^TMP($J,"GMRK","G"_(GCNTD+16))=$S($L(GDT1):$E(Y,1,2)_":"_$E(Y,3,4),1:"") D SETD
 Q
SETD F GI="H","W" S GJ=$S(GI="W":100,1:160),GK=$S($L(GDT1):$O(^TMP($J,"GMRVG",GI,GDT1,"")),1:"") D SETA
 Q
SETA ;
 I GK="Unavailable" S GK="Unavail"
 S (GMRSITE,GMRSITE(1),GMRSITE(2),GMRINF,GMRVJ)=""
 I GK'="" D
 . S GMRSITE(1)=$P($G(^TMP($J,"GMRVG",GI,GDT1,GK)),"^"),GMRVJ=$P($G(^(GK)),"^",2),GMRINF=$P($G(^(GK)),"^",4)
 . I GMRSITE(1)'="" D SYNOARY^GMRVLGQU
 I GI="H" S ^TMP($J,"GMRK","G"_(300+GCNTD))=GK,^("G"_(340+GCNTD))=GMRSITE,^TMP($J,"GMRK","G"_(320+GCNTD))=$S(GK>0:$J(GK*2.54,0,2),1:"") S:GK>0 GMRHT=GK*2.54/100 Q
 S ^TMP($J,"GMRK","G"_(180+GCNTD))=GMRSITE
 I GK>0 D
 . S GMRBMI="",GMRBMI(1)=GDT1,GMRBMI(2)=GK D CALBMI^GMRVBMI(.GMRBMI)
 . S ^TMP($J,"GMRK","G"_(210+GCNTD))=$S(GMRBMI>0:GMRBMI,1:"") K GMRBMI
 S ^TMP($J,"GMRK","G"_(100+GCNTD))=GK,^TMP($J,"GMRK","G"_(120+GCNTD))=$S(GK>0:$J(GK/2.2,0,2),1:"")
 S ^TMP($J,"GMRK","G"_(140+GCNTD))=$S(GK>0:GK-(GWT-10)/5,1:"") S:GK>0 ^TMP($J,"GMRK","G"_(160+GCNTD))="W"
 I GK>0 S ^TMP($J,"GMRK","G"_(140+GCNTD))=$S(^TMP($J,"GMRK","G"_(140+GCNTD))<0.4:0.4,^("G"_(140+GCNTD))>16.5:16.5,1:^("G"_(140+GCNTD))) S:^("G"_(140+GCNTD))<0.5!(^("G"_(140+GCNTD))>16.4) ^TMP($J,"GMRK","G"_(160+GCNTD))="W**"
 Q
