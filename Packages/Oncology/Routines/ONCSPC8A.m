ONCSPC8A ;HIRMFO/GWB -PCE Study of Soft Tissue Sarcoma - Print (continued) ;8/15/96
 ;;2.11;ONCOLOGY;**6,15,16**;Mar 07, 1995
 I IOST'?1"C".E W ! D P Q:EX=U  W ! D P Q:EX=U
 S TABLE="TABLE IV - FIRST COURSE OF TREATMENT"
 W !?15,TABLE,! D P Q:EX=U
 S D0=ONCONUM D DFC^ONCOCOM S DOIT=Y
 W !,"FIRST COURSE TREATMENT DATE.......: ",DOIT D P Q:EX=U
 W !!,"SURGERY:",! D P Q:EX=U
 S NCDS=ONC(165.5,IEN,58.1)
 S (NCDS1,NCDS2)="",LOS=$L(NCDS) I LOS<45 S NCDS1=NCDS G CDS
 S NOP=$L($E(NCDS,1,41)," ")
 S NCDS1=$P(NCDS," ",1,NOP-1),NCDS2=$P(NCDS," ",NOP,999)
CDS S CDS=ONC(165.5,IEN,58.2)
 S (CDS1,CDS2)="",LOS=$L(CDS) I LOS<45 S CDS1=CDS G S
 S NOP=$L($E(CDS,1,41)," ")
 S CDS1=$P(CDS," ",1,NOP-1),CDS2=$P(CDS," ",NOP,999)
S W !,"  NON CANCER-DIRECTED SURGERY DATE: ",ONC(165.5,IEN,58.3) D P Q:EX=U
 W !,"  NON CANCER-DIRECTED SURGERY.....: ",NCDS1 W:NCDS2'="" !,?39,NCDS2 D P Q:EX=U
 W !,"  SURGERY OF PRIMARY SITE DATE....: ",ONC(165.5,IEN,50) D P Q:EX=U
 W !,"  SURGERY OF PRIMARY SITE.........: ",CDS1 W:CDS2'="" !,?39,CDS2
 W !,"  RESIDUAL PRIMARY TUMOR..........: ",ONC(165.5,IEN,59)
 W !,"  TREATING SURGEON................: ",ONC(165.5,IEN,526) D P Q:EX=U
 W !,"  ASA CLASS.......................: ",ONC(165.5,IEN,527) D P Q:EX=U
 W !,"  POSTOPERATIVE DEATH.............: ",ONC(165.5,IEN,441)
 I IOST?1"C".E W ! K DIR S DIR(0)="E" D ^DIR Q:'Y  D HDR^ONCSPC8 W !?15,TABLE_" (continued)" G R
 D P Q:EX=U
R W !!,"RADIATION THERAPY:",! D P Q:EX=U
 W !,"  EXTERNAL BEAM RADIATION...: ",ONC(165.5,IEN,532),?42,"BRACHYTHERAPY...........: ",ONC(165.5,IEN,538) D P Q:EX=U
 W !,"  NUMBER OF FRACTIONS.......: ",ONC(165.5,IEN,533),?42,"NUMBER OF DAYS GIVEN....: ",ONC(165.5,IEN,539) D P Q:EX=U
 W !,"  DOSE......................: ",ONC(165.5,IEN,362),?42,"DOSE....................: ",ONC(165.5,IEN,540) D P Q:EX=U
 W !,"  RADIATION ENERGY (MV).....: ",ONC(165.5,IEN,534),?42,"DATE THERAPY STARTED....: ",ONC(165.5,IEN,541) D P Q:EX=U
 W !,"  DATE THERAPY STARTED......: ",ONC(165.5,IEN,567),?42,"DATE THERAPY ENDED......: ",ONC(165.5,IEN,542) D P Q:EX=U
 W !,"  DATE THERAPY ENDED........: ",ONC(165.5,IEN,361)
 W !!,"  INTRAOPERATIVE RADIATION..: ",ONC(165.5,IEN,535) D P Q:EX=U
 W !,"  DOSE......................: ",ONC(165.5,IEN,536) D P Q:EX=U
 W !,"  RADIATION ENERGY (MV).....: ",ONC(165.5,IEN,537) D P Q:EX=U
 W !!,"  RADIATION/SURGERY SEQUENCE: ",ONC(165.5,IEN,51.3)
 I IOST?1"C".E W ! K DIR S DIR(0)="E" D ^DIR Q:'Y  D HDR^ONCSPC8 W !?15,TABLE_" (continued)" G C
 D P Q:EX=U
C W !!,"CHEMOTHERAPY:",! D P Q:EX=U
 W !,"  CHEMOTHERAPY........: ",ONC(165.5,IEN,53.2) D P Q:EX=U
 W !,"  DATE OF CHEMOTHERAPY: ",ONC(165.5,IEN,53) D P Q:EX=U
 W !!,"  AGENT ADMINISTERED    METHOD OF DELIVERY  LOCATION" D P Q:EX=U
 W !,"  ------------------    ------------------  --------" D P Q:EX=U
 W !,"  CISPLATIN..: ",ONC(165.5,IEN,371),?24,ONC(165.5,IEN,547),?44,ONC(165.5,IEN,553) D P Q:EX=U
 W !,"  CYTOXAN....: ",ONC(165.5,IEN,543),?24,ONC(165.5,IEN,548),?44,ONC(165.5,IEN,554) D P Q:EX=U
 W !,"  DTIC.......: ",ONC(165.5,IEN,544),?24,ONC(165.5,IEN,549),?44,ONC(165.5,IEN,555) D P Q:EX=U
 W !,"  DOXORUBICIN: ",ONC(165.5,IEN,545),?24,ONC(165.5,IEN,550),?44,ONC(165.5,IEN,556) D P Q:EX=U
 W !,"  ETOPOSIDE..: ",ONC(165.5,IEN,546),?24,ONC(165.5,IEN,551),?44,ONC(165.5,IEN,557) D P Q:EX=U
 W !,"  IFOSFAMIDE.: ",ONC(165.5,IEN,375),?24,ONC(165.5,IEN,552),?44,ONC(165.5,IEN,558) D P Q:EX=U
 I IOST?1"C".E W ! K DIR S DIR(0)="E" D ^DIR Q:'Y  D HDR^ONCSPC8 W !?15,TABLE_" (continued)" G CSF
 D P Q:EX=U
CSF W !!,"  COLONY STIMULATION FACTOR........: ",ONC(165.5,IEN,559) D P Q:EX=U
 W !,"  NATIONAL TREATMENT PROTOCOL......: ",ONC(165.5,IEN,560) D P Q:EX=U
 W !,"  OTHER PROTOCOL...................: ",ONC(165.5,IEN,561) D P Q:EX=U
 W !,"  REFERRED TO REHAB SERVICES.......: ",ONC(165.5,IEN,562) D P Q:EX=U
 W !,"  CONSULT W PHYSICAL THERAPY/REHAB.: ",ONC(165.5,IEN,563) D P Q:EX=U
 W !,"  TRANSFERRED TO REHAB FACILITY....: ",ONC(165.5,IEN,564) D P Q:EX=U
 W !,"  NO OF HOSPITALIZATIONS W/I 6 MO..: ",ONC(165.5,IEN,565) D P Q:EX=U
 W !,"  TOTAL LENGTH OF STAYS............: ",ONC(165.5,IEN,566) D P Q:EX=U
 I IOST?1"C".E W ! K DIR S DIR(0)="E" D ^DIR Q:'Y  D HDR^ONCSPC8 G V
 D P Q:EX=U
V S TABLE="TABLE V - FIRST RECURRENCE AND SUBSEQUENT TREATMENT"
 I IOST'?1"C".E W ! D P Q:EX=U  W ! D P Q:EX=U
 W !?15,TABLE,! D P Q:EX=U
 W !,"DATE OF FIRST RECURRENCE..........: ",ONC(165.5,IEN,70) D P Q:EX=U
 W !,"TYPE OF FIRST RECURRENCE..........: ",ONC(165.5,IEN,71) D P Q:EX=U
 W !,"OTHER TYPE OF FIRST RECURRENCE....: ",ONC(165.5,IEN,71.4) D P Q:EX=U
DSOFR W !!,"DISTANT SITE(S) OF FIRST RECURRENCE:",!
 W !,"RECURRENCE SITE 1.................: ",ONC(165.5,IEN,71.1) D P Q:EX=U
 W !,"RECURRENCE SITE 2.................: ",ONC(165.5,IEN,71.2) D P Q:EX=U
 W !,"RECURRENCE SITE 3.................: ",ONC(165.5,IEN,71.3) D P Q:EX=U
 I '$D(^ONCO(165.5,IEN,4,0)) D STFROP W !!,"  NO SUBSEQUENT TREATMENT" G VEXIT
 I $O(^ONCO(165.5,IEN,4,0))="" D STFROP W !!,"  NO SUBSEQUENT TREATMENT" G VEXIT
 I IOST?1"C".E W ! K DIR S DIR(0)="E" D ^DIR Q:'Y  D HDR^ONCSPC8 W !?15,TABLE_" (continued)" G ST
 D P Q:EX=U
ST D STFROP I $D(^ONCO(165.5,IEN,4,0)) S SCTIEN=0 F CRSE=1:1 S SCTIEN=$O(^ONCO(165.5,IEN,4,SCTIEN)) Q:(SCTIEN'>0)!(CRSE=4)!(EX=U)  D SCT
 G:EX=U KILL G VI
SCT S DIC="^ONCO(165.5,",DR=60,DA=IEN,DIQ="ONC"
 S DR(165.51)=".01:32",DA(165.51)=SCTIEN D EN^DIQ1
 S SURG=ONC(165.51,SCTIEN,.04),(SURG1,SURG2)=""
 S LOS=$L(SURG) I LOS<62 S SURG1=SURG G TOS
 S NOP=$L($E(SURG,1,61)," ")
 S SURG1=$P(SURG," ",1,NOP-1),SURG2=$P(SURG," ",NOP,999)
TOS S COURSE=$S(CRSE=1:"SECOND",CRSE=2:"THIRD",CRSE=3:"FOURTH",1:"")
 I IOST?1"C".E,CRSE>1 W !?15,TABLE_" (continued)"
 W !!,"  ",COURSE," COURSE:",! D P Q:EX=U
 W !,"  DATE...........: ",ONC(165.51,SCTIEN,.01) D P Q:EX=U
 W !,"  SURGERY........: ",SURG1 W:SURG2'="" !,?22,SURG2 D P Q:EX=U
 D P Q:EX=U
 W !,"  RADIATION......: ",ONC(165.51,SCTIEN,.05) D P Q:EX=U
 W !,"  CHEMOTHERAPY...: ",ONC(165.51,SCTIEN,.06) D P Q:EX=U
 W !,"  HORMONE THERAPY: ",ONC(165.51,SCTIEN,.07)
 I IOST?1"C".E W ! K DIR S DIR(0)="E" D ^DIR S:'Y EX=U Q:'Y  D HDR^ONCSPC8 Q
 D P Q:EX=U
 Q
VEXIT I IOST?1"C".E W ! K DIR S DIR(0)="E" D ^DIR Q:'Y  D HDR^ONCSPC8 G VI
 D P Q:EX=U
VI S TABLE="TABLE VI - STATUS AT LAST CONTACT"
 I IOST'?1"C".E W ! D P Q:EX=U  W ! D P Q:EX=U
 W !?15,TABLE,! D P Q:EX=U
 S DLC="" I $D(^ONCO(160,ONCOPA,"F","B")) S DLC=$O(^ONCO(160,ONCOPA,"F","B",""),-1)
 I DLC'="" S Y=DLC D DATEOT^ONCOPCE S DLC=Y
 W !,"DATE OF LAST CONTACT OR DEATH.....: ",DLC
 W !,"VITAL STATUS......................: ",ONC(160,ONCOPA,15) D P Q:EX=U
 S CS="" I $D(^ONCO(165.5,IEN,"TS","AA")) D
 .S CSDAT=$O(^ONCO(165.5,IEN,"TS","AA",""))
 .S CSIEN=$O(^ONCO(165.5,IEN,"TS","AA",CSDAT,""))
 .S CSPNT=$P(^ONCO(165.5,IEN,"TS",CSIEN,0),U,2)
 .S CS=$P(^ONCO(164.42,CSPNT,0),U,1)
 W !,"CANCER STATUS.....................: ",CS
 W !!,"COMPLETED BY......................: ",ONC(165.5,IEN,81) D P Q:EX=U
 W !,"REVIEWED BY CANCER COMMITTEE......: ",ONC(165.5,IEN,82) D P Q:EX=U
 W !
 I IOST?1"C".E K DIR S DIR(0)="E" D ^DIR
KILL ;Kill Variables and Exit
 K %,DIR,DIROUT,DIRUT,DTOUT,DUOUT,FILN,ONCOBL,EX,TXT,X,Y,SCTIEN
 Q
P ;Display Data
 I ($Y'<(LIN-2)) D  Q:EX=U  W !?15,TABLE_" (continued)",!
 .I IOST?1"C".E K DIR S DIR(0)="E" D ^DIR I 'Y S EX=U Q
 .D HDR^ONCSPC8 Q
 Q
STFROP W !!,"SUBSEQUENT TREATMENT FOR RECURRENCE OR PROGRESSION:"
 Q
