QAOSPCM0 ;HISC/DAD-COMMITTEE REVIEWER WORKSHEET ;2/16/93  09:19
 ;;3.0;Occurrence Screen;;09/14/1993
 S QAOSSCRN=+$G(^QA(741,QAOSD0,"SCRN")),QAOSCOMM=$O(^QA(741.2,"C",4,0)),QAOSREVR=4,QAOSQUIT=0
 I QAOSDATA=1 S QAOSD1="" D LOOP1 W:$E(IOST)'="C" @IOF D  G EXIT
 . Q:$E(IOST)'="C"  Q:QAOSQUIT
 . K DIR S DIR(0)="E" D ^DIR K DIR
 . S QAOSQUIT=$S(Y'>0:1,1:0)
 . Q
 I QAOSDATA=2,'$D(^QA(741,QAOSD0,"CMTE","B")) S QAOSD1="" D LOOP1 W:$E(IOST)'="C" @IOF D  G EXIT
 . Q:$E(IOST)'="C"  Q:QAOSQUIT
 . K DIR S DIR(0)="E" D ^DIR K DIR
 . S QAOSQUIT=$S(Y'>0:1,1:0)
 . Q
 F QAOSCOMM=0:0 S QAOSCOMM=$O(^QA(741,QAOSD0,"CMTE","B",QAOSCOMM)) Q:QAOSCOMM'>0!QAOSQUIT  D LOOP0
EXIT ;
 K ARRAY,CODE,CODES,D0,DIWF,DIWL,DIWR,LOC,QA,QAOSD1,QAOSHEAD,QAOSMULT,QAOSPAGE,QAOSREVR,QAOSSCRN,QAOSCOMM,X,XX,Y
 Q
LOOP0 ;
 F QAOSD1=0:0 S QAOSD1=$O(^QA(741,QAOSD0,"CMTE","B",QAOSCOMM,QAOSD1)) Q:QAOSD1'>0!QAOSQUIT  D LOOP1 W:$E(IOST)'="C" @IOF I ('QAOSQUIT),$E(IOST)="C" K DIR S DIR(0)="E" D ^DIR K DIR S QAOSQUIT=$S(Y'>0:1,1:0) Q:QAOSQUIT
 Q
LOOP1 ;
 S QAOSMULT=$S(QAOSD1'>0:"",$D(^QA(741,QAOSD0,"CMTE",QAOSD1,0))#2:^(0),1:"")
 S QA=$P(QAOSMULT,"^") S $P(QAOSREVR,"^",2)=$S(QA'>0:"",$D(^QA(741.97,QA,0))#2:$P(^(0),"^"),1:"")
 S QAOSPAGE=1 D ^QAOSPHDR
 S QAOSHEAD="CONFIRMED ISSUE" W !!,QAOSHEAD S LOC=$P(QAOSMULT,"^",5),CODES=$P(^DD(741.017,4,0),"^",3)
 F QA=1:1:$L(CODES,";")-1 S CODE=$P(CODES,";",QA) W !?3,$S(LOC=+CODE:"_X_",1:"___"),?8,$J(+CODE,3,0),?15,$P(CODE,":",2) D CHK Q:QAOSQUIT
 G:QAOSQUIT EXIT
 S QAOSHEAD="COMMENTS" W !!,QAOSHEAD S DIWL=4,DIWR=75,DIWF="" K ^UTILITY($J,"W")
 I QAOSHOW'=3,QAOSDATA=2 F QAOSS0=0:0 S QAOSS0=$O(^QA(741,QAOSD0,"CMTE",+QAOSD1,1,QAOSS0)) Q:QAOSS0'>0  S X=^QA(741,QAOSD0,"CMTE",QAOSD1,1,QAOSS0,0) D ^DIWP
 F QA=0:0 S QA=$O(^UTILITY($J,"W",DIWL,QA)) Q:QA'>0!QAOSQUIT  W !?3,^UTILITY($J,"W",DIWL,QA,0) D CHK
 Q:QAOSQUIT
BLANK I $Y<(IOSL-6) W ! G BLANK
 W !,"SIGNATURE"
 Q
CHK ;
 Q:$Y'>(IOSL-6)  N D0,QA,Y I $E(IOST)="C" K DIR S DIR(0)="E" D ^DIR K DIR S QAOSQUIT=$S(Y'>0:1,1:0) Q:QAOSQUIT
 D ^QAOSPHDR W !!,QAOSHEAD
 Q
