OCXOED03 ;SLC/RJS,CLA - Rule Editor (Rule Element Definition Display) ;10/29/98  12:37
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**32**;Dec 17,1997
 ;;  ;;ORDER CHECK EXPERT version 1.01 released OCT 29,1998
 ;
 ;
S ;
 ;
 Q
EN(OCXR0,OCXR1) ;
 ;
 N OCXACT,OCXRD
 F  K OCXRD,OCXACT S (OCXRD,OCXACT)="" D GETDATA(OCXR0,OCXR1,.OCXRD),DISP(OCXR0,OCXR1,.OCXRD,.OCXACT) Q:$$EN^OCXOED04(OCXR0,OCXR1,.OCXRD,.OCXACT)
 ;
 Q
 ;
DISP(OCXR0,OCXR1,OCXRD,OCXACT) ;
 ;
 N OCXTHLN,OCXTNLN,OCXTRLN,OCXTULN,OCXTNLN
 ;
 S OCXTNLN=$C(27,91,48,109),OCXTRLN=$C(27,91,55,109),OCXTULN=$C(27,91,52,109),OCXTHLN=$C(27,91,49,109)
 ;
 W @IOF,OCXTNLN
 W !,$$CENTER($$FIELD("Rule Element Edit Screen"),80),!!
 W "  ",$$OPT^OCXOEDT("Edit","EDRELE","04",.OCXACT,"OCXR0,OCXR1")
 W "  ",$$FIELD("Label:")," ",$$DATA($G(OCXRD("ELE",OCXR1,.01,"E")),60)
 W !,"       ",$$FIELD("Definition Type:")," ",$$DATA($G(OCXRD("ELE",OCXR1,.02,"E"),"Simple"),30)
 W !!!
 I $G(OCXRD("ELE",OCXR1,.02,"I"))  W " ",$$FIELD("Expression:")," ",$$DATA($G(OCXRD("ELE",OCXR1,2,"E")),60)
 I '$G(OCXRD("ELE",OCXR1,.02,"I"))  D
 .N OCXELEN
 .S OCXELEN=+$G(OCXRD("ELE",OCXR1,1,"I"))
 .W:OCXELEN "  ",$$OPT^OCXOEDT("EL"_OCXELEN,"EDELE","04",.OCXACT,OCXELEN)
 .W " ",$$FIELD("Element:")," ",$$DATA($G(OCXRD("ELE",OCXR1,1,"E")),60)
 ;
 Q
 ;
CENTER(X,M) ;
 N SP S SP="",$P(SP," ",80)=" " Q $E(SP,1,((M\2)-($L(X)\2)))_X
 ;
 ;
SEP(OCXHDR) ;
 ;
 N SPACES S SPACES="",$P(SPACES," ",80-$L(OCXHDR))=" " Q OCXTNLN_OCXTHLN_OCXTULN_$G(OCXHDR)_SPACES_OCXTNLN
 ;
FIELD(OCXHDR) ;
 ;
 Q OCXTHLN_$G(OCXHDR)_OCXTNLN
 ;
DATA(OCXVAL,OCXLEN) ;
 ;
 N SPACES S SPACES="",$P(SPACES," ",OCXLEN+5)=" ",OCXVAL=$G(OCXVAL)
 I ($L(OCXVAL)>OCXLEN) Q $E(OCXVAL,1,OCXLEN-3)_"..."
 Q $E((OCXVAL_SPACES),1,OCXLEN)
 ;
GETDATA(OCXD0,OCXD1,OCXD) ;
 ;
 N OCXDIQ,OCXX,DA
 S OCXDIQ="",DA(1)=OCXD0 D DIQ("^OCXS(860.2,"_OCXD0_",""C"",",OCXD1,"IEN",.OCXDIQ)
 M OCXD("ELE")=OCXDIQ(860.21) K OCXDIQ S OCXDIQ=""
 Q
 ;
DIC(OCXDIC,OCXDIC0,OCXDICA,OCXX,OCXDICS,OCXDR) ;
 ;
 N DIC,X,Y
 S DIC=$G(OCXDIC) Q:'$L(DIC) -1
 S DIC(0)=$G(OCXDIC0) S:$L($G(OCXX)) X=OCXX
 S:$L($G(OCXDICS)) DIC("S")=OCXDICS
 S:$L($G(OCXDICA)) DIC("A")=OCXDICA
 S:$L($G(OCXDR)) DIC("DR")=OCXDR
 D ^DIC Q:(Y<1) 0 Q Y
 ;
DIQ(DIC,DA,OCXDIQ0,OCXARY) ;
 N DR,DIQ S DR=".01:99999",DIQ="OCXARY(",DIQ(0)=$G(OCXDIQ0) D EN^DIQ1
 Q
 ;
