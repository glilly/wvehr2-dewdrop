MCARSRE ;WISC/TJK,RMP-CATH SURGICAL RISK ENTER/EDIT ;5/2/96  13:04
 ;;2.3;Medicine;;09/13/1996
PRE W !!,"CARDIAC CATH SURGERY RISK PRE-OP DATA"
 S DIC="^MCAR(694.5,",DIC(0)="AEQLM",(DLAYGO,DIDEL)=694.5,DIC("A")="Enter Patient name or Date & Time of estimate: " D ^DIC G EXIT:Y<0
 S DIE=DIC,(DA,MCARGDA)=+Y I $D(MCARCODE),MCARCODE="S" S DJSC="MCARSRAPRE",DJDN=+Y D EN^MCARD S DA=MCARGDA,(DIE,DIC)="^MCAR(694.5," K MCARCODE G PRE1
 S DR="[MCARSRAPRE]" D ^DIE
 G EXIT:$D(Y),EXIT:'$D(^MCAR(694.5,DA,4)) I $D(^MCAR(694.5,DA,4)),$P(^(4),U,18)="" G EXIT
PRE1 D ^MCARSRR G EXIT
POST ;POST SURGERY INFORMATION
 W !!,"CARDIAC CATH SURGERY RISK POST-OP DATA"
 S DIC="^MCAR(694.5,",DIC(0)="AEQM",DIC("A")="Select Patient:" D ^DIC G EXIT:Y<0
 S DIE=DIC,DA=+Y I $D(MCARCODE),MCARCODE="S" S DJSC="MCARSRA04",(MCARGDA,DJDN)=+Y D EN^MCARD S DA=MCARGDA K MCARCODE G EXIT
 S DR="[MCARSRAPOST]" D ^DIE
EXIT K DIC,DIE,DA,DR,MCARNM,DLAYGO,II,JJ,KK,XX,Y,DIDEL,MCARGDA
 K %H,%Y,%X,%,C,D,D0,D1,DI,DQ,DZ,I,X,Z
 Q
