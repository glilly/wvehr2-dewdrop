PRSDFIL ;HISC/REL/FPT-PAID CODE FILE MAINTENANCE ;1/27/93  16:15
 ;;4.0;PAID;;Sep 21, 1995
ENT ; Edit/Create File Entries
 K DIC S DIC="^DD(454,",DIC(0)="AEQM",DIC("S")="I Y>.5",DIC("A")="Select FILE: " W ! D ^DIC K DIC G:Y<1 KIL
 S DA=+Y,CODE=$P(^DD(454,DA,0),"^",4),CODE=$P(CODE,";",1)
P1 S (DIC,DIE)="^PRSP(454,1,"""_CODE_""",",DIC(0)="AEQLM",DA(1)=1,DLAYGO=454
 W ! D ^DIC I Y<1 K DIE,DA,DLAYGO,CODE G PRSDFIL
 S DA=+Y,DA(1)=1,DIDEL=454,DR=".01:99" W ! D ^DIE K DA,D0 G P1
LIST ; List File Entries
 K DIC S DIC="^DD(454,",DIC(0)="AEQM",DIC("A")="Select FILE: " W ! D ^DIC K DIC G:Y<1 RSET
 S DA=+Y,CODE=$P(^DD(454,DA,0),"^",4),CODE=$P(CODE,";",1),DHD=$P(^DD(454,DA,0),"^",1)
 S (DIC,DIE)="^PRSP(454,1,"""_CODE_""",",DIC(0)="AEQLM",DA(1)=1
 W ! S L=0,FLDS=".01,1;C8",BY=".01" S (FR,TO)="" S:DA=57 FLDS=".01" D EN1^DIP G LIST
RSET K %ZIS S IOP="" D ^%ZIS K %ZIS,IOP,BY,DHD,FLDS,FR,L,TO
KIL K DIC,DA,CODE,DIE,DLAYGO,DIDEL,DR,POP,X,Y Q
