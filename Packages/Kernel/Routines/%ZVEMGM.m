%ZVEMGM ;DJB,VGL**Main Menu [2/23/99 2:24pm]
 ;;12;VPE;;COPYRIGHT David Bolduc @1993
 ;
READ ;Get input
 ;Next: Quit if SKIP NODE
 I $G(FLAGSKIP),$G(SKIPHLD)]"" D SKIPCHK^%ZVEMGO Q
 ;Next: Quit if CODE SEARCH
 I $P(VEET("STATUS"),"^",4)="SEARCH" S VEET("TOP")=VEET("TOP")+1 Q
 NEW KEY,PKG
READ1 S PKG="IG"_GLS W @VEES("CON") ;Turn cursor back on
 S KEY=$$READ^%ZVEMKTM(PKG) Q:KEY="QUIT"
 I KEY="<TAB>" D  ;
 . S TABHLD=VEET("HLN")_"^"_VEET("H$Y") ;Keeps highlight at same node
 . S KEY=VEET("HLN")-1,KEY=$G(^TMP("VEE",PKG,$J,"SCR",KEY))
 . S:KEY']"" KEY="***"
 I KEY?1"S"1.N G:$$ENDFILE^%ZVEMKTM() READ1 D  D REDRAW^%ZVEMKT2() Q
 . S FLAGSKIP=+$E(KEY,2,99) D SKIP^%ZVEMGO
 I ",?,<ESCH>,A,C,ER,ES,EV,G,M,SA,SC,UN,VEDD,"'[(","_KEY_","),KEY'?1.N W $C(7) G READ1
 D RUN(KEY) Q:FLAGE  H 1 D REDRAW^%ZVEMKT2()
 D:CODE'=0 CDSRCH^%ZVEMGO
 Q
RUN(Z1) ;Z1=Opt selected
 NEW X S X="ERROR^%ZVEMGM",@($$TRAP^%ZVEMKU1) KILL X
 NEW FLAGQ S FLAGQ=0
 I Z1?1.N D PIECE^%ZVEMGM1 Q
 I ",A,C,ES,EV,SC,"[(","_Z1_",") I '$$CHECK^%ZVEMGM1() Q
 I Z1="ER" D EDITR^%ZVEMGE Q
 I Z1="ES" D EDITS^%ZVEMGE Q
 I Z1="EV" D EDITV^%ZVEMGE Q
 I Z1="G" D GOTO^%ZVEMGMC Q
 I Z1="SA" D SAVE^%ZVEMGS Q
 I Z1="SC" D STRIP^%ZVEMGE Q
 I Z1="UN" D UNSAVE^%ZVEMGS Q
 I Z1="<ESCH>" D HELP^%ZVEMKT2 Q
 D ENDSCR^%ZVEMKT2
 I Z1["?" D HELP^%ZVEMKT("VGL2") Q
 ;I Z1="A" NEW FLAGPRM D START^%ZVEMG Q
 I Z1="A" D ALT^%ZVEMGM1 Q
 I Z1="C" D CODE^%ZVEMGO Q
 I Z1="M" D MORE^%ZVEMGM1 Q
 I Z1="VEDD" D VEDD^%ZVEMGY Q
 Q
ERROR ;
 D ENDSCR^%ZVEMKT2,ERRMSG^%ZVEMKU1("VGL/RUN"),PAUSE^%ZVEMKU(1)
 Q
