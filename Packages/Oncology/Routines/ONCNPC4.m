ONCNPC4 ;HIRMFO/GWB - PCE Study of Non-Hodgkin's Lymphoma - Table IV;4/18/97
 ;;2.11;ONCOLOGY;**11,15,16,22**;Mar 07, 1995
 K ONC,TABLE,HTABLE
 S TABLE("DATE OF FIRST COURSE OF TREATMENT")="DOFCT^ONCNPC4"
 S TABLE("SURGERY")="S^ONCNPC4"
 S TABLE("RADIATION THERAPY")="R^ONCNPC4"
 S TABLE("CHEMOTHERAPY")="C^ONCNPC4"
 S TABLE("SYSTEMIC CHEMOTHERAPY")="SC^ONCNPC4A"
 S TABLE("INTRATHECAL CHEMOTHERAPY")="IC^ONCNPC4A"
 S TABLE("IMMUNOTHERAPY")="I^ONCNPC4A"
 S HTABLE(1)="DATE OF FIRST COURSE OF TREATMENT"
 S HTABLE(2)="SURGERY"
 S HTABLE(3)="RADIATION THERAPY"
 S HTABLE(4)="CHEMOTHERAPY"
 S HTABLE(5)="SYSTEMIC CHEMOTHERAPY"
 S HTABLE(6)="INTRATHECAL CHEMOTHERAPY"
 S HTABLE(7)="IMMUNOTHERAPY"
 S CHOICES=7
 S D0=ONCONUM D DFC^ONCOCOM S DOFCT=Y
 S DIC="^ONCO(165.5,",DR="50;51.2;53;53.2;58.2",DA=ONCONUM
 S DIQ="ONC",DIQ(0)="IE" D EN^DIQ1
 S CDS=ONC(165.5,ONCONUM,58.2,"E")
 S RAD=ONC(165.5,ONCONUM,51.2,"I"),RADOT=ONC(165.5,ONCONUM,51.2,"E")
 S CHEMO=ONC(165.5,ONCONUM,53.2,"I"),CHEMOT=ONC(165.5,ONCONUM,53.2,"E")
 W @IOF D HEAD^ONCNPC0
 W !?22,"TABLE IV - FIRST COURSE OF TREATMENT"
 W !?22,"------------------------------------"
 S DIE="^ONCO(165.5,",DA=ONCONUM
DOFCT W !,"DATE OF FIRST COURSE OF TREATMENT.: ",DOFCT
S W !!,"SURGERY"
 W !,"-------"
CDSDT S CDSDT=$P($G(^ONCO(165.5,ONCONUM,3)),U,1)
 S Y=CDSDT D DATEOT^ONCOPCE S CDSDT=Y
 S (CDS1,CDS2)="",LOS=$L(CDS) I LOS<46 S CDS1=CDS G CDS
 S NOP=$L($E(CDS,1,46)," ")
 S CDS1=$P(CDS," ",1,NOP-1),CDS2=$P(CDS," ",NOP,999)
CDS W !,"  SURGERY OF PRIMARY SITE DATE....: ",CDSDT
 W !,"  SURGERY OF PRIMARY SITE.........: ",CDS1 W:CDS2'="" !,?39,CDS2
 I (CDS="")!($E(CDS,1,2)="00") D  G:(Y=0)!(Y="") EXIT G R
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,10)="C888"
 .W !,"  EXTRANODAL SURGERY SITE.........: None"
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,11)="00"
 .W !,"  EXTRANODAL SURGICAL PROCEDURE...: 00 No additional surgical procedures"
 .W ! K DIR S DIR(0)="E" D ^DIR Q:(Y=0)!(Y="")  D HEAD^ONCNPC0
 S DR="855  EXTRANODAL SURGERY SITE........." D ^DIE G:$D(Y) JUMP
 S DR="856  EXTRANODAL SURGICAL PROCEDURE..." D ^DIE G:$D(Y) JUMP
R W !!,"RADIATION THERAPY"
 W !,"-----------------"
 S RADDT=$P($G(^ONCO(165.5,ONCONUM,3)),U,4)
 S Y=RADDT D DATEOT^ONCOPCE S RADDT=Y
 W !,"  RADIATION DATE..................: ",RADDT
 W !,"  RADIATION ......................: ",RADOT
 I RAD=0 D  G C
 .W !!,"  IRRADIATED FIELDS:",!
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,12)=2
 .W !,"    LYMPH NODES ABOVE DIAPHRAGM...: Not irradiated"
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,13)=2
 .W !,"    LYMPH NODES BELOW DIAPHRAGM...: Not irradiated"
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,14)=2
 .W !,"    BRAIN.........................: Not irradiated"
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,15)=2
 .W !,"    OTHER EXTRANODAL SITE(S)......: Not irradiated"
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,16)=2
 .W !,"    TOTAL BODY....................: Not irradiated"
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,17)=8
 .W !!,"  RADIATION/CHEMOTHERAPY SEQUENCE.: NA, no radiation and/or no chemo given"
 .W ! K DIR S DIR(0)="E" D ^DIR Q:(Y=0)!(Y="")  D HEAD^ONCNPC0
 I RAD=9 D  G C
 .W !!,"  IRRADIATED FIELDS:",!
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,12)=8
 .W !,"    LYMPH NODES ABOVE DIAPHRAGM...: NA, unknown if radiation therapy given"
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,13)=8
 .W !,"    LYMPH NODES BELOW DIAPHRAGM...: NA, unknown if radiation therapy given"
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,14)=8
 .W !,"    BRAIN.........................: NA, unknown if radiation therapy given"
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,15)=8
 .W !,"    OTHER EXTRANODAL SITE(S)......: NA, unknown if radiation therapy given"
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,16)=8
 .W !,"    TOTAL BODY....................: NA, unknown if radiation therapy given"
 .S $P(^ONCO(165.5,ONCONUM,"NHL2"),U,17)=7
 .W !!,"  RADIATION/CHEMOTHERAPY SEQUENCE.: Unknown if radiation and/or chemo given"
 W !!,"  IRRADIATED FIELDS:",!
 S DR="857    LYMPH NODES ABOVE DIAPHRAGM..." D ^DIE G:$D(Y) JUMP
 S DR="858    LYMPH NODES BELOW DIAPHRAGM..." D ^DIE G:$D(Y) JUMP
 S DR="859    BRAIN........................." D ^DIE G:$D(Y) JUMP
 S DR="860    OTHER EXTRANODAL SITE(S)......" D ^DIE G:$D(Y) JUMP
 S DR="861    TOTAL BODY...................." D ^DIE G:$D(Y) JUMP W !
 S DR="862  RADIATION/CHEMOTHERAPY SEQUENCE." D ^DIE G:$D(Y) JUMP W !
C W !!,"CHEMOTHERAPY"
 W !,"------------"
 S DR="863  PROTOCOL........................" D ^DIE G:$D(Y) JUMP
 D SC^ONCNPC4A
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!,"GO TO: ",X:DTIME I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !,"CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K CHOICES,HTABLE,TABLE
 K CDS,CDS1,CDS2,CDSDT,CHEMO,DOFCT,LOS,NOP,RAD,RADOT,RADDT,DIQ
 Q
