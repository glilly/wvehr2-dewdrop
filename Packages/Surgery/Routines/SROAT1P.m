SROAT1P ;B'HAM ISC/MAM - ATTENDING SURGEON REPORT (CONT.) ; [ 05/11/04  11:29 AM ]
 ;;3.0; Surgery ;**52,50,129**;24 Jun 93
 S SRSAV=SRSS,SRSS=$P(^SRO(137.45,SRSS,0),"^"),SRSS1="",SRSSL=132-$L(SRSS)\2 F LINE=1:1:$L(SRSS) S SRSS1=SRSS1_"="
 S SRPAGE=1,(SRATT,SRHDR,SRNEW)=0 D HDR
 F  S SRATT=$O(^TMP("SR",$J,SRATT)) Q:SRATT=""!SRSOUT  S SRHDR=1,SRATT1="ATTENDING SURGEON: "_$S($E(SRATT,1,3)'="ZZ ":SRATT,1:$E(SRATT,4,99)) D  Q:SRSOUT
 .I 'SRB D:($Y+10>IOSL) PAGE Q:SRSOUT  D AT,MORE
 .I SRB D:SRNEW PAGE Q:SRSOUT  D:'SRNEW AT S SRNEW=1 D MORE
 I '$D(^TMP("SR",$J)) W $$NODATA^SROUTL0()
 S SRSS=SRSAV Q
MORE S SRATC="" F  S SRATC=$O(^TMP("SR",$J,SRATT,SRATC)) Q:SRATC=""!(SRSOUT)  S SRSDATE=0 F  S SRSDATE=$O(^TMP("SR",$J,SRATT,SRATC,SRSDATE)) Q:'SRSDATE!SRSOUT  D CASE
 Q
CASE S SRTN=0 F  S SRTN=$O(^TMP("SR",$J,SRATT,SRATC,SRSDATE,SRTN)) Q:'SRTN!(SRSOUT)  D SET
 Q
SET ; print case
 I $Y+7>IOSL S SRHDR=1 D PAGE I SRSOUT Q
 K SRCODE S CODE=SRATC I 'CODE S SRCODE="ATTENDING CODE NOT ENTERED"
 I CODE S Y=CODE,C=$P(^DD(130,.166,0),"^",2) D Y^DIQ S SRCODE=Y
 S:SRCODE<36 SRCODE(1)=SRCODE I $L(SRCODE)>35 D
 .N I,J,X,Y S X=SRCODE F I=0:1:34 S J=35-I,Y=$E(X,J) I Y=" " S SRCODE(1)=$E(X,1,J-1) S SRCODE(2)=$E(X,J+1,$L(X)) Q
 S SRDT=$E(SRSDATE,4,5)_"/"_$E(SRSDATE,6,7)_"/"_$E(SRSDATE,2,3),SR(0)=^SRF(SRTN,0),DFN=$P(SR(0),"^") D DEM^VADPT
 S SRDIAG=$S($D(^SRF(SRTN,34)):$P(^(34),"^"),1:"POSTOPERATIVE DIAGNOSIS NOT ENTERED")
 S SR(.1)=$S($D(^SRF(SRTN,.1)):^(.1),1:""),SRSUR=$P(SR(.1),"^",4),SRFST=$P(SR(.1),"^",5),SRTWO=$P(SR(.1),"^",6)
 I SRSUR S SRSUR=$P(^VA(200,SRSUR,0),"^") I $L(SRSUR)>17 S SRSUR=$P(SRSUR,",")_","_$E($P(SRSUR,",",2))
 I SRFST S SRFST=$P(^VA(200,SRFST,0),"^") I $L(SRFST)>17 S SRFST=$P(SRFST,",")_","_$E($P(SRFST,",",2))
 I SRTWO S SRTWO=$P(^VA(200,SRTWO,0),"^") I $L(SRTWO)>17 S SRTWO=$P(SRTWO,",")_","_$E($P(SRTWO,",",2))
OPS S SROPER=$P(^SRF(SRTN,"OP"),"^"),OPER=0 F I=0:0 S OPER=$O(^SRF(SRTN,13,OPER)) Q:OPER=""  D OTHER
 K SROP,MM,MMM S:$L(SROPER)<55 SROP(1)=SROPER I $L(SROPER)>54 S SROPER=SROPER_"  " S SROPER=SROPER_"  " F M=1:1 D LOOP Q:MMM=""
 W !!,SRDT,?12,VADM(1),?52,SRDIAG,?112,SRSUR,!,SRTN,?12,VA("PID"),?52,SROP(1),?112,SRFST,! W:$P($G(^SRF(SRTN,30)),"^") "ABORTED" W ?12,SRCODE(1) W:$D(SROP(2)) ?52,SROP(2) W ?112,SRTWO
 I $D(SRCODE(2))!$D(SROP(3)) W ! W:$D(SRCODE(2)) ?13,SRCODE(2) W:$D(SROP(3)) ?50,SROP(3)
 I $D(SROP(4)) W !,?50,SROP(4)
 Q
OTHER ; other operations
 S SRLONG=1 I $L(SROPER)+$L($P(^SRF(SRTN,13,OPER,0),"^"))>240 S SRLONG=0,OPER=999,SROPERS=" ..."
 I SRLONG S SROPERS=$P(^SRF(SRTN,13,OPER,0),"^")
 S SROPER=SROPER_$S(SROPERS=" ...":SROPERS,1:", "_SROPERS)
 Q
LOOP ; break procedure if greater than 55 characters
 S SROP(M)="" F LOOP=1:1 S MM=$P(SROPER," "),MMM=$P(SROPER," ",2,200) Q:MMM=""  Q:$L(SROP(M))+$L(MM)'<55  S SROP(M)=SROP(M)_MM_" ",SROPER=MMM
 Q
PAGE I $E(IOST)="P" D HDR Q
 W !!,"Press RETURN to continue, or '^' to quit:  " R X:DTIME I '$T!(X["^") S SRSOUT=1 Q
 I X["?" W !!,"There are more cases for the date range listed.  If you want to continue",!,"listing them, enter RETURN.  Enter '^' to exit from this option." G PAGE
 S SRHDR=1
HDR ; print heading
 I $D(ZTQUEUED) D ^SROSTOP I SRHALT S SRSOUT=1 Q
 I +$Y W @IOF
 W !,?(132-$L(SRINST)\2),SRINST,?120,"PAGE: ",SRPAGE,!,?58,"SURGICAL SERVICE",?100,"REVIEWED BY: ",!,?54,"ATTENDING SURGEON REPORT",?100,"DATE REVIEWED: "
 W !,?(132-$L(SRFRTO)\2),SRFRTO,?100,SRPRINT
 W !!,?3,"DATE",?12,"PATIENT",?52,"PRINCIPAL DIAGNOSIS",?112,"SURGEON",!,?2,"CASE #",?14,"ID#",?52,"PRINCIPAL OPERATIVE PROCEDURE",?112,"1ST ASST",!,?12,"ATTENDING CODE",?112,"2ND ASST",! F I=1:1:132 W "="
 W !!,?SRSSL,SRSS,!,?SRSSL,SRSS1 S SRPAGE=SRPAGE+1 D AT
 Q
AT I 'SRHDR S SRHDR=1 Q
 S SRATT2="" F LINE=1:1:$L(SRATT1) S SRATT2=SRATT2_"-"
 S X=132-$L(SRATT1)\2 W !!,?X,SRATT1,!,?X,SRATT2 S SRHDR=0
 Q
