ABSVL1 ;VAMC ALTOONA/CTB&CLH - LOG (IN/OUT) VOLUNTEER CONTINUED ;1/10/97  9:07 AM
V ;;4.0;VOLUNTARY TIMEKEEPING;**7**;JULY 1994;
LI Q:'$D(^ABS(503330.1,"AC",$P(DT,"."),+Y))
 W !!,*7,$$GET^ABSVU1("ALREADY LOGGED IN",PLANG) S ABSVXA=$$GET^ABSVU1("CONTINUE?",PLANG),ABSVXB=$$GET^ABSVU1("YES OR NO",PLANG),%=2 D ^ABSVYN G:%'=1 OUT^ABSVL
 S ABSVXA=$$GET^ABSVU1("ANOTHER ORG/SERV",PLANG),ABSVXB=$$GET^ABSVU1("YES OR NO",PLANG),%=1 D ^ABSVYN G:%=1 LI1
 Q
LI1 N DIC
 S X=ABSVX("VOLDA"),DIC="^ABS(503330.1,",DIC(0)="N" D FILE^DICN
 G ED^ABSVL
NOTNEW S ABSVXA=$$GET^ABSVU1("ALREADY LOGGED",PLANG),ABSVXA(1)=$$GET^ABSVU1("WISH TO CHANGE",PLANG),ABSVXB=$$GET^ABSVU1("YES OR NO",PLANG,"'^'"),%=2
 D ^ABSVYN
 I %<0 S X=$$GET^ABSVU1("START OVER",PLANG)_"*" D MSG^ABSVQ R X:2 G OUT^ABSVL
 QUIT:%=1
 S ABSVXA=$$GET^ABSVU1("ANOTHER ORG/SERV",PLANG),ABSVXB=$$GET^ABSVU1("YES OR NO",PLANG),%=2
 D ^ABSVYN
 I %=1 S DIC("S")="I $P(^(0),U,3)=COMB,$P(^(0),U,9)=ABSV(""SITE"")" G ED^ABSVL
 G RESET^ABSVL
QUES I NEW S DIR(0)="NO^1:9",DIR("A")=$$GET^ABSVU1("HOW MANY HOURS",PLANG)
 I 'NEW S DIR(0)="NAO^1:9",DIR("A")=$$GET^ABSVU1("HOW MANY HOURS",PLANG)_": ",DIR("B")=$P($G(^ABS(503330.1,ABSVX("LDA"),0)),"^",7)
 S DIR("?")="^S X=""!""_$$GET^ABSVU1(""HOURS WORKED"",PLANG,""'^'"")_""*"" D MSG^ABSVQ"
 D ^DIR
 I Y="",$D(DIRUT) K DIRUT X $P(DIR("?"),"^",2,99)
 K DIR S X=$$DIR^ABSVU2
 QUIT
LUNCH N X
 S LUNCH=0
 I $P(^ABS(503330,ABSVX("VOLDA"),4,ABSV("INST"),0),"^",24)=0 QUIT
 S ABSVXA=$$GET^ABSVU1("LUNCH?",PLANG),ABSVXB=$$GET^ABSVU1("YES OR NO",PLANG),%=1
 D ^ABSVYN
 I %<0 S X=$$GET^ABSVU1("LOGIN NOT COMPLETED",PLANG)_"*" D MSG^ABSVQ G OUT^ABSVL
 I %=1,$P(ABSV("PARAM"),"^",12)=2 S %=4
 S LUNCH=$S(%=1:1,%=4:2,1:0)
 QUIT
