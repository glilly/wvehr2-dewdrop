PRCHES3 ;WISC/RHD/AKS-ESIG MAINTENANCE ROUTINE ;3/18/93  12:49
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 ;ROUTINE FOR MAINTAINING SUBFIELD 19.4 (LCS ELEC. SIG)
 ; FIELD .6 (PARTIAL), FILE 442
DECODE(LEVEL0,LEVEL1) ;return esig to readable form
 ;returns "" if unsuccessful
 NEW RECORD,RECORD1,VERSION,PERSON,SIG,CHECKSUM
 S RECORD=$G(^PRC(442,LEVEL0,11,LEVEL1,0)) I RECORD="" Q ""
 S RECORD1=$G(^PRC(442,LEVEL0,11,LEVEL1,1))
 S VERSION=$P(RECORD1,"^",13)
 S PERSON=+$P(RECORD1,"^",3)
 I VERSION'="",VERSION'=1 Q ""
 S SIG=$P(RECORD1,"^",4)
D1 ;decode e signature for version 1
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_LEVEL1_"^"_$$STRING(RECORD,RECORD1))
 Q $$DECODE^PRCUESIG(SIG,PERSON,CHECKSUM)
ENCODE(LEVEL0,LEVEL1,USERNUM,Y) ;Encode e signature for version 1 only
 NEW RECORD,RECORD1,SIGBLOCK,CHECKSUM,OLDUSER
 S USERNUM=+USERNUM
 I USERNUM=0 S Y=-3 Q  ;-3 no user number available for filing
 S SIGBLOCK=$P($G(^VA(200,USERNUM,20)),"^",2)
 I SIGBLOCK="" S Y=-2 Q  ;-2 no signature block
 S RECORD=$G(^PRC(442,LEVEL0,11,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,11,LEVEL1,1))
 I RECORD="" S Y=-1 Q  ;-1 no 2237 record
 I $P(RECORD1,"^",4)'="" S Y=-4 Q  ;-4 cannot re-sign record
 S OLDUSER=+$P(RECORD1,"^",3)
 I OLDUSER=0 S $P(RECORD1,"^",3)=USERNUM
 I OLDUSER>0 S USERNUM=OLDUSER
 S:$P(RECORD1,"^",5)="" $P(RECORD1,"^",5)=$$NOW^PRCUESIG
 S CHECKSUM=$$SUM^PRCUESIG(LEVEL0_"^"_LEVEL1_"^"_$$STRING(RECORD,RECORD1))
 S $P(RECORD1,"^",4)=$$ENCODE^PRCUESIG(SIGBLOCK,USERNUM,CHECKSUM)
 S $P(RECORD1,"^",13,14)="1^"_$$SUM^PRCUESIG(SIGBLOCK)
 S ^PRC(442,LEVEL0,11,LEVEL1,1)=RECORD1
 S Y=1 Q
REMOVE(LEVEL0,LEVEL1) ;Entry point to remove esig
 NEW I,RECORD1
 S RECORD1=$G(^PRC(442,LEVEL0,11,LEVEL1,1)) Q:RECORD1=""
 F I=3,4,5,14 S $P(RECORD1,"^",I)=""
 S ^PRC(442,LEVEL0,11,LEVEL1,1)=RECORD1
 Q
VERIFY(LEVEL0,LEVEL1)      ;extrinsic function to verify version 1 esig  Returns 1 if valid
 NEW RECORD,RECORD1,VERSION,SIGBLOCK
 S RECORD=$G(^PRC(442,LEVEL0,11,LEVEL1,0))
 S RECORD1=$G(^PRC(442,LEVEL0,11,LEVEL1,1))
 S VERSION=$P(RECORD1,"^",13),SIGBLOCK=$P(RECORD1,"^",14)
 I VERSION_SIGBLOCK="" Q 1
 Q ($$SUM^PRCUESIG($$DECODE(LEVEL0,LEVEL1))=SIGBLOCK)
STRING(X,X1)          ;Build String of critical fields
 Q $P(X,"^")_"^"_$P(X1,"^",5)
