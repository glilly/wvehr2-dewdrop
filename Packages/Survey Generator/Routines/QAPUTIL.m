QAPUTIL ;557/THM-SURVEY GENERATOR UTILITIES [ 06/02/95  11:36 AM ]
 ;;2.0;Survey Generator;;Jun 20, 1995
 ;
SCREEN ;screen parameters
 S IOP="HOME" D ^%ZIS,DT^DICRW K IOP
 D ENS^%ZISS S RVON=IORVON,RVOFF=IORVOFF,CEOP=IOEDEOP,BLDON=IOINHI,BLDOFF=IOINLOW,TOPBOT=IOSTBM,IOTM=8,IOBM=24,$P(SPACE," ",85)="" D KILL^%ZISS
 ;I IOST?1"C-VT100"!(IOST?1"C-VT320"),TOPBOT]""
 W:TOPBOT]"" @TOPBOT
 S QAPBAR="W RVON,?(80-$L(QAPHDR)\2),QAPHDR,$E(SPACE,$X,77),RVOFF,!"
 S CLEOP="S DX=0,DY=4 X IOXY W CEOP,!" ;leaves header intact
 S CLEOP1="S DX=0,DY=QLINE X IOXY W CEOP,!" ;resets input line on ?
 Q  ;kill variables via calling program
 ;
EXIT ;package-wide variable kill; used by most programs
 D ^%ZISC I IOST?1"C-VT100"!(IOST?1"C-VT320") S IOTM=1,IOBM=24 W @TOPBOT,@IOF,!
 X ^%ZOSF("EON") K ^TMP($J)
 K %,%DT,%ZIS,%X,%Y,%Z,ACTION,ANS,ANSDA,ANSTEXT,ANSTYPE,ANSW,BANNER,BNKOFF,BNKON,C,CEOP,CHOICE,CLEOP,CLEOP1,CNT,CNTA,CQUES,CURSTAT,D,DO,D1,DA,DAX,DD,DELETE,DEMDTA,DEMO,DEMOG,DEMQUES,DEMTYPE,DI,DIC,DIC,DIE,DIF,DIFQ,DIK,DINUM,DIR
 K DIRUT,QAPQN,DISP,DISYS,DIU,DLAYGO,DO,DQ,DR,DTA,DTOUT,DUOUT,DX,DY,EDIT,EXIT,FILEDA,FILENAME,FILENUM,FIND,GRADIENT,I,IFN,INCMSG,INCREM,INSERT,IOTM,IOBM,IOP,LINE,LORD,LPART,LQUES,MSSG,MSSG0,NINCREM,NLINE,NOPEN,NSRVDA,QAPXX
 K NUM,NWNAM,ODEMOG,OSRVDA,OSRVNAM,OSRVQDA,OUT,PART,PART1,PASSWORD,PASSX,PCNT,PG,POP,PQUES,PRESPON,QA,QANS,QAP,QAPBAR,QAPCNT,QAPDATE,QAPEDTR,QAPFILE,QAPHD,QAPHDR,QAPNAME,QAPOUT,QAPSURV,QAPX,QLINE,QNAME,QNUM,QUES,QUES1,QUIT,QY,QZ
 K REM,RESUME,RVOFF,RVON,S,DISYS,SITE,SORT,SORTTXT,SPACE,SS,STOP,SUBJ,SURVEY,SVST,SVSTAT,SVYNAME,TITLE,TOF,TOPBOT,TOTANS,TOTPART,USER,USERPRT,X,XX,Y,YY,ZTDESC,ZTIO,ZTQUEUED,ZTRTN,ZTSAVE,ZTSK,BANNER1,DMAN,DEMERR
 K RESPONDA,RESPCNT,J,D0,D1,DEMPTR,DANS,SQUEST,CCNT,SCNT,ICNT,QAPNOANS,BLDON,BLDOFF,CNTPG,CREATE,MSGSKP,QAPDUZ,QNUMANS,STATUS,XCNT,ANSX,LFTLBL,RGTLBL,LSTNUM,INP,INP1,QCNT,QUESCNT,BYPASS,BLANKS,DEML,DEMVAL,QUEST,RESPD,WPPRT
 K BLANKNA,BYPASSNA,CODE,DEMONAME,DQUES,FILEPTR,MEANING,OWNER,QAPSELCT,VERS,XDA,XMTEXT,DSTOP,FSTOP,XMSUB,SVYSITE,NEWDA,NEWDA1,SVYNAME,AUTHED,NEWNAME,ONFILE,XMZ,XCNP,XCM,NEWREC,XMDUZ,XMMG,DMANMSTR,%I,LASTDATE
 Q
 ;
INSTRUCT K OUT
 I '$D(BANNER),IOST?1"C-".E D BAN
 W !,"Description:",!! F NLINE=0:0 S NLINE=$O(^QA(748,SURVEY,2,NLINE)) Q:NLINE=""!($D(OUT))  S X=^QA(748,SURVEY,2,NLINE,0) W X,! I IOST?1"C-".E,$Y>(IOSL-6) W !!,"Press RETURN to continue  " R ANS:DTIME S:'$T!(ANS[U) OUT=1 Q:$D(OUT)  D BAN
 W !!,"Instructions:",!! F NLINE=0:0 S NLINE=$O(^QA(748,SURVEY,4,NLINE)) Q:NLINE=""!($D(OUT))  S X=^QA(748,SURVEY,4,NLINE,0) W X,! I IOST?1"C-".E,$Y>(IOSL-6) W !!,"Press RETURN to continue  " R ANS:DTIME S:'$T!(ANS[U) OUT=1 Q:$D(OUT)  D BAN
 I $D(OUT) K ANS Q
 W !,"- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -" X:$D(TOF) TOF I $D(QAPOUT),QAPOUT=1 Q
 W ! X:$D(TOF) TOF I $D(QAPOUT),QAPOUT=1 Q
 W ! X:$D(TOF) TOF I $D(QAPOUT),QAPOUT=1 Q
 Q
 ;
BAN I '$D(BANNER),IOST?1"C-".E W @IOF,! S QAPHDR="Survey Title: "_TITLE X QAPBAR S QAPHDR="Instructions" X QAPBAR W !
 I $D(BANNER) D HDR^QAPPT1
 Q
