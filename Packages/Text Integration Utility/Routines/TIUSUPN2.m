TIUSUPN2 ;SLC/TT - DAILY SEARCH FOR SIGNED DOCUMENT WITH UNSIGNED STATUS; 11/29/04
 ;;1.0; TEXT INTEGRATION UTILITIES;**180**;Jun 20, 1997
 ;
 Q
EN ;Entry point to send mail
 D SNDMAIL
 Q
 ;
SNDMAIL ; SEND MAIL
 ;
 N TIUSTDT,TIUEDT,TIUTOTAL,TIUINFO,TIUDTIME,TIUDA,TIUI,TIUTEXT
 K ^TMP("TIUP180",$J)
 S TIUSTDT=DT,TIUEDT=DT+1,TIUTOTAL=0
 F  S TIUSTDT=$O(^TIU(8925,"F",TIUSTDT)) Q:'TIUSTDT!(TIUSTDT>TIUEDT)  D
 .S TIUDA=$O(^TIU(8925,"F",TIUSTDT,0)) S TIUTOTAL=TIUTOTAL+$$GATHER^TIUSUPN2(TIUDA)
 I TIUTOTAL D
 .S TIUDTIME=0,TIUI=3
 .F  S TIUDTIME=$O(^TMP("TIUP180",$J,TIUDTIME)) Q:'TIUDTIME  D
 ..S TIUINFO=$G(^TMP("TIUP180",$J,TIUDTIME))
 ..S TIUI=TIUI+1,TIUTEXT(TIUI)=$P(TIUINFO,U)
 .D CRTML
 Q
 ;
CRTML ; CREAT MAIL
 ;
 N XMSUB,XMTEXT,XMDUZ,XMY
 S XMDUZ="PATCH TIU*1*180"
 S XMY("G.TIU SIGNED/UNSIGNED DOC")="",XMY(.5)=""
 S TIUTEXT(1)="Below "_$S(TIUTOTAL=1:"is IEN for uncosigned or completed document",1:"are IENs for uncosigned/completed documents")_" with unsigned "_$S(TIUTOTAL=1:"status.",1:"statuses.")
 S TIUTEXT(2)="Please run ""TIU SIGNED/UNSIGNED PN"" option for more detail."
 S TIUTEXT(3)=""
 S XMTEXT="TIUTEXT(",XMSUB="TIU SIGNED/UNSIGNED DOCUMENTS"
 D ^XMD
 Q
 ;
GATHER(TIUIEN) ; GET SIGNED DOCUMENT BUT UNSIGNED STATUS
 ; Input  -- TIUIEN: TIU Document file (#8925) IEN
 ; Output -- CNT: Total documents found
 ; TIUDOC - TIU DOCUMENT NAME
 ; TIUPTN - PATIENT NAME
 ; TIUSIGDT - SIGNATURE DATE/TIME
 ;
 N TIUD0,TIUD15,TIUDOC,TIUPTN,TIUSIGDT,TIUCNT
 S TIUCNT=0
 I TIUIEN'>0 Q 0
 I '$D(^TIU(8925,TIUIEN,0))!('$D(^TIU(8925,TIUIEN,15))) Q 0
 S TIUD0=$G(^TIU(8925,TIUIEN,0)),TIUD15=$G(^TIU(8925,TIUIEN,15))
 I $P(TIUD0,U,5)=5,$P(TIUD15,U,1)>0 D 
 .S TIUSIGDT=$P(TIUD15,U),TIUCNT=TIUCNT+1
 .S ^TMP("TIUP180",$J,TIUSIGDT)=TIUIEN
 Q TIUCNT
