FSCLMIPX ;SLC/STAFF-NOIS List Manager - Installs Patches ;11/15/97  18:49
 ;;1.1;NOIS;;Sep 06, 1998
 ;
ENTRY ; from FSCSTUP
 N DAY,DOMAIN,LINE,MESSAGE,NOMSG,NUM,NUM0,NUM1,PATCHNUM,PACKAGE,SEQ,SITENAME,VERSION
 K ^TMP("FSC INSTALLS",$J),^TMP("FSC PATCH",$J)
 I '$G(FSCDEV) W !
 S VALMCNT=0,VALMCAP="",$P(VALMCAP," ",80)=""
 I '$D(FSCSTU) Q
 S DATE=$G(DATE,DT)
 S NOMSG="         No information on tracking"
 D
 .I FSCSTU="PATCH SITE" D SITE^FSCSTUPI(PATCH,.VALMCNT) Q
 .I FSCSTU="PATCH ALL" D ALL^FSCSTUPI(SITE,DATE,.VALMCNT) Q
 .I FSCSTU="PATCH PACK" D PACK^FSCSTUPI(SITE,MODULE,.VALMCNT) Q
 .I FSCSTU="PATCH SITENOT" D SITENOT^FSCSTUPN(PATCH,.VALMCNT) Q
 .I FSCSTU="PATCH ALLNOT" D ALLNOT^FSCSTUPN(SITE,.VALMCNT) Q
 .I FSCSTU="PATCH PACKNOT" D PACKNOT^FSCSTUPN(SITE,MODULE,.VALMCNT) Q
 .I FSCSTU="PATCH TEST" D TEST^FSCSTUPT(PATCH,.VALMCNT) Q
 .I FSCSTU="PATCH TESTSITE" D SITE^FSCSTUPT(SITE,.VALMCNT) Q
 .I FSCSTU="PATCH TESTPACK" D PACK^FSCSTUPT(MODULE,.VALMCNT) Q
 S ^TMP("FSC INSTALLS",$J)=VALMCNT_U_VALMCNT
 I 'VALMCNT S VALMCNT=2,^TMP("FSC INSTALLS",$J,1,0)=" ",^TMP("FSC INSTALLS",$J,2,0)=NOMSG
 K ^TMP("FSC PATCH",$J)
 Q
 ;
HEADER ; from FSCSTUP
 N HDR
 S FSCSTU=$G(FSCSTU)
 S HDR="Site Tracking"
 I FSCSTU="PATCH SITE" S HDR=HDR_" - Patch "_$P($G(^A1AE(11005,+$G(PATCH),0)),U)_" installed at these sites"
 I FSCSTU="PATCH SITENOT" S HDR=HDR_" - Patch "_$P($G(^A1AE(11005,+$G(PATCH),0)),U)_" is NOT installed at these sites"
 I FSCSTU="PATCH ALL" S HDR=HDR_" - "_$P($G(^FSC("SITE",+$G(SITE),0)),U)_" patch installs backto "_$$FMTE^XLFDT($G(DATE,DT))
 I FSCSTU="PATCH ALLNOT" S HDR=HDR_" - "_$P($G(^FSC("SITE",+$G(SITE),0)),U)_" patches NOT installed"
 I FSCSTU="PATCH PACK" S HDR=HDR_" - "_$P($G(^FSC("SITE",+$G(SITE),0)),U)_" patches for "_$P($G(^FSC("MOD",+$G(MODULE),0)),U)
 I FSCSTU="PATCH PACKNOT" S HDR=HDR_" - "_$P($G(^FSC("SITE",+$G(SITE),0)),U)_" patches NOT installed for "_$P($G(^FSC("MOD",+$G(MODULE),0)),U)
 I FSCSTU="PATCH TEST" S HDR=HDR_" - Test patch "_$P($G(^A1AE(11005,+$G(PATCH),0)),U)_" installed at these sites"
 I FSCSTU="PATCH TESTSITE" S HDR=HDR_" - Test patch installs at "_$P($G(^FSC("SITE",+$G(SITE),0)),U)
 I FSCSTU="PATCH TESTPACK" S HDR=HDR_" - Test patch installs for "_$P($G(^FSC("MOD",+$G(MODULE),0)),U)
 S VALMHDR(1)=HDR
 Q
 ;
VERINST(PACKAGE,DOMAIN) ; $$(package,domain) -> current version#
 N APP,SITENUM
 S DOMAIN=$P($G(^DIC(4.2,+DOMAIN,0)),U)
 I '$L(DOMAIN) Q ""
 S SITENUM=$O(^NTS(2050,"AE",DOMAIN,0))
 I 'SITENUM Q ""
 S APP=+$O(^DIC(120102,"AP",PACKAGE,0))
 I 'APP Q ""
 Q $P($G(^NTS(2050,SITENUM,8,APP,0)),U,7)
