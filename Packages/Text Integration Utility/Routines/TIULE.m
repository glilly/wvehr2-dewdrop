TIULE ; SLC/SBW - Environment Library Functions ;4/22/94  11:28
 ;;1.0;TEXT INTEGRATION UTILITIES;**10**;Jun 20, 1997
SETPARM ; Get parameters for user's division
 N TIUI
 ; S TIUI=$O(^TIU(8925.99,"B",+$O(^DG(40.8,"AD",+$G(DUZ(2)),0)),0))
 S TIUI=$O(^TIU(8925.99,"B",+$G(DUZ(2)),0))
 S:+TIUI'>0 TIUI=+$O(^TIU(8925.99,0))
 S TIUPRM0=$G(^TIU(8925.99,+TIUI,0)),TIUPRM1=$G(^TIU(8925.99,+TIUI,1))
 Q
GETDOCS(TIUY,TIUDUZ,TIUCLASS) ; Get personal document lists
 N TIUI,TIUDA
 S TIUI=0,TIUDA=+$O(^TIU(8925.98,"AC",TIUDUZ,TIUCLASS,0))
 I 'TIUDA S TIUY=0 Q
 S TIUY=$$PNAME^TIULC1(TIUCLASS)_U_$P($G(^TIU(8925.98,+TIUDA,0)),U,3)
 S TIUY=TIUY_U_$P($G(^TIU(8925.1,+$P(TIUY,U,2),0)),U)
 F  S TIUI=$O(^TIU(8925.98,TIUDA,10,"B",TIUI)) Q:+TIUI'>0  D
 . N TIUJ S TIUJ=$O(^TIU(8925.98,TIUDA,10,"B",TIUI,0)) Q:+TIUJ'>0
 . S TIUY(TIUI)=$P($G(^TIU(8925.98,TIUDA,10,TIUJ,0)),U,2,3)
 Q
PERSPRF(USER) ; Get USER's Personal Preferences
 N DA,Y S Y=""
 S DA=+$O(^TIU(8926,"B",USER,0)) I +DA'>0 G PERSPX
 S Y=$G(^TIU(8926,+DA,0))
PERSPX Q Y
PERSDOC(USER,TIUCLASS) ; Get Default document type, given user & class
 N TIUY,TIUTYPE,TIUDA
 S TIUDA=+$O(^TIU(8925.98,"AC",USER,TIUCLASS,0))
 I 'TIUDA S TIUY=0 G PERSDOX
 S TIUTYPE=$P($G(^TIU(8925.98,+TIUDA,0)),U,3)
 I '+$$CANPICK^TIULP(TIUTYPE) S TIUY=0 G PERSDOX
 S TIUY=TIUTYPE_U_$P($G(^TIU(8925.1,+TIUTYPE,0)),U)
PERSDOX Q TIUY
PERSLOC(USER) ; Get Default location (By day of week, if defined)
 N DA,Y S Y=""
 S DA=+$O(^TIU(8926,"B",USER,0)) I +DA'>0 G PERSLOX
 S Y=+$P(^TIU(8926,+DA,0),U,2) S:+Y Y=Y_U_$P($G(^SC(+Y,0)),U)
 I +$O(^TIU(8926,+DA,1,0)) D
 . N DOW,S1
 . S DOW=$$DOW^XLFDT(DT,1)+1,S1=+$O(^TIU(8926,DA,1,"B",DOW,0)) Q:+S1'>0
 . S Y=+$P(^TIU(8926,+DA,1,+S1,0),U,2) S:+Y Y=Y_U_$P($G(^SC(+Y,0)),U)
PERSLOX Q Y
