PRCARFU ;WASH-ISC@ALTOONA,PA/CMS-PREPAYMENT UTILITY ;5/28/93  12:59 PM
V ;;4.5;Accounts Receivable;**104,107,153**;Mar 20, 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
EN(BN) ;If no activity or deceased change to Refund Review
 N DAT,DFN,EDAT,LDAT,PRCAPER,TR,VA,VADM,VAERR,X,Y,%DT
 S DFN=$G(^RCD(340,$P(^PRCA(430,BN,0),U,9),0)) G ENQ1:DFN'[";DPT" S DFN=+DFN
 I $P(^PRCA(430,BN,0),U,2)'=$O(^PRCA(430.2,"AC",33,0)) G ENQ1
 D DEM^VADPT G ENQ0:VAERR I +$G(VADM(6)) D RR^PRCARFD(BN) G ENQ0
 I +$G(^PRCA(430,BN,7))<1 G ENQ1
 S X="T-60",%DT="" D ^%DT S EDAT=Y
 S X="T-365",%DT="" D ^%DT S LDAT=Y,DAT=0
 F TR=0:0 S TR=$O(^PRCA(433,"C",BN,TR)) Q:'TR  S:+$G(^PRCA(433,TR,1))>DAT DAT=+$G(^PRCA(433,TR,1))
 I DAT'>EDAT,+$G(^PRCA(430,BN,7))>25 D RR^PRCARFD(BN) G ENQ0
 I DAT'>LDAT,+$G(^PRCA(430,BN,7))'<1 D RR^PRCARFD(BN) G ENQ0
ENQ1 Q 1
ENQ0 Q 0
