SPNPSR04 ;HIRMFO/DAD,WAA-HUNT,SD-WDE: VITAL STATUS ;8/7/95  15:29
 ;;2.0;Spinal Cord Dysfunction;**11**;01/02/1997
 ;
EN1(D0,SPNVIT) ; *** Search entry point
 ; Input:
 ;  ACTION,SEQUENCE = Search ACTION,SEQUENCE number
 ;  D0       = SCD (SPINAL CORD) REGISTRY file (#154) IEN
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"VITAL STATUS") = 0^DEAD ! 1^ALIVE
 ;..SPNVIT = 0 ! 1
 ; Output:
 ;  $S( D0_Meets_Search_Criteria : 1 , 1 : 0 )
 ;
 N DFN,MEETSRCH,VA,VADM,VAERR,VITLSTAT
 S MEETSRCH=0
 S DFN=+$P($G(^SPNL(154,+D0,0)),U)
 D DEM^VADPT
 I 'VAERR D
 . S VITLSTAT=($P($G(VADM(6)),U)>0)
 . I VITLSTAT=SPNVIT S MEETSRCH=1
 . Q
 Q MEETSRCH
 ;
EN2(ACTION,SEQUENCE) ; *** Prompt entry point
 ; Input:
 ;  ACTION,SEQUENCE = Search ACTION,SEQUENCE number
 ; Output:
 ;  SPNLEXIT = $S( User_Abort/Timeout : 1 , 1 : 0 )
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"VITAL STATUS") = 0^DEAD ! 1^ALIVE
 ;  ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"VITAL STATUS") = $$EN1^SPNPSR(D0,VITAL)
 ;
 N DIR,DIRUT,DTOUT,DUOUT,VITLSTAT
 K ^TMP($J,"SPNPRT",ACTION,SEQUENCE),DIR
 S DIR(0)="SOAM^0:ALIVE;1:DEAD;" ; CM/SD REVERSED ALIVE & DEAD TEXT
 S DIR("A")="Patient vital status: "
 S DIR("?")="Enter 0 for alive or 1 for dead patients" ; CM/SD REV TEXT
 D ^DIR S VITLSTAT=Y_U_$G(Y(0))
 S SPNLEXIT=$S($D(DTOUT):1,$D(DUOUT):1,1:0)
 I 'SPNLEXIT,Y'="" D
 .S ^TMP($J,"SPNPRT",ACTION,SEQUENCE,"VITAL STATUS")=VITLSTAT
 .S ^TMP($J,"SPNPRT",ACTION,SEQUENCE,0)="$$EN1^SPNPSR04(D0,"_$P(VITLSTAT,U)_")"
 .Q
 Q
