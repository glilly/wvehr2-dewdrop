NURA7A ;HIRMFO/MD,FT-HOME PHONE NUMBERS FOR ENTIRE STAFF ;8/8/96  13:45
 ;;4.0;NURSING SERVICE;;Apr 25, 1997
 Q:'$D(^DIC(213.9,1,"OFF"))  Q:$P(^DIC(213.9,1,"OFF"),"^",1)=1
 W ! S (NURSW1,NUROUT,NURPAGE,NURQUIT)=0
 D EN1^NURSAUTL G QUIT:NUROUT
 I NURMDSW S DIC(0)="AEQZ",NURPLSCR=0 D EN5^NURSAGSP G:$G(NUROUT) QUIT
 I NURMDSW=0,NURPLSW=1 S NURPLSCR=0 D PRD^NURSAGSP K NURPLSCR I $G(NUROUT) G QUIT
 W ! S ZTDESC="Nursing Home Phone Numbers for Entrie Staff",ZTRTN="START^NURA7A" D EN7^NURSUT0 G:POP!($D(ZTSK)) QUIT
START ;
 K ^TMP($J)
 S X="" F  S X=$O(^NURSF(210,"AC",X)) Q:X=""  I X'="R" F DA=0:0 S DA=$O(^NURSF(210,"AC",X,DA)) Q:DA'>0  I $G(^NURSF(210,DA,0))'="" S DA(1)=+^NURSF(210,DA,0) D:+DA(1) BLD
 U IO D PRINT
QUIT ;
 K ^TMP($J) D CLOSE^NURSUT1,^NURAKILL
 Q
BLD W:$E(IOST)="C"&'($R(10)) "." Q:NURSZAP>7&(NURSZDA'=DA)  S N1=$S($P($G(^VA(200,DA(1),0)),U)'="":$P($G(^(0)),"^"),1:"  BLANK")
 N X,Y,Z K NUR("DIQ1") S DIC="^NURSF(210,",DR="3;4",DIQ="NUR(""DIQ1"",",DIQ(0)="EN" D EN^DIQ1 K DIC,DIQ,DR
 S NURFAC(2)=$S($G(NUR("DIQ1",210,DA,4,"E"))'="":NUR("DIQ1",210,DA,4,"E"),1:"  BLANK")
 S NURPROG(2)=$S($G(NUR("DIQ1",210,DA,3,"E"))'="":NUR("DIQ1",210,DA,3,"E"),1:"  BLANK")
 I $G(NURMDSW),'$G(NURFAC),$G(NURFAC(1))'=$G(NURFAC(2)) Q
 I $G(NURPLSW),$G(NURPROG)=0,$G(NURPROG(1))'=$G(NURPROG(2)) Q
 I $G(NURPLSW) S:NURPROG(2)="NURSING" NURPROG(2)=" NURSING"
 S ^TMP($J,NURFAC(2),NURPROG(2),N1,DA)=""
 Q
 ; DETAIL LINE PRINT ROUTINE
PRINT S NURFAC(2)="" F  S NURFAC(2)=$O(^TMP($J,NURFAC(2))) Q:NURFAC(2)=""  D P0 Q:NURQUIT
 Q
P0 S NURPROG(2)="" F  S NURPROG(2)=$O(^TMP($J,NURFAC(2),NURPROG(2))) Q:NURPROG(2)=""  D:NURSW1 HEADER Q:NURQUIT  D P1 Q:NURQUIT
 Q
P1 S N1="" F  S N1=$O(^TMP($J,NURFAC(2),NURPROG(2),N1)) Q:N1=""  D P2 Q:NURQUIT
 Q
P2 F DA=0:0 S DA=$O(^TMP($J,NURFAC(2),NURPROG(2),N1,DA)) Q:DA'>0  D WRITE Q:NURQUIT
 Q
WRITE I 'NURSW1!($Y>(IOSL-6)) D HEADER Q:NURQUIT
 S NURSW1=1 W ! I $D(^VA(200,$P(^NURSF(210,DA,0),"^"),0)),$P(^(0),"^",1)'="" W $E($P(^(0),"^",1),1,20)
 D EN2^NURSUT0 W ?36,NPSPOS
 F NS2=0:0 S NS2=$O(^NURSF(210,DA,2,NS2)) Q:NS2'>0  W:NS2'=$O(^NURSF(210,DA,2,0)) ! W:$D(^NURSF(210,DA,2,NS2,0)) ?51,$P(^(0),"^",1) S NO=$P(^(0),"^",2) W ?66,$S(NO="S":"SELF",NO="R":"RELATIVE",NO="N":"NEIGHBOR",NO="O":"OTHER",1:"")
 Q
HEADER I $E(IOST)="C",NURSW1 D ENDPG^NURSUT1 S:NUROUT NURQUIT=+NUROUT Q:NURQUIT
 S NURPAGE=NURPAGE+1 W:$E(IOST)="C"!(NURPAGE>1) @IOF
 I NURMDSW W ?$$CNTR^NURSUT2(NURFAC(2)),$$FACL^NURSUT2(NURFAC(2))
 W !!,"TELEPHONE NUMBERS OF NURSING EMPLOYEES" S X="T" D ^%DT D:+Y D^DIQ W ?44,Y,?59,"PAGE: ",NURPAGE
 W !!,"EMPLOYEE",?36,"SERVICE",?51,"TELEPHONE",?66,"OWNER OF" W !,"NAME",?36,"POSITION",?51,"NUMBER",?66,"PHONE NO." W !,$$REPEAT^XLFSTR("-",80)
 I $G(NURPLSW) N Z S Z=$$PROD^NURSUT2(NURPROG(2)) W !,?$$CNTR^NURSUT2(Z),$G(Z),!,?$$CNTR^NURSUT2(Z),$$REPEAT^XLFSTR("-",$L(Z)+1)
 Q
