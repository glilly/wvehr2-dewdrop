XMKPO ;ISC-SF/GMB-Post, other ;04/11/2002  07:08
 ;;8.0;MailMan;;Jun 28, 2002
 ; Replaces ^XMBPOST and the first part of ^XMS1 (ISC-WASH/THM/RWF/CAP)
 ; Schedule a task to deliver bulletin, server, device
DEVICE(XMDUZ,XMZ,XMDEVICE,XMDVIENS,XMPRTHDR) ; For D.Device or H.Device
 N I,ZTRTN,ZTDESC,ZTSAVE,ZTDTH,ZTSK,ZTIO
 F I="XMDUZ","XMZ","XMDVIENS","XMPRTHDR" S ZTSAVE(I)=""
 S ZTIO=$P(XMDEVICE,".",2,99)
 S ZTDTH=$H
 S ZTRTN="DEVICE^XMTDO"
 S ZTDESC=$$EZBLD^DIALOG(42001,XMDEVICE) ; MailMan: To Device |1|
 D ^%ZTLOAD  ; Schedule Task
 Q
SERVER(XMZ,XMSERVER,XMSVIENS) ; For S.Server
 N I,ZTRTN,ZTDESC,ZTSAVE,ZTDTH,ZTSK,ZTIO
 D SETSTAT^XMTDO(XMSVIENS,$$EZBLD^DIALOG(42002.1)) ; Setting up server task
 F I="XMZ","XMSERVER","XMSVIENS" S ZTSAVE(I)=""
 S ZTDTH=$H
 S ZTRTN="SERVER^XMTDO"
 S ZTDESC=$$EZBLD^DIALOG(42002,XMSERVER) ; MailMan: To Server |1|
 S ZTIO=""
 D ^%ZTLOAD  ; Schedule Task
 D SETSTAT^XMTDO(XMSVIENS,$$EZBLD^DIALOG(42002.2,$G(ZTSK))) ; Server task #: |1|
 Q
BULLETIN(XMDUZ,XMBNAME,XMBIEN,XMPARM,XMBODY,XMTO,XMINSTR,ZTSK,XMATTACH) ;
 N I,XMB,ZTRTN,ZTDESC,ZTSAVE,ZTDTH,ZTIO
 I $G(XMBODY)'="",$D(@XMBODY)>9 D
 . S ZTSAVE("XMBODY")=""
 . S ZTSAVE($$OREF^DILF(XMBODY))=""
 F I="XMDUZ","XMBIEN","XMPARM(","XMTO","XMTO(","XMINSTR(","XMATTACH(" S ZTSAVE(I)=""
 I $G(XMINSTR("ADDR FLAGS"))["I" F I="^TMP(""XMY"",$J,","^TMP(""XMY0"",$J," S ZTSAVE(I)=""
 I $D(XMINSTR("LATER")) S ZTDTH=$$FMTH^XLFDT(XMINSTR("LATER"))
 E  S ZTDTH=$H
 S ZTRTN="TASK^XMXBULL"
 S ZTDESC=$$EZBLD^DIALOG(42003,XMBNAME) ; MailMan: Bulletin |1|
 S ZTIO=""
 D ^%ZTLOAD  ; Schedule Task
 Q
