GMRCSLMV ;SLC/DCM - Set Video attributes for list manager screens ;9/8/98  03:48
 ;;3.0;CONSULT/REQUEST TRACKING;**1,4**;DEC 27, 1997
SET(GMRCNBR) ;Check For Valid input and set inverse video on selected item.
 S GMRCNBR=+$P(GMRCNBR,",")
 I GMRCNBR<1!(GMRCNBR>VALMCNT) W !,$C(7),GMRCNBR_" is not a valid selection. Choose a number between 1 and "_VALMCNT Q
 I $D(GMRC("NMBR")) D RESET(GMRC("NMBR"))
 S GMRC("NMBR")=GMRCNBR
 K ^TMP("VALM VIDEO",$J,VALMEVL,GMRCNBR)
 D CNTRL^VALM10(GMRCNBR,1,80,IORVON,IORVOFF)
 D WRITE^VALM10(GMRCNBR)
 S VALMBCK=""
 Q
RESET(NUMBR) ;turn reverse video off when another item is selected
 K ^TMP("VALM VIDEO",$J,VALMEVL,NUMBR)
 D CNTRL^VALM10(NUMBR,1,80,IOINORM,IOINORM)
 D WRITE^VALM10(NUMBR)
 S VALMBCK="" K GMRCSEL
 Q
AGAIN(NMBR) ;RESET THE VIDEO ATTRIBUTE ONLY, DO NOT REDISPLAY
 Q:'$D(NMBR)  Q:NMBR=""
 K ^TMP("VALM VIDEO",$J,VALMEVL,NMBR)
 D CNTRL^VALM10(NMBR,1,80,IORVON,IORVOFF)
 S VALMBCK=""
 Q
