LRVR2 ;SLC/CJS - LAB ROUTINE DATA VERIFICATION ; 10/9/87  16:29 ;
 ;;5.2;LAB SERVICE;**201**;Sep 27, 1994
 S LRSPEC="",LRVF=0 S:'$D(LREAL) LREAL=1
V3 G V5:$D(^LR(LRDFN,LRSS,LRIDT,0)),V4:"AP EM"[LRSS
V3A IF LRSAMP'="" S LRSPEC=$P(^LAB(62,LRSAMP,0),U,2) G:$D(^LAB(61,+LRSPEC,0)) V4
 I LRDPF'=62.3 Q:$D(LRGVP)  S DIC="^LAB(61,",DIC(0)="AEOQ" D ^DIC S LRSPEC=+Y IF LRSPEC=-1 W !,"The specimen MUST be defined." Q
V4 I '$D(^LR(LRDFN,LRSS,0)) S ^LR(LRDFN,LRSS,0)=U_$P(^DD(63,$O(^DD(63,"GL",LRSS,0,0)),0),U,2)_U
 L +^LR(LRDFN,LRSS) S ^LR(LRDFN,LRSS,0)=$P(^LR(LRDFN,LRSS,0),U,1,2)_U_LRIDT_U_(1+$P(^(0),U,4))
 IF "AP EM"[LRSS S ^LR(LRDFN,LRSS,LRIDT,0)=LRCDT_U_LREAL L -^LR(LRDFN,LRSS) G V5
 S LRVOL="" S:$D(^LRO(69,LRODT,1,LRSN,1)) LRVOL=$P(^(1),U,5) S ^LR(LRDFN,LRSS,LRIDT,0)=LRCDT_U_LREAL_U_U_U_LRSPEC_U_LRAN_U_LRVOL_U_LRMETH_U L -^LR(LRDFN,LRSS)
V5 I LRDPF=62.3 S LRSPEC=$S($D(^LR(LRDFN,LRSS,LRIDT,0)):$P(^(0),U,5),1:"")
 S LRLDT=LRIDT,LRVF=0 G V6:'$P(^LR(LRDFN,LRSS,LRIDT,0),U,3) S LRVF=1,X=$P(^(0),U,4),T=$P(^(0),U,3)
 W:'X&(LRDPF=62.3) !,"This control has been automatically verified" W:'X&(LRDPF'=62.3) !,"Verified"
 W !,"Some results have been approved by ",$S($D(^VA(200,+X,0)):$P(^(0),U),1:"Unknown"),!," on ",$$FMTE^XLFDT(T)
V6 I LRDPF'=62.3 S LRSPEC=$P(^LR(LRDFN,LRSS,LRIDT,0),U,5) G V3A:'+LRSPEC
 W:$D(^LAB(61,+LRSPEC,0)) !,"Specimen: ",$P(^(0),U)
V7 S LRLDT=+$O(^LR(LRDFN,LRSS,LRLDT)) S:LRLDT>LRTM60 LRLDT=-1 G V8:LRLDT<1,V7:'$D(^LR(LRDFN,LRSS,LRLDT,0)) D V9 G:$P(^LR(LRDFN,LRSS,LRLDT,0),U,5)'=LRSPEC!'$P(^(0),U,3)!'$D(LRMA) V7
V8 S LRNTN=1,LRDAT(2)="",Z2="" I LRLDT>0 S Z2=^LR(LRDFN,"CH",LRLDT,0),X=+Z2,Z=Z2 D DAT S LRDAT(2)=LRDAT
 S Z1=^LR(LRDFN,"CH",LRIDT,0),X=+Z1,Z=Z1 D DAT
 K LRNOVER I LRSS="CH",'LRVF S LRNOVER=""
 I $D(LRNOVER) F I=1:0 S I=$O(^LR(LRDFN,"CH",LRIDT,I)) Q:I<1  S LRNOVER(I)=""
 D ^LRVR3
 K DA,DIC,DIE,DR,LREDIT,LRNG,LRNG2,LRNG3,LRNG4,LRNG5
 Q  ;LEAVE LRVR2, BACK TO LRVR1
DAT N LRX
 S LRX=$$FMTE^XLFDT(X,"5M")
 S LRDAT=$P(LRX,"/",1,2)_" "_$P(LRX,"@",2)_$S($P(Z,U,2)!(X'["."):"r",1:"d") Q
V9 K LRMA F I=0:0 S I=$O(^TMP("LR",$J,"TMP",I)) Q:I<1  I $D(^LR(LRDFN,LRSS,LRLDT,I)) S LRMA=1 Q
 Q
