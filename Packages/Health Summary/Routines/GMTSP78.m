GMTSP78 ;ISP/TDP - PRE/POST INSTALL FOR GMTS*2.7*78; 8/25/2005
 ;;2.7;Health Summary;**78**;Oct 20, 1995
 Q
 ;
POST ; Start of Post-init for patch GMTS*2.7*78
 N %
 S %=$$NEWCP^XPDUTL("POST1","POST1^GMTSP78")
 Q
POST1 ;
 D BMES^XPDUTL("Starting Post-init...")
 D SEARCH
 D BMES^XPDUTL("Post-init complete.")
 Q
 ;
SEARCH ; Search EXTERNAL/EXTRACT ROUTINE (#1.1) field of the HEALTH SUMMARY
 ; COMPONENT (#142.1) file for EN1;GMRVUT0 and replace with EN1;GMVHS.
 N CNT,DA,DIC,DIE,DO,DR,GMTSCHG,GMTSEXRT,GMTSIEN,GMTSRTN,GMTSVARY,GMTSVITL,X,Y
 S GMTSRTN="EN1;GMVHS"
 S GMTSVARY="VITAL SIGNS^VITAL SIGNS DETAILED DISPLAY^VITAL SIGNS OUTPATIENT^VITAL SIGNS SELECTED^VITAL SIGNS SELECTED OUTPAT."
 F CNT=1:1:5 S GMTSVITL=$P(GMTSVARY,"^",CNT) D
 . S GMTSIEN=0 F  S GMTSIEN=$O(^GMT(142.1,"B",GMTSVITL,GMTSIEN)) Q:GMTSIEN=""  D
 .. S GMTSCHG=0
 .. I $O(^GMT(142.1,GMTSIEN,.1,"B",GMTSRTN,0)) D  Q
 ... D BMES^XPDUTL(">>> EXTERNAL/EXTRACT ROUTINE FOR "_GMTSVITL_" HEALTH"),MES^XPDUTL(">>> SUMMARY COMPONENT ALREADY CONTAINS EN1;GMVHS.  NO CHANGES MADE.")
 .. S GMTSEXRT=$O(^GMT(142.1,GMTSIEN,.1,"B","EN1;GMRVUT0",0))
 .. I GMTSEXRT'="" D
 ... S DIE="^GMT(142.1,"_GMTSIEN_",.1,",DA=GMTSEXRT,DA(1)=GMTSIEN,DR=".01////@"
 ... D ^DIE K DIE,DA,DR
 ... S GMTSCHG=1
 .. K DO
 .. S DA=+$P($G(^GMT(142.1,GMTSIEN,.1,0)),U,3)+1,DA(1)=GMTSIEN
 .. S DIC="^GMT(142.1,GMTSIEN,.1,",DIC(0)="L",X=GMTSRTN
 .. D FILE^DICN
 .. I GMTSCHG=0 D
 ... I Y=-1 D BMES^XPDUTL(">>> ERROR TRYING TO ADD EXTERNAL/EXTRACT ROUTINE OF EN1;GMVHS TO"),MES^XPDUTL(">>> "_GMTSVITL_" HEALTH SUMMARY COMPONENT.")
 ... I Y>0 D BMES^XPDUTL(">>> EXTERNAL/EXTRACT ROUTINE OF EN1;GMVHS HAS BEEN ADDED TO"),MES^XPDUTL(">>> "_GMTSVITL_" HEALTH SUMMARY COMPONENT.")
 .. I GMTSCHG=1 D
 ... I Y=-1 D BMES^XPDUTL(">>> ERROR TRYING TO CHANGE EXTERNAL/EXTRACT ROUTINE FROM EN1;GMRVUT0"),MES^XPDUTL(">>> TO EN1;GMVHS FOR "_GMTSVITL_" HEALTH SUMMARY COMPONENT.")
 ... I Y>0 D BMES^XPDUTL(">>> CHANGED EXTERNAL/EXTRACT ROUTINE FROM EN1;GMRVUT0 TO EN1;GMVHS FOR"),MES^XPDUTL(">>> "_GMTSVITL_" HEALTH SUMMARY COMPONENT.")
 .. K DA,DIC,X,Y
 Q
