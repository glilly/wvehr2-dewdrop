FSCUS ;SLC/STAFF-NOIS Utilities Sort ;4/22/94  12:36
 ;;1.1;NOIS;;Sep 06, 1998
 ;
SORT(SORT) ; from FSCLMPOS, FSCRPTS
 ; sorts ^TMP("FSC LIST CALLS",$J,  globals using a sequence of fields
 ; SORT(seq #) = zero node of field
 ; SORT(sort seq #,"D") = ""  (exists if field is to be sorted in descending order)
 I '$O(SORT(0)) Q
 N CALL,CHECK,CNT,DA,DESCEND,DIC,DIQ,DR,FLD,GBL,LASTCNT,LEN,LNUM,TMPSORT,TYPE,VAL,VALUE K DIQ,VALUE
 S DESCEND="" K ^TMP("FSC SORT",$J)
 F CNT=1:1 Q:'$D(SORT(CNT))  S LASTCNT=CNT I $D(SORT(CNT,"D")) S DESCEND=DESCEND_CNT_","
 S LEN=60\LASTCNT I '$G(FSCDEV) W !
 S DR="",CNT=0 F  S CNT=$O(SORT(CNT)) Q:CNT<1  S DR=DR_$P(SORT(CNT),U,8)_";"
 S DIC=7100,DIQ="VALUE",DIQ(0)="IE"
 S CALL=0 F  S CALL=$O(^TMP("FSC LIST CALLS",$J,"CX",CALL)) Q:CALL<1  D
 .S DA=CALL K VALUE D EN^DIQ1
 .S GBL="^TMP(""FSC SORT"",$J",CNT=0 F  S CNT=$O(SORT(CNT)) Q:CNT<1  D
 ..S FLD=$P(SORT(CNT),U,8),TYPE=$P(SORT(CNT),U,3)
 ..S VAL=VALUE(7100,CALL,FLD,$S(TYPE["D":"I",1:"E"))
 ..D
 ...I TYPE["D"!(TYPE["N") S VAL=$S(VAL'<1:+VAL,$E(VAL)'=".":+VAL,VAL?1P1N.N:"0"_VAL,1:+VAL) I DESCEND[(CNT_",") S VAL=9999999-VAL Q
 ...S VAL=$$UP^XLFSTR(VAL) I DESCEND[(CNT_",") S VAL=$TR(VAL,"0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ","9876543210ZYXWVUTSRQPONMLKJIHGFEDCBA")
 ..S VAL=""""_$S(DESCEND[(CNT_",")&'$L(VAL):"Z",1:" ")_$E(VAL,1,LEN)_""""
 ..S GBL=GBL_","_VAL
 ..I '$O(SORT(CNT)) S GBL=GBL_","_CALL_")" S @GBL=CALL
 K ^TMP("FSC LIST CALLS",$J),DIC,DIQ,VALUE
 S VALMCNT=0,LNUM=0
 S TMPSORT="^TMP(""FSC SORT"",$J)",CHECK="^TMP(""FSC SORT"","_$J_",""z"""
 F  S TMPSORT=$Q(@TMPSORT)  Q:TMPSORT]CHECK  S CALL=@TMPSORT D SETUP^FSCLML I (VALMCNT#10)=0 D CHECK^FSCLML(.VALMQUIT) I $D(VALMQUIT) S VALMBCK="Q" Q
 K ^TMP("FSC SORT",$J)
 I $D(VALMQUIT) Q
 S ^TMP("FSC LIST CALLS",$J)=LNUM_U_VALMCNT
 S VALMBG=1
 Q
