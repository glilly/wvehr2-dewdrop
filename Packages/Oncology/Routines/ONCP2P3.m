ONCP2P3 ;HINES CIOFO/GWB - 1998 Prostate Cancer Study - Table III ;6/1/98
 ;;2.11;ONCOLOGY;**18**;Mar 07, 1995
 K TABLE,HTABLE
 S TABLE("27. SIZE OF TUMOR (mm)")="SOT"
 S TABLE("28. REGIONAL NODES EXAMINED")="RNE"
 S TABLE("29. REGIONAL NODES POSITIVE")="RNP"
 S TABLE("30. AJCC CLINICAL STAGE (cTNM)")="ACS"
 S TABLE("31. AJCC PATHOLOGIC STAGE (pTNM)")="APS"
 S TABLE("32. STAGED BY")="SB"
 S HTABLE(1)="27. SIZE OF TUMOR (mm)"
 S HTABLE(2)="28. REGIONAL NODES EXAMINED"
 S HTABLE(3)="29. REGIONAL NODES POSITIVE"
 S HTABLE(4)="30. AJCC CLINICAL STAGE (cTNM)"
 S HTABLE(5)="31. AJCC PATHOLOGIC STAGE (pTNM)"
 S HTABLE(6)="32. STAGED BY"
 S CHOICES=6
 W @IOF D HEAD^ONCP2P0
 W !," TABLE III - EXTENT AND STAGE OF DISEASE"
 W !," ---------------------------------------"
 S DIE="^ONCO(165.5,",DA=ONCONUM
SOT S DR="29 27. SIZE OF TUMOR (mm)....." D ^DIE G:$D(Y) JUMP
RNE S DR="33 28. REGIONAL NODES EXAMINED" D ^DIE G:$D(Y) JUMP
RNP S DR="32 29. REGIONAL NODES POSITIVE" D ^DIE G:$D(Y) JUMP
ACS W !!," 30. AJCC CLINICAL STAGE (cTNM):",!
CTCODE S DR="37.1     T-CODE................." D ^DIE G:$D(Y) JUMP
 D CN1^ONCOTN,CN2^ONCOTN
CNCODE S DR="37.2     N-CODE................." D ^DIE G:$D(Y) JUMP
CMCODE S DR="37.3     M-CODE................." D ^DIE G:$D(Y) JUMP
 I '$D(SKAJCC) D CN1^ONCOTN
 S STGIND="C" D ES^ONCOTN
APS W !," 31. AJCC PATHOLOGIC STAGE (pTNM):",!
PTCODE S DR="85     T-CODE................." D ^DIE G:$D(Y) JUMP
 D CN3^ONCOTN,CN4^ONCOTN
PNCODE S DR="86     N-CODE................." D ^DIE G:$D(Y) JUMP
PMCODE S DR="87     M-CODE................." D ^DIE G:$D(Y) JUMP
 I '$D(SKAJCC) D CN3^ONCOTN
 S STGIND="P" D ES^ONCOTN
SB W !," 32. STAGED BY:",!
CSB S DR="19     CLINICAL STAGE........." D ^DIE G:$D(Y) JUMP
PSB S DR="89     PATHOLOGIC STAGE......." D ^DIE G:$D(Y) JUMP
 W ! K DIR S DIR(0)="E" D ^DIR S:$D(DIRUT) OUT="Y"
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!," GO TO ITEM NUMBER: ",X:DTIME I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S:X?1.2N X=X_"." S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K HTABLE,TABLE,CHOICES
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
