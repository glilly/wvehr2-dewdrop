DGBTDST1 ;ALB/SCK - BENEFICIARY TRAVEL DIST. ENTER/EDIT CONT. ;4/26/93
 ;;1.0;Beneficiary Travel;;September 25, 2001
START ;
 Q
 ;
CHECKS() ;
 N ERR
 I $$CRMK W !?5,*7,">> ONE OR MORE ADDITIONAL INFORMATION FIELDS NEED TO BE COMPLETED" S ERR=1
 I $$CZIP W !?5,*7,">> ONE OR MORE ZIP CODES ARE MISSING" S ERR=1
 I $$CMILE W !?5,*7,">> ONE OR MORE DEFAULT MILEAGES ARE MISSING OR SET TO ZERO" S ERR=1
 Q $D(ERR)>0
 ;
CRMK()  ;
 N ERR,CREC,DREC
 S CREC=0 F  S CREC=$O(^DGBT(392.1,CREC)) Q:'CREC!($D(ERR))  D
 . S DREC=0 F  S DREC=$O(^DGBT(392.1,CREC,1,DREC)) Q:'DREC!($D(ERR))  S:+$P($G(^DGBT(392.1,CREC,1,DREC,0)),U,4)=1&(($P($G(^DGBT(392.1,CREC,1,DREC,0)),U,5))']"") ERR=1
 Q $D(ERR)>0
 ;
CZIP() ;
 N ERR,CREC
 S CREC=0 F  S CREC=$O(^DGBT(392.1,CREC)) Q:'CREC!($D(ERR))  S:$P($G(^DGBT(392.1,CREC,0)),U,4)']"" ERR=1
 Q $D(ERR)>0
 ;
CMILE() ;
 N ERR,CREC
 S CREC=0 F  S CREC=$O(^DGBT(392.1,CREC)) Q:'CREC!($D(ERR))  S:$P($G(^DGBT(392.1,CREC,0)),U,3)'>0 ERR=1
 Q $D(ERR)>0
