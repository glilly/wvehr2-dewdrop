XUTMTU ;SEA/RDS - TaskMan: ToolKit, Unschedule ;11/18/94  11:21
 ;;8.0;KERNEL;;Jul 10, 1995
 ;
UNSCHED ;Unschedule Task
 N ZT,ZT1,ZT2,ZT3
 L +^%ZTSK(XUTMT)
 S ZT1=0 F ZT=0:0 S ZT1=$O(^%ZTSCH(ZT1)) Q:'ZT1  I $D(^(ZT1,XUTMT))#2 L +^%ZTSCH(ZT1,XUTMT) S ZT2=^(XUTMT) K ^(XUTMT) L -^%ZTSCH(ZT1,XUTMT) I ZT2]"" S:$D(^%ZTSK(XUTMT)) $P(^(XUTMT,.2),U)=ZT2
 S ZT1="" F ZT=0:0 S ZT1=$O(^%ZTSCH("IO",ZT1)),ZT2="" Q:ZT1=""  F ZT=0:0 S ZT2=$O(^%ZTSCH("IO",ZT1,ZT2)) Q:ZT2=""  D DQIO:$D(^(ZT2,XUTMT))#2
 S ZT1="" F ZT=0:0 S ZT1=$O(^%ZTSCH("JOB",ZT1)) Q:ZT1=""  I $D(^(ZT1,XUTMT))#2 L +^%ZTSCH("JOB",ZT1,XUTMT) K ^(XUTMT) L -^%ZTSCH("JOB",ZT1,XUTMT)
 S ZT1="" F ZT=0:0 S ZT1=$O(^%ZTSCH("LINK",ZT1)),ZT2="" Q:ZT1=""  F ZT=0:0 S ZT2=$O(^%ZTSCH("LINK",ZT1,ZT2)) Q:ZT2=""  I $D(^(ZT2,XUTMT))#2 L +^%ZTSCH("LINK",ZT1,ZT2,XUTMT) K ^(XUTMT) L -^%ZTSCH("LINK",ZT1,ZT2,XUTMT)
 I $D(^%ZTSK(XUTMT)) S $P(^%ZTSK(XUTMT,.1),U,1,3)="F^"_$H_U_ZTNAME
 L -^%ZTSK(XUTMT) S ZTSK=XUTMT K XUTMT
 Q
 ;
DQIO ;Remove A Device Waiting List Entry
 N %ZTIO,ZTDTH,ZTSK S %ZTIO=ZT1,ZTDTH=ZT2,ZTSK=XUTMT L +^%ZTSCH("IO") D DQ^%ZTM4 L -^%ZTSCH("IO") Q
 ;
