ABSVOTE ;VAMC ALTOONA/CTB_CLH - OCCASIONAL TIMEKEEPING ;6/23/94  9:48 AM
V ;;4.0;VOLUNTARY TIMEKEEPING;;JULY 6, 1994
OUT K %,%DT,%X,%Y,ABSVXA,ABSVXB,C,D,D0,DA,DDH,DI,DIC,DIE,DLAYGO,DQ,DR,ABSVX,X,Y Q
GET ;TIME SHEET LOOK-UP
 K DIC,DIE,DA,D0 S DIC=503336,DIC(0)="AEMNQ",DIC("A")="Select TIME SHEET: ",DIC("S")="I $P(^(0),U,3)=ABSV(""SITE"")"
 D MDIV^ABSVSITE,^DIC Q:+Y<0
 S DIE=DIC,DA=+Y
 Q
EN ;ENTER OCCASIONAL TIME SHEET ENTRY
 D ^ABSVSITE G OUT:'%
EN1 S X=$P(^ABS(503336,0),"^",3) S Y=X+1 F Y=Y:1 L +^ABS(503336,Y):5 Q:$T&('$D(^ABS(503336,Y)))  L -^ABS(503336,Y)
 S X=Y,DIC=503336,DLAYGO=DIC,DIC(0)="L"
 D MDIV^ABSVSITE,^DIC L -^ABS(503336,Y)
 K DIC,DLAYGO G:+Y<0 OUT S DA=+Y
 S DIE="^ABS(503336,",DR="[ABSV OCCASIONAL TIME ENTER]" D ^DIE K DIE
 I $D(Y) S DIK="^ABS(503336," D ^DIK K DIK
 S ABSVXA="Add another Occasional Time Sheet Entry",ABSVXB="",%=1 D ^ABSVYN G:%'=1 OUT G EN1
EDIT ;EDIT OCCASIONAL TIME SHEET
 D ^ABSVSITE G OUT:'%
ED1 D GET G:+Y<0 OUT I $P(^ABS(503336,DA,0),"^",9)>1 S ABSVXA="This Time Sheet entry has already been transmitted",ABSVXA(1)="Do you want to continue",ABSVXB="",%=1 D ^ABSVYN G:%'=1 EDIT
 S ABSVXA="This will mark this Time Sheet entry 'READY FOR TRANSMISSION'",ABSVXA(1)="Do you want to continue",ABSVXB="",%=1 D ^ABSVYN G:%'=1 EDIT
 S DR="[ABSV OCCASIONAL TIME ENTER]" D ^DIE K DIE,DA
 S ABSVXA="Edit another Occasional Time Sheet Entry",ABSVXB="",%=2 D ^ABSVYN
 I %=1 G ED1
 G OUT
MARK ;MARK TIME SHEET STATUS AS READY FOR TRANSMISSION
 D ^ABSVSITE G OUT:'%
 D GET G:+Y<0 OUT I $P(^ABS(503336,DA,0),"^",9)=1 W !,"TIME SHEET IS ALREADY MARKED FOR TRANSMISSION.",!,"NO FURTHER ACTION REQUIRED.",*7 K DIE G MARK
 S ABSVXA="Do you want to edit this Time Sheet Entry",ABSVXB="",%=2 D ^ABSVYN
 I %=1 S DR="[ABSV OCCASIONAL TIME ENTER]" D ^DIE
 S DR="8////1" D ^DIE S X=" -- TIME SHEET MARKED READY FOR TRANSMISSION --*" D MSG^ABSVQ
 K DIC,DIE,DA,DR,D0 G MARK
DELTSE ;DELETE TIME SHEET ENTRY
 D ^ABSVSITE G OUT:'%
D1 S DIC=503336,DIC(0)="AEMNQ",DIC("A")="Select TIME SHEET ENTRY: ",DIC("S")="I $P(^(0),U,3)=ABSV(""SITE"")"
 D MDIV^ABSVSITE,^DIC G:+Y<0 OUT S DA=+Y K DIC
 S ABSVXA="Are you sure you want to delete this entry",ABSVXB="",%=2 D ^ABSVYN G:%'=1 DELTSE
 S ABSVXA="ARE YOU SURE",ABSVXB="",%=1 D ^ABSVYN G:%'=1 DELTSE
 S X="While I delete this entry...." D WAIT^ABSVYN,DEL^ABSVOTSD
 S X="  <Time sheet has been deleted>*" D MSG^ABSVQ
 D OUT G D1
