PRPFPNT ;ALTOONA/CTB-OUTPUT DRIVER FOR "PRPF" (PATIENT FUNDS) ;20 APR 02
V ;;3.0;PATIENT FUNDS;**6,7,13**;JUNE 1, 1989
CLERK ;CLERK STATISTICS
 D DRNG^PRPFQ G:'% OUT D WAIT^PRPFYN S PRPFFR=FR,PRPFTO=TO,DIC="^PRPF(470.1,",L=0,BY="+PATIENT FUNDS CLERK,'DATE TRANSACTION ENTERED",FR=","_FR,TO=","_TO,FLDS="!REFERENCE,",DHD="[PRPF CLERK STAT HDR]"
DIP S L=0,DIOEND="W !,""The information contained in this report is protected by the Privacy Act of 1974""" D WAIT^PRPFYN,EN1^DIP D:'$D(XQY) ENCON^PRPFQ
OUT K %H,%I,DATE,DG1,DGA1,DGT,DGX,DINS,DFN,DIYS,DTOUT,DIOEND,DIOP,DNP,M,PRPFD,PRPFFR,PRPFTO,Q3,X,Y D DIKILL^PRPFQ G ZTKILL^PRPFQ QUIT
FISTRANS ;FISCAL TRANSACTION SUMMARY
 D SEL Q:PRPFRNG=""
 W ! D DRNG^PRPFQ G:'% OUT
 S DIC="^PRPF(470.1,",BY="@1:73:99,]+4;S",FLDS="[PRPF FISCAL TRANS SUMMARY]",FR=""_PRPFRNG_","_FR_"",TO=""_PRPFRNG2_","_TO_""
 S:PRPFRNG="@" BY="@1:73,@1:73:99;S1,]+4;S",FR="@,"_FR,TO=","_TO
 G DIP
FISACTIV ;FISCAL ACTIVITY REPORT
 D SEL Q:PRPFRNG=""
 W ! D DRNG^PRPFQ G:'% OUT
 S DIC="^PRPF(470.1,",BY="@1:73:99,]+4;S2,+7;S,+10;S,@6",FLDS="[PRPF FISCAL ACTIVITY REPORT]",FR=""_PRPFRNG_","_FR_"",TO=""_PRPFRNG2_","_TO_""
 S:PRPFRNG="@" BY="@1:73,@1:73:99;S1,]+4;S2,+7;S,+10;S,@6",FR="@,"_FR,TO=","_TO
 G DIP
AUDIT ;DAILY ACTIVITY (AUDIT) LISTING
 D SEL Q:PRPFRNG=""
 W ! D DRNG^PRPFQ G:'% OUT
 S DIC="^PRPF(470.1,",BY="@1:73:99,+5,+7,+8,+10,@6",FLDS="[PRPF DAILY ACTIVITY]",FR=""_PRPFRNG_","_FR_"",TO=""_PRPFRNG2_","_TO_""
 S:PRPFRNG="@" BY="@1:73,@1:73:99;S1,+5,+7,+8,+10,@6",FR="@,"_FR_",,,,",TO=","_TO_",,,,"
 G DIP
TRANS ;LISTING OF TRANSACTIONS
 D SEL Q:PRPFRNG=""
 W ! D DRNG^PRPFQ G:'% OUT
 S DIC="^PRPF(470.1,",BY="@1:73:99;S1,5",FLDS="[PRPF DAILY TRANSACTION SUMMARY]",FR=""_PRPFRNG_","_FR_"",TO=""_PRPFRNG2_","_TO_""
 S:PRPFRNG="@" BY="@1:73,@1:73:99;S1,5",FR="@,"_FR,TO=","_TO
 G DIP
INACTIVE ;INACTIVE ACCOUNT LIST
 S DIC="^PRPF(470,",(BY,FLDS)="[PRPF INACTIVE ACCOUNTS]" G DIP
INDIGENT ;INDIGENT PATIENT LIST
 D SEL Q:PRPFRNG=""
 S DIC="^PRPF(470,",BY="@+73:99;S1,]4,@1,.01",FLDS="[PRPF INDIGENT LIST]",FR=""_PRPFRNG_""_",YES",TO=""_PRPFRNG2_""_",YES"
 S:PRPFRNG="@" BY="@+73,@73:99;S1,]4,@1,.01",FR="@,@,YES",TO=",,YES"
 G DIP
BALANCE ;BALANCE IN ALL ACCOUNTS
 D SEL Q:PRPFRNG=""
 S DIC="^PRPF(470,",FLDS="[PRPF BALANCE IN ACCOUNTS]",FR=PRPFRNG,TO=PRPFRNG2
 S:PRPFRNG'="@" BY="+73:99;"_"""STATION NAME: "_$G(Y(0,0))_"    STATION NUMBER: "_""""_";C1"
 S:PRPFRNG="@" BY="73;C1;S1,+73:99;""STATION NUMBER: "";C1",FR="@,@",TO=","
 G DIP
PATLIST ;PATIENT LIST
 D SEL Q:PRPFRNG=""
 S DIC="^PRPF(470,",BY="@+73:99;S1,].01",FLDS="[PRPF PATIENT LIST]",FR=""_PRPFRNG_"",TO=""_PRPFRNG2_""
 S:PRPFRNG="@" BY="@+73,@73:99;S1,].01",FR="@,@",TO=","
 G DIP
SUMMARY ;;PATIENT SUMMARY REPORT
 D SEL Q:PRPFRNG=""
 S DIC="^PRPF(470,",BY="@+73:99;S1,@1,.01",FLDS="[PRPF PATIENT SUMMARY]",FR=""_PRPFRNG_",A"_"",TO=""_PRPFRNG2_",A"_""
 S:PRPFRNG="@" BY="@+73,@73:99;S1,@1,.01",FR="@,@,A,",TO=",,A,"
 G DIP
SEL D SELRNG^PRPFQ
 I PRPFRNG="" D OUT QUIT
 I PRPFRNG="@" S PRPFRNG2=""
 E  S PRPFRNG2=PRPFRNG
 Q
UNSTA ;;UNNASIGNED STATION NAME LIST
 S DIC="^PRPF(470,",BY="1;S,@73,.01",FLDS="[PRPF UNASSIGNED STATION LIST]",FR="A,@",TO="A,@"
 S DIR(0)="Y",DIR("A")="Would you like to include INACTIVE patients in this report",DIR("B")="NO"
 W !,"" D ^DIR K DIR S:Y FR=",@",TO=",@"
 I $D(DTOUT)!($D(DUOUT))!($D(DIROUT)) K X,Y Q
 G DIP
