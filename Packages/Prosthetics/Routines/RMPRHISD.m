RMPRHISD ;PHX/RFM-DELETE HISTORICAL DATA ENTRY ;8/29/1994
 ;;3.0;PROSTHETICS;**19,90**;Feb 09, 1996
EN D DIV4^RMPRSIT G:$D(X) EXIT D HOME^%ZIS
 S DIC("S")="I $P(^(0),U,13)=13,$P(^(0),U,10)=RMPR(""STA"")"
 S DIC="^RMPR(660,",DIC(0)="AEMQ",DIC("A")="Select PATIENT: ",DIC("W")="D ^RMPRD1" D ^DIC G:Y<0 EXIT
 S %X=DIC_+Y_",",%Y="R1(" D %XY^%RCR K DIC
 S RMPRIEN=+Y,RMPRNAM=$P(^DPT($P(R1(0),U,2),0),U,1),RMPRSSN=$P(^DPT($P(R1(0),U,2),0),U,9)
 S R3("D")="",R4("D")="",RMPRHISD=1
 S $P(R3("D"),U,14)=$S($P(R1(0),U,14)="C":"COMMERCIAL",$P(R1(0),U,14)="V":"VA",1:"")
 S $P(R3("D"),U,4)=$S($P(R1(0),U,4)="I":"INITIAL ISSUE",$P(R1(0),U,4)="X":"REPAIR",$P(R1(0),U,4)="R":"REPLACE",$P(R1(0),U,4)="S":"SPARE",$P(R1(0),U,4)="5":"RENTAL",1:"")
 S $P(R4("D"),U,3)=$S($P(R1("AM"),U,3)=1:"SC/OP",$P(R1("AM"),U,3)=2:"SC/IP",$P(R1("AM"),U,3)=3:"NSC/IP",$P(R1("AM"),U,3)=4:"NSC/OP")
 S:$P(R1("AM"),U,3)=4&($P(R1("AM"),U,4)) $P(R4("D"),U,4)=$S($P(R1("AM"),U,4)=1:"SPECIAL LEGISLATION",$P(R1("AM"),U,4)=2:"A&A",$P(R1("AM"),U,4)=3:"PHC",$P(R1("AM"),U,4)=4:"ELIGIBILITY REFORM",1:"")
 S $P(R3("D"),U,6)=$P(^RMPR(661,$P(R1(0),U,6),0),U,1),RMPROIT=$P(R3("D"),U,6),Y=$P(R1(0),U,12) G:Y="" CO D DD^%DT S $P(R3("D"),U,12)=Y,RMPROQT=$P(R1(0),U,7)
CO D ^RMPRST2
DEL S DIR(0)="Y",DIR("B")="NO",DIR("A")="Would you like to Delete this Transaction" D ^DIR G:$D(DIRUT) EXIT I +Y=1 S DIK="^RMPR(660,",DA=RMPRIEN D ^DIK W !?10,"Deleted..." H 1 G EXIT
 K DIR,Y S DIR(0)="Y",DIR("B")="YES",DIR("A")="Would you like to Edit this Transaction" D ^DIR G:$D(DIRUT) EXIT I +Y=1 S DIE="^RMPR(660,",DA=RMPRIEN,DR="1R;12R;4R;7R;2R;62R;63;14R~UNIT COST;5R;10;9;21" D ^DIE G CO
EXIT K DIK,J,DIC,DIR,DA,%,HL,RMPRNAM,RMPRSSN,RMPRHISD,RMPRIEN,RMPROIT,RMPROQT,RMPRIPT,R1,R3,R4,Y,X,RZZZ,%X,%Y Q
