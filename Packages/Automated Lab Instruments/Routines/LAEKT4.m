LAEKT4 ;SLC/RWF - KODAK EKTACHEM 400 ROUTINE ;7/20/90  08:11 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID OR IDE
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP"),TCID=""
LA2 S TOUT=0 D IN D:TOUT LA3 G QUIT:TOUT,LA2:$E(IN,1,3)'="!41" D QC G LA2:BAD
 I TCID'=$E(IN,25,28) D LA3 S TCID=$E(IN,25,28),V=$E(IN,17,24) D NUM S IDE=+V,ID=IDE,CUP=+$E(IN,27,28),TRAY=+$E(IN,25,26)
 S V=$E(IN,33,34) D NUM S NM=+V,V=$E(IN,35,41) D NUM G LA2:V'<999999
 I $D(TC(NM)) S @TC(NM,1)=+V ;Test number in the file is the Ektchem test number
 G LA2
LA3 Q:'TCID  X LAGEN
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 S TCID="" K TV
 Q
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S BAD=0 I $E(IN,52,53)>2!(+$E(IN,54,56)) S BAD=1
 Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
