LAMLA1KC ;SLC/DLG/FHS -  ELECTRA 900/900C/1000C ;03/25/93  15:41 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY CUP, ENTER ACCESSION NUMBER INTO PATIENT ID FIELD
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 K Y,TEST,TV S TOUT=0 D IN G QUIT:TOUT,LA2:$E(IN,1)'="R" S BAD=0 D QC G LA2:BAD
 F I=0:0 S I=$O(TC(I)) Q:I<1  I $D(TC(I,4)),TC(I,4)=TEST S V=Y(TEST) D NUM S @TC(I,1)=V
 F I=0:0 S I=$O(TC(I)) Q:I<1  I $D(TC(I,2)),TC(I,2)'="" X TC(I,2) S:V>0 @TC(I,1)=V
 S (CUP,ID,IDE)=+$P(IN,"|",3),TRAY=1
LA3 X LAGEN G LA2:'ISQN
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 ;CR=curve results, all tests other than PT,PTT,INR found here
 S TEST=+$P(IN,"|",4),ST=$P(IN,"|",5),CR=$P(IN,"|",12),Y(TEST)=$S(CR>0:$P(IN,"|",14),ST<3:$P(IN,"|",7),1:$P(IN,"|",11)),Y(2)=+$P(IN,"|",17) I ST<3,($P(IN,"|",6)=0!($P(IN,"|",6)>1)) S BAD=1 Q
 I ST>2,($P(IN,"|",8)=0)!($P(IN,"|",8)>1) S BAD=1 Q
 Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 10 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J) D
 .K ^TMP("LA",$J),BAD,CNT,CR,CTRL,CUP,I,ID,IDE,IN,ISQN,JJ,LAGEN
 .K LANM,LATOP,LWL,ST,T,TC,TEST,TOUT,TRAY,TSK,TV,V
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
