XTPMSTAT ;OAK/BP - PRINT PATCH STATISTICS BY REPORT GROUP; 2/7/06
 ;;7.3;TOOLKIT;**98,100**; Apr 25, 1995;Build 4
 ;
 S IOP="HOME" D ^%ZIS K IOP
EN W @IOF,"Patch Monitor Statistics",!!!
 W "Please select the REPORTING GROUP.  You may select as many groups as you",!
 W "wish for each report.  Each group will be reported in a separate section.",!!!
 K TMP("XTBGRP"),TMP("XTBPKG")
 N DIC,DA S DIC(0)="AEQM",DIC="^XPD(9.95," D ^DIC S DA(1)=+Y
 I DA(1)>0 N DIC S DIC(0)="AEQM",DIC="^XPD(9.95,DA(1),2," F  D ^DIC Q:Y<0  DO
 .I $D(TMP("XTBGRP",$P(Y,U,2))) W $C(7),". . . You already have that one" Q
 .S TMP("XTBGRP",$P(Y,U,2))=+Y
 G:X[U EXIT
 G:'$D(TMP("XTBGRP")) EXIT
 W !!,"Do you want a new form/screen between REPORTING GROUPS" S %=1 D YN^DICN S XTBPGF=%
 ;
DATE W ! S %DT="AEP"
 S %DT("A")="Enter BEGINNING Compliance date: " D ^%DT G:Y<0 EXIT S XTBBDT=Y X ^DD("DD") S XTBBDT1=Y
 S %DT="AE",%DT("A")="     and ENDING Compliance date: " D ^%DT G:Y<0 EXIT S XTBEDT=Y X ^DD("DD") S XTBEDT1=Y
 I XTBEDT<XTBBDT W !!,$C(7),"Starting date is later than ending date.",!! H 2 G DATE
 ;
DEV W !! S %ZIS="AEQ" D ^%ZIS G:POP EXIT
 I $D(IO("Q")) S ZTIO=ION,ZTRTN="SORT^XTPMSTAT",ZTSAVE("XTB*")="",ZTSAVE("TMP*")="",ZTDESC="Patch Monitor Statistics" D ^%ZTLOAD D HOME^%ZIS
 I $D(ZTSK) W !,"Queued as task #",ZTSK H 2 G EXIT
 ;
 ; sort patches by compliance date
SORT U IO F XTBCPLDT=(XTBBDT-.0001):0 S XTBCPLDT=$O(^XPD(9.9,"D",XTBCPLDT)) Q:XTBCPLDT=""!(XTBCPLDT>XTBEDT)  DO
 .F XTBDA=0:0 S XTBDA=$O(^XPD(9.9,"D",XTBCPLDT,XTBDA)) Q:XTBDA=""  DO
 ..S XTBDTA=$G(^XPD(9.9,XTBDA,0)) Q:XTBDTA=""
 ..S XTBPTNAM=$P(XTBDTA,U,1),XTBNMSP=$P($P(XTBDTA,U,4)," - ",1) Q:XTBNMSP=""  ;parent package missing in file
 ..S XTBRELDT=$P(XTBDTA,U,2),XTBPRIOR=$P(XTBDTA,U,3)
 ..S TMP("XTBPKG",XTBNMSP,XTBCPLDT,XTBPTNAM,XTBDA)=XTBRELDT_U_XTBPRIOR
PRINT ; read and print sorted groups
 S Y=DT X ^DD("DD") S XTBCURDT=Y
 K XTBLINE S $P(XTBLINE,"-",(IOM-2))="-"
 S PG=0,XTBGRP="",XTBGRP=$O(TMP("XTBGRP",XTBGRP)) G:XTBGRP="" EXIT D HDR ; first header
 S (XTBGRP,XTBNMSP,XTBPTNAM,XTBOLDNM,XTBOLGRP)="",(XTBTPTCH,XTBTLATE)=0
 F  S XTBGRP=$O(TMP("XTBGRP",XTBGRP)) Q:XTBGRP=""  S XTBGPDA=TMP("XTBGRP",XTBGRP)  DO  Q:$D(XTBOUT)
 .; read param file for monitored groups
 .F  S XTBNMSP=$O(^XPD(9.95,1,2,XTBGPDA,1,"B",XTBNMSP)) Q:XTBNMSP=""  I $D(TMP("XTBPKG",XTBNMSP)) DO  Q:$D(XTBOUT)
 ..; read sorted namespaces
 ..F XTBCPLDT=0:0 S XTBCPLDT=$O(TMP("XTBPKG",XTBNMSP,XTBCPLDT)) Q:XTBCPLDT=""  F  S XTBPTNAM=$O(TMP("XTBPKG",XTBNMSP,XTBCPLDT,XTBPTNAM)) Q:XTBPTNAM=""  DO  Q:$D(XTBOUT)
 ...F XTBDA=0:0 S XTBDA=$O(TMP("XTBPKG",XTBNMSP,XTBCPLDT,XTBPTNAM,XTBDA)) Q:XTBDA=""  DO  Q:$D(XTBOUT)
 ....S XTBTPTCH=XTBTPTCH+1
 ....S XTBDTA=TMP("XTBPKG",XTBNMSP,XTBCPLDT,XTBPTNAM,XTBDA)
 ....S XTBRELDT=$P(XTBDTA,U),XTBPRIOR=$P(XTBDTA,U,2)
 ....S XTBRCVDT=$P($G(^XPD(9.9,XTBDA,0)),U,2)
 ....S XTBPTYPE=$P($G(^XPD(9.9,XTBDA,0)),U,10)
 ....I +XTBPTYPE=0 S D0=XTBDA D ^XTPMKPCF S XTBINSDT=X K D0
 ....I +XTBPTYPE=1 S XTBINSDT=$P($G(^XPD(9.9,XTBDA,0)),U,11)
 ....I XTBINSDT]"" S X1=XTBINSDT,X2=XTBCPLDT D ^%DTC S XTBDAYLT=X
 ....I XTBINSDT="" S X1=DT,X2=XTBCPLDT D ^%DTC S XTBDAYLT=X
 ....S XTBGRPHD="Report group: "_XTBGRP
 ....I XTBOLGRP="",PG=1 W XTBGRPHD,!!
 ....I XTBOLDNM'="",XTBNMSP'=XTBOLDNM W !
 ....I XTBOLDNM'="",XTBGRP'=XTBOLGRP,XTBPGF=1 D:IOST?1"C-".E PAUSE Q:$D(XTBOUT)  D HDR W XTBGRPHD,!!
 ....I XTBOLDNM'="",XTBGRP'=XTBOLGRP,XTBPGF=0 W !,XTBGRPHD,!!
 ....I $Y>(IOSL-6),IOST?1"C-".E D PAUSE Q:$D(XTBOUT)  D HDR I XTBGRP'=XTBOLGRP W XTBGRPHD,!!
 ....S Y=XTBINSDT X ^DD("DD") S XTBINSDT=Y
 ....S Y=XTBCPLDT X ^DD("DD") S XTBCPLDX=Y
 ....S Y=XTBRELDT X ^DD("DD") S XTBRELDT=Y
 ....S XTBPRIOR=$S(XTBPRIOR="m":"Mandatory",XTBPRIOR="e":"Emergency",1:"Unknown")
 ....W XTBCPLDX,?14,XTBPTNAM,?27,XTBRELDT,?41,XTBINSDT,?55,XTBPRIOR
 ....I XTBDAYLT>0 W ?67,$J(XTBDAYLT,3,0)_$S(XTBDAYLT>1:" days",1:" day") S XTBTLATE=XTBTLATE+1
 ....W ! S XTBOLDNM=XTBNMSP,XTBOLGRP=XTBGRP
 ....I $Y>(IOSL-6),IOST?1"C-".E D PAUSE Q:$D(XTBOUT)
 ....I $Y>(IOSL-6) D HDR I XTBGRP'=XTBOLGRP W XTBGRPHD,!!
 G:$D(XTBOUT) EXIT
 I $Y>(IOSL-6),IOST?1"C-".E D HDR
 W !!?6,"Totals patches received for date range: ",XTBTPTCH,!
 W "Total patches installed past compliance date: ",XTBTLATE,!!
 S XTBDIVOK=0 I XTBTPTCH>0 S XTBDIVOK=1
 W ?25,"Delinquent patch % : ",$S(XTBDIVOK=1:$J((XTBTLATE/XTBTPTCH*100),6,2),1:0)_" %",!
 W ?25,"      Compliance % : ",$S(XTBDIVOK=1:$J(100-(XTBTLATE/XTBTPTCH*100),6,2),1:0)," %",!
 I IOST?1"C-".E K XTBANS W !!,"Press ENTER to end " R XTBANS:DTIME
 ;
EXIT I IOST?1"C-".E W @IOF,!
 D ^%ZISC
 K %,%DT,%ZIS,XTBNMSP,XTBANS,XTBBDT,XTBBDT1,XTBCPLDT,XTBCPLDX,XTBDA,XTBEDT,XTBEDT1,XTBDAYLT,TMP
 K XTBGRPDA,XTBGRP,XTBINSDT,XTBLINE,XTBNMSP,XTBOLDNM,XTBNMSP,XTBPTNAM,XTBPTYPE,XTBDTA,XTBGPDA
 K XTBRCVDT,XTBTLATE,XTBTPTCH,D0,DIC,PG,POP,X,X1,X2,Y,ZTDESC,ZTIO,ZTRTN,ZTSAVE,%T,%Y,XTBGRPHD
 K TMP("XTBGRP"),TMP("XTBPKG"),XTBOUT,XTBPGF,XTBOLGRP,ZTSK,XTBRELDT,XTBPRIOR,XTBCURDT,XTBDIVOK
 Q
 ;
HDR S PG=PG+1 I IOST?1"P-".E,PG>1 W @IOF
 I IOST?1"C-".E W @IOF
 W XTBCURDT S X="Patch Statistical Report for "_^DD("SITE")
 W ?(IOM-$L(X)\2),X,?(IOM-12),"Page: ",PG,!
 S X="Date range: "_XTBBDT1_" to "_XTBEDT1 W ?(IOM-$L(X)\2),X,!
 W !,"Compliance",?14,"Patch",?27,"Release",?41,"Install",?67,"# Days",!
 W "Date",?14,"Number",?27,"Date",?41,"Date",?55,"Priority",?67,"Delinquent",!,XTBLINE,!
 Q
 ;
PAUSE Q:IOST'?1"C-".E
 K XTBANS,XTBOUT W !!,"Press ENTER to continue or '^' to end " R XTBANS:DTIME
 I XTBANS[U!('$T) S (XTBNMSP,XTBPTNAM,XTBCPLDT,XTBDA)="ZZZZZZ",XTBOUT=1
 Q
