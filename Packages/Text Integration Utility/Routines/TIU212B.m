TIU212B ; BAYPINES/ELR - UTILITY ROUTINE PATCH TIU*1.0*212 ;3/22/2006
 ;;1.0;TEXT INTEGRATION UTILITIES;**212**;Jun 20, 1997
 Q
EN NEW TIUDA,TIUS0,TIUNM,TIUCNT,TIUJ,TIUDEV
 S (TIUDA,TIUCNT)=0
 S TIUJ=$J
 K ^TMP("TIU212B",TIUJ)
 F  S TIUDA=$O(^TIU(8925.95,TIUDA)) Q:+TIUDA'>0  D
 .  S TIUS0=$P($G(^TIU(8925.95,TIUDA,0)),U)
 .  S TIUNM=$P($G(^TIU(8925.1,TIUS0,0)),U)
 .  I TIUNM?.E1N.E S TIUCNT=TIUCNT+1,^TMP("TIU212B",TIUJ,TIUCNT)=TIUNM
 I $G(XPDNM)["TIU*1.0*212" D PRINTA G PRINTX
 S TIUDEV=$$DEVICE^TIUDEV(.IO) ; Get Device/allow queueing
 I IO']"" G PRINTX
 I $G(ION)="" G PRINTX
 I $D(IO("Q")) D QUE^TIUDEV("PRINT^TIU212B",TIUDEV) G PRINTX
 D PRINT
PRINTX D ^%ZISC
 Q
PRINT ;
 S TIUDA=0
 U IO
 W !,"Patch TIU*1.0*212 analysis of Document Parameter File (8925.95) entries that"
 W !,"point to titles that contain numbers."
 W !,"Verify that these definitions go with the document.",!!
 W !,"SUSPECT 8925.95 DOCUMENT TITLES"
 W !,"-------------------------------",!
 F  S TIUDA=$O(^TMP("TIU212B",TIUJ,TIUDA)) Q:+TIUDA'>0  D
 . W !,$G(^TMP("TIU212B",TIUJ,TIUDA))
 I $G(TIUCNT)'>0 W !,"NO OCCURANCES FOUND"
 K ^TMP("TIU212B",TIUJ)
 Q 
PRINTA ;
 K ^TMP(TIUJ,"TIU212B")
 NEW TIUHD,TIUCNT1
 S (TIUDA,TIUCNT1)=0
 S TIUHD="Patch TIU*1.0*212 analysis of Document Parameter File (8925.95) entries that"
 D MAIL(TIUHD),BMES^XPDUTL(TIUHD)
 S TIUHD="point to titles that contain numbers."
 D MAIL(TIUHD),MES^XPDUTL(TIUHD)
 S TIUHD="Verify that these definitions go with the document."
 D MAIL(TIUHD),MES^XPDUTL(TIUHD)
 D MAIL(" "),MES^XPDUTL(" ")
 S TIUHD="SUSPECT 8925.95 DOCUMENT TITLES"
 D MAIL(TIUHD),MES^XPDUTL(TIUHD)
 S TIUHD="-------------------------------"
 D MAIL(TIUHD),MES^XPDUTL(TIUHD)
 D MAIL(" "),MES^XPDUTL(" ")
 F  S TIUDA=$O(^TMP("TIU212B",TIUJ,TIUDA)) Q:+TIUDA'>0  D
 . S TIUHD=$G(^TMP("TIU212B",TIUJ,TIUDA))
 . D MAIL(TIUHD),MES^XPDUTL(TIUHD)
 I $G(TIUCNT1)'>0 S TIUHD="NO OCCURANCES FOUND" D MAIL(TIUHD),BMES^XPDUTL(TIUHD)
 K ^TMP("TIU212B",TIUJ)
 S XMSUB="TEXT INTEGRATION UTILITY",XMTEXT="^TMP("_TIUJ_","_"""TIU212B"""_",",XMDUZ="PATCH TIU*1.0*212",XMY(DUZ)=""
 NEW DIFROM D ^XMD K XMDUZ,XMSUB,XMY(DUZ),XMTEXT
 K ^TMP(TIUJ,"TIU212B")
 Q
MAIL(X) ;
 S TIUCNT1=TIUCNT1+1,^TMP(TIUJ,"TIU212B",TIUCNT1)=X
 Q 
