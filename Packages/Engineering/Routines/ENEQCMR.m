ENEQCMR ;(WIRMFO)/DH,SAB-EIL Summary and Signature Page ;5.12.97
 ;;7.0;ENGINEERING;**39,60**;Aug 17, 1993
 ;
SUM ; Print Summary for EIL report (called by DIOEND)
 ; input
 ;   EN("CMR","HD")=EIL header information
 ;   ENT(station,fund,sgl)=count^lease cost^asset value
 N ENSN,ENFUND,ENSGL,ENY,ENGT
 I $Y+10>IOSL D
 . I IO=IO(0),$E(IOST,1,2)="C-" R !,"Press <RETURN> to continue",X:DTIME
 . W @IOF,"EQUIPMENT INVENTORY LISTING (EIL)         ",$$FMTE^XLFDT(DT)
 . W !," FOR EIL: ",EN("CMR","HD"),!
 W !,?5,"TOTALS:"
 W !,?10,"STATION",?19,"FUND",?27,"SGL",?33,"COUNT",?40,"LEASE COST",?56,"ASSET VALUE"
 W !,?10,"-------",?19,"------",?27,"----",?33,"-----",?40,"--------------",?56,"--------------"
 S ENGT=""
 S ENSN="" F  S ENSN=$O(ENT(ENSN)) Q:ENSN=""  D
 . S ENFUND="" F  S ENFUND=$O(ENT(ENSN,ENFUND)) Q:ENFUND=""  D
 . . S ENFUND("E")=$S(ENFUND:$P($G(^ENG(6914.6,ENFUND,0)),U),1:ENFUND)
 . . S ENSGL="" F  S ENSGL=$O(ENT(ENSN,ENFUND,ENSGL)) Q:ENSGL=""  D
 . . . S ENSGL("E")=$S(ENSGL:$P($G(^ENG(6914.3,ENSGL,0)),U),1:ENSGL)
 . . . S ENY=ENT(ENSN,ENFUND,ENSGL)
 . . . S $P(ENGT,U)=$P(ENGT,U)+$P(ENY,U)
 . . . S $P(ENGT,U,2)=$P(ENGT,U,2)+$P(ENY,U,2)
 . . . S $P(ENGT,U,3)=$P(ENGT,U,3)+$P(ENY,U,3)
 . . . W !,?10,ENSN,?19,ENFUND("E"),?27,ENSGL("E"),?33,$J($P(ENY,U),5)
 . . . W ?40,$J($FN($P(ENY,U,2),",",2),14)
 . . . W ?56,$J($FN($P(ENY,U,3),",",2),14)
 W !,?33,"-----",?40,"--------------",?56,"--------------"
 W !,?33,$J($P(ENGT,U),5)
 W ?40,$J($FN($P(ENGT,U,2),",",2),14)
 W ?56,$J($FN($P(ENGT,U,3),",",2),14)
 I IO=IO(0),$E(IOST,1,2)="C-" R !,"Press <RETURN> to continue",X:DTIME
 Q
 ;
SIG ; Print Signature Page for EIL report (called by DIOEND)
 ; input
 ;   EN("CMR")     - CMR Name
 ;   EN("CMR",.5)  - CMR Service
 ;   EN("CMR",.6)  - CMR Brief Description
 I '$D(EN("CMR")) Q
 I '$D(DT) S X="T",%DT="" D ^%DT S DT=+Y
 W @IOF,"EQUIPMENT INVENTORY LISTING (EIL)" S Y=DT X ^DD("DD") W ?60,Y
 W !,"SIGNATURE PAGE"
 W !!,"EIL: ",EN("CMR"),!,?5,EN("CMR",.5) W:EN("CMR",.5)]"" " SERVICE"
 W:EN("CMR",.6)]"" "   ",EN("CMR",.6)
 W !!,"I UNDERSTAND MY RESPONSIBILITIES LISTED IN VA DIRECTIVE 7125, PART 4, 5006",!,"AND THAT I MAY BE HELD LIABLE UNDER CONDITIONS THEREIN. I ASSUME"
 W !,"RESPONSIBILITY FOR ITEMS LISTED ABOVE WHICH WERE ON HAND ON THE DATE SIGNED."
 W !!,"I PERSONALLY REVIEWED AND EVALUATED THE NEED FOR THE EQUIPMENT ASSIGNED",!,"TO MY ACTIVITY AND FIND THAT:"
 W !!," __",!,"|__|  (1) ALL EQUIPMENT IS ESSENTIAL FOR THE PROPER FUNCTIONING OF THIS"
 W !,"          ACTIVITY OR,"
 W !!," __",!,"|__|  (2) THE ATTACHED VA FORM(S) 90-2237 (OR COMPUTER GENERATED EQUIVALENT)"
 W !,?10,"HAS (HAVE) BEEN PREPARED TO TURN IN THE EQUIPMENT DETERMINED",!,?10,"TO BE EXCESS TO THE NEEDS OF THIS ACTIVITY OR,"
 W !!," __",!,"|__|  (3) THE ATTACHED VA FORM(S) 10-1274 (OR COMPUTER GENERATED EQUIVALENT),",!,?10,"RESEARCH EQUIPMENT AVAILABLE TO VA REGIONAL RESEARCH EQUIPMENT"
 W !,?10,"PROGRAM HAS (HAVE) BEEN PREPARED FOR DISPOSITION AS APPROPRIATE IN",!,?10,"ACCORDANCE WITH VA DIRECTIVE 7343, PART 4, 307-59 OR,"
 W !!," __",!,"|__|  (4) THE ITEM(S) LISTED ON ATTACHED REPORT OF SURVEY FORM(S), VA 90-1217",!,?10,"(OR COMPUTER GENERATED EQUIVALENT), IS (ARE) MISSING OR DAMAGED."
 W !,?10,"IT IS UNDERSTOOD THAT ACCOUNTABILITY WILL BE DROPPED FROM THE EIL BUT",!,?10,"MY RESPONSIBILITY FOR SUCH ITEMS WILL BE TERMINATED ONLY WHEN FINAL"
 W !,?10,"SURVEY ACTION HAS BEEN COMPLETED."
 W !!,"I ALSO CERTIFY THAT ANY PERSONALLY OWNED PROPERTY WHICH HAS BEEN PLACED INTO",!,"OFFICIAL USE HAS BEEN LISTED ON VA FORM 90-2235 (LIST OF PERSONALLY OWNED"
 W !,"PROPERTY PLACED IN OFFICIAL USE), AND HAS BEEN SUBMITTED THROUGH THE PROPER",!,"CHANNELS FOR APPROVAL."
 W !!!!,"SIGNATURE: ________________________________________"
 W !!!!,"    TITLE: ________________________________________"
 W !!!!,"     DATE: ________________________________________"
 Q
 ;ENEQCMR
