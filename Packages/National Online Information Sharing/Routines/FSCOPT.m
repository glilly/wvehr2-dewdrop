FSCOPT ;SLC/STAFF-NOIS Option Routing ;1/13/98  14:03
 ;;1.1;NOIS;;Sep 06, 1998
 ;
NEW ; from FSCOP
 N VALMAR
 W !!
 D NEW^FSCEL
 D TMP^FSCOPTU
 Q
 ;
EDIT ; from FSCOP
 N CALL,OK
 D LOOKUP^FSCULOOK("CALL",.CALL,"AEMOQ",.OK)
 I 'OK Q
 D SETUP1^FSCOPTU(+CALL)
 S XQORM("B")="Edit"
 D EDIT^FSCLM("EVALUES")
 D CLEANUP^FSCOPTU
 D TMP^FSCOPTU
 Q
 ;
CLOSE ; from FSCOP
 N CALL,OK
 D LOOKUP^FSCULOOK("CALL",.CALL,"AEMOQ",.OK,"I '$P(^(0),U,4)")
 I 'OK Q
 D SETUP1^FSCOPTU(+CALL)
 S XQORM("B")="Close Call"
 D EDIT^FSCLM("EVALUES")
 D CLEANUP^FSCOPTU
 D TMP^FSCOPTU
 Q
 ;
VIEW ; from FSCOP
 N CALL,DISPLAY,FSCSTYLE,OK,VALMAR K ^TMP("FSC LIST",$J)
 S FSCLNAME=" ",FSCLNUM=0,FSCL0="",FSCQUERY=1
 S OK=1 F  D  Q:'OK
 .N CALL D LOOKUP^FSCULOOK("CALL",.CALL,"AEMOQ",.OK)
 .I 'OK Q
 .S ^TMP("FSC LIST",$J,+CALL)=""
 D
 .I '$O(^TMP("FSC LIST",$J,0)) Q
 .D EXPAND^FSCUX(.FSCSTYLE,.OK,"DETAILED") I 'OK Q
 .S FSCSTYLE=$$STYLE^FSCU(FSCSTYLE)
 .S DISPLAY="" I '($D(FSCSTYLE("E"))!$D(FSCSTYLE("T"))) D DISPLAY^FSCRU(.DISPLAY,.OK) I 'OK Q
 .I DISPLAY="DEVICE" D DEV^FSCRU("","^TMP(""FSC LIST"",$J,","DQ^FSCRUDQ","NOIS Report",.OK) Q
 .D SETUP^FSCRPTS I $D(VALMQUIT) Q
 .D VIEW^FSCLM(VALMAR,"VVALUES",.FSCSTYLE)
 D CLEANUP^FSCOPTU
 D TMP^FSCOPTU
 Q
 ;
LIST ; from FSCOP
 N FSCEDIT,FSCEXIT,FSCLIMIT,LIST,OK
 S LIST=$P($$DEFLIST^FSCU(DUZ),U,2)
 D LIST^FSCULOOK(.LIST,.FSCLIMIT,.OK)
 I 'OK Q
 D LIST^FSCLM($P(LIST,U,2))
 D CLEANUP^FSCOPTU
 D TMP^FSCOPTU
 Q
 ;
QUERY ; from FSCOP
 W !!
 N FSCEDIT,FSCEXIT,FSCLIMIT,OK,VALMAR
 S FSCLNAME="Temporary List",FSCLNUM=0,OK=1,FSCQUERY=1
 D BROWSE^FSCQB("",FSCLNAME,0,.OK,"Add")
 I 'OK Q
 D LIST^FSCLM(FSCLNAME)
 D CLEANUP^FSCOPTU
 D TMP^FSCOPTU
 Q
 ;
REPORTS ; from FSCOP
 D REPORTS^FSCRPTS
 D CLEANUP^FSCOPTU
 D TMP^FSCOPTU
 Q
 ;
FILES ; from FSCOP
 D FILES^FSCUEDIT
 D TMP^FSCOPTU
 Q
 ;
EVENT ; from FSCOP
 D FILES^FSCUEDS
 D TMP^FSCOPTU
 Q
