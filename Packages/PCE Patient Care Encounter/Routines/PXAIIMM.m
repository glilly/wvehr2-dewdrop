PXAIIMM ;ISL/PKR - Set the IMMUNIZATION nodes. ;12/18/97
 ;;1.0;PCE PATIENT CARE ENCOUNTER;**45,124**;Aug 12, 1996
 ;
IMM ;Main entry point.
 ;
 K PXAERR
 S PXAERR(8)=PXAK
 S PXAERR(7)="IMMUNIZATION"
 ;
 N IND,PXAA
 S IND=""
 F  S IND=$O(@PXADATA@("IMMUNIZATION",PXAK,IND)) Q:IND=""  D
 . S PXAA(IND)=@PXADATA@("IMMUNIZATION",PXAK,IND)
 ;
 ;Validate the data.
 N STOP
 D VAL^PXAIIMMV
 I $G(STOP) Q
 ;
SETVARA ;Set the after visit variables.
 N AFTER0,AFTER12,AFTER811,AFTER812
 S $P(AFTER0,U,1)=$G(PXAA("IMMUN"))
 I $G(PXAA("DELETE")) S $P(AFTER0,U,1)="@"
 S $P(AFTER0,U,2)=$G(PATIENT)
 S $P(AFTER0,U,3)=$G(PXAVISIT)
 S $P(AFTER0,U,4)=$G(PXAA("SERIES"))
 S $P(AFTER0,U,6)=$G(PXAA("REACTION"))
 S $P(AFTER0,U,7)=$G(PXAA("CONTRAINDICATED"))
 S $P(AFTER0,U,8)=$G(PXAA("DIAGNOSIS"))
 S $P(AFTER0,U,9)=$G(PXAA("DIAGNOSIS 2"))
 S $P(AFTER0,U,10)=$G(PXAA("DIAGNOSIS 3"))
 S $P(AFTER0,U,11)=$G(PXAA("DIAGNOSIS 4"))
 S $P(AFTER0,U,12)=$G(PXAA("DIAGNOSIS 5"))
 S $P(AFTER0,U,13)=$G(PXAA("DIAGNOSIS 6"))
 S $P(AFTER0,U,14)=$G(PXAA("DIAGNOSIS 7"))
 S $P(AFTER0,U,15)=$G(PXAA("DIAGNOSIS 8"))
 S $P(AFTER12,U,1)=$G(PXAA("EVENT D/T"))
 S $P(AFTER12,U,4)=$G(PXAA("ENC PROVIDER"))
 S $P(AFTER811,U,1)=$G(PXAA("COMMENT"))
 ;
 ;--PACKAGE AND SOURCE
 S $P(AFTER812,"^",2)=$G(PXAPKG)
 S $P(AFTER812,"^",3)=$G(PXASOURC)
 ;
 S ^TMP("PXK",$J,"IMM",PXAK,0,"AFTER")=AFTER0
 S ^TMP("PXK",$J,"IMM",PXAK,12,"AFTER")=AFTER12
 S ^TMP("PXK",$J,"IMM",PXAK,811,"AFTER")=AFTER811
 S ^TMP("PXK",$J,"IMM",PXAK,812,"AFTER")=AFTER812
 ;
SETVARB ;Set the before variables.
 N BEFOR0,BEFOR12,BEFOR811,BEFOR812
 N IENB,PXAAX,PXBCNT,PXBKY,PXBSKY,PXBSAM
 D IMM^PXBGIMM(PXAVISIT)
 ;
 S IENB=""
 I PXBCNT>0 D
 . S PXAAX("IMMUN")=$P($G(^AUTTIMM(PXAA("IMMUN"),0)),U,1)
 . S IENB=$O(PXBKY(PXAAX("IMMUN"),IENB))
 I $G(IENB) D
 . S BEFOR0=$G(^AUPNVIMM(IENB,0))
 . S BEFOR12=$G(^AUPNVIMM(IENB,12))
 . S BEFOR811=$G(^AUPNVIMM(IENB,811))
 . S BEFOR812=$G(^AUPNVIMM(IENB,812))
 E  S (BEFOR0,BEFOR11,BEFOR12,BEFOR811,BEFOR812)=""
 ;
 S ^TMP("PXK",$J,"IMM",PXAK,0,"BEFORE")=BEFOR0
 S ^TMP("PXK",$J,"IMM",PXAK,12,"BEFORE")=BEFOR12
 S ^TMP("PXK",$J,"IMM",PXAK,811,"BEFORE")=BEFOR811
 S ^TMP("PXK",$J,"IMM",PXAK,812,"BEFORE")=BEFOR812
 S ^TMP("PXK",$J,"IMM",PXAK,"IEN")=IENB
 ;
 Q
