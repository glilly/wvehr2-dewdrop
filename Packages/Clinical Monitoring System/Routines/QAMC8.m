QAMC8 ;HISC/DAD-CONDITION: LENGTH OF STAY ON A WARD ;2/10/92  07:33
 ;;1.0;Clinical Monitoring System;;09/13/1993
EN1 ; *** CONDITION CODE
 S LOS=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P1"))#2:+^("P1"),1:0)
 S WARDGRP=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P2"))#2:+^("P2"),1:0)
 S X1=QAMTODAY,X2=-LOS D C^%DTC S (X1,START)=X,X2=1 D C^%DTC S END=X,START=START-.0000001 K ^UTILITY($J,"QAM TEMP")
 F WARDDT=START:0 S WARDDT=$O(^DGPM("AMV1",WARDDT)) Q:(WARDDT'>0)!(WARDDT'<END)!(WARDDT\1'?7N)  F QAMDFN=0:0 S QAMDFN=$O(^DGPM("AMV1",WARDDT,QAMDFN)) Q:QAMDFN'>0  F IEN=0:0 S IEN=$O(^DGPM("AMV1",WARDDT,QAMDFN,IEN)) Q:IEN'>0  D LOOP1
 F WARDDT=START:0 S WARDDT=$O(^DGPM("AMV2",WARDDT)) Q:(WARDDT'>0)!(WARDDT'<END)!(WARDDT\1'?7N)  F QAMDFN=0:0 S QAMDFN=$O(^DGPM("AMV2",WARDDT,QAMDFN)) Q:QAMDFN'>0  F IEN=0:0 S IEN=$O(^DGPM("AMV2",WARDDT,QAMDFN,IEN)) Q:IEN'>0  D LOOP1
 F QAMDFN=0:0 S QAMDFN=$O(^UTILITY($J,"QAM TEMP",QAMDFN)) Q:QAMDFN'>0  F WARDDT=0:0 S WARDDT=$O(^UTILITY($J,"QAM TEMP",QAMDFN,WARDDT)) Q:WARDDT'>0  D LOOP2
 K ^UTILITY($J,"QAM TEMP"),LOS,WARDGRP,X1,X2,X,END,START,WARDDT,IEN
 Q
LOOP1 I WARDGRP S WARD=+$S($D(^DGPM(IEN,0))#2:$P(^(0),"^",6),1:0) Q:$O(^QA(743.5,WARDGRP,"GRP","AB",WARD,0))'>0
 K ^UTILITY($J,"QAM TEMP",QAMDFN) S ^UTILITY($J,"QAM TEMP",QAMDFN,WARDDT)=IEN
 Q
LOOP2 S X=$O(^DGPM("APTT3",QAMDFN,WARDDT)) Q:(X'>QAMTODAY)&X  S X=$O(^DGPM("APTT2",QAMDFN,WARDDT)) Q:(X'>QAMTODAY)&X
 S ^UTILITY($J,"QAM CONDITION",QAMD1,QAMDFN)="",^UTILITY($J,"QAM CONDITION",QAMD1,QAMDFN,QAMTODAY)=^UTILITY($J,"QAM TEMP",QAMDFN,WARDDT)
 Q
EN2 ; *** PARAMETER CODE
 K DIR,DIRUT S DIR(0)="NO^1:365:0",DIR("A")="LENGTH OF STAY",DIR("B")=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P1"))#2:^("P1"),1:"") K:DIR("B")="" DIR("B")
 S DIR("?",1)="Enter the LOS from the date of the patient's transfer onto a ward.",DIR("?")="Enter the ward length of stay from 1-365 days."
 S QAMPARAM="P1" D EN3^QAMUTL1 I $D(DIRUT) S Y=-1 G Y
 S:Y]"" ^QA(743,QAMD0,"COND",QAMD1,"P1")=Y
2 K DIC,DIR,DIRUT S DIC=743.5,DIC(0)="EMNQZ",DIC("S")="I $P(^(0),""^"",2)=42",DIC("A")="WARD GROUP: ",DIC("B")=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P2"))#2:$P(^("P2"),"^",2),1:"") K:DIC("B")="" DIC("B")
 S DIR("?",1)="Enter a GROUP name that contains MAS ward locations.",DIR("?")="press 'RETURN' for all ward locations."
 S QAMPARAM="P2" D EN2^QAMUTL1 I $D(DIRUT) S Y=-1 G Y
 S:Y]"" ^QA(743,QAMD0,"COND",QAMD1,"P2")=+Y_"^"_Y(0,0)
EXIT K Y
 K QAMPARAM
Y Q
