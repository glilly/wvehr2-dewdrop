IMRPINQ1 ;HCIOFO/FT/FAI-Patient Inquiry (cont) ;07/17/00  16:00
 ;;2.1;IMMUNOLOGY CASE REGISTRY;**5**;Feb 09, 1998
 ; check security
 I '$D(^XUSEC("IMRA",DUZ)) S IMRLOC="IMRPINQ1" D ACESSERR^IMRERR,H^XUS K IMRLOC
 ; get & display lab test results
 Q:$G(DFN)'>0  ;quit if dfn not defined
 ; CD4 or T4 (ACTUAL LEVEL)","ELISA FOR HIV","WESTERN BLOT","VIRAL LOAD"
 I $Y>(IOSL-4) D PRTC^IMRPINQ Q:IMROUT  D HDR^IMRPINQ
 D ^IMRPLAB
 I $Y>(IOSL-4) D PRTC^IMRPINQ Q:IMROUT  D HDR^IMRPINQ
 D ^IMRTST
 I $Y>(IOSL-4) D PRTC^IMRPINQ Q:IMROUT  D HDR^IMRPINQ
 K IMRCD,IMRCNT,IMRLOOP,X
 Q
IMMUN ; display immunization data
 I $Y>(IOSL-4) D PRTC^IMRPINQ Q:IMROUT  D HDR^IMRPINQ
 W !!?29,"***MOST RECENT IMMUNIZATIONS***",!
 K ^TMP("PXI",$J)
 S X="PXRHS03" X ^%ZOSF("TEST")
 I $T D IMMUN^PXRHS03(DFN) ;get immunization data
 S IMRLOOP=0,(IMRVAC(1),IMRVAC(2))=""
 F IMRI=1:1:2 S IMRLOOP=$O(^TMP("PXI",$J,"HEP A",IMRLOOP)) Q:'IMRLOOP  D
 .S IMRLOOP(1)=+$O(^TMP("PXI",$J,"HEP A",IMRLOOP,0))
 .S IMRVAC(IMRI)=$P($G(^TMP("PXI",$J,"HEP A",IMRLOOP,IMRLOOP(1),0)),U,3)
 .Q
 I $Y>(IOSL-4) D PRTC^IMRPINQ Q:IMROUT  D HDR^IMRPINQ
 W !?17,"LAST HEPATITIS A: " I IMRVAC(1)>0 W ?35,$$FMTE^XLFDT(IMRVAC(1),"2D")
 W !?16,"PRIOR HEPATITIS A: " I IMRVAC(2)>0 W ?35,$$FMTE^XLFDT(IMRVAC(2),"2D")
 S IMRLOOP=0,(IMRVAC(1),IMRVAC(2),IMRVAC(3))=""
 F IMRI=1:1:3 S IMRLOOP=$O(^TMP("PXI",$J,"HEP B",IMRLOOP)) Q:'IMRLOOP  D
 .S IMRLOOP(1)=+$O(^TMP("PXI",$J,"HEP B",IMRLOOP,0))
 .S IMRVAC(IMRI)=$P($G(^TMP("PXI",$J,"HEP B",IMRLOOP,IMRLOOP(1),0)),U,3)
 .Q
 I $Y>(IOSL-4) D PRTC^IMRPINQ Q:IMROUT  D HDR^IMRPINQ
 W !?17,"LAST HEPATITIS B: " I IMRVAC(1)>0 W ?35,$$FMTE^XLFDT(IMRVAC(1),"2D")
 W !?16,"PRIOR HEPATITIS B: " I IMRVAC(2)>0 W ?35,$$FMTE^XLFDT(IMRVAC(2),"2D")
 W !?16,"PRIOR HEPATITIS B: " I IMRVAC(3)>0 W ?35,$$FMTE^XLFDT(IMRVAC(3),"2D")
 K IMRVAC S IMRVAC=""
 S IMRLOOP=+$O(^TMP("PXI",$J,"INFLUENZA",0))
 S IMRLOOP(1)=+$O(^TMP("PXI",$J,"INFLUENZA",IMRLOOP,0))
 S IMRVAC=$P($G(^TMP("PXI",$J,"INFLUENZA",IMRLOOP,IMRLOOP(1),0)),U,3)
 I $Y>(IOSL-4) D PRTC^IMRPINQ Q:IMROUT  D HDR^IMRPINQ
 W !?24,"INFLUENZA: " I IMRVAC>0 W ?35,$$FMTE^XLFDT(IMRVAC,"2D")
 S IMRVAC=""
 S IMRLOOP=+$O(^TMP("PXI",$J,"TD-ADULT",0))
 S IMRLOOP(1)=+$O(^TMP("PXI",$J,"TD-ADULT",IMRLOOP,0))
 S IMRVAC=$P($G(^TMP("PXI",$J,"TD-ADULT",IMRLOOP,IMRLOOP(1),0)),U,3)
 W !?8,"TETANUS DIPTHERIA (ADULT): " I IMRVAC>0 W ?35,$$FMTE^XLFDT(IMRVAC,"2D")
 S IMRVAC=""
 S IMRLOOP=+$O(^TMP("PXI",$J,"PNEUMO-VAC",0))
 S IMRLOOP(1)=+$O(^TMP("PXI",$J,"PNEUMO-VAC",IMRLOOP,0))
 S IMRVAC=$P($G(^TMP("PXI",$J,"PNEUMO-VAC",IMRLOOP,IMRLOOP(1),0)),U,3)
 W !?21,"PNEUMOCOCCAL: " I IMRVAC>0 W ?35,$$FMTE^XLFDT(IMRVAC,"2D")
 Q
