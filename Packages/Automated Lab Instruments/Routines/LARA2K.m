LARA2K ;SLC/RWF/DCM - FOR RA 2000 THRU PORT EXPANDER ;7/20/90  10:03 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID OR IDE
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 K TV,Y,NX S TOUT=0,A=1 D IN G QUIT:TOUT,LA2:$E(IN,34,40)'?7N D QC
 S TOUT=0,BAD=0,V=$E(IN,1,6) D NUM S IDE=+V S V=$E(IN,34,40) D NUM S ID=+V,TRAY=$E(ID,1),CUP=+$E(ID,2,7) F A=2:1 D IN Q:TOUT  S:$E(IN,1,4)?4N NX=1 Q:$D(NX)  Q:$L(IN)<2  D QC
 G LA2:$D(Y)<10 I $D(NX) S ^LA(TSK,"I",0)=^LA(TSK,"I",0)-1
 F I=2:1:(A-1) S II=+Y(I) I II,$D(TC(II)) S V=$E(Y(I),25,31) D NUM S:+V @TC(II,1)=+V
LA3 X LAGEN G LA2:'ISQN ;Can be changed by the cross-link code
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S Y(A)=IN Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
OUT S CNT=^LA(TSK,"O")+1,^("O")=CNT,^("O",CNT)=OUT
 LOCK ^LA("Q") S Q=^LA("Q")+1,^("Q")=Q,^("Q",Q)=TSK LOCK
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
