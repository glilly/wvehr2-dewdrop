LACOLT2 ;SLC/RWF - COULTER S PLUS II DATA PROCESSING ;7/20/90  07:53 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK ID = SEQ # ,TRAY=1, CUP = ID, IDE=0
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)),U="^" Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 S TOUT=0,TP="",LRTOP=$P(^LAB(69.9,1,1),U,1) D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
 S SS="CH"
 F I=1:1:15 S ZZ(I)=$P("DATE^TEST^WBC^RBC^HGB^HCT^MCV^MCH^MCHC^RDW^PLT^MPV^LY%^LY#",U,I)
LA2 K Y S A=0,TOUT=0 D IN G QUIT:TOUT,C3:IN["DATE",LA2:IN'["-------"
 ;
C2 S TOUT=0 D IN G QUIT:TOUT,SAVE:IN["-------",C2:IN'[","
C3 ;S A=A+1,Y(A)=""
 S X="" F I=1:1:$L(IN) S:$A(IN,I)-32 X=X_$E(IN,I)
 S NM=$P(X,",",1),NM=$S(NM?1P.A:$E(NM,2,9),1:NM) F A=1:1:15 Q:ZZ(A)=NM
 G C2:A>14 S Y(A)=$P(X,",",2),F(A)=$E(IN,1)
 ;IF NM'[$P(ZZ,U,A) G LOST:$P(ZZ,U,A)'="DATE" G C2
 S X=Y(A),Y(A)=$S(X=".....":"",1:+X)_$S(A>2&(F(A)]" "):F(A),1:"") G C2
SAVE G LOST:'$D(Y(2)) F I=0:0 S I=$O(TC(I)) Q:I<1  X TC(I,2) S @TC(I,1)=V
 S ID=+Y(2),TRAY=1,CUP=ID,IDE=0 G LA2:ID'>0 S SPEC=$P(^LAB(69.9,1,1),U,1)
LA3 X LAGEN G LA2:ISQN<1
 F I=0:0 S I=$O(TV(I)) Q:I<1  D LA4
 G LA2
LA4 I $D(TV(I,1)),TV(I,1)]"" S ^LAH(LWL,1,ISQN,I)=TV(I,1) Q
 Q
LOST S:'$D(^LA("LOST",DT)) ^LA("LOST",DT)=1
 S AA=^LA("LOST",DT),^LA("LOST",DT,AA)=IN,^LA("LOST",DT)=AA+1
 G LA2
 Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 9 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
OUT S CNT=^LA(TQ,"O")+1,^("O")=CNT,^("O",CNT)=OUT
 LOCK ^LA("Q") S Q=^LA("Q")+1,^("Q")=Q,^("Q",Q)=TQ LOCK
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
TEST S ^LA(5,"I",0)=0 G LACOLT2
