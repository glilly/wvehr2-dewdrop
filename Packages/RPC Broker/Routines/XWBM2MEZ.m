XWBM2MEZ ;OIFO-Oakland/JLI - M2M Broker EZ Usage ;08/01/2006  7925229.750776
 ;;1.1;RPC BROKER;**45**;Mar 28, 1997
 ;
 QUIT
 ;
CONNECT(PORT,IP,AV) ;
 S XWBPARMS("MODE")="RPCBroker"
 S XWBPARMS("RETRIES")=3
 I '$$CONNECT^XWBM2MC(PORT,IP,AV) Q "Didn't get port open or AV failed."
 Q 1
 ;
CLOSE ;Close the connection.
 N X
 S X=$$CLOSE^XWBM2MC()
 Q
 ;
SETCNTXT(CONTX) ;
 I '$$SETCONTX^XWBM2MC(CONTX) D CLOSE Q "Didn't get Context"
 Q 1
 ;
DORPC(RESULT,RPCNAME) ;
 S RESULT=$G(RESULT)
 K XWBY
 Q $$CALLRPC^XWBM2MC(RPCNAME,.RESULT,1)
 ;
CLEARP ;
 K XWBPARMS
 S XWBPARMS("MODE")="RPCBroker"
 Q
 ;
SETPARAM(INDEX,TYPE,VALUE) ;Set a Params entry
 S XWBPARMS("PARAMS",INDEX,"TYPE")=TYPE
 S XWBPARMS("PARAMS",INDEX,"VALUE")=VALUE
 Q
 ;
