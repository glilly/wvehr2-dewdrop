GMRVVS0 ;HIRMFO/YH-PATIENT INTAKE/OUTPUT REPORT ;2/25/91
 ;;4.0;Vitals/Measurements;;Apr 25, 1997
EN1 ;PATIENT VITAL SIGNS & I/O SHEET - SF511
 S GRPT=5 I '$D(^GMRD(126.95,1,1)) W !,"NUR SHIFT FILE NOT DEFINED",! S GMROUT=1 D Q Q
 S GMRDAY=$P(^GMRD(126.95,1,1),"^",2),GMREVE=$P(^(1),"^",3),GMRNIT=$P(^(1),"^") I GMRDAY=""!(GMREVE="")!(GMRNIT="") W !,"NUR SHIFT STARTING HOUR NOT DEFINED",! S GMROUT=1 D Q Q
 S Y=GMRSTRT D DD^%DT S GMRDT1=Y,Y=GMRFIN D DD^%DT S GMRDT2=Y
 D STARTD^GMRYRP1 U IO S (GQ,GQT,GPC)=0
 D PATIENT^GMRYUT9 G:GMROUT Q D ^GMRYRP2 G:GMROUT Q D REPORT1^GMRYRP3 D:'GMROUT SF511^GMRVVS3 D:$E(IOST)="C" STOP D Q
 Q
Q ;
 K GSTAR,GNSH,GNDATE,GOP,DA,GAMOUNT,GCSHFT,GCURDT,GDATE,GDAY,GNSHFT,GDSHFT,GESHFT,GHR,GID,GIN,GINDT,GIO,GIVDT,GIVSTRT,GIVSUB,GIVTYP,GLASTDT,GLINE,GMRDOT
 K NN,GAMT,GCOL,GCSFT,GDRESS,GIV,GLAB,GLEN,GLOC,GNURSE,GPOS,GPRT,GRATE,GREC,GSFT,GT,GTIME,GTUBE,GVOL,GSOL,GITEM,GDTSTRT,GDTFIN
 K II,JJ,LL,KK,GMIN,GMRINDT,GNSHFT,GNXNSF,GNXTDT,GOPT,GOUT,GSHIFT,GSITE,GSTRT,GSUB,GTEXT,GTOTIN,GTOTLI,GTOTLO,GTOTOUT,GTYPE,GTYP,GX,GY,^TMP($J,"GMRY"),X,Y,^TMP($J,"GMR")
 K GNURSX,GMRNAM,GI,SSN,GMRBED,GMRBTH,GMRDIAG,GMRSEX,GMRVADM,GMRWARD,GMRVDT,GMRAGE,GMR,GMRDT,GMRQ,GMRS,GMRX1,GMRX2,GDT,GFOUND,GMRHLOC
 Q
Q2 K GMRHT,GFLAG,GMRWARD,GNN,GMRLEN,GMRQUAL,GMROP,GMRCOL,GROOM,GBED,GMRX,GNRMBD,ZTSAVE,GMRPT,GNBED,GMRDAY,GMREVE,GMRNIT,GQ,GQT,GPC,GMRDT1,GMRDT2,DFN,GBLNK,GMRSTRT,GMRFIN,GMREDB,ZTIO,ZTSK,%ZIS,IO("Q") W:$E(IOST)'="" @IOF D ^%ZISC
 K G,GDA,GMRVARY,GMRVHLOC,GMRVWLOC,GMRY,GMRYDT,GMRINF,GVAR,GLINE,GMRI,VAERR,VAIN,GMRNUR,GMROUT,GRPT,VAROOT,GTYPI,GTYPO,GN,GLN Q
STOP Q:GMROUT  W !,"Press return to continue or ""^"" to stop " R X:DTIME
 I '$T!(X="^") S GMROUT=1 Q
 Q
DATE ;ENTER START DATE AND END DATE FOR PATIENT INTAKE/OUTPUT REPORT
 S %DT("A")="Enter start DATE for this report: ",%DT("B")="T-3",%DT="AETX" D ^%DT K %DT S:+Y'>0 GMROUT=1 Q:GMROUT  S GMRSTRT=+Y
 S %DT("A")="Go to DATE: ",%DT="AETXS",%DT("B")="NOW" D ^%DT K %DT S:+Y'>0 GMROUT=1 Q:GMROUT  S (X1,GMRFIN)=+Y Q:GMRFIN=GMRSTRT  S X2=GMRSTRT D ^%DTC
 I X<0!(X=0&(((+("."_$P(GMRFIN,".",2))*10000)-((+("."_$P(GMRSTRT,".",2))*10000)))'>0)) W !,?5,"ENDING DATE needs to be greater than STARTING DATE",!,?5,$C(7),"Please reenter!!!" G DATE
 Q
