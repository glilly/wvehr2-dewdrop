PSN50612 ;BIR/LDT - API FOR INFORMATION FROM FILE 50.612; 5 Sep 03
 ;;4.0; NATIONAL DRUG FILE;**80**; 30 Oct 98
 ;
DATA(PSNIEN,LIST) ;
 ;PSNIEN - IEN of entry in NATIONAL DRUG TRANSLATION file (#50.612).
 ;LIST - Subscript of ^TMP array in the form ^TMP($J,LIST,Field Number where Field Number is the
 ;       Field Number of the data piece being returned.
 N DIERR,ZZERR,PSN50612,PSN,CNT
 I $G(LIST)']"" Q
 K ^TMP($J,LIST)
 I +$G(PSNIEN)'>0 S ^TMP($J,LIST,0)=-1_"^"_"NO DATA FOUND" Q
 I $G(PSNIEN)]"",+$G(PSNIEN)'>0 S ^TMP($J,LIST,0)=-1_"^"_"NO DATA FOUND" Q
 I $G(PSNIEN)]"" N PSNIEN2 S PSNIEN2=$$FIND1^DIC(50.612,"","B","`"_PSNIEN,,,"") D
 .I +PSNIEN2'>0 S ^TMP($J,LIST,0)=-1_"^"_"NO DATA FOUND" Q
 .S ^TMP($J,LIST,0)=1
 .D GETS^DIQ(50.612,+PSNIEN,"10;11","IE","PSN50612") S PSN(1)=0
 .F  S PSN(1)=$O(PSN50612(50.612,PSN(1))) Q:'PSN(1)  D SETZRO
 K ^TMP("DILIST",$J)
 Q
SETZRO ;
 S ^TMP($J,LIST,+PSN(1),10)=$G(PSN50612(50.612,PSN(1),10,"I"))
 S ^TMP($J,LIST,+PSN(1),11)=$G(PSN50612(50.612,PSN(1),11,"I"))
 Q
