PSOCST8 ;BHAM ISC/SAB - DRUG COSTS BY DIVISION ; 08/19/92 9:03
 ;;7.0;OUTPATIENT PHARMACY;**31**;DEC 1997
 ;External Ref. to ^PS(59, is supp. by DBIA# 212
BEG S RP=8 D HDC^PSOCSTX F  D CDT^PSOCSTX Q:$G(CTR)  D DVS^PSOCSTX Q:$G(CTR)  S RP=0 D CTP^PSOCSTX Q:$G(CTR)  I RP=0 D DEV Q
 D EX Q
DEV D DVC^PSOCSTX Q:$G(CTR)
 K PSOION I $D(IO("Q")) S ZTDESC="DRUG COST BY DIVISION",ZTRTN="START^PSOCST8" D PAS^PSOCSTX
 I  K IO("Q") D ^%ZTLOAD W:$D(ZTSK) !,"REPORT QUEUED TO PRINT !!",! D EX Q
START U IO K ^TMP($J) F PSDT=(BEGDATE-1):0:ENDDATE S PSDT=$O(^PSCST(PSDT)) Q:'PSDT!(PSDT>ENDDATE)  D @$S('IFN:"PAT",1:"DIV")
 D ZER^PSOCSTX S DIVX="" D HD I $O(^TMP($J,DIVX))']"" D HDN^PSOCSTX Q
 F I=0:0 S DIVX=$O(^TMP($J,DIVX)) Q:DIVX=""  D HD:($Y+4)>IOSL Q:$G(CTR)  S Y=^TMP($J,DIVX),TTX=DIVX D PRT^PSOCSTX
 I 'CTR,'IFN D HD:($Y+2)>IOSL D FTX^PSOCSTX
EX D EX^PSOCSTX Q
PAT F DIV=0:0 S DIV=$O(^PSCST(PSDT,"V",DIV)) Q:'DIV  D DIV
 Q
DIV I $D(^PSCST(PSDT,"V",DIV,0)) S X=^(0) D STORE
 Q
STORE Q:'$D(^PS(59,DIV,0))  S DIVX=$P(^(0),"^") S:'$D(^TMP($J,DIVX)) ^TMP($J,DIVX)="^0^0^0"
 S UTL=^TMP($J,DIVX),^TMP($J,DIVX)="^"_($P(UTL,"^",2)+$P(X,"^",2))_"^"_($P(UTL,"^",3)+$P(X,"^",3))_"^"_($P(UTL,"^",4)+$P(X,"^",4))
 Q
HD D HD^PSOCSTX Q
