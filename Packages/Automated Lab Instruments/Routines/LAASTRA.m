LAASTRA ;SLC/RWF - ASTRA 4,6,8,8E,IDEAL (CALCULATING IE ANION GAP ;7/20/90  07:13 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID,IDE OR CUP
LA1 S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 K TV S TOUT=0 D IN G QUIT:TOUT,LA2:$E(IN,1,4)'="[01,"
 S IDE=+$E(IN,8,16),TRAY=+$E(IN,30,31),CUP=+$E(IN,33,34),ID=TRAY_CUP
 F A=0:0 S TOUT=0 D IN G QUIT:TOUT Q:IN'["[02,"&(IN'["[03,")  S NM=$P(IN,",",2),V=$P(IN,",",3) D NUM I V'="0000",V'="T.O" S V=$P(V,"]") F I=0:0 S I=$O(TC(I)) Q:I<1  I TC(I,4)=NM S @TC(I,1)=V Q
LA3 X LAGEN G LA2:'ISQN ;Can be changed by the cross-link code
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 9 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J) S:$D(ZTQUEUED) ZTREQ="@"
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
