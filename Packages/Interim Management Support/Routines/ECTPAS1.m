ECTPAS1 ;B'ham ISC/PTD-PAID Data for All Services - CONTINUED ;01/29/91 08:00
V ;;1.05;INTERIM MANAGEMENT SUPPORT;**8,10**;
EN1 S (TAL,TSL,TLWOP,TAA,TCE,TCU,TUNS,TOT)=0,PGCT=1,QFLG="",$P(LN,"-",81)="",SRV=0
 I '$O(^TMP($J,0)) D HDR W !!?16,"==>  NO DATA IN THE FILE FOR SELECTED DATES  <==",!! G EXIT
SRV F J=0:0 S SRV=$O(^TMP($J,SRV)) Q:'SRV  D:(PGCT>1)&(BYRPP'=EYRPP) PRTCHK G:QFLG EXIT D:PGCT<2 HDR W !!?1,"SERVICE:  ",$P(^ECC(730,SRV,0),"^") S (SAL,SSL,SLWOP,SAA,SCE,SCU,SUNS,SOT,YP)=0 D YP G:QFLG EXIT D SRTOT
WRTOT W !!?14 F J=1:1:66 W "="
 W !?4,"TOTAL",?11,$J(TAL,7,2),?19,$J(TSL,7,2),?27,$J(TLWOP,7,2),?36,$J(TAA,7,2),?45,$J(TCE,7,2),?54,$J(TCU,7,2),?63,$J(TUNS,7,2),?72,$J(TOT,7,2),!!
 ;
 I $E(IOST)'="C" W @IOF
EXIT K %,%H,%DT,%I,AA,AA1,AA2,AL,AL1,AL2,BPP,BYR,BYRPP,CTE,CTE1,CTE2,CTU,CTU1,CTU2,DIR,DTOUT,DUOUT,EMPDA,EPP,EYR,EYRPP,FLG,FST,G,J,K,L,LN,LOC,LOC0,LOC1,LWOP,LWOP1,LWOP2,OT,OT1,OT2,PAA,PAL,PCTE,PCTU,PGCT,PLWOP,POP,POT,PSL,PUNS,QFLG
 K PC,FR,WH,SAA,SAL,SCE,SCU,SL,SL1,SL2,SLWOP,SOT,SRV,SRVDA,SRVTL,SSL,SUNS,TAA,TAL,TCE,TCU,TL,TLWOP,TMP,TOT,TSL,TUNS,UNS,UNS1,UNS2,X,Y,YP,^TMP($J),ZTDESC,ZTRTN,ZTSAVE,ZTSK D ^%ZISC I IO="" S IOP="HOME" D ^%ZIS
 Q
 ;
YP F K=0:0 S YP=$O(^TMP($J,SRV,YP)) Q:'YP  S LOC=^TMP($J,SRV,YP) D WRTLN Q:QFLG
 Q
 ;
HDR ;PRINT REPORT MAIN HEADER
 W @IOF,!?27,"PAID DATA FOR ALL SERVICES",!?16,"FROM PAY PERIOD: "_BPP_" - '"_$E(BYR,2,3)_" TO PAY PERIOD: "_EPP_" - '"_$E(EYR,2,3)
 D NOW^%DTC S Y=$E(%,1,12) X ^DD("DD") W !!?45,Y,?70,"PAGE ",PGCT S PGCT=PGCT+1 W !!?49,"CT",?58,"CT",?65,"UNSCH",?75,"OVER",!,"PAY PERIOD",?15,"AL",?23,"SL",?30,"LWOP",?40,"AA",?47,"EARNED",?57,"USED",?66,"REG",?75,"TIME",!,LN
 Q
 ;
WRTLN D:$Y+5>IOSL PRTCHK Q:QFLG  W !?1,$E(YP,4,5)_" - '"_$E(YP,2,3),?11,$J($P(LOC,"^"),7,2),?19,$J($P(LOC,"^",2),7,2),?27,$J($P(LOC,"^",3),7,2),?36,$J($P(LOC,"^",4),7,2),?45,$J($P(LOC,"^",5),7,2)
 W ?54,$J($P(LOC,"^",6),7,2),?63,$J($P(LOC,"^",7),7,2),?72,$J($P(LOC,"^",8),7,2)
 S SAL=SAL+$P(LOC,"^"),SSL=SSL+$P(LOC,"^",2),SLWOP=SLWOP+$P(LOC,"^",3),SAA=SAA+$P(LOC,"^",4),SCE=SCE+$P(LOC,"^",5),SCU=SCU+$P(LOC,"^",6),SUNS=SUNS+$P(LOC,"^",7),SOT=SOT+$P(LOC,"^",8)
 Q
 ;
PRTCHK I $E(IOST)="C" S DIR(0)="E" D ^DIR I Y=0 S QFLG=1 Q
 D HDR
 Q
 ;
SRTOT ;WRITE SUB-TOTAL FOR SERVICE
 D:$Y+5>IOSL PRTCHK Q:QFLG  I BYRPP'=EYRPP W !?14 F L=1:1:66 W "-"
 I BYRPP'=EYRPP W !?1,"SUB-TOTAL",?12,$J(SAL,6,2),?20,$J(SSL,6,2),?28,$J(SLWOP,6,2),?37,$J(SAA,6,2),?46,$J(SCE,6,2),?55,$J(SCU,6,2),?64,$J(SUNS,6,2),?73,$J(SOT,6,2),!
 S TAL=TAL+SAL,TSL=TSL+SSL,TLWOP=TLWOP+SLWOP,TAA=TAA+SAA,TCE=TCE+SCE,TCU=TCU+SCU,TUNS=TUNS+SUNS,TOT=TOT+SOT
 Q
 ;
