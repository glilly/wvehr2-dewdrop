IBDFQS ;ALB/MAF -REQUEUE OF PRINT JOB FOR A SINGLE PARAMETER GROUP - FEB 9 1995
 ;;3.0;AUTOMATED INFO COLLECTION SYS;**12**;APR 24, 1997
 ; -- Queue of a single print job (parameter group)
 ; -- Selection of what print group/s to be printed
 N IBDVALM,IBDAT,VALMY,IBDFSING,IBDFTSTP,IB1FLAG,IB1TASK,IBDFQT
 D NOW^%DTC S IBDFQT=$E($P(%,".",2),1,4)
 S IBDFSING=1,IBDFTSTP=1
 S VALMBCK=""
 D EN^VALM2($G(XQORNOD(0))) G REP:'$O(VALMY(0)) S IBDVALM=0
 D FULL^VALM1 S VALMBCK="R"
 F IBDVALM=0:0 S IBDVALM=$O(VALMY(IBDVALM)) Q:'IBDVALM  S IBDFDA=$S($D(IBDTYPE(IBDVALM)):IBDTYPE(IBDVALM),1:"") I IBDFDA]"" D
 .N IBDFNUM,IBDFNAME,IBDFIFN,IBDFSEQ
 .S QUIT=0
 .S (DA,IBDFIFN)=$P(IBDFDA,"^",1),IBDFNUM=$P(IBDFDA,"^",2)
 .S IBDFNODE=$G(^IBD(357.09,IBDFNUM,"Q",DA,0)),IBDFNAME=$P(IBDFNODE,"^",1)
 .S IBDFSEQ=$P(IBDFNODE,"^",10)
 .S IB1FLAG=1
 .D UP^IBDFQB($$QUEUE^IBDFQB(IBDFIFN))
 .D STAT
 .Q
REP  S VALMBCK="R" Q
 ;
STAT ; -- Find out the status of the queued job and kill a tasked job
 N IBDFSTAT
 D FULL^VALM1
 W !!!,"Job Queued <<< "_$P(IBDFNODE,"^",1)_" >>>",!
 I $D(IB1TASK) W !,"       TASK #: ",IB1TASK,!
 D PAUSE^VALM1
 Q
