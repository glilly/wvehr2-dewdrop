ORSLTR ; slc/RWF - Print Big Letters ;4/30/92  14:47 ;
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**23**;Dec 17, 1997
 Q:'$L($G(ORSLTR))  I '$D(^UTILITY("ORSLTR",$J,"A")) D ^ORSLTR2
B1 ; Setup for banner print
 N ORSDIV,ORSLEN,ORSXXX,ORSBLNK,X
 S ORSLEN=$L(ORSLTR),ORSDIV=60\ORSLEN,$P(ORSXXX,"X",(ORSDIV\5+1))=""
 S $P(ORSBLNK," ",(ORSDIV\5+1))=""
 S ORSLTY=$E(ORSLTR,1,$S(ORSLEN'>2:ORSLEN,1:(IOM\ORSDIV-1))),ORSLTX=""
 F ORSLT1=1:1:$L(ORSLTY) D
 . I $A(ORSLTY,ORSLT1)>32,$D(^UTILITY("ORSLTR",$J,$E(ORSLTY,ORSLT1))) D
 . . S ORSLTX=ORSLTX_$E(ORSLTY,ORSLT1)
 W:$E($G(IOST),1,2)="C-" @IOF
 I $D(ORTIT) W ?(IOM-$L(ORTIT)\2),ORTIT,!
 I $D(ORSPNM) W ?10,"Patient: ",ORSPNM,!
 I $D(ORSWDN) W "Hospital Location: ",ORSWDN,!
 I $D(DUZ) W ?13,"User: ",$P($G(^VA(200,DUZ,0)),U),!
 W ! F ORSLT=1:1:4 D B2
 W:$E($G(IOST),1,2)'="C-" @IOF K ORSLT1,ORSLT2,ORSLT3,ORSLTX,ORSLTY,ORSLT,^UTILITY("ORSLTR",$J) Q
B2 W !
 F ORSLT1=9:-1:1 W ! F ORSLT3=1:1:$L(ORSLTX) S X=^UTILITY("ORSLTR",$J,$E(ORSLTX,ORSLT3)) W "   " F ORSLT2=1:1:5 W $S($E(X,(ORSLT2-1*9+ORSLT1)):ORSXXX,1:ORSBLNK)
 Q
