LAH480 ;SLC/RWF - HEMATRAK 360, 480, 590 ;7/20/90  08:35 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID OR IDE
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 K TV,Y S TOUT=0 D IN G QUIT:TOUT,LA2:$L(IN)'=24
 S A=1 D QC F A=2:1:6 S TOUT=0 D IN,QC G QUIT:TOUT
 S M1=$E(Y(2),1),M2=$E(Y(2),2),M3=$E(Y(2),3)
 F I=0:0 S I=$O(TC(I)) Q:I<1  X TC(I,2) S @TC(I,1)=V
 S V=$E(Y(1),1,9) D NUM S ID=+V,TRAY=1,(CUP,IDE)=ID
LA3 X LAGEN G LA2:'ISQN
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S Y(A)=IN Q
ACODE S V=$P("^Reduced^Adeq.^Inc.^Few^Mod.^Many^","^",V+1) Q
NCODE S V=$P("^^^^1+^2+^3+^4+^","^",V+1) Q
BCODE S V=$P("^^^^S1./Few^Mod.^Marked^","^",V+1) Q
CCODE S V=$P("^Decr.^Adeq.^Incr.^^^^","^",V+1) Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
