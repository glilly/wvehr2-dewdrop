LRDIST4 ;SLC/DCM - GRAPH ENTRY FOR OE/RR ;8/11/97
 ;;5.2;LAB SERVICE;**121,153**;Sep 27, 1994
OERR ;OE/RR entry for lab graph
 I '$D(LRPARAM) D EN^LRPARAM
 D DT^LRX K DIC S LREND=0,LRDPF=+$P(@("^"_$P(ORVP,";",2)_"0)"),"^",2)_"^"_$P(ORVP,";",2) D END^LRDPA Q:LRDFN<1  D PT^LRX
 D LIST^LRDIST
 Q
SET ;
 S LRCW=8,LRFLAG="",LRCTRL=0
 K LREDT D ^LRWU3 Q:LREND
 S LRNSET=80,N=LRNSET
L2 F I=0:0 W !,"How many time points? ",LRNSET,"//" R X:DTIME Q:X[U!'$L(X)!(X\1=X&(X'<1))!(X?.N)  W "  Enter a whole number."
 Q:X[U  S:X'="" LRNSET=X S N=LRNSET
L3 K ^TMP("LR",$J,"TMP"),^TMP("LR",$J,"X"),X,LRORD,DIC,ZTRTN,ZTIO,ZTDESC,ZTSAVE,ZTSK S LRSPEC=-1,DIC(0)="AEOQZ" S DIC="^LAB(61,",DIC("A")="Select SITE/SPECIMEN: ANY//" D ^DIC S LRSPEC=+Y I $D(DUOUT) S OREND=1 Q
L4 S LRSS="CH" K DIC("A") S:'LRFLAG LRSDNORM=0 IF (LRSPEC>0!LRCTRL)&'LRFLAG W !,"Plot relative to ",$S(LRCTRL:"expected",1:"normal")," values (if available)" S %=1 D YN^DICN Q:%=-1  G L4:%=0 S:%=1 LRSDNORM=1
 S:N<2 N=30 S LRSSP=0,DIC="^LAB(60,",DIC("S")="I $P(^(0),U,4)=""CH"""_$S(LRCTRL:"",1:$S('$D(^XUSEC("LRLIASON",DUZ)):",""N""'[$P(^(0),U,3)",1:"")) D ^DIC Q:Y<1
 IF $L($P(^LAB(60,+Y,.1),U,5)) W !,"ASK FOR TESTS INDIVIDUALLY" Q
TX S LRSSP=LRSSP+1,LRTEST(LRSSP)=+Y_U_Y(0) D ^DIC G TX:Y>0
 S LRNX=0,LRPANEL=0 K ^TMP("LR",$J,"X"),X,^TMP("LR",$J,"TMP"),LRORD,DIC F I=1:1 Q:'$D(LRTEST(I))  S X=LRTEST(I),(LRNTN,LRNT,T1)=I,(S1,J)=0,LRCFL="" D EX2^LREXPD
 K LRTEST,T1,^TMP("LR",$J,"TMP") S I=0 F  S I=$O(LRORD(I)) Q:I<1  S J=LRORD(I),LRTEST(I)=$O(^LAB(60,"C","CH;"_J_";1",0))_U_$P(^LAB(60,$O(^(0)),0),U,1)_U_J,LRNM=I
 Q
CLEAN ;
 K AGE,C,DFN,J,LRBLOOD,LRDFN,LRDPF,LRDT0,LREND,LRLABKY,LRMX,LRPARAM,LRPLASMA,LRSERUM,LRUNKNOW,LRURINE,S1,SEX,X,Y
 K A,A9,DFN,DIC,DOB,I,K,LAST,LRORD,LRCHM,LRCOUNT,LRCTRL,LRCW,LRDPF
 K LRCV,LREDT,LREND,LRFLAG,LRFOOT,LRHIGH,LRIDT,LRII,LRIY,LRLM1,LRLM1F,LRLM2,LRLM2F
 K LRLOW,LRM,LRTEST,LRNC,LRNEX,LRNM,LRNSET,LRNT,LRNTN,LRNX,LROK
 K LRPANEL,LRSB,LRSDNORM,LRSDT,LRSPC,LRSPEC,LRSS,LRSSP,LRSSX,LRSTEPS
 K ^TMP("LR",$J,"X"),LRSTS,LRSUB,LRSX,LRTN,LRVAL,LRWRD,N,PNM,SSN,X,Y,Z
 K LRCV,LRECV,LREM,LRESD,LRLF,LRSD,LRSDD,LRTAB,LRXF,LRTEC,LRTM60,LRTS,LRTX,LRUSI,LRVF,LRVOL,LRVRM,LRWDTL,LRXD,LRXDH,LRXDP,S2,T1
 K LRDFN,DUOUT,DTOUT,R1,LRACD,LRAOD,LRCDT,LRCFL,LRDAT,LRDEL,LRDV,LRDVF,LREAL,LREDIT,LREXEC,LRFAN,LRFFLG,LRFP,LRGVP,LRINI,LRIOZERO,LRLAN,LRLCT,LRMD,LRMETH,LRNG,LRNG2,LRNG3,LRNG4,LRNG5,LRODT,LROUTINE,LRPER,LRPLOC,LRSAMP,LRSN,LRSSQ,LRSTAR
 Q
