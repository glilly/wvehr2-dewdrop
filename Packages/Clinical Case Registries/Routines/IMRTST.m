IMRTST ;HCIOFO/FAI-LOOKUP LAB TEST VALUES (HIV ANTIBODY);07/11/00  11:21;
 ;;2.1;IMMUNOLOGY CASE REGISTRY;**9,5**;Feb 09, 1998
 ; called from IMRCD4 routine
TYPE ; Entry with IMRDFN defined and pointers for local lab test name & NLT
 K ^TMP($J)
 D HEAD,DATA,SORT
 K ^TMP($J)
 Q
DATA K IMRCD
 I $G(IMRTSTLR)="" W !!,?13,"** SORRY NO LABORATORY REFERENCE IN PLACE **" Q
 S (IMRTSTI,IMRTSTII)="",ILR=IMRTSTLR
 S LGN="" F  S LGN=$O(^IMR(158.95,"B",LGN)),LIG="" Q:LGN=""  S LIG=$O(^IMR(158.95,"B",LGN,LIG)) Q:LIG=""  D LOCAL
 Q
LOCAL Q:LGN'="HIV ANTIBODY"
 S IMRCD="" F  S IMRCD=$O(^IMR(158.9,1,3,"B",LIG,IMRCD)),IMS="" Q:IMRCD=""  F  S IMS=$O(^IMR(158.9,1,3,IMRCD,1,"B",IMS)),IMLM="" Q:IMS=""  F  S IMLM=$O(^IMR(158.9,1,3,IMRCD,1,"B",IMS,IMLM)) Q:IMLM=""  D LLT
 Q
LLT S IMLO="" F  S IMLO=$O(^IMR(158.9,1,3,IMRCD,1,IMLM,1,"B",IMLO)),TNN="" Q:IMLO=""  F  S TNN=$O(^IMR(158.9,1,3,IMRCD,1,IMLM,1,"B",IMLO,TNN)) Q:TNN=""  D LWK
 Q
LWK S IMWK=$P($G(^IMR(158.9,1,3,IMRCD,1,IMLM,1,TNN,0)),U,2),LNM=$P($G(^LAB(60,IMLO,0)),U,1),LLOC=$P($G(^LAB(60,IMLO,0)),U,5)
 I LLOC'="" S UNN=$P($G(^LAB(60,IMLO,1,0)),U,3),LDAT=$P(LLOC,";",2) S:UNN'="" UNS=$P($G(^LAB(60,IMLO,1,UNN,0)),U,7) D CHEMS Q
 I LLOC="" D PANEL Q
 Q
PANEL F PN=0:0 S PN=$O(^LAB(60,IMLO,2,PN)) Q:PN'>0  S LPN=$P($G(^LAB(60,IMLO,2,PN,0)),U,1),LNM=$P($G(^LAB(60,LPN,0)),U,1),LLOC=$P($G(^LAB(60,LPN,0)),U,5) D PAN2
 Q
PAN2 S UNN=$P($G(^LAB(60,LPN,1,0)),U,3)
 S:UNN'="" UNS=$P($G(^LAB(60,LPN,1,UNN,0)),U,7)
 S:LLOC'="" LDAT=$P(LLOC,";",2)
 D CHEMS
 Q
CHEMS S LDT="" F  S LDT=$O(^LR(ILR,"CH",LDT)),DNAM="" Q:LDT=""  F  S DNAM=$O(^LR(ILR,"CH",LDT,DNAM)) Q:DNAM=""  S LRES=$P($G(^LR(ILR,"CH",LDT,LDAT)),U,1),DTRC=$P($G(^LR(ILR,"CH",LDT,0)),U,1),Y=DTRC D DD^%DT S (DTAA,DTRC)=Y D PLBS
 Q
PLBS Q:(LRES["CANC")!(LRES["canc")
 Q:(DTRC["CANC")!(DTRC["canc")
 S DTR1=$E(DTAA,1,3),DTR2=$E(DTAA,9,12),DTRD=DTR1_","_DTR2
 S DTRC=$E(DTRC,1,12),LDO=$E(LDT,1,7)
 Q:DNAM'=LDAT  S ^TMP($J,LDO,LNM)="HIV_ANTIBODY"_U_UNS_U_LRES_U_DTRC
 Q
HEAD W !!!,?20,"Western Blot and Elisa Values",!!,?3,"DATE",?18,"RESULT",?32,"TEST"
 W !,?3,"----",?18,"---------",?33,"------"
 Q
SORT I '$D(^TMP($J)) W !!,?20,"** NO DATA FOUND **" Q
 S MDT="" F  S MDT=$O(^TMP($J,MDT)),LNM="" Q:MDT=""  F  S LNM=$O(^TMP($J,MDT,LNM)) Q:LNM=""  S RC=^TMP($J,MDT,LNM),UNS=$P(RC,U,2),RES=$P(RC,U,3),IMDATE=$P(RC,U,4) W !,IMDATE,?18,RES,?33,LNM
 Q
