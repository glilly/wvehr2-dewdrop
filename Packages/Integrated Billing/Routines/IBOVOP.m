IBOVOP ;ALB/RLW-Report of Visits for NSC Outpatients ;12-JUN-92
 ;;2.0;INTEGRATED BILLING;**91,153,176,249**;21-MAR-94
 ;; Per VHA Directive 10-93-142, this routine should not be modified
 ;
INIT ;
 ;
 D DT^DICRW,HOME^%ZIS S IBQUIT=0
 N IBJ,IBAMT,IBAIEN,IBPAGE,IBLINE,IBACT,IBDATA,IBTITLE,IBDATE,IBADFN,IBEL,IBIEN,IBCL,IBCKIN,IBSDATA,IBSTART,IBPRNT,IBAPPT,IBSTAT,IBFLD1,IBFLD2,IBFLD3,IBFLD4,IBFLD5,IBFLD6
 S DIR(0)="DA^2901001:NOW:EX",DIR("A")="Start with DATE: " D ^DIR K DIR G:$D(DIRUT) END S IBBDT=+Y
 S DIR(0)="DA^"_+Y_":NOW:EX",DIR("A")="Go to DATE: " D ^DIR K DIR G:$D(DIRUT) END S IBEDT=+Y
 S %ZIS="QM" D ^%ZIS G:POP END
 I $D(IO("Q")) D  G END
 .S ZTRTN="QUE^IBOVOP",ZTDESC="MEANS TEST/LTC OUTPATIENT/REGISTRATION EVENTS",ZTSAVE("IB*")=""
 .D ^%ZTLOAD W !!,$S($D(ZTSK):"This job has been queued.  The task number is "_ZTSK_".",1:"Unable to queue this job.")
 .K ZTSK,IO("Q") D HOME^%ZIS
 U IO
 ;
 ;
 D QUE
END K IBBDT,IBEDT,IBQUIT,ZTQUEUED,ZTDESC,ZTREQ
 ;
 ;
 Q
 ;
QUE ;entry point if queued
 N IBQUERY K ^TMP($J,"SDAMA301")
 ;
 S IBPAGE=0
 S IBDATE=IBBDT D MAIN^IBOVOP1(.IBQUERY) I IBQUIT D CLOSEQ(.IBQUERY) Q
 F  S X1=IBDATE,X2=1 D C^%DTC Q:X>IBEDT  S IBDATE=X K ^TMP("IBOVOP",$J) D MAIN^IBOVOP1(.IBQUERY) Q:IBQUIT
 D CLOSEQ(.IBQUERY)
 ;
 K ^TMP($J,"SDAMA301")
 ;
 I $D(ZTQUEUED) S ZTREQ="@" Q
 D ^%ZISC
 Q
 ;
CLOSEQ(IBQUERY) ; Close any QUERY, if open
 D CLOSE^IBSDU(.IBQUERY)
 Q
 ;
