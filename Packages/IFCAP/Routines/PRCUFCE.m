PRCUFCE ;WISC/SJG-CONTINUATION OF OBLIGATION PROCESSING ;7/21/93  13:51
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 QUIT
 ; No top level entry point
EDIT ; Perform edit check for required FMS fields
 S PARAM1="^"_PRC("SITE")_"^"_+$P(PO(0),U,3)_"^"_PRC("FY")_"^"_PRCFA("BBFY")
 D DOCREQ^PRC0C(PARAM1,"SPE","PRCFMO")
 S PRCFMO("G/N")=$P(PRCFMO,U,12)
 QUIT
 ;
STACK(MOD) ; Create entry in GECS Stack File
 N FMSSYS,FMSSTA,FMSDOC,FMSTRA,FMSSEC,FMSMOD,FMSFCP,FMSDES
 S PRCSTA=PRC("SITE"),PRCCC=+$P(PO(0),U,5),PRCCP=+$P(PO(0),U,3)_"^"_PRC("FY")_"^"_PRCFA("BBFY"),PRCREQST=$P(PO(0),U,12)
 I $D(PRCFA("CONVG")),PRCFA("CONVG")=1 S PRCCP=PRC("CP")_"^"_PRC("FY")_"^"_PRCFA("BBFY")
 S PRCSTR=$$ACC^PRC0C(PRCSTA,PRCCP)
 S FMSSYS="I",FMSSTA=PRCSTA,FMSDOC=PRCFA("REF"),FMSTRA=PRCFA("TT")
 S FMSSEC=$$SEC1^PRC0C(PRC("SITE")),FMSMOD=MOD,FMSDES=PRCFA("IDES")
 ;S FMSFCP=$$FMSFCP^PRCFFU5(PRCREQST,PRCFA("SFC"),PRCFA("MP"))
 I $D(PRCFA("RETRAN")),PRCFA("RETRAN")=1 Q
 D CONTROL^GECSUFMS(FMSSYS,FMSSTA,FMSDOC,FMSTRA,FMSSEC,FMSMOD,"Y",FMSDES)
 QUIT
