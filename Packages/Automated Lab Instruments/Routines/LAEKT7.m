LAEKT7 ;SLC/RWF - KODAK EKTACHEM 700 ROUTINE ;7/20/90  08:11 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;**18,21**;Sep 27, 1994
 ;CROSS LINK BY ID OR IDE
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP"),(ID,IDE)=0 S:'$D(LALCT) LALCT="" Q:LALCT="N"
LA2 S TOUT=0 D IN G QUIT:TOUT,LA2:IN'["!" D QC,HDR:LATYPE="a",DATA:LATYPE="f",CALC:LATYPE="g",LA3:LATYPE="h" G LA2
HDR S V=$E(IN,30,44) D NUM S (ID,IDE)=+V,V=$E(IN,47,48) D NUM S CUP=+V,V=$E(IN,49,63) D NUM S TRAY=+V K TV Q
DATA S V=$E(IN,6,9) D NUM S NM=V,V=$E(IN,10,17) D NUM Q:$E(IN,26)>2&($E(IN,26)<7)!($E(IN,27)#2)
DA2 I LALCT="U" F I=0:0 S I=$O(^TMP("LA",$J,I)) Q:I<1  D
 .I ^TMP("LA",$J,I,4)=NM&($D(^TMP("LA",$J,I,2))) X ^(2)
 .I ^TMP("LA",$J,I,4)=NM,V<99999.99 S @^TMP("LA",$J,I,1)=V ;Param 3 has the report name.
 I LALCT'="U" F I=0:0 S I=$O(TC(I)) Q:I<1  D
 .I TC(I,4)=NM&($D(TC(I,2))) X TC(I,2)
 .I TC(I,4)=NM,V<99999.99 S @TC(I,1)=V
 Q
CALC S V=$E(IN,6,9) D NUM S NM=V,V=$E(IN,10,18) D NUM Q:$E(IN,27)>2  G DA2
LA3 Q:ID=""  X LAGEN
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 S ID="" K TV
 Q
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S IN="!"_$P(IN,"!",2)
 S LATYPE=$E(IN,5)
 Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) I (^LA(TSK,"I"))'=(^LA(TSK,"I",0)) S TOUT=0 L  G LA2
 H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J) L
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
