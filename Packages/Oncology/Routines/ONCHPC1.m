ONCHPC1 ;Hines OIFO/GWB - 2000 Hepatocellular Cancers PCE Study ;01/06/00
 ;;2.11;ONCOLOGY;**26**;Mar 07, 1995
 ;Patient Information 
 K TABLE,HTABLE
 S TABLE("1. FACILITY ID NUMBER (FIN)")="FIN"
 S TABLE("2. ACCESSION NUMBER")="ACC"
 S TABLE("3. SEQUENCE NUMBER")="SEQ"
 S TABLE("4. POSTAL CODE AT DIAGNOSIS")="PCD"
 S TABLE("5. PLACE OF BIRTH")="POB"
 S TABLE("6. DATE OF BIRTH")="DOB"
 S TABLE("7. RACE")="RACE"
 S TABLE("8. SPANISH ORIGIN")="SO"
 S TABLE("9. SEX")="SEX"
 S TABLE("10. PRIMARY PAYER AT DIAGNOSIS")="PPD"
 S HTABLE(1)=" 1. FACILITY ID NUMBER (FIN)"
 S HTABLE(2)=" 2. ACCESSION NUMBER"
 S HTABLE(3)=" 3. SEQUENCE NUMBER"
 S HTABLE(4)=" 4. POSTAL CODE AT DIAGNOSIS"
 S HTABLE(5)=" 5. PLACE OF BIRTH"
 S HTABLE(6)=" 6. DATE OF BIRTH"
 S HTABLE(7)=" 7. RACE"
 S HTABLE(8)=" 8. SPANISH ORIGIN"
 S HTABLE(9)=" 9. SEX"
 S HTABLE(10)="10. PRIMARY PAYER AT DIAGNOSIS"
 S CHOICES=10
 S D0=ONCOPA D DOB1^ONCOES S Y=X D DATEOT^ONCOPCE S DOB=Y
 S IE=ONCONUM
 W @IOF D HEAD^ONCHPC0
 W !," PATIENT INFORMATION"
 W !," -------------------"
FIN W !,"  1. FACILITY ID NUMBER (FIN)......: ",$$IIN^ONCFUNC
ACC W !,"  2. ACCESSION NUMBER..............: ",$$GET1^DIQ(165.5,IE,.05)
SEQ W !,"  3. SEQUENCE NUMBER...............: ",$$GET1^DIQ(165.5,IE,.06)
PCD S DIE="^ONCO(165.5,",DA=ONCONUM
 S DR="9  4. POSTAL CODE AT DIAGNOSIS......" D ^DIE G:$D(Y) JUMP
POB S DIE="^ONCO(160,",DA=ONCOPA
 S DR="7  5. PLACE OF BIRTH................" D ^DIE G:$D(Y) JUMP
DOB W !,"  6. DATE OF BIRTH.................: ",DOB
RACE S DIE="^ONCO(160,",DA=ONCOPA
 S DR="8  7. RACE.........................." D ^DIE G:$D(Y) JUMP
SO S DIE="^ONCO(160,",DA=ONCOPA
 S DR="9  8. SPANISH ORIGIN................" D ^DIE G:$D(Y) JUMP
SEX S DIE="^ONCO(160,",DA=ONCOPA
 S DR="10  9. SEX..........................." D ^DIE G:$D(Y) JUMP
PPD S DIE="^ONCO(165.5,",DA=ONCONUM
 S DR="18 10. PRIMARY PAYER AT DIAGNOSIS...." D ^DIE G:$D(Y) JUMP
 W ! K DIR S DIR(0)="E" D ^DIR S:$D(DIRUT) OUT="Y"
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!," GO TO ITEM NUMBER: ",X:DTIME
 I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S:X?1.2N X=X_"." S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K HTABLE,TABLE,CHOICES,DOB,IE
 K DIC,DR,DA,DIQ,DIE,DIR,DIROUT,DIRUT,DTOUT,DUOUT,X,Y
 Q
