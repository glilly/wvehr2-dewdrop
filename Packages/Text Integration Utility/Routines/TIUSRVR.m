TIUSRVR ; SLC/JER - Server fns for record manipulation ; 01/22/2002 16:18
 ;;1.0;TEXT INTEGRATION UTILITIES;**19,28,69,89,122,109,112,186**;Jun 20, 1997
SGET(TIUY,DA) ; Get fixed fields for record TIUDA
 N DOC,LOC,PT,AUT,EDT,DTFMT,TIUPT,TIULST4,TIUREC,TIUR0,TIUR12,TIUR13
 N SUBJ,TIUCNT
 S TIUR0=$G(^TIU(8925,+DA,0)),TIUR12=$G(^TIU(8925,+DA,12))
 S TIUR13=$G(^TIU(8925,+DA,13)),TIUPT=$G(^DPT(+$P(TIUR0,U,2),0))
 S SUBJ=$G(^TIU(8925,+DA,17))
 S DOC=$$PNAME^TIULC1(+TIUR0)
 S LOC=$P($G(^SC(+$P(TIUR12,U,5),0)),U)
 S PT=$$NAME^TIULS($P(TIUPT,U),"LAST, FIRST MI")
 S TIULST4=$E($P(TIUPT,U,9),6,9)
 S TIULST4="("_$E(PT)_TIULST4_")"
 S AUT=$$SIGNAME^TIULS(+$P(TIUR12,U,2))
 S DTFMT=$S($L(+TIUR13,".")=2:"MM/DD/YY HR:MIN",1:"MM/DD/YY")
 S EDT=$$DATE^TIULS(+TIUR13,DTFMT)
 S TIUCNT=+$G(TIUCNT)+1
 S TIUY=DOC_U_EDT_U_PT_" "_TIULST4_U_AUT_U_LOC_U_SUBJ
 Q
TGET(TIUY,TIUDA,TIUJ,TIUDAD) ; Get each component
 N TIUKID,TIUDADT,TIUI,TIUSEE S TIUI=0
 S TIUSEE=$$CANDO^TIULP(TIUDA,"VIEW")
 I '+TIUSEE D  Q
 . S TIUY(1)="  "
 . S TIUY(2)=$P(TIUSEE,"^ ",2),(TIUI,TIUJ)=2
 F  S TIUI=$O(^TIU(8925,+TIUDA,"TEXT",TIUI)) Q:+TIUI'>0  D
 . S TIUJ=+$G(TIUJ)+1
 . S TIUY(TIUJ)=$G(^TIU(8925,+TIUDA,"TEXT",TIUI,0))
 . ;S ^TMP("TIUTEXT",$J,TIUJ,0)=$G(^TIU(8925,+TIUDA,"TEXT",TIUI,0))
 ; Iterate through children, and get their text fields
 S TIUKID=0
 F  S TIUKID=$O(^TIU(8925,"DAD",+TIUDA,TIUKID)) Q:+TIUKID'>0  D
 . D TGET(.TIUY,TIUKID,.TIUJ,$G(TIUDAD,TIUDA))
 ;I $D(^TMP("TIUTEXT",$J)) S TIUY="^TMP(""TIUTEXT"",$J)"
 Q
GET4EDIT(TIUY,TIUDA,DR) ; Get data in preparation for editing a record
 N CANEDIT,ERR,D0,DIQ2,TIUARR,TIUF,TIUI
 I +$D(^TIU(8925,TIUDA,"TEMP")),'+$$IFTEXT() D MERGTEXT(TIUDA)
 K ^TMP("TIUEDIT",$J),^TMP("TIULQ",$J)
 S TIUY=$NA(^TMP("TIUEDIT",$J)),TIUARR=$NA(^TMP("TIULQ",$J))
 S CANEDIT=$$CANDO^TIULP(TIUDA,"EDIT RECORD")
 I +CANEDIT'>0 S ^TMP("TIUEDIT",$J,0)="~"_$P(CANEDIT,U,2) Q
 D EXTRACT^TIULQ(TIUDA,TIUARR,.ERR,$G(DR),"",1,"IE",1)
 I $D(ERR) M TIUY=ERR Q
 S TIUF=0
 F  S TIUF=$O(@TIUARR@(TIUDA,TIUF)) Q:+TIUF'>0  D
 . S ^TMP("TIUEDIT",$J,TIUF)=TIUF_U_@TIUARR@(TIUDA,TIUF,"I")_U_@TIUARR@(TIUDA,TIUF,"E")
 S TIUI=0
 F  S TIUI=$O(@TIUARR@(TIUDA,"TEXT",TIUI)) Q:+TIUI'>0  D
 . S ^TMP("TIUEDIT",$J,"TEXT",TIUI)=$G(@TIUARR@(TIUDA,"TEXT",TIUI,0))
 S ^TMP("TIUEDIT",$J,"TEXT",0)="$TXT",^TIU(8925,"ASAVE",DUZ,TIUDA)=""
 K @TIUARR
 Q
IFTEXT() ;
 N TIUCHK
 S TIUCHK=0 F  S TIUCHK=$O(^TIU(8925,TIUDA,"TEXT",TIUCHK)) Q:TIUCHK=""!TIUCHK>0
 Q TIUCHK
GETPREF(TIUY,USER) ; Get user's personal preferences
 ; Call with TIUY (by ref)
 ;           USER is pointer to file 200
 ; Returns   TIUY = USER ^ DEFAULT LOCATION ^ REVIEW SCREEN SORT FIELD ^
 ;               ==>REVIEW SCREEN SORT ORDER ^ DISPLAY MENUS ^ PATIENT
 ;               ==>SELECTION PREFERENCE ^ ASK 'Save changes?' AFTER
 ;               ==>EDIT ^ ASK SUBJECT FOR PROGRESS NOTES ^
 S TIUY=$$PERSPRF^TIULE(USER)
 Q
GETALRT(TIUY,XQAID) ;  Retrieve DFN and document type for a TIU alert
 N X,TIUDA,TIUDFN,ORTAB,TIUDAD,GMRCO
 S TIUDA=$TR($P(XQAID,";",1),"ABCDEFGHIJKLMNOPQRSTUVWXYZ") ; Strip Text
 I '+TIUDA!('$D(^TIU(8925,+TIUDA,0))) S TIUY="-1" Q
 S X=$P($G(^TIU(8925,TIUDA,0)),U)
 S TIUDFN=$P(^TIU(8925,TIUDA,0),U,2)
 I $P(^TIU(8925,TIUDA,0),U,6)'="" D
 . S TIUDAD=$P(^TIU(8925,TIUDA,0),U,6)
 . S X=$P($G(^TIU(8925,TIUDAD,0)),U)
 I ('+X)!('+TIUDFN) S TIUY="-1" Q
 S ORTAB=903    ;DEFAULT TO PN
 I +$$ISDS^TIULX(X) S ORTAB=901
 I +$$ISA^TIULX(X,$$CLASS^TIUSROI("SURGICAL REPORTS")) S ORTAB=904
 I +$$ISA^TIULX(X,$$CLASS^TIUCNSLT)!(+$$ISA^TIULX(X,+$$CLASS^TIUCP)) D
 . S GMRCO=$P(^TIU(8925,$S(+$G(TIUDAD):TIUDAD,1:TIUDA),14),U,5)
 . S ORTAB=902_";"_GMRCO
 S TIUY=TIUDA_U_TIUDFN_U_ORTAB
 Q
GET1405(TIUY,TIUDA) ; Get the Request (field #1405) for a document
 N TIUDAD,TIUTYP,TIU1405
 I '+TIUDA!('$D(^TIU(8925,+TIUDA,0))) S TIUY="-1^TIU Document does not exist" Q
 S TIUTYP=$P($G(^TIU(8925,TIUDA,0)),U)
 I $P(^TIU(8925,TIUDA,0),U,6)'="" D
 . S TIUDAD=$P(^TIU(8925,TIUDA,0),U,6)
 . S TIUTYP=$P($G(^TIU(8925,TIUDAD,0)),U)
 I '+TIUTYP S TIUY="-1^TIU parent document does not exist" Q
 S TIU1405=$P($G(^TIU(8925,$S(+$G(TIUDAD):TIUDAD,1:TIUDA),14)),U,5)
 I '+TIU1405 S TIUY="-1^No Request found for this document" Q
 S TIUY=TIU1405
 Q
MERGTEXT(TIUDA) ; Merge text from "TEMP"-node to "TEXT"-node
 N TIU
 D CLEANTXT(TIUDA)
 D GETTIU^TIULD(.TIU,TIUDA)
 D MERGTEXT^TIUEDI1(TIUDA,.TIU)
 K ^TIU(8925,TIUDA,"TEMP")
 Q
CLEANTXT(TIUDA) ; Remove "TEXT"-nodes of document and components
 N TIUI S TIUI=0
 K ^TIU(8925,TIUDA,"TEXT")
 F  S TIUI=$O(^TIU(8925,"DAD",TIUDA,TIUI)) Q:+TIUI'>0  D
 . I +$$ISADDNDM^TIULC1(TIUI) Q
 . K ^TIU(8925,TIUI,"TEXT")
 Q
