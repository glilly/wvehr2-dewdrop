LAPARAP ;SLC/RWF - PARALLEL - PRINTER PORT FORMAT ;7/20/90  09:57 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID OR IDE
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
 F I=0:0 S I=$O(TC(I)) Q:I<1  S TEST(TC(I,4))=I
LA2 K TV S (TOUT,LC)=0 D IN G QUIT:TOUT,LA2:$E(IN,5,8)'="CHEM" S LC=LC+1,TOUT=0 D IN G QUIT:TOUT S LC=LC+1
 F I=0:0 S TOUT=0 D IN G QUIT:TOUT S LC=LC+1 Q:$E(IN,1,5)="-----"  I $L(IN) S V=$E(IN,3,15) D NUM I $L(V),$D(TEST(V)) S II=V,V=$E(IN,24,29) D NUM I V S @TC(TEST(II),1)=V
 G LA2:LC'=48 F I=0:0 S TOUT=0 D IN G QUIT:TOUT Q:$E(IN,7,12)="Number"
 S V=$E(IN,15,25) D NUM S IDE=+V,TOUT=0 D IN G QUIT:TOUT S V=$E(IN,57,62) D NUM S (ID,CUP)=+V
LA3 X LAGEN G LA2:'ISQN
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
