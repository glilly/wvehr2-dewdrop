VAQDIS33 ;ALB/JFP - BUILDS DISPLAY ARRAY FOR (MAS DATA);3JUL91
 ;;1.5;PATIENT DATA EXCHANGE;;NOV 17, 1993
SCR11 ;SCREEN 11 OF MAS
R0 ; -- HEADER
 D BLANK^VAQDIS20
 S VAQLN=$$REPEAT^VAQUTL1(" ",79)
 S VAQCTR=" -- ELIGIBILITY VERIFICATION DATA -- "
 S X=$$CENTER^VAQDIS20(VAQLN,VAQCTR)
 D TMP,BLANK^VAQDIS20
 K VAQLN,VAQCTR
R1 ;
 S X=$$SETSTR^VALM1("ELIGIBILITY STATUS: "_$G(@XTRCT@("VALUE",2,.3611,0)),"",4,48)
 S Y=$G(@XTRCT@("VALUE",2,.3612,0))
 S VAQINF=$S(Y="":"UNANSWERED",1:Y)
 S X=$$SETSTR^VALM1("Status Date: "_VAQINF,X,49,30)
 D TMP
R2 ;
 S X=$$SETSTR^VALM1("Status Entered by: "_$G(@XTRCT@("VALUE",2,.3616,0)),"",5,74)
 D TMP
R3 ;
 S Y=$G(@XTRCT@("VALUE",2,.3614,0))
 S VAQINF=$S(Y="":"UNANSWERED",1:Y)
 S X=$$SETSTR^VALM1("Interim Response: "_VAQINF,"",6,73)
 D TMP
R4 ;
 S X=$$SETSTR^VALM1("Verif. Method: "_$G(@XTRCT@("VALUE",2,.3615,0)),"",9,70)
 D TMP
R5 ;
 S Y=$G(@XTRCT@("VALUE",2,.306,0))
 S VAQINF=$S(Y="":"UNANSWERED",1:Y)
 S X=$$SETSTR^VALM1("Money Verified: "_VAQINF,"",8,71)
 D TMP
R6 ;
 S Y=$G(@XTRCT@("VALUE",2,.322,0))
 S VAQINF=$S(Y="":"NOT VERIFIED",1:Y)
 S X=$$SETSTR^VALM1("Service Verified: "_VAQINF,"",6,73)
 D TMP
R7 ;
 S SEQ=""
 F J=1:1  S SEQ=$O(@XTRCT@("VALUE",2.04,.01,SEQ))  Q:SEQ=""  D
 .S VAQTMP1=$G(@XTRCT@("VALUE",2.04,.01,SEQ))
 .S VAQTMP2=$G(@XTRCT@("VALUE",2.04,2,SEQ))
 .S VAQTMP3=$G(@XTRCT@("VALUE",2.04,3,SEQ))
 .S VAQTMP4=$S(VAQTMP3="YES":"SC",1:"NSC")
 .S VAQINF=VAQTMP1_" ("_VAQTMP2_"%-"_VAQTMP4_")"
 .S:J=1 X=$$SETSTR^VALM1("Rated Disabilities: "_VAQINF,"",4,75)
 .S:J'=1 X=$$SETSTR^VALM1(VAQINF,"",24,55)
 .D TMP
 I J=1 S X=$$SETSTR^VALM1("Rated Disability: NONE","",24,19) D TMP
 K VAQTMP1,VAQTMP2,VAQTMP3,VAQTMP4,SEQ,J
 ;
EXIT K VAQINF,VAQTMP,Y,X
 QUIT
 ;
TMP ; -- Sets up display array
 S VALMCNT=VALMCNT+1
 S @ROOT@(VALMCNT,0)=$E(X,1,79)
 QUIT
 ;
END ;End of Code
 QUIT
