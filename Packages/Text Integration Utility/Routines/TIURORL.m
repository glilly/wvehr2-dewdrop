TIURORL ; SLC/JER - List Management Library for OE screen ;4/17/00
 ;;1.0;TEXT INTEGRATION UTILITIES;**100**;Jun 20, 1997
UPDATE(ITEM) ; Updates list following actions
 N AUT,RDT,TITL,TIUD0,TIUD12,TIUD13,TIUDA,IDKIDFLG
 N STAT,TIUR ;10/2/00
 S TIUDA=+$P(ITEM,U,2)
 S TIUD0=$G(^TIU(8925,+TIUDA,0)),TIUD12=$G(^(12)),TIUD13=$G(^(13))
 S TITL=$$PNAME^TIULC1(+TIUD0),IDKIDFLG=1
 I +$O(^TIU(8925,"DAD",+TIUDA,0)),+$$HASADDEN^TIULC1(TIUDA,IDKIDFLG) S TITL="+ "_TITL ;**100**
 S AUT=$$NAME^TIULS($$PERSNAME^TIULC1(+$P(TIUD12,U,2)),"LAST,FI")
 S RDT=$$DATE^TIULS(+TIUD13,"MM/DD/YY HR:MIN")
 S STAT=$$LOW^XLFSTR($P($G(^TIU(8925.6,+$P(TIUD0,U,5),0)),U))
 S TIUR=$$SETFLD^VALM1(VALMCNT,$G(TIUR),"NUMBER")
 S TIUR=$$SETFLD^VALM1(TITL,$G(TIUR),"TITLE")
 S TIUR=$$SETFLD^VALM1(AUT,$G(TIUR),"AUTHOR")
 S TIUR=$$SETFLD^VALM1(RDT,$G(TIUR),"REF DATE")
 S TIUR=$$SETFLD^VALM1(STAT,$G(TIUR),"STATUS")
 S ^TMP("TIUR",$J,VALMCNT,0)=TIUR
 D RESTORE^VALM10(+ITEM),CNTRL^VALM10(+ITEM,1,VALM("RM"),IOINHI,IOINORM)
 Q
