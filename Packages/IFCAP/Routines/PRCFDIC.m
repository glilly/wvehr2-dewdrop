PRCFDIC ;WISC/LEM-LOOK UP INVOICES BY P.O. OR VENDOR ;8/18/94  14:20
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
PO ; LOOK UP INVOICES BY P.O.
 K DA,DR,X,Y
 ;S PRCF("PO")=$G(X) Q:X=""
PO1 S X=$G(PRCF("PO")) Q:X=""
 N MULT,VIEW S MULT=0,I="" I $D(^PRCF(421.5,"D",X)) D
 . F  S I=$O(^PRCF(421.5,"D",X,I)) Q:I=""  S MULT=MULT+1 Q:MULT>1
 . Q
 N DIC,D S DIC="^PRCF(421.5,",DIC(0)="EZ",D="D"
 S X=$P(X,"-",1,2)
 D IX^DIC Q:Y<0  S VIEW=+Y
 I VIEW=$G(PRCF("CIDA")) S X="  ("_+Y_" - THIS Invoice.)*" D MSG^PRCFQ
 I VIEW D VIEW G PO1:MULT>1
 Q
VENDOR ; LOOK UP INVOICES BY VENDOR
 S X=$G(PRCF("VENDA")) Q:X=""
 N MULT,VIEW S MULT=0,I="" I $D(^PRCF(421.5,"C",X)) D
 . F  S I=$O(^PRCF(421.5,"C",X,I)) Q:I=""  S MULT=MULT+1 Q:MULT>1
 . Q
 N DIC S DIC="^PRCF(421.5,",DIC(0)="EZ",D="C"
 D IX^DIC Q:Y<0  S VIEW=+Y
 I VIEW=$G(PRCF("CIDA")) S X="  ("_+Y_" - THIS Invoice.)*" D MSG^PRCFQ
 I VIEW D VIEW G VENDOR:MULT>1
 Q
VIEW ;VIEW INDIVIDUAL CERTIFIED INVOICE
 S (FR,TO)=$P(Y,"^",2),L=0,BY="@.01;",FLDS="[CAPTIONED]",IOP="HOME"
 D WAIT^PRCFYN,EN1^DIP
OUTV K DIC,DA,DR,X,Y
 Q
DUP ; Look for Duplicate Invoice(s)
 K PRCF("DUP") S PRCF("DUP")=0 Q:'$G(PRCF("CIDA"))
 Q:'$G(PRCF("VENDA"))  Q:'$D(^PRCF(421.5,"C",PRCF("VENDA")))
 S PRCF("INVNO")=$P($G(^PRCF(421.5,PRCF("CIDA"),0)),U,3)
 Q:PRCF("INVNO")=""
 N X S X="Checking for duplicate invoices . . .*" D MSG^PRCFQ
 N I S I="" F  S I=$O(^PRCF(421.5,"C",PRCF("VENDA"),I)) Q:I=""  D
 . Q:I=PRCF("CIDA")
 . I PRCF("INVNO")=$P($G(^PRCF(421.5,I,0)),U,3) D
 . . S PRCF("DUP")=PRCF("DUP")+1
 . . N CIDNO S CIDNO=$P($G(^PRCF(421.5,I,0)),U,1)
 . . S PRCF("DUP",CIDNO)=""
 . . Q
 . Q
 I PRCF("DUP")=0 N X S X="none found.*" D MSG^PRCFQ Q
 S X="WARNING!  Identical invoices numbers for this vendor were found in the following Tracking ID#s:*"
 D MSG^PRCFQ S I="" F  S I=$O(PRCF("DUP",I)) Q:I=""  W !?10,I
 W !! S X="Please review these records and check for duplicate invoices.*"
 D MSG^PRCFQ
 Q
PPT ; Load Prompt Payment Terms from File 442
 Q:'$G(PRCF("CIDA"))  Q:'$G(PRCF("PODA"))
 Q:$D(^PRCF(421.5,PRCF("CIDA"),6))  Q:'$D(^PRC(442,PRCF("PODA"),5,1,0))
 N PPT S PPT=$G(^PRC(442,PRCF("PODA"),5,1,0))
 N PCT,DAYS S PCT=$P(PPT,U,1),DAYS=$P(PPT,U,2)
 S ^PRCF(421.5,PRCF("CIDA"),6,0)="^421.531A^1^1"
 S ^PRCF(421.5,PRCF("CIDA"),6,1,0)="1^^"_PCT_"^^"_DAYS
 S ^PRCF(421.5,PRCF("CIDA"),6,"B",1,1)=""
 Q
INPUT N X0 S X0=$TR(X,"net","NET")
 I X]"",$E("NET",1,$L(X0))=X0 S X=0 Q
 ; Native FileMan Input Transform follows:
 K:+X'=X!(X>99.999)!(X<0)!(X?.E1"."4N.N) X
 Q
OUTPUT I Y?1"0"."."."0" S Y="NET"
 Q
 N DA S DA(1)=$G(PRCF("CIDA")) Q:DA(1)=""
 N NODE S NODE=$G(^PRCF(421.5,DA(1),5,0))
 I NODE="" S ^PRCF(421.5,DA(1),5,0)=U_$P(^DD(421.5,41,0),U,2)
 N CTR,I S (CTR,I)=0 F  S I=$O(PRCFD(I)) Q:I'>0  D
 . S CTR=$S(I=991:CTR,1:CTR+1),CTR=$S(CTR=991:992,1:CTR)
 . N DIC S DIC="^PRCF(421.5,"_DA(1)_",5,",DIC(0)="L"
 . S X=$P(PRCFD(I),U,1),AMT=+$P(PRCFD(I),U,2)
 . K DD,DO D FILE^DICN I Y'>0 W "ERROR" Q
 . N DIE S DIE=DIC,DA=+Y,FMSL=$S(I=991:991,1:CTR)
 . N DR S DR="1////^S X=AMT;2////^S X=FMSL" D ^DIE
 . Q
 Q
DISC ; COMPUTE FMS LINE LIQ AMT FROM TOTAL AMT & DISCOUNT TERMS
 ; INPUT: PRCF("CIDA") - IEN FOR PAYMENT/INVOICE TRACKING RECORD
 ;        PRCFA("LAMT") - FMS LINE AMOUNT FOR THIS INVOICE
 ; OUTPUT: PRCFA("LIQ") - FMS COMPUTED LIQUIDATION AMOUNT
 Q:'$D(PRCF("CIDA"))!'$D(PRCFA("LAMT"))
 N I,DISC,HIGHDISC S (HIGHDISC,I)=0
 F  S I=$O(^PRCF(421.5,PRCF("CIDA"),6,I)) Q:+I'=I  D
 . S DISC=+$P($G(^PRCF(421.5,PRCF("CIDA"),6,I,0)),U,3)
 . I DISC>HIGHDISC S HIGHDISC=DISC
 . Q
 S PRCFA("LIQ")=$FN(1-(HIGHDISC/100)*PRCFA("LAMT"),"",2)
 Q
SUM ;
 ; INPUT: PRCF("CIDA") - IEN FOR PAYMENT/INVOICE TRACKING RECORD
 ;        PRCFA("CAMT") - TOTAL INVOICE AMOUNT CERTIFIED FOR PAYMENT
 ; OUTPUT: OK - 1 IF SUM OF LINE AMOUNTS = TOTAL AMOUNT CERTIFIED
 ;            - 0 IF AMOUNTS NOT EQUAL
 Q:'$D(PRCF("CIDA"))!'$D(PRCF("CAMT"))
 N I,LAMT S (I,OK,PRCF("TAMT"))=0
 F  S I=$O(^PRCF(421.5,PRCF("CIDA"),5,I)) Q:+I'=I  D
 . S LAMT=+$P($G(^PRCF(421.5,PRCF("CIDA"),5,I,0)),U,2)
 . S PRCF("TAMT")=PRCF("TAMT")+LAMT
 . Q
 I PRCF("CAMT")/100=PRCF("TAMT") S OK=1
 Q
SCREEN ; CHECK BOC
 I $G(X) I $D(PRCFX("SA",X))
 Q
LOOKUP(X,PARTIAL) ; X = STA-PAT # - LOOKUP returns next available PARTIAL #.
 N DIC S DIC="^PRCF(421.9,",DIC(0)="O" K DD,DO D ^DIC
 I Y<0 D FILE^DICN
 I +Y,$P(Y,U,3)=1 S PARTIAL="01",$P(^PRCF(421.9,+Y,0),U,2)="01" Q
 S P=$P($G(^PRCF(421.9,+Y,0)),U,2),P=P+1
 S P="00"_P,P=$E(P,$L(P)-1,$L(P))
 S PARTIAL=P,$P(^PRCF(421.9,+Y,0),U,2)=P
 Q
