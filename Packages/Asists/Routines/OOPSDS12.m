OOPSDS12 ;WIOFO/CAH-CA1/CA2 DISPLAY ;11/24/99
 ;;2.0;ASISTS;;Jun 03, 2002
EN1(CALLER) ;
 I CALLER="S" S DIC("S")="I $$GET1^DIQ(2260,Y,53,""I"")=DUZ!($$GET1^DIQ(2260,Y,53.1,""I"")=DUZ)"
 I CALLER="E" S DIC("S")="I $$GET1^DIQ(2260,Y,5,""I"")=$$GET1^DIQ(200,DUZ,9,""I"")"
 S DIC="^OOPS(2260,",DIC(0)="AEQMZ" D ^DIC Q:Y=-1
 S IEN2260=+Y
 S INIL=$$GET1^DIQ(2260,IEN2260,52,"I")
 K IOP,%ZIS
 S %ZIS="MQ" W ! D ^%ZIS K %ZIS,IOP G:POP EXIT
 I $D(IO("Q")) D  G EXIT
 .S ZTRTN="PRINT^OOPSDS12"
 .S ZTDESC="PRINT CA FORM"
 .S ZTSAVE("IEN2260")="",ZTSAVE("CALLER")=""
 .S ZTSAVE("INIL")=""
 .D ^%ZTLOAD,HOME^%ZIS
 .Q
PRINT ;
 D:INIL=1 CA1^OOPSCA1(IEN2260,CALLER)
 D:INIL=2 CA2^OOPSCA2(IEN2260,CALLER)
EXIT K CALLER,DIC,IEN2260,INIL,Y
 D ^%ZISC
 Q
