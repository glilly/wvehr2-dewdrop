RMPR29D ;PHX/JLT-DISPLAY 2529-3 INFO ;8/29/1994 [ 09/29/94  10:05 AM ]
 ;;3.0;PROSTHETICS;**12,41,77**;Feb 09, 1996
 ;RVD 3/18/03 patch #77 - prevent error in word processing field. 
DISP ;GET AND DISPLAY 2529-3 INFO
 ;CALLED BY:RMPR29,RMPR29A,RMPR29B,RMPR29C,RMPR29J,RMPR29P,RMPR29S,RMPR29T
 ;VARIABLES REQUIRED: RMPRDA - ENTRY IN FILE 664.
 D HOME^%ZIS K ^UTILITY("DIQ1",$J),HLD
 Q:$G(RMPRDA)'>0  S DIC="^RMPR(664.1,",DA=RMPRDA,DR=".02;.11;.04;.09;2;4;13;15;19"
 D EN^DIQ1 K DIQ,DR S PAGE=1
 S DA=RMPRDA
 F RI=0:0 S RI=$O(^RMPR(664.1,DA,2,RI)) Q:RI'>0  I $D(^(RI,0)) D
 .S DIC="^RMPR(664.1,",DR="6"
 .S DR(664.16)=".01;2;3;8;9;10;7;12;13;13.1",DA(664.16)=RI
 .S HLD(RI)=$$ITM1^RMPR31U($P(^RMPR(664.1,DA,2,RI,0),U))
 .D EN^DIQ1 K DIQ,DR
 I '$D(PNK) D HD^RMPR29W G ITD
 I $D(PNK) D HDC^RMPR29W G ITD
 G ASK^RMPR29T
ITD ;ITEM DISPLAY
 ;CALLED BY RMPR29T
 ;VARIABLES REQUIRED: HLD - ARRAY OF ITEMS
 ;                    RI  - ITEM NUMBER
 G:($Y+8>IOSL)!('$D(HLD)) ASK^RMPR29T
 S RI=$O(HLD(0))
 W !,HLD(RI),?10,$E(^UTILITY("DIQ1",$J,664.16,RI,.01),1,15)
 W ?27,$E(^UTILITY("DIQ1",$J,664.16,RI,12),1,15),?45,^(2),?50,^(3),?55,^(8),?65,^(9)
 ;HCPCS Display
 W !,?10,"HCPCS: ",^UTILITY("DIQ1",$J,664.16,RI,13)
 W !,?10,"CPT MODIFIER: ",^UTILITY("DIQ1",$J,664.16,RI,13.1)
WP ;WORD PROCESSING FIELD DISPLAY
 G:($Y+8>IOSL) ASK^RMPR29T S RWP=$O(^UTILITY("DIQ1",$J,664.16,RI,7,0))
 I RWP'>0 K HLD(RI) K D0 D ADC^RMPR293(RMPRDA,RI) W ! G ITD
 S X=$G(^UTILITY("DIQ1",$J,664.16,RI,7,RWP))
 K ^UTILITY("DIQ1",$J,664.16,RI,7,RWP)
 K ^UTILITY($J) S DIWL=1,DIWR=60,DIWF="R" D ^DIWP
EXT ;COMMON EXIT POINT
 ;CALLED BY RMPR29T
 ;VARIABLES REQUIRED: - UTILITY GLOBAL CONTAINING INFO TO PRINT AND KILL.
 G:($Y+8>IOSL)!'$D(DIWL) ASK^RMPR29T
 S RL=$O(^UTILITY($J,"W",DIWL,0)) I +RL W !,?10,^(RL,0) K ^(0) G EXT
 K ^UTILITY($J) G WP
CHK ;CHECK DISABILITY AND ITEMS
 ;kill record if not all mandatory fields defined
 ;CALLED BY RMPR29T,RMPR29
 ;VARIABLES REQUIRED: RMPRDA - ENTRY NUMBER IN FILE 664.1
 K RKILL
 F RCK=1,2,3,4,11,15 I $P(^RMPR(664.1,RMPRDA,0),U,RCK)="" S RKILL=1 S DA=RMPRDA,DIK="^RMPR(664.1," D ^DIK W !!,?5,$C(7),"ALL MANDATORY FIELDS NOT DEFINED FORM 2529-3 DELETED" Q
 I $D(RKILL) G EXIT^RMPR29
 ;disability code missing
 K DKILL
 I '$D(^RMPR(664.1,RMPRDA,1))!('$O(^RMPR(664.1,RMPRDA,1,0))) S DKILL=1
 F RI=0:0 S RI=$O(^RMPR(664.1,RMPRDA,1,RI)) Q:RI'>0  I $P(^(RI,0),U,1)=""!($P(^(0),U,2)="") S DKILL=1
 ;item missing
 K IKILL
 I '$D(^RMPR(664.1,RMPRDA,2))!('$O(^RMPR(664.1,RMPRDA,2,0))) S IKILL=1
 F RI=0:0 S RI=$O(^RMPR(664.1,RMPRDA,2,RI)) Q:RI'>0  I $P(^(RI,0),U,1)=""!($P(^(0),U,2)="")!($P(^(0),U,3)="")!($P(^(0),U,7)="")!($P(^(0),U,8)="") S IKILL=1
ER1 ;error message
 I $D(DKILL) W $C(7),!!,?5,"2529-3 FORM INCOMPLETE.  DISABILITY CODE INFORMATION IS MISSING!!"
 I $D(IKILL) W $C(7),!!,?5,"2529-3 FORM INCOMPLETE.  ITEM INFORMATION IS MISSING!!"
 I $D(IKILL)!($D(DKILL)) G DEL^RMPR29
 ;see internal notes
 K DA,DIC,DIK,DIWF,DIWL,DIWR,PAGE,PNK,RCK,RI,RL,RWP,X
 G LAB^RMPR29
