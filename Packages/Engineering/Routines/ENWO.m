ENWO ;(WASH ISC)/DLM/DH-Work Order Driver ;5-28-93
V ;;7.0;ENGINEERING;;Aug 17, 1993
 ;OVERLAYS EN
 D SSHOP Q:ENSHKEY<1
 D:'$D(DT) DT^DICRW S U="^",S=";",O=$T(OPT) I $D(^DOPT($P(O,S,5),"VERSION")),($P($T(V),S,3)=^DOPT($P(O,S,5),"VERSION")) G IN
 K ^DOPT($P(O,S,5))
 F I=1:1 Q:$T(OPT+I)=""  S ^DOPT($P(O,S,5),I,0)=$P($T(OPT+I),S,3),^DOPT($P(O,S,5),"B",$P($P($T(OPT+I),S,3),U,1),I)=""
 S K=I-1,^DOPT($P(O,S,5),0)=$P(O,S,4)_U_1_U_K_U_K K I,K,X S ^DOPT($P(O,S,5),"VERSION")=$P($T(V),S,3)
IN I $P(O,S,6)'="" D @($P(O,S,6))
PR S O=$T(OPT),S=";" S IOP="HOME" D ^%ZIS W:IOST'["PK-" @IOF K IOP
 D HDR F J=1:1 Q:'$D(^DOPT($P(O,S,5),J,0))  W !,?15,J,". ",$P(^DOPT($P(O,S,5),J,0),U,1)
RE W ! S DIC("A")="Select "_$P($T(OPT),S,4)_": EXIT// ",DIC="^DOPT("_""""_$P($T(OPT),S,5)_""""_",",DIC(0)="AEQMN" D ^DIC K:X=""!(X=U) ENSHKEY G:X=""!(X=U) EXIT G:Y<0 RE K DIC,J,O D @($P($T(OPT+Y),S,4)) G PR
HDR W @IOF,!!,?17,"ENGINEERING WORK ORDER MODULE",! S X=$P(^DIC(6910,1,0),U) W ?((65-$L(X))/2)\1,X,!! K X Q
SSHOP ;SEL SHOP
 I $D(ENSHKEY),ENSHKEY>0 S DIC("B")=$P(^DIC(6922,ENSHKEY,0),U)
 K DIC("S") S DIC="^DIC(6922,",DIC(0)="AEQM" D ^DIC S ENSHKEY=+Y K DIC
 I '$D(ENLO) D ZIS^EN
 Q
SHOP S DIC("S")="I $D(^(2)),$P(^(2),U,1)=ENSHKEY" Q
EXIT K %IS,DIC,DIE,DA,Y,DR,N,I,J,K,O,S,X,Y,Z
 S:$D(ZTQUEUED) ZTREQ="@"
 Q
 ;
OPT ;;WORK ORDER OPTION;WORK ORDER OPTION;ENWO;
 ;;ENTER NEW WORK ORDER;^ENWONEW
 ;;EDIT WORK ORDER DATA;ENT^ENWO1
 ;;CLOSE OUT WORK ORDER;CLSOUT^ENWO1
 ;;INCOMPLETE WORK ORDER STATUS;^ENWOST
 ;;DISPLAY WORK ORDER;EDIT^ENWOD
 ;;PRINT EQUIPMENT HISTORY BY ENTRY NUMBER;EQHI^ENWO1
 ;;TRANSFER WORK ORDER TO NEW SHOP;TRANS^ENWONEW1
 ;;DISAPPROVE WORK ORDER;DISAP^ENWO2
 ;;REPRINT WORK ORDERS;^ENWOREP
