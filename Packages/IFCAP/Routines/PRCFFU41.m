PRCFFU41 ;WISC/SJG-FMS DOCUMENT GENERATOR (CONT) ;3/7/95  3:32 PM
V ;;5.1;IFCAP;;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
EN4 ;ENTER DATA INTO STATUS OF FUNDS FILE
 K DIC("A") S DIC="^PRC(420,",DR="[PRCB STATUS FUNDS]",DIC(0)="AEMNQ" D ^DIC K DIC("A") I Y>0 S DIE=DIC,DA=+Y D ^DIE
 K %,%X,%Y,D,D0,D1,DA,DD,DIC,DIE,DIX,DO,DQ,DR,DZ,J,K,X,Y Q
EN5 ;UPDATE ESTIMATED BALANCE FIELD OF CONTROL POINT FILE
 ;REQUIRES VARIABLE PRC("SITE")
 I '$D(PRC("SITE")) S PRCF("X")="AS" D ^PRCFSITE G:'% OUT5
 K DIC("A") W !,$C(7),"REMEMBER, DO NOT ENTER TRANSACTION FOR FUTURE QUARTERS!",!
X S DIC="^PRC(420,"_PRC("SITE")_",1,",DIC(0)="AEQMNZ" D ^DIC K DIC G:'$T!(X[U) OUT5 I Y<0 W $C(7),!!,"I'M CONFUSED ABOUT WHICH CONTROL POINT YOU WANT, TRY AGAIN. ",!,"USE AN '^' TO QUIT",! G X
 S PRC("CP")=+Y,PRC("CP",0)=Y(0)
EN51 W !,"ENTER TRANSACTION AMOUNT: " R X:$S($D(DTIME):DTIME,1:60) Q:X="^"  I X'?.1"+".1"-".N1"."2N W !,"ENTER AMOUNT OF TRANSACTION, INCLUDING THE DECIMAL POINT",! G EN51
 I X<0 S X=-(X)
 I X'?.N.1".".2N W $C(7),"??" G EN51
 S X1=X
R W !,"(I)ncrease or (D)ecrease to balance? D//" R X:$S($D(DTIME):DTIME,1:300) G:'$T!(X["^") OUT5
 S:X="" X="D" I X["?"!(X'["D"&(X'["I")) W !!,"Enter a <CR> or 'D' to DECREASE the balance in the status, an 'I' to INCREASE",!,"the balance, or an '^' to ABORT the option." G R
 I X["D" S X1=-(X1)
 W !,"THE OLD ESTIMATED BALANCE IS $",$J($P(PRC("CP",0),U,8),0,2) K PRCFX S PRCFX=$P(PRC("CP",0),U,8)+X1
 W !,"THE NEW ESTIMATED BALANCE IS $",$J(PRCFX,0,2),!!
 S %A="OK TO POST",%B="A 'NO' or '^' will prevent posting action from occurring." S %=1 D ^PRCFYN
 I %=1 S $P(^PRC(420,PRC("SITE"),1,PRC("CP"),0),U,8)=PRCFX W !,"POSTED",!!
 E  W !,"NO ACTION TAKEN! " S %A=" DO YOU WISH TO RE-ENTER DATA",%B="" S %=1 D ^PRCFYN G:%=1 X G OUT5
 S DIC("A")="Select Next Control Point Name: " G X
OUT5 K %,DIC,I,J,K,PRCFX,X,X1,Y,Z Q
EN7(XA,XB,XC,XD) ; Post FMS Document information to Purchase Order
 ; XA - Transaction Type, eg MO,SO
 ; XB - Document Action, eg E,M,X
 ; XC - Obligation Processing Date
 ; XD - PAT Number (w/o Station), eg A51234
EN7A Q:'$D(GECSFMS("DOC"))  Q:GECSFMS("DOC")=""  Q:PRCFA("SYS")'="FMS"
 I '$D(PRCFA("PODA")) Q
 S PO=PRCFA("PODA") I '$D(^PRC(442,+PO,0)) Q
 S PO(0)=^PRC(442,+PO,0) D NOW^%DTC K %H,%I S (DATE,Y)=% D DD^%DT K PRCFA("CK") I '$D(^PRC(442,+PO,10,0)) S ^PRC(442,+PO,10,0)="^442.09A^0^0"
 K DD,DO N GETNUM S GETNUM=$P(^PRC(442,+PO,10,0),U,3)
 F  S GETNUM=GETNUM+1 Q:$G(^PRC(442,+PO,10,GETNUM,0))=""
 S DINUM=GETNUM,DIC(0)="MNL",DLAYGO=442,DIC="^PRC(442,"_+PO_",10,"
EN7B S:XB=0 XB="E" S:XB=1 XB="M"
 S XC=$$DATE1^PRCFFU2(XC),X=XA_"."_XB_"."_XC_"."_XD_"."_Y,X=""_X_"" D FILE^DICN K DINUM,DLAYGO,DD,DO Q:Y<1
 S MESSAGE=""
EN7C I +PO>0 D
 .N PRCCSID S PRCCSID=$P(GECSFMS("DOC"),U,3)_"-"_$P(GECSFMS("DOC"),U,4)
 .I $D(GECSFMS("BAT")) S PRCCSID=PRCCSID_"-"_$P(GECSFMS("BAT"),U,3)
 .S ^PRC(442,+PO,10,+Y,0)=$P(^PRC(442,+PO,10,+Y,0),U,1)_U_+PRC("PER")_U_U_PRCCSID,PRCFA("PODA")=+PO D:$D(POESIG) ENCODE^PRCHES4(+PO,+Y,+PRC("PER"),.MESSAGE)
 .S $P(^PRC(442,+PO,10,+Y,0),U,9)=GECSFMS("DA")
 .I $D(PRCFA("TT")) I PRCFA("TT")="SO"!(PRCFA("TT")="AR"),PRCFA("MP")=21,$P(TRNODE(0),U,2)="A" S $P(^PRC(442,+PO,10,+Y,0),U,11)=TRDA
 .I $D(PRCFA("TT")) I PRCFA("TT")="SO"!(PRCFA("TT")="AR"),PRCFA("MP")=2,$D(PRCFA("AMEND#")) S $P(^PRC(442,+PO,10,+Y,0),U,10)=PRCFA("AMEND#")
 .I $D(PRCFA("TT")) I PRCFA("TT")="MO",$D(PRCFA("AMEND#")) S $P(^PRC(442,+PO,10,+Y,0),U,10)=PRCFA("AMEND#")
EN7D I $D(PRCFA("OBLDATE")) S $P(^PRC(442,+PO,10,+Y,0),U,12)=PRCFA("OBLDATE")
 I $D(PRCFA("ACCPD")) S $P(^PRC(442,+PO,10,+Y,0),U,13)=$P(PRCFA("ACCPD"),U,3)
 K MESSAGE,POESIG,DATE
 Q
EN71 ;MARK PO AS OBLIGATED
 ;S PTYPE=+$P(^PRC(442,PRCFA("PODA"),0),"^",2),PTYPE=$S($D(^PRCD(442.5,PTYPE,0)):$P(^(0),"^",4),1:"")
 ;I PTYPE'["Y" S $P(^PRC(442,PRCFA("PODA"),7),U,1)=$O(^PRCD(442.3,"AC",40,0)) K PTYPE Q
 ;S FSO=$P(^PRC(442,PRCFA("PODA"),7),U,1),FSO=$P(^PRCD(442.3,FSO,0),"^",3)+15,FSO=$O(^PRCD(442.3,"AC",FSO,0)),$P(^PRC(442,PRCFA("PODA"),7),"^",1)=FSO K FSO
 Q
EN72 ;MARK PO AS COMPLETE
 S FSO=+^PRC(442,PRCFA("PODA"),7),FSO=$P(^PRCD(442.3,FSO,0),"^",3)
 I FSO=35!(FSO=36),$D(PRCFA("LIQ")),"CF"[PRCFA("LIQ") S X=FSO+5,DA=PRCFA("PODA") D ENF^PRCHSTAT
 I $D(PRCFA("PARTIAL")) S $P(^PRC(442,PRCFA("PODA"),11,PRCFA("PARTIAL"),0),U,6)="Y"
 Q
EN73 G EN73^PRCFAC
EN731 G EN731^PRCFAC
EN732 G EN732^PRCFAC
