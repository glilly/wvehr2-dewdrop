SRORAT1 ;B'HAM ISC/MAM - CANCELLATION RATES, 1 SPECIALTY ; [ 04/06/00  10:19 AM ]
 ;;3.0; Surgery ;**14,63,94**;24 Jun 93
 S (PAGE,SRSOUT,SRCT)=0 F  S SRCT=$O(SRSS(SRCT)) Q:'SRCT!SRSOUT  S SRSS=SRSS(SRCT) D SPEC I $E(IOST)'="P" D PAGE Q:SRSOUT
 S SRSOUT=1 Q
SPEC K ^TMP("SR",$J),^TMP("SRT",$J) S SRHDR=0,^TMP("SR",$J)="0^0^0"
 S SRSDATE=SRSD-.0001,SREDT=SRED+.9999,SRSSNM="** "_$P(^SRO(137.45,SRSS,0),"^")_" **"
 F  S SRSDATE=$O(^SRF("AC",SRSDATE)) Q:'SRSDATE!(SRSDATE>SREDT)  S SRTN=0 F  S SRTN=$O(^SRF("AC",SRSDATE,SRTN)) Q:'SRTN  I $D(^SRF(SRTN,0)),$$MANDIV^SROUTL0(SRINSTP,SRTN) D UTIL
 D HDR Q:SRSOUT
 S X=^TMP("SR",$J),Y=$P(X,"^"),TOTAL=Y+$P(X,"^",3),X=$P(X,"^",2),CANRATE=$S(Y>0:X/Y*100,1:0),CANRATE=CANRATE+.5\1*1,TOTRATE=$S(TOTAL>0:X/TOTAL*100,1:0),TOTRATE=TOTRATE+.5\1*1
 S TCAN=$S(TOTAL>0:Y/TOTAL*100,1:0),TCAN=TCAN+.5\1*1,TPRCNT="CANCELLATION RATE FOR SCHEDULED CASES: "_TCAN_" %"
 S SPRCNT="AVOIDABLE CANCELLATION RATE FOR SCHEDULED CASES: "_TOTRATE_" %"
 S CPRCNT="AVOIDABLE CANCELLATION RATE FOR CANCELLED CASES: "_CANRATE_" %"
 S TOTAL="TOTAL SCHEDULED SURGICAL CASES: "_TOTAL
 S SRCOL=80-$L(SRSSNM)\2 W !!,?SRCOL,SRSSNM
 W !!,TOTAL,!,TPRCNT,!,SPRCNT,!,CPRCNT D SUB
 S REASON=0 F  S REASON=$O(^TMP("SR",$J,REASON)) Q:REASON=""!(SRSOUT)  D REASON
 S X=$P(^TMP("SR",$J),"^"),Y=$P(^($J),"^",2)
 W !,?47,"-----",?64,"-----",!,"TOTAL CANCELLATIONS",?47,$J(X,4),?64,$J(Y,4)
 Q
REASON I $Y+5>IOSL D PAGE Q:SRSOUT  D SUB
 S X=$P(^TMP("SR",$J,REASON),"^"),Y=$P(^(REASON),"^",2)
 W !,$S($E(REASON,1,2)="ZZ":$E(REASON,3,45),1:REASON),?47,$J(X,4),?64,$J(Y,4)
 Q
UTIL ; set ^TMP
 Q:$P($G(^SRF(SRTN,"NON")),"^")="Y"  S STORE=0,TYPE=$P(^SRF(SRTN,0),"^",10) Q:TYPE="EM"  I $P($G(^SRF(SRTN,30)),"^")=""&($P($G(^(31)),"^",4)="")&($P($G(^(31)),"^",5)="") Q
 S X=$P(^SRF(SRTN,0),"^",4) I X'=SRSS Q
 I $P($G(^SRF(SRTN,30)),"^") S STORE=2
 I $P($G(^SRF(SRTN,31)),"^",8) S STORE=2
 I $P($G(^SRF(SRTN,.2)),"^",12) S STORE=1,AVOID=""
 I 'STORE Q
 I STORE=2 S AVOID=0,X=$S($D(^SRF(SRTN,31)):$P(^(31),"^",8),1:""),REASON=$S(X:$P(^SRO(135,X,0),"^")_"^"_$P($G(^SRF(SRTN,30)),"^",2),1:"ZZNO REASON ENTERED"_"^"_"A") S AVOID=$S($P(REASON,"^",2)="N":0,1:1),REASON=$P(REASON,"^")
 I STORE=2,'$D(^TMP("SR",$J,REASON)) S ^TMP("SR",$J,REASON)="0^0"
 I STORE=1 S $P(^TMP("SR",$J),"^",3)=$P(^TMP("SR",$J),"^",3)+1 Q
 S $P(^TMP("SR",$J),"^")=$P(^TMP("SR",$J),"^")+1 I STORE=2 S $P(^TMP("SR",$J,REASON),"^")=$P(^TMP("SR",$J,REASON),"^")+1
 I 'AVOID Q
 S $P(^TMP("SR",$J),"^",2)=$P(^TMP("SR",$J),"^",2)+1,$P(^TMP("SR",$J,REASON),"^",2)=$P(^TMP("SR",$J,REASON),"^",2)+1
 Q
PAGE S X="" I $E(IOST)'="P" W !!!,"Press RETURN to continue, or '^' to quit:  " R X:DTIME I '$T!(X["^") S SRSOUT=1 Q
 I X["?" W !!,"Enter RETURN to continue printing this report, or '^' to exit from this option." G PAGE
HDR ; print heading
 I $D(ZTQUEUED) D ^SROSTOP I SRHALT S SRSOUT=1 Q
 S X=$E(SRSD,4,5)_"/"_$E(SRSD,6,7)_"/"_$E(SRSD,2,3),Y=$E(SRED,4,5)_"/"_$E(SRED,6,7)_"/"_$E(SRED,2,3),PAGE=PAGE+1 I $Y W @IOF
 I $E(IOST)'="P" Q
 W !,?(80-$L(SRINST)\2),SRINST,?70,"PAGE: "_PAGE,!,?32,"SURGICAL SERVICE",!,?28,"CANCELLATION RATE REPORT",!,?27,"FROM "_X_"  TO "_Y
 S X=$E(DT,4,5)_"/"_$E(DT,6,7)_"/"_$E(DT,2,3) W !,?29,"DATE PRINTED: "_X
 W !,?21,"REVIEWED BY:",?45,"DATE REVIEWED:",!
 W ! F LINE=1:1:80 W "="
 Q
SUB ; print column headings
 W !!,"CANCELLATION REASON",?45,"TOTAL CANCELS",?62,"TOTAL AVOIDABLE",! F LINE=1:1:80 W "-"
 Q
