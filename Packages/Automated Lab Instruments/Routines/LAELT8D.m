LAELT8D ;SLC/RWF - ORTHO ELT8 WITH 3 CELL DIFF / ELT 1500 ;7/20/90  08:16 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID OR IDE use format option 4
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 K TV,Y S TOUT=0 D IN G QUIT:TOUT,LA2:'($L(IN)=30!($L(IN)=35)) S Y(1)=IN S BAD=0 D QC1 G LA2:BAD
 S TOUT=0 D IN G QUIT:TOUT,LA2:$L(IN)'=57 S Y(2)=IN S BAD=0 D QC2 G LA2:BAD
 F I=0:0 S I=$O(TC(I)) Q:I<1  X TC(I,2) D NUM S:TC(I,3) V=V/TC(I,3) S @TC(I,1)=V
 S (ID,IDE,CUP)=+$E(Y(2),41,49),TRAY=1
LA3 X LAGEN G LA2:'ISQN ;Can be changed by the cross-link code
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
QC1 ;F I=4:5:29 I $E(Y(1),I)'=" " S BAD=1
 Q
QC2 ;F I=4:5:39 I $E(Y(2),I)'=" " S BAD=1
 Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
