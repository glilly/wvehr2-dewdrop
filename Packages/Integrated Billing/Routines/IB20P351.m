IB20P351 ;ALB/CXW-FY07 DSS CLINIC STOP CODES IB*2.0*351 POST INIT ;25-SEP-06
 ;;2.0;INTEGRATED BILLING;**351**;21-MAR-94;Build 4
 ;;Per VHA Directive 2004-038, this routine should not be modified.
 ;
POST ;
 N IBEFFDT,U
 S U="^",IBEFFDT=3061001 ;effective date OCT 1st, 2006 
 D START,ADD(IBEFFDT),UPDATE(IBEFFDT),FINISH
 Q
 ;
START D BMES^XPDUTL("FY07 DSS Clinic Stop Codes, Post-Install Starting")
 Q
 ;
FINISH D BMES^XPDUTL("FY07 DSS Clinic Stop Codes, Post-Install Complete")
 Q
 ;
 ;
ADD(IBEFFDT) ;
 ;add a new code
 N Y,IBC,IBT,IBX,IBY,IBCODE,IBTYPE,IBDES,IBOVER
 D BMES^XPDUTL(" Adding new codes to file 352.5")
 S IBC=0
 F IBX=1:1 S IBT=$P($T(NCODE+IBX),";",3) Q:'$L(IBT)  D
 . S IBCODE=+$P(IBT,U)
 . S IBY=$S(IBCODE=372:3041001,IBCODE=373:3041001,1:IBEFFDT)
 . I $D(^IBE(352.5,"AEFFDT",IBCODE,-IBY)) D  Q
 . . D BMES^XPDUTL(" Duplication of stop code "_IBCODE)
 . S IBTYPE=$P(IBT,U,2)
 . S IBDES=$E($P(IBT,U,3),1,30)
 . S IBOVER=$P(IBT,U,4)
 . S Y=+$$ADD3525(IBCODE,IBY,IBTYPE,IBDES,IBOVER) S:Y>0 IBC=IBC+1
 D BMES^XPDUTL("     "_IBC_$S(IBC<2:" entry",1:" entries")_" added to 352.5")
 Q
 ;
UPDATE(IBEFFDT) ;
 ;update an old code
 N Y,IB1,IBC,IBT,IBX,IBCODE,IBMSG,IBTYPE,IBDES,IBOVER,IBLSTDT
 S (IBC,IBMSG(1),IBMSG(2),IBMSG(3))=0
 F IBX=1:1 S IBT=$P($T(OCODE+IBX),";",3) Q:'$L(IBT)  D
 . S IBCODE=+$P(IBT,U)
 . I $D(^IBE(352.5,"AEFFDT",IBCODE,-IBEFFDT)) D  Q 
 . . D BMES^XPDUTL(" Duplication of stop code "_IBCODE)
 . S IBLSTDT=$O(^IBE(352.5,"AEFFDT",IBCODE,-9999999))
 . I +IBLSTDT=0 D  Q
 . . D BMES^XPDUTL(" Code "_IBCODE_" not found in file 352.5")
 . S IB1=$O(^IBE(352.5,"AEFFDT",IBCODE,IBLSTDT,0))
 . S IB1=$G(^IBE(352.5,IB1,0))
 . S IBTYPE=$S($P(IBT,U,2):$P(IBT,U,2),1:$P(IB1,U,3))
 . S IBDES=$S($P(IBT,U,3)'="":$E($P(IBT,U,3),1,30),1:$P(IB1,U,4))
 . S IBOVER=$S($P(IBT,U,4)'="":$P(IBT,U,4),1:$P(IB1,U,5))
 . I $P(IBT,U,2) D
 . . I 'IBMSG(1) D BMES^XPDUTL(" Updating billable type in file 352.5") S IBMSG(1)=1
 . I $P(IBT,U,3)'="" D
 . . I 'IBMSG(2),$P(IBT,U,3)'="" D BMES^XPDUTL(" Updating description in file 352.5") S IBMSG(2)=1
 . I '$P(IBT,U,2),$P(IBT,U,3)="" D
 . . I 'IBMSG(3) D BMES^XPDUTL(" Updating effective date in file 352.5") S IBMSG(3)=1
 . S Y=+$$ADD3525(IBCODE,IBEFFDT,IBTYPE,IBDES,IBOVER) S:Y>0 IBC=IBC+1
 D BMES^XPDUTL("     "_IBC_$S(IBC<2:" update",1:" updates")_" added to file 352.5")
 Q
 ;
ADD3525(IBCODE,IBEFFDT,IBTYPE,IBDES,IBOVER) ;
 ;add a new entry
 D BMES^XPDUTL("   "_IBCODE_"  "_IBDES)
 N IBIENS,IBFDA,IBER,IBRET
 S IBRET=""
 S IBIENS="+1,"
 S IBFDA(352.5,IBIENS,.01)=IBCODE
 S IBFDA(352.5,IBIENS,.02)=IBEFFDT
 S IBFDA(352.5,IBIENS,.03)=IBTYPE
 S IBFDA(352.5,IBIENS,.04)=IBDES
 S:IBOVER IBFDA(352.5,IBIENS,.05)=1
 D UPDATE^DIE("","IBFDA","IBRET","IBER")
 I $D(IBER) D BMES^XPDUTL(IBER("DIERR",1,"TEXT",1))
 Q $G(IBRET(1))
 ;
 ;new codes
NCODE ;;code^billable type^description^override flag
 ;;372^1^MOVE PROGRAM INDIVIDUAL
 ;;373^1^MOVE PROGRAM GROUP
 ;;159^1^COMPLEMENTARY ALTERNATIVE THERAPIES
 ;;182^0^TELEPHONE CASE MANAGEMENT^1
 ;;310301^0^TRANSRECTAL ULTRASOUND F-U IND^1
 ;;394301^0^TRANSRECTAL ULTRASOUND F-U GRP^1
 ;;571^0^RETURN VET OUTREACH ED/CARE-IND^1
 ;;572^0^RETURN VET OUTREACH ED/CARE-GRP^1
 ;;582^1^PSYC/SOC REHAB/RECOV CENTR-IND
 ;;583^1^PSYC/SOC REHAB/RECOV CENTR-GRP
 ;;584^0^TELEPHONE PSYC/SOC REHAB/RECOVERY^1
 ;;643^0^SEND-OUT PROCEDURES - RADIOLOGY^1
 ;;697^0^CHART CONSULT
 ;
 ;codes update
OCODE ;;code^billable type^description^override flag
 ;;142^1^^0
 ;;640
 ;;641
 ;;642
 ;;656
 ;;670
 ;;704^^FEMALE GENDER SPECIFIC CANCER SCREENING
 ;
