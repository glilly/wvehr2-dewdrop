FSCLMPON ;SLC/STAFF-NOIS List Manager Protocol Other Notify ;1/11/98  18:37
 ;;1.1;NOIS;;Sep 06, 1998
 ;
NOTIFY(CALL) ; from FSCLMPO
 N CNT,LISTNAME,LISTNUM,LISTSNUM,MSG,NUM,OK,OWNER
 K ^TMP("FSC NOTIFY",$J)
 W !,"Notifications on this call:" S (CNT,OK)=1
 D NOTINFO^FSCNOT(CALL)
 S MSG="" F  S MSG=$O(^TMP("FSC NOTIFY",$J,"B",MSG)) Q:MSG=""  D  I 'OK Q
 .S NUM=0 F  S NUM=$O(^TMP("FSC NOTIFY",$J,"B",MSG,NUM)) Q:NUM<1  D  I 'OK Q
 ..W !,?2,^TMP("FSC NOTIFY",$J,NUM)
 ..S CNT=CNT+1 I CNT'<(IOSL-1) S CNT=1 D PAUSE^FSCU(.OK) I 'OK Q
 K ^TMP("FSC NOTIFY",$J)
 I 'OK Q
 W !,"This call is found on the following lists:" S CNT=CNT+1
 I CALL D
 .S LISTSNUM=0 F  S LISTSNUM=$O(^FSCD("LISTS","B",CALL,LISTSNUM)) Q:LISTSNUM<1  D  I 'OK Q
 ..S LISTNUM=+$P(^FSCD("LISTS",LISTSNUM,0),U,2)
 ..Q:'$D(^FSC("LIST",LISTNUM,0))  S LISTNAME=$P(^(0),U),OWNER=+$P(^(0),U,2)
 ..W !?2,LISTNAME I OWNER W ?24," Owner: ",$$VALUE^FSCGET(OWNER,7107.1,1),"  ",$$SITESP(OWNER)
 ..S CNT=CNT+1 I CNT'<(IOSL-1) S CNT=1 D PAUSE^FSCU(.OK) I 'OK Q
 I 'OK Q
 D PAUSE^FSCU(.OK)
 Q
 ;
SITESP(USER) ; $$(user) -> (site - area)
 N AREA,BOTH,SITE
 S SITE=$P($G(^FSC("SPEC",USER,0)),U,3),AREA=$P($G(^(0)),U,4),BOTH=""
 I 'SITE,'AREA Q ""
 I SITE,AREA S BOTH=" - "
 Q "("_$$VALUE^FSCGET(SITE,7105.2,3)_BOTH_$$VALUE^FSCGET(AREA,7105.2,4)_")"
