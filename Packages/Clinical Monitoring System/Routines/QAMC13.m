QAMC13 ;HISC/DAD-CONDITION: READMISSION ;9/3/93  13:13
 ;;1.0;Clinical Monitoring System;;09/13/1993
EN1 ; *** CONDITION CODE
 S QAMDAYS=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P1"))#2:+^("P1"),1:0)
 F ADMDT=(QAMTODAY-.0000001):0 S ADMDT=$O(^DGPM("AMV1",ADMDT)) Q:(ADMDT'>0)!(ADMDT>(QAMTODAY+.9999999))!(ADMDT\1'?7N)  F QAMDFN=0:0 S QAMDFN=$O(^DGPM("AMV1",ADMDT,QAMDFN)) Q:QAMDFN'>0  D LOOP0
 K ADMD0,QAMDAYS,ADMDT,X1,X2,X,STOPDT,QAMDC
 Q
LOOP0 F ADMD0=0:0 S ADMD0=$O(^DGPM("AMV1",ADMDT,QAMDFN,ADMD0)) Q:ADMD0'>0  D LOOP1
 Q
LOOP1 S X1=ADMDT,X2=-QAMDAYS D C^%DTC S STOPDT=9999999.9999999-X
 F QAMDC=(9999999.9999999-ADMDT):0 S QAMDC=$O(^DGPM("ATID3",QAMDFN,QAMDC)) Q:(QAMDC'>0)!(QAMDC\1'?7N)!(QAMDC>STOPDT)  S ^UTILITY($J,"QAM CONDITION",QAMD1,QAMDFN)="",^(QAMDFN,ADMDT)=ADMD0 Q
 Q
EN2 ; *** PARAMETER CODE
 K DIR,DIRUT S DIR(0)="NO^1:365:0",DIR("A")="DAYS BETWEEN DISCHARGE AND ADMISSION",DIR("B")=$S($D(^QA(743,QAMD0,"COND",QAMD1,"P1"))#2:^("P1"),1:"") K:DIR("B")="" DIR("B")
 S DIR("?",1)="Enter the number of QAMDAYS between the patient's last discharge",DIR("?")="and current admission."
 S QAMPARAM="P1" D EN3^QAMUTL1 I $D(DIRUT) S Y=-1 G Y
 S:Y]"" ^QA(743,QAMD0,"COND",QAMD1,"P1")=+Y
EXIT K Y
 K QAMPARAM
Y Q
