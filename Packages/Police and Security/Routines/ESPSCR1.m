ESPSCR1 ;DALISC/CKA - OFFENSE REPORT SCREENS-NAMES & NARRATIVE;2/93
 ;;1.0;POLICE & SECURITY;**12**;Mar 31, 1994
EN Q  ;CALLED FROM ESPOFF
COMP ;COMPLAINANT SCREEN
 S ESPNOT=0
 F ESPN=1:1 Q:'$D(ESPFN)&(ESPN'=1)!($D(DTOUT))  D  Q:ESPNOT
 .  I $D(^TMP($J,"UOR","C",ESPN)) D WARN I 'ESPNOT K ^TMP($J,"UOR","C")
 .  Q:ESPNOT
 .  W !!,"COMPLAINANT #",ESPN
 .  K ESPFN D EN^ESPMNI
 .  I $D(ESPFN),(ESPFN>0) S ^TMP($J,"UOR","C",ESPN)=ESPFN
 QUIT
VIC ;VICTIM SCREEN
 S ESPNOT=0
 F ESPN=1:1 Q:'$D(ESPFN)&(ESPN'=1)!($D(DTOUT))  D  Q:ESPNOT
 .  I $D(^TMP($J,"UOR","V",ESPN)) D WARN I 'ESPNOT K ^TMP($J,"UOR","V")
 .  Q:ESPNOT
 .  W !!,"VICTIM #",ESPN
 .  K ESPFN D EN^ESPMNI
 .  Q:$D(DTOUT)
 .  I $D(ESPFN),(ESPFN>0) S ^TMP($J,"UOR","V",ESPN)=ESPFN W !,"MEDICAL TREATMENT: " S DWLW=80,DWPK=1,DIC="^TMP($J,""MT"","_ESPN_",",DIWESUB="MEDICAL TREATMENT" D EN^DIWE
 QUIT
WIT ;WITNESS SCREEN
 S ESPNOT=0
 F ESPN=1:1 Q:'$D(ESPFN)&(ESPN'=1)!($D(DTOUT))  D  Q:ESPNOT
 .  I $D(^TMP($J,"UOR","W",ESPN)) D WARN I 'ESPNOT K ^TMP($J,"UOR","W")
 .  Q:ESPNOT
 .  W !!,"WITNESS #",ESPN
 .  K ESPFN D EN^ESPMNI
 .  I $D(ESPFN),(ESPFN>0) S ^TMP($J,"UOR","W",ESPN)=ESPFN
 QUIT
NARR ;NARRATIVE SCREEN
 W !!,"ORIGIN:"
 S DWLW=80,DWPK=1,DIC="^TMP($J,""N1"",",DIWESUB="ORIGIN" D EN^DIWE
 I $D(DTOUT) S NOUPD=1 Q
 W !!,"INITIAL OBSERVATION:"
 S DWLW=80,DWPK=1,DIC="^TMP($J,""N2"",",DIWESUB="INITIAL OBSERVATION" D EN^DIWE
 I $D(DTOUT) S NOUPD=1 Q
 W !!,"INVESTIGATION:"
 S DWLW=80,DWPK=1,DIC="^TMP($J,""N3"",",DIWESUB="INVESTIGATION" D EN^DIWE
 I $D(DTOUT) S NOUPD=1 Q
 W !!,"DISPOSITION:"
 S DWLW=80,DWPK=1,DIC="^TMP($J,""N4"",",DIWESUB="DISPOSITION" D EN^DIWE
 I $D(DTOUT) S NOUPD=1 Q
 QUIT
NOTIF ;NOTIFICATIONS SCREEN INPUT
 S ESPNOT=0 K DIRUT
 F ESPN=1:1 Q:$D(DIRUT)  D OAN Q:ESPNOT
 Q:$D(DTOUT)
 S ESPNOT=0 K DIRUT
 F ESPN=1:1 Q:$D(DIRUT)  D ATY Q:ESPNOT!($D(DTOUT))
 QUIT
NOTIN ;NOTIFICATIONS INPUT
OAN ;OTHER AGENCY NOTIFIED
 I $D(^TMP($J,"UOR","OTH",ESPN)) D WARN I 'ESPNOT K ^TMP("UOR","OTH")
 QUIT:ESPNOT
 W !!,"OTHER AGENCY"
OAG S DIR(0)="912.14,.01" D RD Q:$D(DIRUT)  S ESPD(.01)=Y
CONT S DIR(0)="912.14,.02" D RD G:$D(DUOUT) SNO G:$D(DTOUT) NOUPD S ESPD(.02)=Y
AG S DIR(0)="912.14,.03" D RD G:$D(DUOUT) SNO G:$D(DTOUT) NOUPD S ESPD(.03)=Y
SNO S ^TMP($J,"UOR","OTH",ESPN)=ESPD(.01)_"^"_$G(ESPD(.02))_"^"_$G(ESPD(.03))
 K DIRUT
 QUIT
ATY ;U.S. ATTORNEY NOTIFIED
 I $D(^TMP($J,"UOR","ATY",ESPN)) D WARN I 'ESPNOT K ^TMP("UOR","ATY")
 QUIT:ESPNOT
 W !!,"U.S. ATTORNEY"
 S DIR(0)="912.15,.01" D RD Q:$D(DIRUT)  S ESPD(.01)=Y
INSTR W !,"Instructions Received: " S DWLW=80,DWPK=1,DIC="^TMP($J,""INS"","_ESPN_",",DIWESUB="Instructions Received" D EN^DIWE
 I $D(DTOUT) S NOUPD=1 Q
 S ^TMP($J,"UOR","ATY",ESPN)=ESPD(.01)_"^"
 QUIT
HELD ;PROPERTY (HELD) SCREEN INPUT
 S ESPNOT=0 K DIRUT
 F ESPN=1:1 Q:$D(DIRUT)  D PHIN Q:ESPNOT!($D(DTOUT))
 QUIT
PHIN ;PROPERTY HELD INPUT
 I $D(^TMP($J,"UOR","PH",ESPN)) D WARN I 'ESPNOT K ^TMP("UOR","PH")
 QUIT:ESPNOT
 W !!,"ITEM #",ESPN
NUM S DIR(0)="912.02,.01" D RD Q:$D(DIRUT)  S ESPD(.01)=Y
QUAN S DIR(0)="912.02,.02" D RD G:$D(DUOUT) SPH G:$D(DTOUT) NOUPD S ESPD(.02)=Y
PUR S DIR(0)="912.02,.03" D RD G:$D(DUOUT) SPH G:$D(DTOUT) NOUPD S ESPD(.03)=Y
DESC W !,"Description: " S DWLW=80,DWPK=1,DIC="^TMP($J,""DE"","_ESPD(.01)_"," D EN^DIWE
 I $D(DTOUT) S NOUPD=1 Q
SPH S ^TMP($J,"UOR","PH",ESPN)=ESPD(.01)_"^"_$G(ESPD(.02))_"^"_$G(ESPD(.03))
 QUIT
EXIT QUIT
NO W $C(7),!!?5,"NO '^'S ALLOWED!",!!
 QUIT
NOUPD W !!,$C(7),?20,"NO UPDATING HAS OCCURRED!!!",!! K ESPCL,ESPD,ESPDTR,ESPX,ESPY,^TMP($J) S NOUPD=1 QUIT
RD D ^DIR I $S($L(X)>1&($E(X)=U):1,($L(X)>1&(X[U)):1,1:0) D NO K X,Y G RD
 K DIR
 QUIT
WARN W !!,$C(7),"Warning:  The names that you have previously entered will be replaced if you enter a name."
 S DIR(0)="Y",DIR("A")="Are you sure you want to do this",DIR("B")="NO" D ^DIR K DIR
 I 'Y S ESPNOT=1
 QUIT
