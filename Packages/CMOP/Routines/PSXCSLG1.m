PSXCSLG1 ;BIR/JMB-Adds & Updates Cost Task Log-CONTINUED ;[ 04/08/97   2:06 PM ]
 ;;2.0;CMOP;;11 Apr 97
QUE ;Adds task to log
 Q:'$D(PSXBDT)!('$D(PSXEDT))!('$D(DUZ))!('$D(PSXJOB))!('$D(ZTSK))
 S:'$D(^PSX(554,1,2,0)) ^PSX(554,1,2,0)="^554.02DA^^"
 S X=$E($$HTFM^XLFDT($H),1,14),DINUM=PSXSTART
 S DIC("DR")="1///0;2///^S X=PSXBDT;3///^S X=PSXEDT;4///^S X=DUZ;5///^S X=PSXJOB"_$S($D(ZTSK):";7///^S X=ZTSK",1:"")
 S DLAYGO=554.02,DIC(0)="LMZ",DA(1)=1,DIC="^PSX(554,1,2,"
 K DD,DO D ^DIC K DA,DIC,DINUM,DLAYGO,X
 Q
RUN S DIE="^PSX(554,1,2,",DA(1)=1,DA=PSXSTART,DR="1///1" D ^DIE K DA,DIE,DR
 Q
END S PSXJOBE=$E($$HTFM^XLFDT($H),1,14)
 S DIE="^PSX(554,1,2,",DA(1)=1,DA=PSXSTART,DR="1///2;6///^S X=PSXJOBE" D ^DIE K DA,DIE,DR,PSXJOBE
 Q
