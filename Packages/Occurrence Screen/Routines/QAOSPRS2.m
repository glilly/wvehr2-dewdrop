QAOSPRS2 ;HISC/DAD,JCW-REVIEW SUMMARY REPORT ;11/12/92  15:43
 ;;3.0;Occurrence Screen;;09/14/1993
SETUP ; INITIALIZE ARRAYS
 D KILLTMP
 S QAOSCLIN=$O(^QA(741.2,"C",1,0)),QAOSPEER=$O(^QA(741.2,"C",2,0)),QAOSMGMT=$O(^QA(741.2,"C",3,0)),QAOSRV=$P(^DD(42,.03,0),"^",3)_"^:UNKNOWN;"
 F QAOSREVR="OCCR","EXCP","DELT","RECR","SEVR","CLIN","PEER","MGMT","CMTE" F QAOSTYPE=1:1:$L(QAOSLIST) S QAOTOTAL(QAOSREVR,$E(QAOSLIST,QAOSTYPE))=0
 F QAOSD0=0:0 S QAOSD0=$O(^QA(741.6,QAOSD0)) Q:QAOSD0'>0  S QAOSZERO=^QA(741.6,QAOSD0,0) I +QAOSZERO'=3 F QAOSTYPE=1:1:$L(QAOSLIST) D
 . S QAOSTYPE(0)=$E(QAOSLIST,QAOSTYPE),X=$P(QAOSZERO,"^",3)
 . I X["1" S ^TMP($J,QAOSTYPE(0),"CLIN",+QAOSZERO)=""
 . I X["2" S ^TMP($J,QAOSTYPE(0),"PEER",+QAOSZERO)=""
 . Q
 F QAOSD0=0:0 S QAOSD0=$O(^QA(741.7,QAOSD0)) Q:QAOSD0'>0  S QAOSZERO=^QA(741.7,QAOSD0,0) I $P(QAOSZERO,"^",2)["3" F QAOSTYPE=1:1:$L(QAOSLIST) S QAOSTYPE(0)=$E(QAOSLIST,QAOSTYPE),^TMP($J,QAOSTYPE(0),"MGMT",+QAOSZERO)=""
 F QAOSD0=0:0 S QAOSD0=$O(^QA(741.8,QAOSD0)) Q:QAOSD0'>0  S QAOSEVER=+^QA(741.8,QAOSD0,0) F QAOSTYPE=1:1:$L(QAOSLIST) S QAOSTYPE(0)=$E(QAOSLIST,QAOSTYPE),^TMP($J,QAOSTYPE(0),"SEVR",QAOSEVER)=""
 F QAOSCONF=1:1:4 F QAOSTYPE=1:1:$L(QAOSLIST) S QAOSTYPE(0)=$E(QAOSLIST,QAOSTYPE),^TMP($J,QAOSTYPE(0),"CMTE",QAOSCONF)=""
 Q
KILLTMP ; CLEANUP ^TMP
 N I,J
 F I="1","L","N" F J="CLIN","PEER","MGMT","SEVR","CMTE" K ^TMP($J,I,J)
 Q
