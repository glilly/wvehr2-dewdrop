FSCLM ;SLC/STAFF-NOIS List Manager ;10/27/97  15:50
 ;;1.1;NOIS;;Sep 06, 1998
 ;
LIST(FSCLNAME,FSCINDX) ; from FSCOPT
 N FSCL0,FSCLNUM
 S FSCLNUM=+$O(^FSC("LIST","B",FSCLNAME,0))
 S FSCL0=$G(^FSC("LIST",FSCLNUM,0))
 I $P(FSCL0,U,5) D INDEX(.FSCLNAME,.FSCINDX,FSCL0) Q:'$G(FSCINDX)
 D MRU^FSCMR(DUZ,FSCLNUM,+$G(FSCINDX))
 S VALMSG=""
 D EN^VALM("FSC LIST CALLS")
 Q
 ;
MODIFY(FSCLNAME,FSCLNUM) ; from FSCLMPC, FSCLMPO
 S VALMSG=""
 D EN^VALM("FSC MODIFY LISTS")
 Q
 ;
INSTALLS ; from FSCSTUR
 S VALMSG=""
 D EN^VALM("FSC INSTALLS")
 Q
 ;
EDIT(FSCCALLS) ; from FSCLMPE, FSCOPT
 I $O(^TMP("FSC SELECT",$J,FSCCALLS,+$O(^TMP("FSC SELECT",$J,FSCCALLS,0)))) D EN^VALM("FSC EDIT MULTIPLE CALLS") Q
 S VALMSG=""
 D EN^VALM("FSC EDIT SINGLE CALL")
 Q
 ;
VIEW(LISTREF,LISTSEL,FSCFMT) ; from FSCLMPD, FSCOPT, FSCRPTS
 ; LIST = list #^list name
 S VALMSG=""
 D EN^VALM("FSC VIEW CALLS")
 Q
 ;
INDEX(FSCLNAME,FSCINDX,FSCL0) ; from FSCLMPC, FSCRPTS
 N DIC,FIELD0,X,Y K DIC
 I '$G(FSCINDX) D  Q
 .S FIELD0=$G(^FSC("FLD",$P(FSCL0,U,5),0)),DIC=+$P($P(FIELD0,U,3),"P",2) I 'DIC Q
 .S DIC(0)="AEMOQ",DIC("A")="  Select "_$P(FIELD0,U,2)_": "
 .I $E(FSCLNAME,1,4)="MRE:"!($E(FSCLNAME,1,4)="MRA:") S DIC("A")="  Select User: ",DIC("B")=DUZ
 .F  D ^DIC Q:Y'<1  Q:$D(DTOUT)  Q:$D(DUOUT)  W "  You must make an entry or '^' to exit.",$C(7)
 .K DIC Q:$D(DTOUT)  Q:$D(DUOUT)
 .S FSCINDX=+Y,FSCLNAME=FSCLNAME_" "_$S($P(Y,U,2)'=+$P(Y,U,2):$P(Y,U,2),1:$$VALUE^FSCGET($P(Y,U,2),+$P($P(FIELD0,U,3),"P",2),.01))
 S FSCLNAME=FSCLNAME_" "_$P(FSCINDX,U,2),FSCINDX=+FSCINDX
 Q
 ;
PROTOHDR(SCREEN) ; from protocol header action on protocol menus
 S XQORM("KEY","PAGE")=+$O(^ORD(101,"B","VALM GOTO PAGE",0))_"^1"
 S (XQORM("KEY","SEARCH"),XQORM("KEY","ST"),XQORM("KEY","TEXT"))=+$O(^ORD(101,"B","VALM SEARCH LIST",0))_"^1"
 S XQORM("KEY","QUI")=+$O(^ORD(101,"B","VALM QUIT",0))_"^1"
 S XQORM("KEY","PG")=+$O(^ORD(101,"B","VALM GOTO PAGE",0))_"^1"
 S XQORM("KEY","EXIT")=+$O(^ORD(101,"B","FSC HIDDEN EXIT",0))_"^1"
 S XQORM("KEY","EX")=+$O(^ORD(101,"B","FSC HIDDEN EXIT",0))_"^1"
 I SCREEN="LIST" D
 .I XQORM("B")="View",VALMBG+15'>VALMCNT S XQORM("B")="Next Screen"
 I SCREEN="EDIT" D
 .I $O(^TMP("FSC MULT",$J,FSCCNT)),VALMBG+15>VALMCNT S XQORM("B")="Next Call"
 I XQORM("B")="Quit",$$EXIT^FSCUF(DUZ) S XQORM("B")="Exit"
 Q
 ;
NAV ; *** temporary call to reset hidden menu protocols, needs to be removed
 S X=+$O(^ORD(101,"B","FSC MENU HIDDEN ACTIONS",0)) I X S X=X_";ORD(101," D EN^XQOR
 Q
