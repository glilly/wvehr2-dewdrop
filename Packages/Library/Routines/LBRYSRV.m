LBRYSRV ;SSI/ALA-LIBRARY SERVER PROGRAM ;[ 03/09/94  10:57 AM ]
 ;;2.5;Library;;Mar 11, 1996
SERV ;
 S TTY("TIR")="TITLE RECORD FROM FORUM",TTY("PUR")="PUBLISHER RECORD FROM FORUM"
 S TTY("FRR")="FREQUENCY RECORD FROM FORUM",TTY("ISR")="INDEXING SOURCE RECORD FROM FORUM"
 S TTY("PPR")="PREDICTION PATTERN RECORD FROM FORUM",TTY("CTI")="CORRECTED TITLE RECORD"
 S TTY("CPL")="CORRECTED PREDICTION PATTERN LISTING",TTY("CFR")="CORRECTED FREQUENCY RECORD"
 S TTY("TIC")="LOCAL TITLE CONFIRMATION",TTY("DLT")="TITLE DELETION"
 S TTY("CPR")="NEW PREDICTION PATTERN FROM FORUM"
 F  D HDR Q:XMER'=0
 Q
HDR ;  Receiving transactions to be processed
 X XMREC Q:XMER'=0  Q:$P(XMRG,U)="$"
 Q:"LIB"'[$P(XMRG,U)
TYP S TYP=$P(XMRG,U,4),SSTN=$P(XMRG,U,2)
 I TYP="CPR"!(TYP="PPR") D PR^LBRYSRV1 Q
 I TYP="FRR"!(TYP="CFR") D FR^LBRYSRV1 Q
 I TYP="PUR" D PU^LBRYSRV1 Q
 I TYP="ISR" D IN^LBRYSRV1 Q
 D TR
 Q
TR ;  Put title information into transaction file
 D TRN^LBRYUTL K X,Y
TR1 X XMREC
 I $P(XMRG,U)="$" D BUL:TYP'="LTR",REJ:TYP="LTR" Q
 I $P(XMRG,U)="TI" S $P(^LBRY(682.1,LBRYDA,0),U,5)=$P(XMRG,U,6) S ND=1 D FIL S TIT=$P(XMRG,U,2)
 I $P(XMRG,U)="T2" S ND=2 D FIL
 I $P(XMRG,U)="T3" S ND=3 D FIL
 I $P(XMRG,U)="T4" S ND=4 D FIL
 I $P(XMRG,U)="T5" S CT=$P(XMRG,U,2) S ^LBRY(682.1,LBRYDA,6,CT,0)=$P(XMRG,U,3)
 I $P(XMRG,U)="T6" S CT=$P(XMRG,U,2) S ^LBRY(682.1,LBRYDA,7,CT,0)=$P(XMRG,U,3)
 I $P(XMRG,U)="T7" S CT=$P(XMRG,U,2) S ^LBRY(682.1,LBRYDA,10,CT,0)=$P(XMRG,U,3)
 G TR1
FIL F I=2:1:6 S $P(^LBRY(682.1,LBRYDA,ND),U,I-1)=$P(XMRG,U,I)
 Q
BUL ;  Send mail message about transaction
 I TYP="CTI" S TIT="now: "_TIT,OTI=$P(^LBRY(682.1,LBRYDA,1),U,3) I OTI'="" S OTIT="  was: "_$P($G(^LBRY(680.5,OTI,0)),U)
 S TYP=$G(TTY(TYP)) S:TYP="" TYP="UNKNOWN TRANSACTION"
 S XMB="LBRYFORUM",XMB(1)=TYP,XMB(2)=TIT,XMY("G.LBRYUPDT")="",XMB(3)=$G(OTIT)
 S XMDUZ="FORUM LIBRARIANS" D ^XMB K XMY,XMB,TIT
 Q
REJ S XMSUB="LOCAL SERIAL TITLE REJECTION",XMTEXT="MSG("
 S XMY("G.LBRYUPDT")="",XMDUZ="FORUM LIBRARIANS"
 S MSG(1)="You have received a Local Serial Title Rejection from FORUM for "_TIT_"."
 S MSG(2)="  "
 S MSG(3)="Comments: " S I=0 F  S I=$O(^LBRY(682.1,LBRYDA,10,I)) Q:I'>0  S MSG(3+I)=^LBRY(682.1,LBRYDA,10,I,0)
 D ^XMD K MSG,XMY,XMSUB,XMTEXT
 Q
