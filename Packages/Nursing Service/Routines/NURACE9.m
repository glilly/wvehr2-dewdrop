NURACE9 ;HIRMFO/MD-PATIENT CLASSIFICATION MEDICAL (SCI) ;7/89
 ;;4.0;NURSING SERVICE;;Apr 25, 1997
EN1 ;VALIDATE SCI FACTORS
 S (ABORTSW,CHANGESW,NURS1SW,NURS3SW,NURS5SW,NURS7SW,NURS9SW,NURS11SW,NURS13SW)=0
 I FACT["?" D EN5^NURACE3 S REENTSW=1 Q
 F I=1:1:$L(FACT) S:(($E(FACT,I)'?1A)!($A(FACT,I)<65)!($A(FACT,I)>76)) ABORTSW=1 Q:ABORTSW=1  S FACT($E(FACT,I))=0
 I ABORTSW=1 W $C(7),"  *** BAD ENTRY - TRY AGAIN ***" S REENTSW=1 Q
 D ONECK
 S FACT="",NXT="" F I=0:0 S NXT=$O(FACT(NXT)) Q:NXT=""  S FACT=FACT_NXT
 I ((FACT="")&(FACTORS="")) W !,$C(7),"**** NO FACTORS ENTERED - CLASSIFICATION NOT UPDATED ****" H 3 S OUTSW=1 Q
 I NURS1SW=1 W !,$C(7),"*** A CANNOT BE USED WITH NUMBERS B,C,D,E,F,G,H or I ***" S REENTSW=1 Q
 I NURS3SW=1 W !,$C(7),"*** B CANNOT BE USED WITH NUMBER C ***" S REENTSW=1 Q
 I NURS5SW=1 W !,$C(7),"*** E CANNOT BE USED WITH NUMBER D ***" S REENTSW=1 Q
 I NURS7SW=1 W !,$C(7),"*** F and G CANNOT BE USED TOGETHER ***" S REENTSW=1 Q
 I NURS9SW=1 W !,$C(7),"*** H and I CANNOT BE USED TOGETHER ***" S REENTSW=1 Q
 I NURS11SW=1 W !,$C(7),"*** J and K CANNOT BE USED TOGETHER ***" S REENTSW=1 Q
 I NURS13SW=1 W !,$C(7),"*** A and L CANNOT BE USED TOGETHER ***" S REENTSW=1 Q
 I ((FACT="")!(FACT=FACTORS)) S NURSCKSW=1 G EN2
 S FACTORS=FACT F I=1:1:$L(FACT) S:I=1 FACTX=$E(FACT,1) S:I'=1 FACTX=FACTX_","_$E(FACT,I)
 S CHANGESW=1
 G EN2
ONECK ;DETERMINE IF FACTORS CAN BE USED WITH EACH OTHER
 I FACT["A" I ((FACT["B")!(FACT["C")!(FACT["D")!(FACT["E")!(FACT["F")!(FACT["G")!(FACT["H")!(FACT["I")) S NURS1SW=1
 I FACT["B"&(FACT["C") S NURS3SW=1
 I FACT["E" I FACT["D" S NURS5SW=1
 I ((FACT["F")&(FACT["G")) S NURS7SW=1
 I ((FACT["H")&(FACT["I")) S NURS9SW=1
 I ((FACT["J")&(FACT["K")) S NURS11SW=1
 I ((FACT["A")&(FACT["L")) S NURS13SW=1
 Q
EN2 ;CALCULATE NEW SCI CLASSIFICATION
 I FACTORS["A" S CLASSX=1 G CHKCLASS
 I FACTORS["L" S CLASSX=5 G CHKCLASS
 S (CAT(1),CAT(2),CAT(3),CAT(4))=0
 F I="B","D","F","H" S:FACTORS[I CAT(2)=CAT(2)+1
 F I="B","C","D","E","F","G","H","I","J" S:FACTORS[I CAT(3)=CAT(3)+1
 F I="C","E","G","I","J","K" S:FACTORS[I CAT(4)=CAT(4)+1
 S CAT(1)=CAT(1)+1,CAT(2)=CAT(2)+1,CAT(3)=CAT(3)+.5
 I (((CAT(2)>CAT(3))!(CAT(2)=CAT(3)))&(CAT(2)>CAT(4))) S CLASSX=2 G CHKCLASS
 I (((CAT(3)>CAT(2))!(CAT(3)=CAT(2)))&(CAT(3)>CAT(4))) S CLASSX=3 G CHKCLASS
 S CLASSX=4
CHKCLASS ;ENTER IN CLASSIFICATION IF WANT TO CHANGE
 Q:NURSNSW=1
 I $D(XCLAS) I ((NURSCKSW=1)&(CLASSX=XCLAS)) S CHANGESW=1
CHKCLAS1 ;
 W !,"Enter Classification: " W:(CLASSX'="") CLASSX,"//" R X:DTIME S X=$E(X,1,2)
 I (X="^")!('$T) D EN4^NURACE8 S OUTSW=1 Q
 I X["?" W !,"ANSWER WITH A NUMBER BETWEEN 1 AND 5" G CHKCLAS1
 I $L(X)=0 S:CHANGESW=1 CONFIGX="COMPUTER" Q
 I (($L(X)>1)!(X?1A)!(X<1)!(X>5)) W *7,"   *** BAD ENTRY - TRY AGAIN ***" G CHKCLASS
 I X=CLASSX S:CHANGESW=1 CONFIGX="COMPUTER" Q
 S CHANGESW=1,CLASSX=X,CONFIGX="USER"
 Q
