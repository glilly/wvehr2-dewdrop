NURA6A1 ;HIRMFO/JH/MD,FT-ACADEMIC DEGREE REPORT BY SVC. CATEGORY ;8/8/96  12:52
 ;;4.0;NURSING SERVICE;**13**;Apr 25, 1997
CAT D HSKEEP G QUIT:$G(NUROUT)
 D EN3^NURSAGSP G QUIT:$G(NUROUT)
 D EN2^NURSAGP0 G QUIT:$G(NUROUT)
 D DEV G:POP!($D(ZTSK)) QUIT
START ;
 K ^TMP($J),^TMP("NURA",$J) U IO S (NURPAGE,NURSW1,NURQUIT)=0,NURSSP="",$P(NURSSP," ",80)=" "
 S NURSTIL="ACADEMIC EDUCATION PROFILE BY SERVICE CATEGORY       ",NURSTIL1=$E(NURSSP,1,10)_"SERVICE      EMPLOYEE"_$E(NURSSP,1,18)_"HIGHEST",NURSTIL2="NO.       CATEGORY     NAME"_$E(NURSSP,1,22)_"ACAD. DEGREE"
 S NURROU=11,NRPT=1,NURSLEV=3 W ! D EN1^NURAAGS0,CHK G:$G(NUROUT) QUIT
 D PRINT^NURAGEN G:NX="^" QUIT
 D TOTALS
QUIT D CLOSE^NURSUT1,^NURAKILL
 Q
POS ;ACADEMIC DEGREE REPORT BY SERVICE POSITION
 D HSKEEP G QUIT:$G(NUROUT)
 W ! D EN2^NURSAGSP G QUIT:$G(NUROUT)
 D EN2^NURSAGP0 G QUIT:$G(NUROUT)
 D DEV G:POP!($D(ZTSK)) QUIT
START1 ;
 K ^TMP($J),^TMP("NURA",$J) U IO S (NURPAGE,NURSW1,NURQUIT)=0,NURSSP="",$P(NURSSP," ",132)=" "
 S NURSTIL="ACADEMIC EDUCATION PROFILE BY SERVICE POSITION",NURSTIL1=$E(NURSSP,1,6)_"SERVICE    EMPLOYEE"_$E(NURSSP,1,23)_"HIGHEST",NURSTIL2="NO.   POSITION   NAME"_$E(NURSSP,1,27)_"ACAD. DEGREE"
 S NURROU=12,NRPT=1,NURSLEV=4 W ! D EN2^NURAAGS0,CHK G:$G(NUROUT) QUIT
 D PRINT^NURAGEN G:NX="^" QUIT
 S NURSORT(2)=2,NURSORT(3)="N(1)" D TOTALS
 G QUIT
DEV ;
 S ZTDESC=$S(ANS2=1:"ACADEMIC EDUCATION PROFILE BY SERVICE CATEGORY",1:"ACADEMIC EDUCATION PROFILE BY SERVICE POSITION")
 S ZTRTN=$S(ANS2=1:"START^NURA6A1",1:"START1^NURA6A1") D EN7^NURSUT0
 Q
CHK D NODATA^NURAGEN
 Q
HSKEEP ;
 D EN1^NURSAUTL Q:$G(NUROUT)
 I NURMDSW D EN12^NURSAGSP Q:$G(NUROUT)  S DIC(0)="AEQZ",NURPLSCR=0 D EN5^NURSAGSP Q:$G(NUROUT)
 I NURMDSW=0,NURPLSW=1 S NURPLSCR=0 D PRD^NURSAGSP K NURPLSCR
 Q
TOTALS ;
 I $Y>(IOSL-6) D HEADER^NURAGEN
 W !,?47,$$REPEAT^XLFSTR("-",33) D EN2^NURAED3 W !!,"TOTAL NUMBER OF ASSIGNMENTS ",?47,$J(NTCT,4,0),!!,"TOTAL NUMBER OF PERSONNEL",?47,$J(NTOT,4,0)
 Q
