ONCHPC5 ;Hines OIFO/GWB - 2000 Hepatocellular Cancers PCE Study ;01/12/00
 ;;2.11;ONCOLOGY;**26**;Mar 07, 1995
 ;Recurrence 
 K TABLE,HTABLE
 S TABLE("55. DATE OF FIRST RECURRENCE")="DFR"
 S TABLE("56. TYPE OF FIRST RECURRENCE")="TFR"
 S HTABLE(1)="55. TYPE OF FIRST RECURRENCE"
 S HTABLE(2)="56. DATE OF FIRST RECURRENCE"
 S CHOICES=2
 W @IOF D HEAD^ONCHPC0
 W !," RECURRENCE"
 W !," ----------"
 S DIE="^ONCO(165.5,",DA=ONCONUM,DR=70
DFR S DR="70 55. DATE OF FIRST RECURRENCE" D ^DIE G:$D(Y) JUMP
TFR S DR="71 56. TYPE OF FIRST RECURRENCE" D ^DIE G:$D(Y) JUMP
PRTC W ! K DIR S DIR(0)="E" D ^DIR S:$D(DIRUT) OUT="Y"
 G EXIT
JUMP ;Jump to prompts
 S XX="" R !!," GO TO ITEM NUMBER: ",X:DTIME
 I (X="")!(X[U) S OUT="Y" G EXIT
 I X["?" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 I '$D(TABLE(X)) S:X?1.2N X=X_"." S XX=X,X=$O(TABLE(X)) I ($P(X,XX,1)'="")!(X="") W *7,"??" D  G JUMP
 .W !," CHOOSE FROM:" F I=1:1:CHOICES W !,?5,HTABLE(I)
 S X=TABLE(X)
 G @X
EXIT K CHOICES,HTABLE,PIECE,TABLE
 K DA,DIE,DIR,DIROUT,DIRUT,DR,DTOUT,DUOUT,X,XX,Y
 Q
