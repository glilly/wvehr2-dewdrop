GMRVVS3 ;HIRMFO/YH-PATIENT VITAL SIGNS-I/O SF 511 GRAPH - 3 ;3/17/97
 ;;4.0;Vitals/Measurements;;Apr 25, 1997
SF511 ;PRODUCE PATIENT VITAL SIGNS-I/O GRAPH
 S GMRHT=0,GMRS=(9999999-GMRFIN)-.0001,GMRQ=9999999-GMRSTRT D INP^VADPT
 F GMRTY="B","P","R","T","H","W" D SETT^GMRVSR1
 F GMRK="P","T","B","H","W","R","XI1" D GMRDT
 I $D(^TMP($J,"GMR","XI"_GN(1))) F GMRI=0:0 S GMRI=$O(^TMP($J,"GMR","XI"_GN(1),GMRI)) Q:GMRI'>0  S GFOUND=0 D CKDT S:GFOUND=0 ^TMP($J,"GMRDT",GMRI)=""
 I $D(^TMP($J,"GMR","XO"_GN(2))) F GMRI=0:0 S GMRI=$O(^TMP($J,"GMR","XO"_GN(2),GMRI)) Q:GMRI'>0  S GFOUND=0 D CKDT S:GFOUND=0 ^TMP($J,"GMRDT",GMRI)=""
 S (GMRTNM,GMRI)=0 F  S GMRI=$O(^TMP($J,"GMRDT",GMRI)) Q:GMRI'>0  S GMRTNM=GMRTNM+1
 D ^GMRVVS1 K GMR3,GMRDAT,GMREN,GMRHDR1,GMRHDR11,GMRHDR10,GMRHDR2,GMRHT,GMRI,GMRJ,GMRK,GMRLINE,GDATA,GMROLD,GMRP,GMRPDIF,GMRPG,GMRPGC,GMRPGS,GMRPHI,GMRPLO,GMRPOFF,GMRSITE,GMRT,GMRTDIF,GMRTHI,GMRTLO,GMRTNM
 K GMRTOFF,GMRTY,GMRNM,GMRVX,GMRVX1,GMRVX2,^TMP($J,"GMRY")
 Q
CKDT F GMRJ=0:0 S GMRJ=$O(^TMP($J,"GMRDT",GMRJ)) Q:GMRJ'>0!(+$P(GMRJ,".")>GMRI)  S:+$P(GMRJ,".")=GMRI GFOUND=1
 Q
GMRDT S GMRTNM(GMRK)=0 F GMRI=0:0 S GMRI=$O(^TMP($J,"GMR",GMRK,GMRI)) Q:GMRI'>0  S GMRJ="" F X=0:0 S GMRJ=$O(^TMP($J,"GMR",GMRK,GMRI,GMRJ)) Q:GMRJ=""  S GMRTNM(GMRK)=GMRTNM(GMRK)+1 S:GMRK'="XI1" ^TMP($J,"GMRDT",GMRI)=""
 Q
