LASPEC ;SLC/RWF - ABBOTT SPECTRUM VERSION M2.0 ;7/20/90  10:12 ;
 ;;5.2;AUTOMATED LAB INSTRUMENTS;;Sep 27, 1994
 ;CROSS LINK BY ID OR IDE
LA1 S:$D(ZTQUEUED) ZTREQ="@" S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
LA2 K TV S TOUT=0 D IN G QUIT:TOUT,LA2:'$L($P(IN,"/",3)) S IN=$P(IN,"/",3)
LA2A S V=$E(IN,8,24) D NUM,NUM1 S (ID,IDE)=+V,TRAY=$S(+$E(IN,25):$E(IN,25),1:99),CUP=$S(+$E(IN,26,27):+$E(IN,26,27),1:IDE)
 I $E(IN,48,49)="80" S V=$E(IN,32,37) D NUM S TEST=V,V=$E(IN,38,45) D NUM S V=+V D LA2B
 F I=0:0 S TOUT=0 D IN G QUIT:TOUT Q:'$L(IN)  I $E(IN,17,18)="80" S V=$E(IN,1,6) D NUM S TEST=V,V=$E(IN,7,14) D NUM S V=+V D LA2B
 D LA3 G LA2
LA2B F I=0:0 S I=$O(TC(I)) Q:I<1  I TEST=TC(I,4) S V=$J(V,0,+TC(I,3)) S @TC(I,1)=V Q
 Q
LA3 X LAGEN Q:'ISQN
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
NUM1 F JJ=1:1:$L(V) Q:+$E(V,JJ)
 S V=+$E(V,JJ,$L(V)) Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^TMP($J),^TMP("LA",$J)
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
