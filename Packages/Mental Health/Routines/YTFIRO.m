YTFIRO ;SLC/DKG-TEST PKG: FIRO TEST ;3/22/91  13:22 ;
 ;;5.01;MENTAL HEALTH;;Dec 30, 1994
 ;
 S X=^YTD(601.2,YSDFN,1,YSET,1,YSED,1),YSLFT=0 F I=1:1:6 D FSCR
 D DTA^YTREPT W !!?28,$P(^YTT(601,YSTEST,"P"),U),!!!?29,"INCLUSION  CONTROL  AFFECTION"
 W !!?18,"EXPRESSED",$J(R(1),7,0),$J(R(3),10,0),$J(R(5),10,0)
 W !?19,"WANTED",$J(R(2),9,0),$J(R(4),10,0),$J(R(6),10,0)
END ;
 W ! K A,I,YSIT,YSIX,J,R,X,Y Q
FSCR ;
 S R(I)=0,Y=^YTT(601,YSTEST,"S",I,"K",1,0) F J=1:2:17 S YSIT=$P(Y,U,J),YSIX=$P(Y,U,J+1) S:YSIX[$E(X,YSIT) R(I)=R(I)+1
 Q
PR ;
 W ! F J=1:1 Q:'$D(^YTT(601,YSTEST,"G",A,1,J,0))  D:IOST?1"C-".E WAIT:$Y>(IOSL-4) Q:YSLFT  W !,^(0)
 Q
WAIT ;
 ;  Added 5/6/94 LJA
 N A,B,B1,C,D,E,E1,F,F1,G,G1,H,I,J,J1,J2,J3,J4,K,L,L1,L2,M,N
 N N1,N2,N3,N4,P,P0,P1,P3,R,R1,S,S1,T,T1,T2,TT,V,V1,V2,V3
 N V4,V5,V6,W,X,X0,X1,X2,X3,X4,X7,X8,X9,Y,Y1,Y2,Z,Z1,Z3
 ;
 F I0=1:1:(IOSL-$Y-2) W !
 N DTOUT,DUOUT,DIRUT
 S DIR(0)="E" D ^DIR K DIR S YSTOUT=$D(DTOUT),YSUOUT=$D(DUOUT),YSLFT=$D(DIRUT)
 W @IOF Q
